      SUBROUTINE M3DREW(NBIN,KNTRVL,IKMAX,NATM,                         &
     &  I3DGEN,I3DDAT,I3DMOL,M3DMOL)

!     M3DREW RE-WRITES THE MOD3D MOLECULAR DATA FILE PLACING ALL
!     SPECTRAL DATA FOR A GIVEN SPECTRAL FREQUENCY IN ONE RECORD.
      IMPLICIT NONE

!     PARAMETERS:
!       MDAT3D   MAXIMUM NUMBER OF MOD3D DATA FOR A GIVEN FREQUENCY:
      INTEGER MDAT3D
      PARAMETER(MDAT3D=170000)

!     ARGUMENTS:
!       I3DGEN   GENERAL INFORMATION FILE UNIT NUMBER FOR MOD3D.
!       I3DDAT   MOLECULAR EXTINCTION FILE UNIT NUMBER BY ATMOSPHERE.
!       I3DMOL   MOLECULAR EXTINCTION DATA FILE UNIT NUMBER FOR MOD3D.
!       NBIN     NUMBER OF SPECTRAL DATA POINTS.
!       KNTRVL   NUMBER OF ABSORPTION COEFFICIENT (K) SUB-INTERVALS.
!       IKMAX    NUMBER OF PATH SEGMENTS.
!       NATM     NUMBER OF ATMOSPHERES IN MOLECULAR GRID.
!       M3DMOL   NAME OF MOLECULAR EXTINCTION DATA FILE FOR MOD3D.
      INTEGER I3DGEN,I3DDAT,I3DMOL,NBIN,KNTRVL,IKMAX,NATM
      CHARACTER M3DMOL*(*)

!     FUNCTIONS:
!       IRECLN   RETURNS THE RECORD LENGTH FOR A DIRECT ACCESS FILES
!                CONTAINING A KNOWN NUMBER OF VARIABLES IN EACH RECORD.
      INTEGER IRECLN

!     LOCAL VARIABLES:
!       LRECLN   RECORD LENGTH OF A DIRECT ACCESS FILE.
!       NDAT3D   NUMBER OF MOD3D DATA FOR A GIVEN FREQUENCY.
!       IBIN     SPECTRAL BIN COUNTER.
!       INTRVL   ABSORPTION COEFFICIENT (K) SUB-INTERVAL INDEX.
!       IK       LAYER INDEX.
!       IATM     ATMOSPHERE INDEX.
!       NREC     BINARY FILE RECORD NUMBER FOR M3DDAT FILE.
!       IATM     COUNTER FOR NUMBER OF DIFFERENT MOLECULAR ATMOSPHERES.
!       IDAT3D   MOD3D DATA COUNTER FOR A GIVEN FREQUENCY:
!       XTOT     TEMPORARY STORAGE ARRAY FOR MOLECULAR EXTINCTION DATA.
      INTEGER LRECLN,NDAT3D,IBIN,INTRVL,IK,NREC,IATM,IDAT3D
      REAL XTOT(MDAT3D)

!     CLOSE MOD3D GENERAL INFORMATION FILE:
      CLOSE(I3DGEN,STATUS='KEEP')

!     RETURN IF SPECTRAL DATA DOES NOT FIT IN XTOT:
!     ***  Should use MALLOC command to allocate necessary memory.
      NDAT3D=KNTRVL*IKMAX*NATM
      IF(NDAT3D.GT.MDAT3D)RETURN

!     OPEN MOD3D MOLECULAR ABSORPTION DATA FILE:
      LRECLN=IRECLN(NDAT3D)
      CALL OPNFL(I3DMOL,LRECLN,M3DMOL,'UNKNOWN','UNFORMATTED','M3DREW')

!     LOOP OVER SPECTRAL FREQUENCIES:
      DO IBIN=0,NBIN-1

!         LOOP OVER LAYERS:
          DO IK=0,IKMAX-1

!             LOOP OVER ATMOSPHERES:
              DO IATM=0,NATM-1

!                 M3DDAT RECORD NUMBER:
                  NREC=1+IK+(IATM*NBIN+IBIN)*IKMAX
                  READ(I3DDAT,REC=NREC)                                 &
     &              (XTOT(INTRVL+KNTRVL*(IATM+NATM*IK)),INTRVL=1,KNTRVL)

!             END ATMOSPHERE LOOP:
              ENDDO

!         END LAYER LOOP:
          ENDDO
          WRITE(I3DMOL,REC=1+IBIN)(XTOT(IDAT3D),IDAT3D=1,NDAT3D)

!     END SPECTRAL FREQUENCY LOOP:
      ENDDO

!     CLOSE MOD3D FILES:
      CLOSE(I3DDAT,STATUS='DELETE')
      CLOSE(I3DMOL,STATUS='KEEP')
      RETURN
      END
