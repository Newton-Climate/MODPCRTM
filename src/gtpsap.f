      REAL FUNCTION GTPSAP(VCEN,IK)

!     GTPSAP RETURNS THE SPECTRAL AEROSOL PROFILES (SAP) PHASE
!     FUNCTION [SR-1].
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     INPUT ARGUMENTS:
!       VCEN     SPECTRAL FREQUENCY [CM-1].
!       IK       LINE-OF-SIGHT BOUNDARY INDEX (1 AT H1, IKMAX+1 AT H2).
      REAL VCEN
      INTEGER IK

!     COMMONS:

!     /SAP/
!       NWVSAP   NUMBER OF AEROSOL SPECTRAL GRID POINTS.
!       NLGSAP   HIGHEST AEROSOL PHASE FUNCTION LEGENDRE MOMENT.
!       NANSAP   NUMBER OF AEROSOL PHASE FUNCTION ANGULAR POINTS.
!       LEVSAP   NUMBER OF AEROSOL ATMOSPHERIC LEVELS.
!       ANGSAP   AEROSOL PHASE FUNCTION ANGULAR GRID [DEGREES].
!       COSSAP   COSINE OF THE AEROSOL PHASE FUNCTION ANGLES.
!       WAVSAP   AEROSOL SPECTRAL GRID [MICRONS].
!       LEGSAP   AEROSOL LEGENDRE COEFFICIENTS.
!       PFSAP    AEROSOL PHASE FUNCTION [SR-1].
!       LOSSAP   LOGICAL FLAG, TRUE FOR LINE-OF-SIGHT PATH.
      INTEGER NWVSAP,NLGSAP,NANSAP,LEVSAP
      DOUBLE PRECISION ANGSAP,COSSAP,PFSAP
      REAL WAVSAP,LEGSAP
      LOGICAL LOSSAP
      COMMON/SAP/NWVSAP,NLGSAP,NANSAP,LEVSAP,ANGSAP(MANSAP),            &
     &  COSSAP(MANSAP),WAVSAP(MWVSAP),LEGSAP(MLGSAP,MWVSAP,LAYDIM),     &
     &  PFSAP(MANSAP,MWVSAP,LAYDIM),LOSSAP

!     /SAPV/
!       ILOSAP   SAP DATA LOWER WAVELENGTH INDEX AT CURRENT FREQUENCY.
!       IHISAP   SAP DATA HIGHER WAVELENGTH INDEX AT CURRENT FREQUENCY.
!       FRCSAP   SAP DATA INTERPOLATION FRACTION AT CURRENT FREQUENCY.
      INTEGER ILOSAP,IHISAP
      REAL FRCSAP
      COMMON/SAPV/ILOSAP,IHISAP,FRCSAP

!     /SAPPF/
!       ASMSAP   SPECTRAL AEROSOL PROFILE ASYMMETRY FACTOR.
!       PFPSAP   SPECTRAL AEROSOL PROFILE PHASE FUNCTION [SR-1].
      REAL ASMSAP,PFPSAP
      COMMON/SAPPF/ASMSAP(MWVSAP,LAYDM1),PFPSAP(MANSAP,MWVSAP,LAYDM1)

!     /PATH/
!       PTHCOS   COSINE OF PATH ZENITH AT PATH BOUNDARIES.
!       PTHZEN   PATH ZENITH AT PATH BOUNDARIES [DEG].
!       PTHECA   SENSOR TO PATH EARTH CENTER ANGLE [DEG].
!       PTHALT   ALTITUDES AT PATH BOUNDARIES [KM].
!       PTH_MS   ALTITUDES AT PATH BOUNDARIES FOR THE MS PATH.
!       PTHSEG   PATH SEGMENT LENGTH [KM].
!       PTHRNG   SENSOR TO PATH BOUNDARY RANGE [KM].
!       JMAX     NUMBER OF DISTINCT LOS PATH SEGMENT ENDPOINT ALTITUDES.
!       IKHMIN   PATH BOUNDARY INDEX OF PATH MINIMUM ALTITUDE.
!       IKHMAX   PATH BOUNDARY INDEX OF PATH MAXIMUM ALTITUDE.
!       IKOUT    NUMBER OF PATH BOUNDARIES K DATA IS OUTPUT.
!       NTKDIS   RECORD NUMBER FOR K-DISTRIBUTION TRANSMITTANCE FILE.
!       NRKDIS   RECORD NUMBER FOR K-DISTRIBUTION RADIANCE FILE.
!       MAPPTH   MAPPING FROM PATH SEGMENT MIDPOINT TO VERTICAL LAYER.
!       IPTHHT   ALTITUDES (HEIGHTS) AT PATH BOUNDARIES [M].
!       LOWALT   VERTICAL LAYER BOUNDARY INDEX AT OR JUST BELOW PTHALT.
!       FACALT   ALTITUDE INTERPOLATION FRACTION FOR PTHALT
!       PATH_T   TEMPERATURE AT PATH BOUNDARIES [K].
!       PATH_P   PRESSURE AT PATH BOUNDARIES [ATM].
!       PTHRH    RELATIVE HUMIDITY AT PATH BOUNDARIES [K].
!       LSSGEO   LOGICAL FLAG, .TRUE. FOR SOLAR PATHS.
!       LTANMX   LOGICAL FLAG, .TRUE. IF PATH HAS A TANGENT MAXIMUM.
      DOUBLE PRECISION PTHCOS,PTHZEN,PTHECA,PTHALT,PTH_MS,PTHSEG,PTHRNG
      INTEGER JMAX,IKHMIN,IKHMAX,IKOUT,NTKDIS,NRKDIS,MAPPTH,            &
     &  IPTHHT,LOWALT
      REAL FACALT,PATH_T,PATH_P,PTHRH
      LOGICAL LSSGEO,LTANMX
      COMMON/PATH/PTHCOS(0:LAYTWO),PTHZEN(0:LAYTWO),PTHECA(0:LAYTWO),   &
     &  PTHALT(0:LAYTWO,1:MLOS),PTH_MS(0:LAYDIM),PTHSEG(LAYTWO),        &
     &  PTHRNG(0:LAYTWO,1:MLOS),JMAX,IKHMIN(MLOS),IKHMAX(MLOS),         &
     &  IKOUT(MLOS),MAPPTH(LAYTWO,1:MLOS),IPTHHT(0:LAYTWO),NTKDIS,      &
     &  NRKDIS,LOWALT(0:LAYTWO,1:MLOS),FACALT(0:LAYTWO,1:MLOS),         &
     &  PATH_T(0:LAYTWO,1:MLOS),PATH_P(0:LAYTWO,1:MLOS),                &
     &  PTHRH(0:LAYTWO,1:MLOS),LSSGEO,LTANMX

!     /SAPCOS/
!       ICSSAP   HIGHER SCATTERING ANGLE INDEX (SMALLER COSINE VALUE).
!       FCSSAP   SCATTERING ANGLE COSINE INTERPOLATION FRACTION.
      INTEGER ICSSAP
      REAL FCSSAP
      COMMON/SAPCOS/ICSSAP(LAYTWO),FCSSAP(LAYTWO)

!     LOCAL VARIABLES:
!       LEVEL    LEVEL INDEX FOR CURRENT PATH BOUNDARY.
!       WAVLEN   SPECTRAL WAVELENGTH (MICRONS).
!       PSAPHI   SCATTERING PHASE FUNCTION AT HIGHER WAVELENGTH [SR-1].
!       PSAPHI   SCATTERING PHASE FUNCTION AT LOWER WAVELENGTH [SR-1].
      INTEGER LEVEL
      REAL WAVLEN,PSAPHI,PSAPLO

!     FUNCTIONS:
!       EXPINT   EXPONENTIAL INTERPOLATION (LINEAR IF NON-POSITIVE ARG).
      REAL EXPINT

!     SAVED VARIABLES:
!       IWAVLO   LOWER SPECTRAL WAVELENGTH INDEX.
!       IWAVHI   HIGHER SPECTRAL WAVELENGTH INDEX.
!       FWAVE    SPECTRAL WAVELENGTH INTERPOLATION FRACTION.
      INTEGER IWAVLO,IWAVHI
      REAL FWAVE
      SAVE IWAVLO,IWAVHI,FWAVE

!     IS THIS THE FIRST TIME THROUGH FOR FREQUENCY VCEN?
      IF(IK.EQ.1)THEN

!         DETERMINE SPECTRAL INTERPOLATION DATA:
          WAVLEN=10000/VCEN
          IF(WAVLEN.GE.WAVSAP(NWVSAP))THEN

!             NO EXTRAPOLATION ABOVE HIGHEST WAVELENGTH:
              IWAVLO=NWVSAP
              IWAVHI=NWVSAP
          ELSEIF(WAVLEN.LE.WAVSAP(1))THEN

!             NO EXTRAPOLATION BELOW LOWEST WAVELENGTH:
              IWAVLO=1
              IWAVHI=1
          ELSE

!             FOR VBAND5 ( VBAND5 - 5 < VCEN < VBAND5 ), ILOSAP & IHISAP
!             ARE THE BOUNDING INDICES.  USE THEM AS A STARTING POINT.
              DO IWAVHI=IHISAP,NWVSAP
                  IF(WAVLEN.LE.WAVSAP(IWAVHI))THEN
                      IWAVLO=IWAVHI-1
                      FWAVE=(WAVLEN-WAVSAP(IWAVLO))                     &
     &                  /(WAVSAP(IWAVHI)-WAVSAP(IWAVLO))
                      GOTO 10
                  ENDIF
              ENDDO
          ENDIF
      ENDIF

!     DETERMINE THE LEVEL INDEX CORRESPONDING TO PATH BOUNDARY IK:
   10 CONTINUE
      LEVEL=ABS(IK-IKHMIN(1)-1)+1

!     EXPONENTIALLY INTERPOLATE OVER COSINE OF SCATTERING ANGLE:
      IF(IWAVLO.EQ.IWAVHI)THEN
          GTPSAP=EXPINT(PFPSAP(ICSSAP(IK)  ,IWAVHI,LEVEL),              &
     &                  PFPSAP(ICSSAP(IK)-1,IWAVHI,LEVEL),FCSSAP(IK))
      ELSE
          PSAPHI=EXPINT(PFPSAP(ICSSAP(IK)  ,IWAVHI,LEVEL),              &
     &                  PFPSAP(ICSSAP(IK)-1,IWAVHI,LEVEL),FCSSAP(IK))
          PSAPLO=EXPINT(PFPSAP(ICSSAP(IK)  ,IWAVLO,LEVEL),              &
     &                  PFPSAP(ICSSAP(IK)-1,IWAVLO,LEVEL),FCSSAP(IK))
          GTPSAP=PSAPLO+FWAVE*(PSAPHI-PSAPLO)
      ENDIF
      RETURN
      END
