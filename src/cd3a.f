      SUBROUTINE CD3A(ANGLEM,IDAY,ISOURC)

!     PROCESS CARD3A1 AND CARD3A2 INPUTS:
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     OUTPUT ARGUMENTS:
!       ANGLEM   LUNAR PHASE ANGLE [0 TO 180 DEG].
!       IDAY     DAY OF YEAR [0-366, DEFAULT (0) IS DAY 91].
!       ISOURC   SOURCE FLAG [0=SUN AND 1=MOON].
      DOUBLE PRECISION ANGLEM
      INTEGER IDAY,ISOURC

!     COMMONS:
      INCLUDE 'IFIL.h'

!     /CNTRL/
!       NSEG     NUMBER OF PATH SEGMENTS ALONG LINE-OF-SIGHT.
!       ML       NUMBER OF ATMOSPHERIC PROFILE LEVELS.
!       MLFLX    NUMBER OF LEVELS FOR WHICH FLUX VALUES ARE WRITTEN.
!       IMULT    MULTIPLE SCATTERING FLAG
!                  (0=NONE, 1=AT SENSOR, -1=AT FINAL OR TANGENT POINT).
!       THERML   FLAG TO CALCULATE THERMAL SCATTER.
      INTEGER NSEG,ML,MLFLX,IMULT
      LOGICAL THERML
      COMMON/CNTRL/NSEG(0:MLOSP1),ML,MLFLX,IMULT,THERML

!     /CARD1/
!       MODEL    MODEL ATMOSPHERE INDEX.
!       ITYPE    SLANT PATH TYPE.
!       IEMSCT   RADIATIVE TRANSFER MODE.
!                  0 FOR TRANSMITTANCE
!                  1 FOR THERMAL EMISSION ONLY
!                  2 FOR THERMAL EMISSION PLUS SOLAR SCATTER
!                  3 FOR TRANSMITTED SOLAR IRRADIANCE
!                  4 FOR SOLAR SCATTER ONLY
!       M1       MODEL ATMOSPHERE FOR PRESSURE & TEMPERATURE PROFILES.
!       M2       MODEL ATMOSPHERE FOR H2O PROFILE.
!       M3       MODEL ATMOSPHERE FOR O3 PROFILE.
!       I_RD2C   READ CARD 2C, 2C1, ... IF EQUAL 1; SKIP IF EQUAL TO 0.
!       NOPRNT   PRINT FLAG.
!       MODTRN   MODTRAN BAND MODEL FLAG.
      INTEGER MODEL,ITYPE,IEMSCT,M1,M2,M3,I_RD2C,NOPRNT
      LOGICAL MODTRN
      COMMON/CARD1/MODEL,ITYPE,IEMSCT,M1,M2,M3,I_RD2C,NOPRNT,MODTRN

!     /JM3A1/
      INTEGER IPARM
      COMMON/JM3A1/IPARM

!     /CARD3A/
!       G        SCATTERING FUNCTION ASYMMETRY FACTOR.
      INTEGER IPH
      REAL G
      COMMON/CARD3A/IPH,G

!     /JM3A2/
!       IPRMSV   SAVED VALUE OF IPARM INPUT.
!       PARM1    SENSOR LATITUDE IF IPARM=0 OR =1 [DEG N OF EQUATOR].
!                TARGET LATITUDE IF IPARM=10 OR =11 [DEG N OF EQUATOR].
!                SOLAR REL AZIMUTH AT SENSOR IF IPARM=2 [DEG E OF N].
!                SOLAR REL AZIMUTH AT TARGET IF IPARM=12 [DEG E OF N].
!       PARM2    SENSOR LONGITUDE IF IPARM=0 OR =1 [DEG W OF GRNWCH].
!                TARGET LONGITUDE IF IPARM=10 OR =11 [DEG W OF GRNWCH].
!                SOLAR ZENITH AT SENSOR IF IPARM IS 2 [DEG].
!                SOLAR ZENITH AT TARGET IF IPARM IS 12 [DEG].
!       SRCLAT   SOURCE (SUN OR MOON) LATITUDE [DEG NORTH OF EQUATOR].
!       SRCLON   SOURCE (SUN OR MOON) LONGITUDE [DEG WEST OF GREENWICH].
!       GMTIME   GREENWICH MEAN TIME [DECIMAL HOURS].
!       TRUEAZ   PATH TRUE AZIMUTH ANGLE [DEG EAST OF NORTH].
      INTEGER IPRMSV
      DOUBLE PRECISION PARM1,PARM2,SRCLAT,SRCLON,GMTIME,TRUEAZ
      COMMON/JM3A2/PARM1,PARM2,SRCLAT,SRCLON,GMTIME,TRUEAZ,IPRMSV

!     /JM5/
!       IRPT     REPEAT INPUT FLAG (0=NONE, 1=ALL, 3=GEOM, 4=SPEC).
!       IFAC     CURRENT COLUMN SCALING FACTOR INDEX.
!       NFACMN   NUMBER OF COLUMN SCALING FACTOR LESS THAN 1.
!       NFACMX   NUMBER OF COLUMN SCALING FACTOR GREATER THAN 1.
!       FACMC    CURRENT COLUMN SCALING FACTOR.
!       SCALMN   MINIMUM COLUMN SCALING FACTOR.
!       SCALMX   MAXIMUM COLUMN SCALING FACTOR.
!       LBMRES   LOGICAL FLAG, .TRUE. FOR BAND MODEL AND .FALSE.
!                FOR 1 NM SPECTRAL RESOLUTION OUTPUT.
      INTEGER IRPT,IFAC,NFACMN,NFACMX
      REAL FACMC
      DOUBLE PRECISION SCALMN,SCALMX
      LOGICAL LBMRES
      COMMON/JM5/SCALMN,SCALMX,IRPT,IFAC,NFACMN,NFACMX,FACMC,LBMRES

!     /DISRT/
!       UMU      MONOTONICALLY INCREASING LIST OF DISTINCT USER-PATH
!                COSINE POLAR ANGLES.
!       PHI      MONOTONICALLY INCREASING LIST OF DISTINCT RELATIVE
!                SOLAR AZIMUTH ANGLES [0 TO 180 DEG].
!       NSTR     NUMBER OF DISCRETE ORDINATE STREAMS.
!       NAZ      NUMBER OF DISORT AZIMUTH COMPONENTS.
!       N2GAUS   ORDER OF DOUBLE-GAUSS QUADRATURES.
!       NUMU     NUMBER OF DISTINCT USER LINE-OF-SIGHT POLAR ANGLES.
!       MAPUMU   MAPPING FROM LINE-OF-SIGHT INDEX TO UMU ARRAY ENTRY.
!       NPHI     NUMBER OF DISTINCT RELATIVE SOLAR AZIMUTH ANGLES.
!       MAPPHI   MAPPING FROM LINE-OF-SIGHT INDEX TO PHI ARRAY ENTRY.
!       DIS      LOGICAL FLAG, TRUE FOR DISORT MULTIPLE SCATTERING.
!       DISAZM   LOGICAL FLAG, TRUE FOR DISORT WITH AZIMUTH DEPENDENCE.
!       DISALB   LOGICAL FLAG, TRUE FOR DISORT SPHERICAL ALBEDO OPTION.
!       LDISCL   LOGICAL FLAG, TRUE FOR ISAACS SCALED TO DISORT.
      REAL UMU,PHI
      INTEGER NSTR,NAZ,N2GAUS,NUMU,MAPUMU,NPHI,MAPPHI
      LOGICAL DIS,DISAZM,DISALB,LDISCL
      COMMON/DISRT/UMU(MXUMU),PHI(MAXPHI),NSTR,NAZ,N2GAUS,NUMU,         &
     &  MAPUMU(MLOS),NPHI,MAPPHI(MLOS),DIS,DISAZM,DISALB,LDISCL
      SAVE /DISRT/

!     /SAVEMS/
!       LUSEMS  LOGICAL, TRUE IF MULTIPLE SCATTERING DATA IS REUSED.
!       LSAVMS  LOGICAL, TRUE IF MULTIPLE SCATTERING DATA IS  SAVED.
      LOGICAL LUSEMS,LSAVMS
      COMMON/SAVEMS/LUSEMS,LSAVMS

!     FUNCTIONS:
!       COMPAR   COMPARES SOLAR GEOMETRY INPUTS TO CHECK FOR CHANGE.
      LOGICAL COMPAR

!     LOCAL VARIABLES:
!       LOPEN    LOGICAL FLAG, .TRUE. IF FILE IS OPEN.
      LOGICAL LOPEN

!     DATA:
!       IPHSV    SAVED VALUE OF INPUT IPHSV.
!       IDAYSV   SAVED VALUE OF INPUT IDAY.
!       ISRCSV   SAVED VALUE OF INPUT ISOURC.
!       PRM1SV   SAVED VALUE OF INPUT PARM1.
!       PRM2SV   SAVED VALUE OF INPUT PARM2.
!       SLATSV   SAVED VALUE OF INPUT SRCLAT.
!       SLONSV   SAVED VALUE OF INPUT SRCLON.
!       GMTSV    SAVED VALUE OF INPUT TIME.
!       GSV      SAVED VALUE OF INPUT G.
!       ANGMSV   SAVED VALUE OF INPUT ANGLEM.
      INTEGER IPHSV,IDAYSV,ISRCSV
      REAL GSV
      DOUBLE PRECISION PRM1SV,PRM2SV,SLATSV,SLONSV,GMTSV,ANGMSV
      SAVE IPHSV,IDAYSV,ISRCSV,                                         &
     &  PRM1SV,PRM2SV,SLATSV,SLONSV,GMTSV,GSV,ANGMSV
      DATA IPHSV,IDAYSV,ISRCSV/3*0/,GSV/0./,                            &
     &  PRM1SV,PRM2SV,SLATSV,SLONSV,GMTSV,ANGMSV/6*0.D0/


!     SOLAR RADIANCE CALCULATION?
      LUSEMS=.FALSE.
      LSAVMS=.FALSE.
      IF(IEMSCT.NE.2)THEN
          IPARM =-99
          IPH   =-99
          PARM1 =-99.D0
          PARM2 =-99.D0
          SRCLAT=-99.D0
          SRCLON=-99.D0
          GMTIME=-99.D0
          TRUEAZ=-99.D0
          G     =-99.
          RETURN
      ENDIF

!     READ CARDS 3A1 AND 3A2:
      IF(LJMASS)THEN
          CALL INITCD('CARD3A1')
          CALL INITCD('CARD3A2')
      ELSE

!         CARD 3A1:
          READ(IRD,'(4I5)')IPARM,IPH,IDAY,ISOURC
          WRITE(IPR,'(/A,4I5)')' CARD 3A1*****',IPARM,IPH,IDAY,ISOURC

!         CARD 3A2:
          READ(IRD,'(8F10.0)')                                          &
     &      PARM1,PARM2,SRCLAT,SRCLON,GMTIME,TRUEAZ,ANGLEM,G
          WRITE(IPR,'(/A,8F10.5)')' CARD 3A2*****',                     &
     &          PARM1,PARM2,SRCLAT,SRCLON,GMTIME,TRUEAZ,ANGLEM,G
      ENDIF

      IF(IMULT.NE.0 .AND. .NOT.DIS)THEN

!         ISAACS TWO-STREAM WITH SOLAR/LUNAR WILL BE USED.
          INQUIRE(ISCRCH,OPENED=LOPEN)
          IF(.NOT.LOPEN)THEN

!             FIRST ISAACS SOLAR CALCULATION
              CALL OPNFL(ISCRCH,0,' ','SCRATCH','UNFORMATTED','CD3A')
          ELSE

!             COMPARE SOLAR PARAMETERS; LUSEMS IS .T. IF THEY MATCH.
              IF(IRPT.EQ.3)LUSEMS=COMPAR(IPARM,IPH,IDAY,ISOURC,PARM1,   &
     &          PARM2,SRCLAT,SRCLON,GMTIME,G,ANGLEM,IPRMSV,IPHSV,IDAYSV,&
     &          ISRCSV,PRM1SV,PRM2SV,SLATSV,SLONSV,GMTSV,GSV,ANGMSV)
              REWIND(ISCRCH)
          ENDIF

!         SAVE SOLAR PARAMETERS FOR COMPARING LATER.
          CALL SVSOLA(IPARM,IPH,IDAY,ISOURC,PARM1,PARM2,SRCLAT,         &
     &      SRCLON,GMTIME,G,ANGLEM,IPRMSV,IPHSV,IDAYSV,ISRCSV,          &
     &      PRM1SV,PRM2SV,SLATSV,SLONSV,GMTSV,GSV,ANGMSV)
          LSAVMS=.TRUE.
      ENDIF
      IF(IPH.EQ.0)THEN
          IF(G.GT..9999)THEN
              G=.9999
          ELSEIF(G.LT.-.9999)THEN
              G=-.9999
          ENDIF
      ELSEIF(IPH.EQ.1)THEN
          CALL CD3B3C
      ENDIF
      IF(IRPT.EQ.3)THEN
          IF(IPARM.EQ.1 .OR. IPARM.EQ.11)                               &
     &      CALL SUBSOL(SRCLAT,SRCLON,GMTIME,IDAY)
          RETURN
      ENDIF

!     INTREPRET SOLAR SCATTERING PARAMETERS
      IF(LJMASS)THEN
          IF(IPARM.EQ.1 .OR. IPARM.EQ.11)                               &
     &      CALL SUBSOL(SRCLAT,SRCLON,GMTIME,IDAY)
          RETURN
      ENDIF

!     WRITE OUT SOLAR INPUTS:
      WRITE(IPR,'(/A)')' SINGLE SCATTERING CONTROL PARAMETERS SUMMARY'
      IF(IPARM.EQ.2)THEN
          WRITE(IPR,'(/(10X,A,T48,F10.4,A))')                           &
     &      'RELATIVE AZIMUTH AT H1ALT =',PARM1,' DEG EAST OF NORTH',   &
     &      'SOLAR ZENITH AT H1ALT =',PARM2,' DEG'
      ELSEIF(IPARM.EQ.12)THEN
          WRITE(IPR,'(/(10X,A,T48,F10.4,A))')                           &
     &      'RELATIVE AZIMUTH AT H2ALT =',PARM1,' DEG EAST OF NORTH',   &
     &      'SOLAR ZENITH AT H2ALT =',PARM2,' DEG'
      ELSE
          IF(IPARM.LT.2)THEN
              WRITE(IPR,'(/(10X,A,T48,F10.4,A))')                       &
     &          'LATITUDE AT H1ALT =',PARM1,' DEG NORTH OF EQUATOR',    &
     &          'LONGITUDE AT H1ALT =',PARM2,' DEG WEST OF GREENWICH'
          ELSE
              WRITE(IPR,'(/(10X,A,T48,F10.4,A))')                       &
     &          'LATITUDE AT H2ALT =',PARM1,' DEG NORTH OF EQUATOR',    &
     &          'LONGITUDE AT H2ALT =',PARM2,' DEG WEST OF GREENWICH'
          ENDIF
          IF(IPARM.EQ.1 .OR. IPARM.EQ.11)                               &
     &      CALL SUBSOL(SRCLAT,SRCLON,GMTIME,IDAY)
          WRITE(IPR,'(/(10X,A,T48,F10.4,A))')                           &
     &      'SUBSOLAR LATITUDE =',SRCLAT,' DEG NORTH OF EQUATOR',       &
     &      'SUBSOLAR LONGITUDE =',SRCLON,' DEG WEST OF GREENWICH'
      ENDIF
      IF(IPARM.LE.2)THEN
          WRITE(IPR,'((10X,A,T48,F10.4,A))')'TIME (<0 UNDEF) =',GMTIME, &
     &      ' GREENWICH TIME','PATH AZIMUTH (FROM H1ALT TO H2ALT) =',   &
     &      TRUEAZ,' DEG EAST OF NORTH'
      ELSE
          WRITE(IPR,'((10X,A,T48,F10.4,A))')'TIME (<0 UNDEF) =',GMTIME, &
     &      ' GREENWICH TIME','PATH AZIMUTH (FROM H2ALT TO H1ALT) =',   &
     &      TRUEAZ,' DEG EAST OF NORTH'
      ENDIF
      WRITE(IPR,'(10X,A,T48,I10)')'DAY OF THE YEAR =',IDAY
      IF(ISOURC.EQ.0)WRITE(IPR,'(/A)')                                  &
     &  ' EXTRATERRESTIAL SOURCE IS THE SUN'
      IF(ISOURC.EQ.1)WRITE(IPR,'(/A,F10.3,A)')' EXTRATERRESTIAL'        &
     &  //' SOURCE IS THE MOON, MOON PHASE ANGLE =',ANGLEM,' DEG'
      IF(IPH.EQ.0)WRITE(IPR,'(/A,F10.5)')' H-G PHASE FUNCTION ,G=',G
      IF(IPH.EQ.1)WRITE(IPR,'(/A)')' USER SUPPLIED PHASE FUNCTION'
      IF(IPH.EQ.2)WRITE(IPR,'(/A)')' PHASE FUNCTION FROM MIE DATA BASE'
!     RETURN TO DRIVER:
      RETURN
      END
