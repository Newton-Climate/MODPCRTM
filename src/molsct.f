      SUBROUTINE MOLSCT(V,PFMOL0,PFMOL2,P2RAY)

!     MOLSCT RETURNS MOLECULAR SCATTERING PHASE FUNCTION SPECTRAL
!     PARAMETERS.  GIVEN THESE PARAMETERS, THE PHASE FUNCTION EQUALS

!                                               2
!     PHASE FUNCTION  =  PFMOL0  +  PFMOL2 * COS (SCATTERING ANGLE)

!     INPUT ARGUMENTS:
!       V        SPECTRAL FREQUENCY [CM-1].
      REAL V

!     OUTPUT ARGUMENTS:
!       PFMOL0   MOLECULAR PHASE FUNCTION ISOTROPIC SCATTERING TERM.
!       PFMOL2   MOLECULAR PHASE FUNCTION ANISOTROPIC SCATTERING TERM.
!       P2RAY    SECOND LEGENDRE EXPANSION COEFFICIENT OVER 5 (= 2N+1).
      REAL PFMOL0,PFMOL2,P2RAY

!     PARAMETERS:
!       DEPOL0   DEPOLARIZION SPECTRAL EXPANSION CONSTANT.
!       DEPOL2   DEPOLARIZION SPECTRAL EXPANSION QUADRATIC COEFFICIENT.
!       DEPOL4   DEPOLARIZION SPECTRAL EXPANSION QUARTIC COEFFICIENT.
!       C3O8PI   THREE OVER EIGHT PI.
      REAL DEPOL0,DEPOL2,DEPOL4,C3O8PI
      PARAMETER(DEPOL0=.0271323,DEPOL2=3.07929E-12,DEPOL4=1.62964E-21,  &
     &  C3O8PI=.1193662)

!     LOCAL VARIABLES:
!       V2       SPECTRAL FREQUENCY SQUARED [CM-2].
!       DEPOL    DEPOLARIZATION EQUAL TO THE RATIO OF POLARIZED
!                INTENSITIES AT A 90 DEGREE SCATTERING ANGLE.
!       DENOM    DEPOLARIZATION FACTOR DENOMINATOR.
!       PFMOL    RAYLEIGH SCATTERING PHASE FUNCTION TERM.
      REAL V2,DEPOL,DENOM,PFMOL

!     DEPOLARIZATION (SHETTLE@POAMB.NRL.NAVY.MIL, 1999):
      V2=V*V
      DEPOL=DEPOL0+V2*(DEPOL2+V2*DEPOL4)

!     SECOND LEGENDRE EXPANSION COEFFICIENT OVER 5 (= 2N+1):
      DENOM=2+DEPOL
      P2RAY=(1-DEPOL)/(5*DENOM)

!     PHASE FUNCTION COEFFICIENTS:
      PFMOL=C3O8PI/DENOM
      DEPOL=DEPOL*PFMOL
      PFMOL0=PFMOL+DEPOL
      PFMOL2=PFMOL-DEPOL
      RETURN
      END
