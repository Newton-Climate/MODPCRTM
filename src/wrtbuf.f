      SUBROUTINE WRTBUF(ERCODE)

!     THIS ROUTINE READS WARNINGS AND ERRORS FROM MODTRAN STANDARD
!     OUTPUT FILES AND STORES THEN IN THE ERROR BUFFER.

!     INPUT ARGUMENTS:
!       ERCODE    ERROR CODE (0=SUCCESS, 1=WARNING, 2=FATAL ERROR).
      INTEGER ERCODE

!     HEADER FILES:
      INCLUDE 'PARAMS.h'
      INCLUDE 'ERROR.h'
      INCLUDE 'JMASS.h'

!     COMMONS:
      INCLUDE 'IFIL.h'

!     /SUNFLG/
!       LNFLRT   LENGTH OF I/O FILE ROOT NAME, 0 IF NO mod5root.in FILE.
!       LRDSUN   SOLAR DATA FLAG, TRUE IF IRRADIANCES IN COMMON BLOCK
!                /SOL01/ HAVE BEEN MODIFIED FROM THE BLOCK DATA.
      INTEGER LNFLRT
      LOGICAL LRDSUN
      COMMON/SUNFLG/LNFLRT,LRDSUN

!     /SUNNAM/
!       SUNFIL   NAME OF DEFAULT FILE CONTAINING SOLAR IRRADIANCE DATA.
!       FLRT     ROOT NAME FOR ALL I/O FILES.
      CHARACTER FLRT*(NAMLEN-4),SUNFIL*(LENSUN)
      COMMON/SUNNAM/FLRT,SUNFIL

!     /CARD1/
!       MODEL    MODEL ATMOSPHERE INDEX.
!       ITYPE    SLANT PATH TYPE.
!       IEMSCT   RADIATIVE TRANSFER MODE.
!                  0 FOR TRANSMITTANCE
!                  1 FOR THERMAL EMISSION ONLY
!                  2 FOR THERMAL EMISSION PLUS SOLAR SCATTER
!                  3 FOR TRANSMITTED SOLAR IRRADIANCE
!                  4 FOR SOLAR SCATTER ONLY
!       M1       MODEL ATMOSPHERE FOR PRESSURE & TEMPERATURE PROFILES.
!       M2       MODEL ATMOSPHERE FOR H2O PROFILE.
!       M3       MODEL ATMOSPHERE FOR O3 PROFILE.
!       I_RD2C   READ CARD 2C, 2C1, ... IF EQUAL 1; SKIP IF EQUAL TO 0.
!       NOPRNT   PRINT FLAG.
!       MODTRN   MODTRAN BAND MODEL FLAG.
      INTEGER MODEL,ITYPE,IEMSCT,M1,M2,M3,I_RD2C,NOPRNT
      LOGICAL MODTRN
      COMMON/CARD1/MODEL,ITYPE,IEMSCT,M1,M2,M3,I_RD2C,NOPRNT,MODTRN

!     /JM1A1/
!       RESCHR   BAND MODEL RESOLUTION CHARACTER STRING.
!       DISSTR   CHARACTER STRING USED TO READ IN DISORT LOGICALS.
!       H2OSTR   VERTICAL WATER COLUMN CHARACTER STRING (IF THE
!                FIRST NON-BLANK CHARACTER IS A "G", THE WATER
!                COLUMN IN GM/CM2 FOLLOWS "G"; IF THE FIRST
!                NON-BLANK CHARACTER IS AN "A", THE WATER COLUMN
!                IN ATM-CM AT 273.15K FOLLOWS "A"; OTHERWISE THE
!                STRING CONTAINS A WATER COLUMN SCALING FACTOR).
!       O3STR    VERTICAL OZONE COLUMN CHARACTER STRING (IF THE
!                FIRST NON-BLANK CHARACTER IS A "G", THE OZONE
!                COLUMN IN GM/CM2 FOLLOWS "G"; IF THE FIRST
!                NON-BLANK CHARACTER IS AN "A", THE OZONE COLUMN
!                IN ATM-CM AT 273.15K FOLLOWS "A"; OTHERWISE THE
!                STRING CONTAINS AN OZONE COLUMN SCALING FACTOR).
!       USRSUN   USER-SPECIFIED TOP-OF-ATMOSPHERE SOLAR IRRADIANCE FILE.
!       BMROOT   PREFIX OF MOLECULAR BAND MODEL PARAMETERS FILE.
!       FILTNM   NAME OF FILTER RESPONSE FUNCTION FILE.
!       H2OAER   FLAG, TRUE IF DEFAULT AEROSOL PROPERTIES ARE REVISED
!                BASED ON WATER COLUMN SCALING.
!       DATDIR   NAME OF THE MODTRAN DATA DIRECTORY.
      CHARACTER RESCHR*2,DISSTR*3,H2OSTR*10,O3STR*10,USRSUN*(NAMLEN),   &
     &  FILTNM*(NAMLEN),BMROOT*(NAMLEN),H2OAER*1,DATDIR*(NAMLEN-LENSUN)
      COMMON/JM1A1/RESCHR,DISSTR,H2OSTR,O3STR,USRSUN,                   &
     &  FILTNM,BMROOT,H2OAER,DATDIR

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      SAVE /SUNFLG/,/SUNNAM/
      EXTERNAL DEVCBD

!     FUNCTIONS:
!       NUNIT    RETURNS AN UNUSED FILE UNIT NUMBER.
!       LENSTR   RETURNS STRING LENGTH AFTER TRIMMING LEADING BLANKS.
      INTEGER NUNIT,LENSTR

!     LOCAL VARIABLES:
!       IOS      STATUS OF READ.
!       NBUG     UNIT NUMBER OF DEBUG OUTPUT FILE.
!       ICOUNT   SPECTRAL FREQUENCY COUNTER.
!       LENDAT   LENGTH OF DATDIR STRING.
!       LOPEN    OPEN FILE FLAG.
!       DFTSUN   DEFAULT TOP-OF-ATMOSPHERE SOLAR IRRADIANCE FILE.
      INTEGER IOS,NBUG,ICOUNT,LENDAT
      LOGICAL LOPEN
      CHARACTER DFTSUN*(NAMLEN)

!     DATA:
!       SVSTAT   FILE SAVE STATUS, SET TO 'KEEP' FOR DEBUGGING.
      CHARACTER SVSTAT*6

!NORM
!NORM DATA SVSTAT/'DELETE'/
!BUG
      DATA SVSTAT/'KEEP'/

!     CHECK IF SOLAR BLOCK DATA HAS BEEN MODIFIED:
      IF(LRDSUN)THEN

!         SOLAR IRRADIANCE BLOCK DATA WAS OVER-WRITTEN IN PREVIOUS
!         RUN.  THE ORIGINAL IS NOW NEEDED AND MUST BE REGENERATED.
          LENDAT=LENSTR(DATDIR)
          DFTSUN=DATDIR(1:LENDAT)//SUNFIL
          USRSUN='6'
          CALL RDSUN(1.,DFTSUN,USRSUN,0.,LNFLRT,FLRT)
          LRDSUN=.FALSE.
      ENDIF

!     REWIND STANDARD OUTPUT AND READ IN EACH LINE:
      INQUIRE(IPR,OPENED=LOPEN)
      IF(.NOT.LOPEN)RETURN
      REWIND(IPR)
      READ(IPR,'((A512))',IOSTAT=IOS)(ERRORBUF(LSTERR),LSTERR=0,MAXERR)
      IF(IOS.EQ.0)THEN

!         END-OF-FILE WAS NEVER REACHED:
          LSTERR=MAXERR
          WRITE(ERRORBUF(LSTERR)(1:98),'(A37,I4,A57)')                  &
     &      ' WARNING from WRTBUF:  Only the first',LSTERR,             &
     &      ' lines of messages from modtran are included in ERRORBUF.'
          ERRORBUF(LSTERR)(99:512)=' '
          ERRORCODE=MAX(ERRORCODE,WARNING)
      ELSEIF(IOS.LT.0)THEN

!         END-OF-FILE REACHED:
          IF(LSTERR.LE.0)THEN

!             OUTPUT FILE IS EMPTY:
              LSTERR=-1
              ERRORCODE=ERCODE
          ELSE
              LSTERR=LSTERR-1
              ERRORCODE=MAX(WARNING,ERCODE)
          ENDIF
      ELSE

!         ERROR READING STANDARD OUTPUT:
          WRITE(ERRORBUF(LSTERR)(1:75),'(A38,I4,A33)')                  &
     &      ' Error in WRTBUF:  Unable to read line',LSTERR,            &
     &      ' of MODTRAN standard output file.'
          ERRORBUF(LSTERR)(76:512)=' '

!         RETURN WITHOUT DELETING MODTRAN FILES:
          ERRORCODE=FATAL
          RETURN
      ENDIF

!     DELETE ALL OPEN MODTRAN I/O FILES AND CLOSE DATA/SCRATCH FILES:
      INQUIRE(IRD,OPENED=LOPEN)
      IF(LOPEN)CLOSE(IRD,STATUS=SVSTAT)
      INQUIRE(IPR,OPENED=LOPEN)
      IF(LOPEN)CLOSE(IPR,STATUS=SVSTAT)
      INQUIRE(IPU,OPENED=LOPEN)
      IF(LOPEN)CLOSE(IPU,STATUS=SVSTAT)
      INQUIRE(IPR1,OPENED=LOPEN)
      IF(LOPEN)CLOSE(IPR1,STATUS=SVSTAT)
      INQUIRE(ISCRCH,OPENED=LOPEN)
      IF(LOPEN)CLOSE(ISCRCH)
      INQUIRE(IPLOT,OPENED=LOPEN)
      IF(LOPEN)CLOSE(IPLOT,STATUS=SVSTAT)
      INQUIRE(IPUSCN,OPENED=LOPEN)
      IF(LOPEN)CLOSE(IPUSCN,STATUS=SVSTAT)
      INQUIRE(IPTSCN,OPENED=LOPEN)
      IF(LOPEN)CLOSE(IPTSCN,STATUS=SVSTAT)
      INQUIRE(IFLUX,OPENED=LOPEN)
      IF(LOPEN)CLOSE(IFLUX,STATUS=SVSTAT)
      INQUIRE(ICR,OPENED=LOPEN)
      IF(LOPEN)CLOSE(ICR,STATUS=SVSTAT)
      INQUIRE(IDBOUT,OPENED=LOPEN)
      IF(LOPEN)CLOSE(IDBOUT,STATUS=SVSTAT)

!     BIN OUTFILES
      INQUIRE(JPU,OPENED=LOPEN)
      IF(LOPEN)CLOSE(JPU,STATUS=SVSTAT)
      INQUIRE(JPR1,OPENED=LOPEN)
      IF(LOPEN)CLOSE(JPR1,STATUS=SVSTAT)
      INQUIRE(JPLOT,OPENED=LOPEN)
      IF(LOPEN)CLOSE(JPLOT,STATUS=SVSTAT)
      INQUIRE(JPUSCR,OPENED=LOPEN)
      IF(LOPEN)CLOSE(JPUSCR,STATUS=SVSTAT)

!     WRITE OUT COMMON BLOCK DATA AND STOP IF DEBUG OPTION IS ON:
      IF(SVSTAT.EQ.'KEEP')THEN

!         DEBUG OPTION IS ON.  OPEN DEBUG DATA FILE.
          NBUG=NUNIT()
          CALL OPNFL(NBUG,0,'DEBUG.DAT','UNKNOWN','FORMATTED','WRTBUF')

!         WRITE TWO COLUMN PLOT DATA:
          IF(IEMSCT.EQ.0)THEN

!             FREQUENCY [CM-1] VERSUS SPECTRAL TRANSMITTANCE:
              WRITE(NBUG,'((0PF15.0,F15.8))')(FREQS(ICOUNT),            &
     &          TOTTRANS(ICOUNT),ICOUNT=1,OUTPUTCOUNT)
          ELSEIF(IEMSCT.EQ.3)THEN

!             FREQUENCY [CM-1] VERSUS TRANSMITTED SOLAR
!             SPECTRAL IRRADIANCE [W CM-2 / CM-1]:
              WRITE(NBUG,'((0PF15.0,1PE15.5))')(FREQS(ICOUNT),          &
     &          TRANSIRRADCM(ICOUNT),ICOUNT=1,OUTPUTCOUNT)
          ELSE

!             FREQUENCY [CM-1] VERSUS TOTAL SPECTRAL
!             RADIANCE [W CM-2 SR-1 / CM-1].
              WRITE(NBUG,'((0PF15.0,1PE15.5))')(FREQS(ICOUNT),          &
     &          TRADCM(ICOUNT),ICOUNT=1,OUTPUTCOUNT)
          ENDIF
          CLOSE(NBUG,STATUS='KEEP')
          STOP ' Error in WRTBUF:  Debug option is on.'
      ENDIF
      RETURN
      END
