      SUBROUTINE SEG5MS(VBAND5,ISEG,IPATH,P2RAY,WPATH,XSAP,ASAP)

!     SEG5MS DEFINES THE SEGMENT DEPENDENT 5 CM-1 SPECTRAL
!     GRID DATA FOR MULTIPLE SCATTERING CALCULATION PATHS.
      IMPLICIT NONE

!     PARAMETERS:
!       C2       SECOND RADIATION CONSTANT [CM K].
!       T296     HITRAN STANDARD TEMPERATURE [K].
!       TDIF     TEMPERATURE RANGE OF H2O SELF CONTINUUM DATA [K].
!       C2D      C2 OVER TDIF [CM].
!       T296D    T296 OVER TDIF.
      REAL C2,T296,TDIF,C2D,T296D
      PARAMETER(C2=1.438786,T296=296.,TDIF=T296-260.,                   &
     &  C2D=C2/TDIF,T296D=T296/TDIF)
      INCLUDE 'PARAMS.h'

!     INPUT ARGUMENTS:
!       VBAND5   5 CM-1 RESOLUTION DATA SPECTRAL FREQUENCY [CM-1].
!       ISEG     PATH SEGMENT INDEX.
!       IPATH    PATH TYPE SWITCH.
!       P2RAY    SECOND LEGENDRE EXPANSION COEFFICIENT OVER 5 (= 2N+1).
!       WPATH    PATH COLUMN AMOUNTS:
!       XSAP     AEROSOL SPECTRAL EXTINCTION OPTICAL DEPTHS.
!       ASAP     AEROSOL SPECTRAL ABSORPTION OPTICAL DEPTHS.
!       TRANSM   FLAG (TRUE FOR TRANSMITTANCE ONLY CALCULATIONS).
!       MSPATH   FLAG (TRUE FOR VERTICAL MULTIPLE SCATTERING PATH.
      INTEGER ISEG,IPATH
      REAL VBAND5,P2RAY,WPATH(0:*),XSAP(*),ASAP(*)

!     COMMONS:
      INCLUDE 'BASE.h'
      INCLUDE 'SEGDAT.h'
      INCLUDE 'WSOL.h'

!     /RCNSTN/
!       PI       THE CONSTANT PI
!       DEG      NUMBER OF DEGREES IN ONE RADIAN.
!       BIGNUM   MAXIMUM SINGLE PRECISION NUMBER.
!       BIGEXP   MAXIMUM EXPONENTIAL ARGUMENT WITHOUT OVERFLOW.
!       RRIGHT   SMALLEST SINGLE PRECISION REAL ADDED TO 1 EXCEEDS 1.
      REAL PI,DEG,BIGNUM,BIGEXP,RRIGHT
      COMMON/RCNSTN/PI,DEG,BIGNUM,BIGEXP,RRIGHT

!     /CARD1/
!       MODEL    MODEL ATMOSPHERE INDEX.
!       ITYPE    SLANT PATH TYPE.
!       IEMSCT   RADIATIVE TRANSFER MODE.
!                  0 FOR TRANSMITTANCE
!                  1 FOR THERMAL EMISSION ONLY
!                  2 FOR THERMAL EMISSION PLUS SOLAR SCATTER
!                  3 FOR TRANSMITTED SOLAR IRRADIANCE
!                  4 FOR SOLAR SCATTER ONLY
!       M1       MODEL ATMOSPHERE FOR PRESSURE & TEMPERATURE PROFILES.
!       M2       MODEL ATMOSPHERE FOR H2O PROFILE.
!       M3       MODEL ATMOSPHERE FOR O3 PROFILE.
!       I_RD2C   READ CARD 2C, 2C1, ... IF EQUAL 1; SKIP IF EQUAL TO 0.
!       NOPRNT   PRINT FLAG.
!       MODTRN   MODTRAN BAND MODEL FLAG.
      INTEGER MODEL,ITYPE,IEMSCT,M1,M2,M3,I_RD2C,NOPRNT
      LOGICAL MODTRN
      COMMON/CARD1/MODEL,ITYPE,IEMSCT,M1,M2,M3,I_RD2C,NOPRNT,MODTRN

!     /CARD2/
!       IHAZE    BOUNDARY LAYER AEROSOL MODEL NUMBER.
!       ISEASN   SEASON NUMBER (1=SPRING-SUMMER, 2=FALL-WINTER).
!       IVULCN   VOLCANIC AEROSOL MODEL NUMBER.
!       ICSTL    COASTAL AIRMASS MODEL NUMBER.
!       ICLD     CLOUD MODEL NUMBER.
!       IVSA     VERTICAL STRUCTURE ALGORITHM (0=OFF, 1=ON).
!       VIS      SURFACE VISIBILITY (GROUND METEOROLOGICAL RANGE) [KM].
!       WSS      CURRENT WIND SPEED (M/S).
!       WHH      24-HOUR WIND SPEED (M/S).
!       RAINRT   RAIN RATE (MM/HR)
!       LSAP     LOGICAL FLAG FOR SPECTRAL AEROSOL PROFILES INPUT.
      INTEGER IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA
      REAL VIS,WSS,WHH,RAINRT
      LOGICAL LSAP
      COMMON/CARD2/IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA,                 &
     &  VIS,WSS,WHH,RAINRT,LSAP

!     /JM5/
!       IRPT     REPEAT INPUT FLAG (0=NONE, 1=ALL, 3=GEOM, 4=SPEC).
!       IFAC     CURRENT COLUMN SCALING FACTOR INDEX.
!       NFACMN   NUMBER OF COLUMN SCALING FACTOR LESS THAN 1.
!       NFACMX   NUMBER OF COLUMN SCALING FACTOR GREATER THAN 1.
!       FACMC    CURRENT COLUMN SCALING FACTOR.
!       SCALMN   MINIMUM COLUMN SCALING FACTOR.
!       SCALMX   MAXIMUM COLUMN SCALING FACTOR.
!       LBMRES   LOGICAL FLAG, .TRUE. FOR BAND MODEL AND .FALSE.
!                FOR 1 NM SPECTRAL RESOLUTION OUTPUT.
      INTEGER IRPT,IFAC,NFACMN,NFACMX
      REAL FACMC
      DOUBLE PRECISION SCALMN,SCALMX
      LOGICAL LBMRES
      COMMON/JM5/SCALMN,SCALMX,IRPT,IFAC,NFACMN,NFACMX,FACMC,LBMRES

!     /AER/ THERE ARE "MAER=17" PARTICULATE COMPONENTS:
!           1       AEROSOL 1 (NOMINALLY, BOUNDARY LAYER AEROSOL).
!           2       AEROSOL 2 (NOMINALLY, TROPOSPHERIC AEROSOL).
!           3       AEROSOL 3 (NOMINALLY, STRATOSPHERIC AEROSOL).
!           4       AEROSOL 4 (NOMINALLY, VOLCANIC AEROSOL).
!           5       CIRRUS CLOUD.
!           6       CLOUD 1 (NOMINALLY, WATER CLOUD).
!           7       CLOUD 2 (NOMINALLY, ICE CLOUD).
!           8-17    NOVAM (NAVY OCEANIC VERTICAL AEROSOL MODEL) LAYERS.
!       NAER     NUMBER OF ACTIVE AEROSOLS.
!       EXTV     SPECTRAL EXTINCTION (NORMALIZED TO 1 AT 550 NM).
!       ABSV     SPECTRAL ABSORPTION (1-ABSV/EXTV=SCATTERING ALBEDO).
!       ASYV     SPECTRAL LEGENDRE MOMENT (DIVIDED BY 2N+1).
!       FRAC5    5 CM-1 GRID SPECTRAL INTERPOLATION FRACTION.
!       ASYVLO   ASYMMETRY FACTOR FROM PREVIOUS SPECTRAL FREQUENCY.
      INTEGER NAER
      REAL EXTV,ABSV,ASYV,FRAC5,ASYVLO
      COMMON/AER/NAER,EXTV(MAER),ABSV(MAER),ASYV(MXCMU,MAER),           &
     &  FRAC5,ASYVLO(MAER)
      INTEGER IBND
      REAL AA,BB,CC,QA,CPS
      COMMON/AABBCC/AA(11),BB(11),CC(11),IBND(11),QA(11),CPS(11)

!     /FRQ5/
!       O2C127   O2 1.27 MICRON BAND CONTINUUM ABSORPTION.
!       O2C106   O2 1.06 MICRON BAND CONTINUUM ABSORPTION.
!       O2_O2V   O2 VISIBLE ABSORBANCE (89.5CM, 296K, 55ATM).
!       A_CH4    CH4 VIS/NIR ABSORPTION COEFFICIENT [CM-1 / ATM].
      REAL SIGO20,SIGO2A,SIGO2B,O3T0,O3T1,O3T2,RAYSCT,N2CONT,SH2OT0,    &
     &  SH2OT,FH2O,CHNO3,O2HERZ,O2C127,O2C106,O2_O2V,CRSNO2,CRSSO2,A_CH4
      COMMON/FRQ5/SIGO20,SIGO2A,SIGO2B,O3T0,O3T1,O3T2,                  &
     &  RAYSCT,N2CONT,SH2OT0,SH2OT,FH2O,CHNO3,O2HERZ,                   &
     &  O2C127,O2C106,O2_O2V,CRSNO2,CRSSO2,A_CH4

!     /SEG5DT/
!       SPCHI    LINE-OF-SIGHT SPECTRAL DATA @ HIGHER 5 CM-1 GRID POINT.
!       SPCMHI   VERTICAL PATH SPECTRAL DATA @ HIGHER 5 CM-1 GRID POINT.
!       SPCLO    LINE-OF-SIGHT SPECTRAL DATA @ LOWER 5 CM-1 GRID POINT.
!       SPCMLO   VERTICAL PATH SPECTRAL DATA @ LOWER 5 CM-1 GRID POINT.
!            1   SEGMENT SCATTERING OD WEIGHTED ASYMMETRY FACTOR
!            2   SEGMENT AEROSOL SCATTERING OPTICAL DEPTH
!            3   TOTAL O2 CONTINUUM TRANSMITTANCE.
!            4   N2 CONTINUUM TRANSMITTANCE.
!            5   TOTAL H2O CONTINUUM TRANSMITTANCE.
!            6   RAYLEIGH MOLECULAR SCATTERED TRANSMITTANCE.
!            7   TRANSMITTANCE FROM AEROSOL EXTINCTION.
!            8   TOTAL OZONE CONTINUUM TRANSMITTANCE.
!            9   ALL CONTINUUM TRANSMITTANCES EXCEPT O2 AND HNO3.
!           10   TRANSMITTANCE FROM AEROSOL ABSORPTION.
!           11   HNO3 TRANSMITTANCE.
!           12   MOLECULAR CONTINUUM OPTICAL DEPTH
!           13   SEGMENT AEROSOL EXTINCTION OPTICAL DEPTH
!           14   COMBINED SPECIES INCREMENTAL CONTINUUM OPTICAL DEPTH
!           15   RAYLEIGH MOLECULAR SCATTERING OPTICAL DEPTH
!           16   CIRRUS CLOUD TRANSMITTANCE (ICLD=20 ONLY)
!           17   UV/VIS NO2 TRANSMISSION
!           18   UV/VIS SO2 TRANSMISSION
!           19   SEGMENT WATER DROPLET SCATTERING OPTICAL DEPTH
!           20   SEGMENT ICE PARTICLE SCATTERING OPTICAL DEPTH
!           21   SEGMENT BOUNDARY LAYER AEROSOL SCATTERING OPTICAL DEPTH
!           22   SEGMENT TROPOSPHERIC AEROSOL SCATTERING OPTICAL DEPTH
!           23   SEGMENT STRATOSPHERIC AEROSOL SCATTERING OPTICAL DEPTH
!           24   SEGMENT MESOSPHERIC+ AEROSOL SCATTERING OPTICAL DEPTH
!           25   SEGMENT NOVAM AEROSOL SCATTERING OPTICAL DEPTH
!           26   SEGMENT NOVAM AEROSOL ASYMMETRY FACTOR
!           27   SEGMENT STD OR SUB-VIS CIRRUS SCATTERING OPTICAL DEPTH
!           28   SEGMENT CLOUD EXTINCTION OPTICAL DEPTH
!           29   SEGMENT CLOUD SCATTERING OD WEIGHTED ASYMMETRY FACTOR
!           30   SEGMENT RAIN EXTINCTION OPTICAL DEPTH
!           31   SEGMENT RAIN SCATTERING OPTICAL DEPTH
!           32   SEGMENT RAIN ASYMMETRY FACTOR
!           33   "14" WITHOUT RAIN, CLOUD AND AEROSOL.
!      NSEG5-2   VIS/NIR CH4 OPTICAL DEPTH
!      NSEG5-1   H2-H2 DIMER OPTICAL DEPTH
!        NSEG5   H2-HE DIMER OPTICAL DEPTH
!       SSAPH    LOS SAP SCATTERING OPTICAL DEPTH @ HIGHER 5 CM-1 POINT.
!       SSAPHM   MS SAP SCATTERING OPTICAL DEPTH @ HIGHER 5 CM-1 POINT.
!       SSAPL    LOS SAP SCATTERING OPTICAL DEPTH @ LOWER 5 CM-1 POINT.
!       SSAPLM   MS SAP SCATTERING OPTICAL DEPTH @ LOWER 5 CM-1 POINT.
!       TXLEG    SPECTRALLY INTERPOLATED LEGENDRE COEFFICIENTS FOR THE
!                SCATTERING PHASE FUNCTION (OPTICAL DEPTH WEIGHTED SUM).
      REAL SPCHI,SPCMHI,SPCLO,SPCMLO,SSAPH,SSAPHM,SSAPL,SSAPLM,TXLEG
      COMMON/SEG5DT/SPCHI(NSEG5,LAYTWO,MLOS,3),SPCMHI(NSEG5,LAYDIM,3),  &
     &  SPCLO(NSEG5,LAYTWO,MLOS,3),SPCMLO(NSEG5,LAYDIM,3),              &
     &  SSAPH(LAYTWO,MLOS,3),SSAPHM(LAYDIM,3),SSAPL(LAYTWO,MLOS,3),     &
     &  SSAPLM(LAYDIM,3),TXLEG(2:MXCMU,1:LAYDIM,0:1)
      SAVE/SEG5DT/

!     /DISRT/
!       UMU      MONOTONICALLY INCREASING LIST OF DISTINCT USER-PATH
!                COSINE POLAR ANGLES.
!       PHI      MONOTONICALLY INCREASING LIST OF DISTINCT RELATIVE
!                SOLAR AZIMUTH ANGLES [0 TO 180 DEG].
!       NSTR     NUMBER OF DISCRETE ORDINATE STREAMS.
!       NAZ      NUMBER OF DISORT AZIMUTH COMPONENTS.
!       N2GAUS   ORDER OF DOUBLE-GAUSS QUADRATURES.
!       NUMU     NUMBER OF DISTINCT USER LINE-OF-SIGHT POLAR ANGLES.
!       MAPUMU   MAPPING FROM LINE-OF-SIGHT INDEX TO UMU ARRAY ENTRY.
!       NPHI     NUMBER OF DISTINCT RELATIVE SOLAR AZIMUTH ANGLES.
!       MAPPHI   MAPPING FROM LINE-OF-SIGHT INDEX TO PHI ARRAY ENTRY.
!       DIS      LOGICAL FLAG, TRUE FOR DISORT MULTIPLE SCATTERING.
!       DISAZM   LOGICAL FLAG, TRUE FOR DISORT WITH AZIMUTH DEPENDENCE.
!       DISALB   LOGICAL FLAG, TRUE FOR DISORT SPHERICAL ALBEDO OPTION.
!       LDISCL   LOGICAL FLAG, TRUE FOR ISAACS SCALED TO DISORT.
      REAL UMU,PHI
      INTEGER NSTR,NAZ,N2GAUS,NUMU,MAPUMU,NPHI,MAPPHI
      LOGICAL DIS,DISAZM,DISALB,LDISCL
      COMMON/DISRT/UMU(MXUMU),PHI(MAXPHI),NSTR,NAZ,N2GAUS,NUMU,         &
     &  MAPUMU(MLOS),NPHI,MAPPHI(MLOS),DIS,DISAZM,DISALB,LDISCL
      SAVE /DISRT/

!     /SAP/
!       NWVSAP   NUMBER OF AEROSOL SPECTRAL GRID POINTS.
!       NLGSAP   HIGHEST AEROSOL PHASE FUNCTION LEGENDRE MOMENT.
!       NANSAP   NUMBER OF AEROSOL PHASE FUNCTION ANGULAR POINTS.
!       LEVSAP   NUMBER OF AEROSOL ATMOSPHERIC LEVELS.
!       ANGSAP   AEROSOL PHASE FUNCTION ANGULAR GRID [DEGREES].
!       COSSAP   COSINE OF THE AEROSOL PHASE FUNCTION ANGLES.
!       WAVSAP   AEROSOL SPECTRAL GRID [MICRONS].
!       LEGSAP   AEROSOL LEGENDRE COEFFICIENTS.
!       PFSAP    AEROSOL PHASE FUNCTION [SR-1].
!       LOSSAP   LOGICAL FLAG, TRUE FOR LINE-OF-SIGHT PATH.
      INTEGER NWVSAP,NLGSAP,NANSAP,LEVSAP
      DOUBLE PRECISION ANGSAP,COSSAP,PFSAP
      REAL WAVSAP,LEGSAP
      LOGICAL LOSSAP
      COMMON/SAP/NWVSAP,NLGSAP,NANSAP,LEVSAP,ANGSAP(MANSAP),            &
     &  COSSAP(MANSAP),WAVSAP(MWVSAP),LEGSAP(MLGSAP,MWVSAP,LAYDIM),     &
     &  PFSAP(MANSAP,MWVSAP,LAYDIM),LOSSAP

!     /SAPV/
!       ILOSAP   SAP DATA LOWER WAVELENGTH INDEX AT CURRENT FREQUENCY.
!       IHISAP   SAP DATA HIGHER WAVELENGTH INDEX AT CURRENT FREQUENCY.
!       FRCSAP   SAP DATA INTERPOLATION FRACTION AT CURRENT FREQUENCY.
      INTEGER ILOSAP,IHISAP
      REAL FRCSAP
      COMMON/SAPV/ILOSAP,IHISAP,FRCSAP

!     LOCAL VARIABLES:
!       ISTR     LOOP INDEX FOR MULTIPLE SCATTERING STREAMS.
!       LEVEL    LEVEL INDEX FOR CURRENT PATH BOUNDARY
!                (LEVEL EQUALS ONE FOR THE PATH MINIMUM ALTITUDE).
!       WRAT     TEMPERATURE INTERPOLATION FACTOR.
!       EXPON    EXPONENTIAL USED IN RADIATION FIELD CALCULATION.
!       GLEG     LEGENDRE MOMENT EXPANSION VARIABLE FOR PHASE FUNCTIONS.
!       EXTSAP   EXTINCTION OPTICAL DEPTH OF SPECTRAL AEROSOL PROFILES.
!       ABSSAP   ABSORPTION OPTICAL DEPTH OF SPECTRAL AEROSOL PROFILES.
!       SCTSAP   SCATTERING OPTICAL DEPTH OF SPECTRAL AEROSOL PROFILES.
!       BEGMOM   PHASE FUNCTION LEGENDRE MOMENT AT BEGINNING OF SEGMENT.
!       ENDMOM   PHASE FUNCTION LEGENDRE MOMENT AT END OF SEGMENT.
      INTEGER IAER,K,ISTR,LEVEL
      REAL ABSSM,SCTSM,ASYMSM,EXTSM,XRAIN,ARAIN,SRAIN,GRAIN,SCTTRM(8),  &
     &  WRAT,EXPON,GLEG,EXTSAP,ABSSAP,SCTSAP,BEGMOM,ENDMOM

!     FUNCTIONS:
!       EXPINT   EXPONENTIAL INTERPOLATION.
!       H2H2DT   H2-H2 DIMER COLLISION INDUCED ABSORPTION [CM-1/ATM^2].
!       H2HEDT   H2-HE DIMER COLLISION INDUCED ABSORPTION [CM-1/ATM^2].
      REAL EXPINT,DBLTX,H2H2DT,H2HEDT

!     RAYLEIGH OPTICAL DEPTH:
      TX(6)=RAYSCT*WPATH(6)

!     CONTRIBUTION OF RAIN TO EXTINCTION, SCATTERING AND ABSORPTION
      IF(WPATH(3).LE.0.)THEN
          XRAIN=0.
          ARAIN=0.
          SRAIN=0.
          GRAIN=0.
      ELSE
          CALL RAIN(VBAND5,WPATH,XRAIN,ARAIN,SRAIN,GRAIN)
      ENDIF

!     CALCULATE INCREMENTAL OPTICAL DEPTH VARIABLES
      IF(IPATH.EQ.3)THEN

!         VERTICAL MULTIPLE SCATTERING PATH:  RAIN OPTICAL DATA.
          SPCMLO(30,ISEG,3)=SPCMHI(30,ISEG,3)
          SPCMHI(30,ISEG,3)=XRAIN
          SPCMLO(31,ISEG,3)=SPCMHI(31,ISEG,3)
          SPCMHI(31,ISEG,3)=SRAIN
          SPCMLO(32,ISEG,3)=SPCMHI(32,ISEG,3)
          SPCMHI(32,ISEG,3)=GRAIN

!         BOUNDARY LAYER AEROSOL (1):
          SCTTRM(1)=WPATH(7)*(EXTV(1)-ABSV(1))
          ASYMSM=SCTTRM(1)*ASYV(1,1)
          SCTSM=SCTTRM(1)
          EXTSM=WPATH(7)*EXTV(1)
          ABSSM=ARAIN+WPATH(7)*ABSV(1)
          SPCMLO(21,ISEG,3)=SPCMHI(21,ISEG,3)
          SPCMHI(21,ISEG,3)=SCTTRM(1)

!         TROPOSPHERIC AEROSOL (2):
          SCTTRM(2)=WPATH(12)*(EXTV(2)-ABSV(2))
          ASYMSM=ASYMSM+SCTTRM(2)*ASYV(1,2)
          SCTSM=SCTSM+SCTTRM(2)
          EXTSM=EXTSM+WPATH(12)*EXTV(2)
          ABSSM=ABSSM+WPATH(12)*ABSV(2)
          SPCMLO(22,ISEG,3)=SPCMHI(22,ISEG,3)
          SPCMHI(22,ISEG,3)=SCTTRM(2)

!         STRATOSPHERIC AEROSOL (3):
          SCTTRM(3)=WPATH(13)*(EXTV(3)-ABSV(3))
          ASYMSM=ASYMSM+SCTTRM(3)*ASYV(1,3)
          SCTSM=SCTSM+SCTTRM(3)
          EXTSM=EXTSM+WPATH(13)*EXTV(3)
          ABSSM=ABSSM+WPATH(13)*ABSV(3)
          SPCMLO(23,ISEG,3)=SPCMHI(23,ISEG,3)
          SPCMHI(23,ISEG,3)=SCTTRM(3)

!         VOLCANIC AEROSOL (4):
          SCTTRM(4)=WPATH(14)*(EXTV(4)-ABSV(4))
          ASYMSM=ASYMSM+SCTTRM(4)*ASYV(1,4)
          SCTSM=SCTSM+SCTTRM(4)
          EXTSM=EXTSM+WPATH(14)*EXTV(4)
          ABSSM=ABSSM+WPATH(14)*ABSV(4)
          SPCMLO(24,ISEG,3)=SPCMHI(24,ISEG,3)
          SPCMHI(24,ISEG,3)=SCTTRM(4)

!         STANDARD OR SUB-VISUAL CIRRUS MODEL (5):
          ABSSM=ABSSM+WPATH(16)*ABSV(5)
          SCTTRM(5)=WPATH(16)*(EXTV(5)-ABSV(5))
          SPCMLO(27,ISEG,3)=SPCMHI(27,ISEG,3)
          SPCMHI(27,ISEG,3)=SCTTRM(5)
          SPCMLO(28,ISEG,3)=SPCMHI(28,ISEG,3)
          SPCMHI(28,ISEG,3)=WPATH(16)*EXTV(5)
          SPCMLO(29,ISEG,3)=SPCMHI(29,ISEG,3)
          SPCMHI(29,ISEG,3)=SCTTRM(5)*ASYV(1,5)

!         WATER DROPLET CLOUD (6):
          ABSSM=ABSSM+WPATH(66)*ABSV(6)
          SCTTRM(6)=WPATH(66)*(EXTV(6)-ABSV(6))
          SPCMLO(19,ISEG,3)=SPCMHI(19,ISEG,3)
          SPCMHI(19,ISEG,3)=SCTTRM(6)
          SPCMHI(28,ISEG,3)=SPCMHI(28,ISEG,3)+WPATH(66)*EXTV(6)
          SPCMHI(29,ISEG,3)=SPCMHI(29,ISEG,3)+SCTTRM(6)*ASYV(1,6)

!         ICE PARTICLE CLOUD (7):
          SCTTRM(7)=WPATH(67)*(EXTV(7)-ABSV(7))
          SPCMLO(20,ISEG,3)=SPCMHI(20,ISEG,3)
          SPCMHI(20,ISEG,3)=SCTTRM(7)
          ABSSM=ABSSM+WPATH(67)*ABSV(7)
          SPCMHI(28,ISEG,3)=SPCMHI(28,ISEG,3)+WPATH(67)*EXTV(7)
          SPCMHI(29,ISEG,3)=SPCMHI(29,ISEG,3)+SCTTRM(7)*ASYV(1,7)

!         SPECTRAL AEROSOL PROFILES (SAP) DATA:
          IF(LSAP)THEN
              EXTSAP=EXPINT(XSAP(ILOSAP),XSAP(IHISAP),FRCSAP)
              ABSSAP=EXPINT(ASAP(ILOSAP),ASAP(IHISAP),FRCSAP)

!             ASYMMETRY FACTOR AT BOTTOM OF LAYER FOR MS PATH:
!***********VINCENT ROSS CHANGED TO SOLVE BUG***************************
!VR              LEVEL=ISEG-LAYTWO
!don't need to remove LAYTWO factor since MSOFF not applied any more
              LEVEL = ISEG
!**************END VINCENT ROSS*****************************************              
              BEGMOM=LEGSAP(1,ILOSAP,LEVEL)                             &
     &          +FRCSAP*(LEGSAP(1,IHISAP,LEVEL)-LEGSAP(1,ILOSAP,LEVEL))

!             ASYMMETRY FACTOR AT TOP OF LAYER FOR MS PATH:
              LEVEL=LEVEL+1
              ENDMOM=LEGSAP(1,ILOSAP,LEVEL)                             &
     &          +FRCSAP*(LEGSAP(1,IHISAP,LEVEL)-LEGSAP(1,ILOSAP,LEVEL))
              SCTSAP=EXTSAP-ABSSAP
              ASYMSM=ASYMSM+SCTSAP*(BEGMOM+ENDMOM)/2
              SCTSM=SCTSM+SCTSAP
              EXTSM=EXTSM+EXTSAP
              ABSSM=ABSSM+ABSSAP
              SSAPLM(ISEG,3)=SSAPHM(ISEG,3)
              SSAPHM(ISEG,3)=SCTSAP
          ENDIF

!         PHASE FUNCTION LEGENDRE MOMENTS (SCATTERING DEPTH WEIGHTED):
          GLEG=SRAIN*GRAIN
          DO ISTR=2,NSTR
              TXLEG(ISTR,ISEG,0)=TXLEG(ISTR,ISEG,1)
              GLEG=GLEG*GRAIN
              TXLEG(ISTR,ISEG,1)=  GLEG+SCTTRM(1)*ASYV(ISTR,1)          &
     &          +SCTTRM(2)*ASYV(ISTR,2)+SCTTRM(3)*ASYV(ISTR,3)          &
     &          +SCTTRM(4)*ASYV(ISTR,4)+SCTTRM(5)*ASYV(ISTR,5)          &
     &          +SCTTRM(6)*ASYV(ISTR,6)+SCTTRM(7)*ASYV(ISTR,7)
          ENDDO

!         ADD RAYLEIGH SCATTERING CONTRIBUTION TO THE SECOND MOMENT:
          TXLEG(2,ISEG,1)=TXLEG(2,ISEG,1)+TX(6)*P2RAY

!         ADD SPECTRAL AEROSOL PROFILES VALUES:
          IF(LSAP)THEN
              DO ISTR=2,NSTR

!                 LEGENDRE MOMENT AT BOTTOM OF LAYER:
                  BEGMOM=LEGSAP(ISTR,ILOSAP,ISEG)+FRCSAP                &
     &              *(LEGSAP(ISTR,IHISAP,ISEG)-LEGSAP(ISTR,ILOSAP,ISEG))

!                 LEGENDRE MOMENT AT TOP OF LAYER:
                  ENDMOM=LEGSAP(ISTR,ILOSAP,ISEG+1)                     &
     &              +FRCSAP*(LEGSAP(ISTR,IHISAP,ISEG+1)                 &
     &                      -LEGSAP(ISTR,ILOSAP,ISEG+1))
                  TXLEG(ISTR,ISEG,1)                                    &
     &              =TXLEG(ISTR,ISEG,1)+SCTSAP*(BEGMOM+ENDMOM)/2
              ENDDO
          ENDIF

!         NOVAM AEROSOL (IAER=8-17):
          IF(NAER.GT.MAER-10)THEN
              SPCMLO(25,ISEG,3)=SPCMHI(25,ISEG,3)
              SPCMHI(25,ISEG,3)=0.
              SPCMLO(26,ISEG,3)=SPCMHI(26,ISEG,3)
              SPCMHI(26,ISEG,3)=0.
              DO IAER=8,NAER
                  SCTTRM(8)=WPATH(60+IAER)*(EXTV(IAER)-ABSV(IAER))
                  SPCMHI(25,ISEG,3)=SPCMHI(25,ISEG,3)+SCTTRM(8)
                  SPCMHI(26,ISEG,3)                                     &
     &              =SPCMHI(26,ISEG,3)+SCTTRM(8)*ASYV(1,IAER)
                  EXTSM=EXTSM+WPATH(60+IAER)*EXTV(IAER)
                  ABSSM=ABSSM+WPATH(60+IAER)*ABSV(IAER)
                  DO ISTR=2,NSTR
                      TXLEG(ISTR,ISEG,1)                                &
     &                  =TXLEG(ISTR,ISEG,1)+SCTTRM(8)*ASYV(ISTR,IAER)
                  ENDDO
              ENDDO
              SCTSM=SCTSM+SPCMHI(25,ISEG,3)
              ASYMSM=ASYMSM+SPCMHI(26,ISEG,3)
              IF(SPCMHI(25,ISEG,3).GT.0.)SPCMHI(26,ISEG,3)              &
     &          =SPCMHI(26,ISEG,3)/SPCMHI(25,ISEG,3)
          ENDIF

!         COMBINED EXTINCTION AND SCATTERING TERMS:
          SPCMLO( 1,ISEG,3)=SPCMHI( 1,ISEG,3)
          SPCMLO( 2,ISEG,3)=SPCMHI( 2,ISEG,3)
          SPCMLO(13,ISEG,3)=SPCMHI(13,ISEG,3)
          SPCMHI( 1,ISEG,3)=ASYMSM
          SPCMHI( 2,ISEG,3)=SCTSM
          SPCMHI(13,ISEG,3)=EXTSM
          EXTSM=EXTSM+XRAIN+SPCMHI(28,ISEG,3)
          TX(16)=SPCMHI(28,ISEG,3)
      ELSE

!         ADD AEROSOL EXTINCTION AND ABSORPTION TO RAIN CONTRIBUTION
          EXTSM=WPATH( 7)*EXTV( 1)+WPATH(12)*EXTV( 2)+WPATH(13)*EXTV( 3)&
     &         +WPATH(14)*EXTV( 4)+WPATH(16)*EXTV( 5)+WPATH(66)*EXTV( 6)&
     &         +WPATH(67)*EXTV( 7)+WPATH(68)*EXTV( 8)+WPATH(69)*EXTV( 9)&
     &         +WPATH(70)*EXTV(10)+WPATH(71)*EXTV(11)+WPATH(72)*EXTV(12)&
     &         +WPATH(73)*EXTV(13)+WPATH(74)*EXTV(14)+WPATH(75)*EXTV(15)&
     &         +WPATH(76)*EXTV(16)+WPATH(77)*EXTV(17)+XRAIN
          ABSSM=WPATH( 7)*ABSV( 1)+WPATH(12)*ABSV( 2)+WPATH(13)*ABSV( 3)&
     &         +WPATH(14)*ABSV( 4)+WPATH(16)*ABSV( 5)+WPATH(66)*ABSV( 6)&
     &         +WPATH(67)*ABSV( 7)+WPATH(68)*ABSV( 8)+WPATH(69)*ABSV( 9)&
     &         +WPATH(70)*ABSV(10)+WPATH(71)*ABSV(11)+WPATH(72)*ABSV(12)&
     &         +WPATH(73)*ABSV(13)+WPATH(74)*ABSV(14)+WPATH(75)*ABSV(15)&
     &         +WPATH(76)*ABSV(16)+WPATH(77)*ABSV(17)+ARAIN
          IF(LSAP)THEN
              EXTSAP=EXPINT(XSAP(ILOSAP),XSAP(IHISAP),FRCSAP)
              EXTSM=EXTSM+EXTSAP
              ABSSAP=EXPINT(ASAP(ILOSAP),ASAP(IHISAP),FRCSAP)
              ABSSM=ABSSM+ABSSAP
          ENDIF
          TX(16)=WPATH(16)*EXTV(5)+WPATH(66)*EXTV(6)+WPATH(67)*EXTV(7)
      ENDIF

!     N2 CONTINUUM [TX(4)]:
      TX(4)=FACMC*N2CONT*WPATH(4)

!     SUM OF H2O CONTINUA [TX(5)]:
      IF(WPATH(5).GT.0. .AND. SH2OT0.GT.0.)THEN

!         TEMPERATURE-DEPENDENT H2O CONTINUUM:
          WRAT=WPATH(9)/WPATH(5)
          TX(5)=WPATH(10)*FH2O+WPATH(5)*SH2OT0*SH2OT**WRAT
          IF(VBAND5.LT.2100.)THEN

!             RADIATION FIELD (SPECTRAL FREQUENCY ALREADY INCLUDED):
              EXPON=EXP(C2D*VBAND5/(WRAT-T296D))
              TX(5)=TX(5)*(1-EXPON)/(1+EXPON)
          ENDIF
          TX(5)=FACMC*TX(5)
      ELSE
          TX(5)=0.
      ENDIF

!     H2-H2 AND H2-He DIMERS:
      IF(VBAND5.LE.16490.)THEN

!         MULTIPLE SCATTERING PATH:
          IF(IPATH.EQ.3)THEN

!             MULTIPLE SCATTERING PATH:  VERTICAL SEGMENT.
              TX(MEXTX-1)=WPATH(MEXTX-1)*H2H2DT(TSEGMS(ISEG),VBAND5)
              TX(MEXTX)=WPATH(MEXTX)*H2HEDT(TSEGMS(ISEG),VBAND5)
          ELSE

!             MULTIPLE SCATTERING LEVEL TO SUN PATH:
              TX(MEXTX-1)                                               &
     &          =WPATH(MEXTX-1)*H2H2DT(TSSEGM(NMOL+14,ISEG),VBAND5)
              TX(MEXTX)=WPATH(MEXTX)*H2HEDT                             &
     &          (SQRT(TSSEGM(NMOL+14,ISEG)*TSSEGM(NMOL+15,ISEG)),VBAND5)
          ENDIF
      ELSE
          TX(MEXTX-1)=0.
          TX(MEXTX)=0.
      ENDIF

!     VISIBLE AND ULTRAVIOLET O3 [TX(8)]:
      TX(8)=FACMC*O3T0*(.269*WPATH(8)+O3T1*WPATH(59)+O3T2*WPATH(60))

!     HNO3 ABSORPTION (LOWTRAN CALCULATIONS ONLY) [TX(11)]:
      TX(11)=FACMC*CHNO3*WPATH(11)

!     O2 CONTINUUM CONTRIBUTIONS [TX(3)]:
      TX(3)=FACMC*(WPATH(58)*O2HERZ+WPATH(82)*O2C127+WPATH(83)*O2C106   &
     &  +WPATH(84)*O2_O2V+SIGO20*(WPATH(63)                             &
     &  +SIGO2A*(WPATH(1)-220*WPATH(63))+SIGO2B*WPATH(2)))
      TX(64)=FACMC*CRSNO2*WPATH(64)
      TX(65)=FACMC*CRSSO2*WPATH(65)

!     SUM OF CONTINUUM CONTRIBUTIONS EXCLUDING O2 AND HNO3 [TX(9)]:
      TX(9)=TX(4)+TX(5)+TX(6)+TX(8)+EXTSM

!     CIRRUS CLOUDS [TX(16)]:
      IF(ICLD.EQ.20)THEN
          TX(16)=2*WPATH(16)
          TX(9)=TX(9)+TX(16)
      ENDIF

!     STORE CUMULATIVE AEROSOL PARAMETERS FOR DIFFERENT VERTICAL REGIONS
      TX(10)=ABSSM
      TX(7)=EXTSM

!     VIS/NIR CH4 OPTICAL DEPTH:
      SPCMLO(NSEG5-2,ISEG,IPATH)=SPCMHI(NSEG5-2,ISEG,IPATH)
      SPCMHI(NSEG5-2,ISEG,IPATH)=A_CH4*WPATH(46)

!     H2-H2 AND H2-HE DIMER OPTICAL DEPTHS:
      SPCMLO(NSEG5-1,ISEG,IPATH)=SPCMHI(NSEG5-1,ISEG,IPATH)
      SPCMHI(NSEG5-1,ISEG,IPATH)=TX(MEXTX-1)
      SPCMLO(NSEG5,ISEG,IPATH)=SPCMHI(NSEG5,ISEG,IPATH)
      SPCMHI(NSEG5,ISEG,IPATH)=TX(MEXTX)

!     STORE OPTICAL THICKNESS PARAMETERS:
      SPCMLO(12,ISEG,IPATH)=SPCMHI(12,ISEG,IPATH)
      SPCMHI(12,ISEG,IPATH)=TX(4)+TX(5)+TX(8)+TX(3)                     &
     &  +A_CH4*WPATH(46)+TX(MEXTX-1)+TX(MEXTX)
      SPCMLO(14,ISEG,IPATH)=SPCMHI(14,ISEG,IPATH)
      SPCMHI(14,ISEG,IPATH)=TX(9)+TX(3)+TX(64)+TX(65)                   &
     &  +A_CH4*WPATH(46)+TX(MEXTX-1)+TX(MEXTX)
      SPCMLO(33,ISEG,IPATH)=SPCMHI(33,ISEG,IPATH)
      SPCMHI(33,ISEG,IPATH)=TX(4)+TX(5)+TX(6)+TX(8)+TX(3)               &
     &  +TX(64)+TX(65)+A_CH4*WPATH(46)+TX(MEXTX-1)+TX(MEXTX)
      SPCMLO(15,ISEG,IPATH)=SPCMHI(15,ISEG,IPATH)
      SPCMHI(15,ISEG,IPATH)=TX(6)
      DO K=3,11
          SPCMLO(K,ISEG,IPATH)=SPCMHI(K,ISEG,IPATH)
          IF(TX(K).LE.BIGEXP)THEN
              SPCMHI(K,ISEG,IPATH)=EXP(-TX(K))
          ELSE
              SPCMHI(K,ISEG,IPATH)=1/BIGNUM
          ENDIF
      ENDDO
      SPCMLO(16,ISEG,IPATH)=SPCMHI(16,ISEG,IPATH)
      IF(TX(16).LE.BIGEXP)THEN
          SPCMHI(16,ISEG,IPATH)=EXP(-TX(16))
      ELSE
          SPCMHI(16,ISEG,IPATH)=1/BIGNUM
      ENDIF
      SPCMLO(17,ISEG,IPATH)=SPCMHI(17,ISEG,IPATH)
      IF(TX(64).LE.BIGEXP)THEN
          SPCMHI(17,ISEG,IPATH)=EXP(-TX(64))
      ELSE
          SPCMHI(17,ISEG,IPATH)=1/BIGNUM
      ENDIF
      SPCMLO(18,ISEG,IPATH)=SPCMHI(18,ISEG,IPATH)
      IF(TX(65).LE.BIGEXP)THEN
          SPCMHI(18,ISEG,IPATH)=EXP(-TX(65))
      ELSE
          SPCMHI(18,ISEG,IPATH)=1/BIGNUM
      ENDIF

!     RETURN IF LOWTRAN BAND MODEL IS NOT USED:
      IF(MODTRN)RETURN

!     LOWTRAN7 DOUBLE EXPONENTIAL MOLECULAR TRANSMITTANCES:
      DO K=1,11
          IF(CPS(K).GT.-20.)THEN
              TX(MPOINT(K))=DBLTX(FACMC*WPATH(IBND(K)),CPS(K),QA(K))
          ELSE
              TX(MPOINT(K))=1.
          ENDIF
      ENDDO
      RETURN
      END
