      INTEGER FUNCTION GTSTRT(IBINPT,ITB,LSTREC)

!     FIND THE RECORD (IREC) WHERE FREQUENCY IV STARTS IN THE
!     BAND MODEL PARAMETER TAPE.  ITB IS THE UNIT NUMBER.

!     ARGUMENTS:
!       IBINPT   BIN NUMBER OF CURRENT SPECTRAL POINT.
!                (CENTER FREQUENCY = IBINPT * BNDWID + OSHIFT).
!       ITB      FILE UNIT NUMBER.
!       LSTREC   LAST RECORD IN FILE ITB.
      INTEGER IBINPT,ITB,LSTREC

!     LOCAL VARIABLES:
!       IBIN     BIN NUMBER OF CURRENT RECORD.
!       IRECLO   RECORD NUMBER LOWER BOUND.
!       IRECHI   RECORD NUMBER HIGHER BOUND.
!       IREC     TEST RECORD NUMBER.
!       IDREC    IRECHI MINUS IRECLO.
      INTEGER IBIN,IRECLO,IRECHI,IREC,IDREC

!     SET IRECLO TO 2 SKIPPING HEADER.
!     SET IRECHI TO THE MAXIMUM RECORD.
      IRECLO=2
      IRECHI=LSTREC

!     START BISECTION LOOP.  FIND FREQUENCY AT THE MID-RECORD
!     BETWEEN IRECLO AND IRECHI.  RESET IRECLO AND IRECHI.
   10 CONTINUE
      IDREC=IRECHI-IRECLO
      IF(IDREC.GT.1)THEN
          IREC=IRECLO+IDREC/2
          READ(ITB,REC=IREC)IBIN
          IF(IBIN.LT.IBINPT)THEN
              IRECLO=IREC
          ELSE
              IRECHI=IREC
          ENDIF
          GOTO 10
      ENDIF
      READ(ITB,REC=IRECLO)IBIN
      IF(IBIN.EQ.IBINPT)THEN
          GTSTRT=IRECLO
      ELSE
          GTSTRT=IRECHI
      ENDIF
      RETURN
      END
