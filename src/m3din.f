      SUBROUTINE M3DIN(IKMX)

!     DEFINES SPECTRALLY INDEPENDENT PATH SEGMENT DATA FOR MOD3D.

!     ARGUMENTS:
!       IKMX     NUMBER OF LINE-OF-SIGHT PATH SEGMENTS.
      INTEGER IKMX

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     COMMONS:
      INCLUDE 'SEGDAT.h'

!     /CONSTN/
!       AMWT     MOLECULAR WEIGHTS [GM/MOLE].
!                  1  H2O   2  CO2   3  O3    4  N2O   5  CO    6  CH4
!                  7  O2    8  NO    9  SO2   10 NO2   11 NH3   12 HNO3
      REAL AMWT
      COMMON/CONSTN/AMWT(12)

!     /M3DDEN/
!       PRES     LAYER PRESSURE [ATM].
!       AER550   AEROSOL EXTINCTION OPTICAL DEPTH AT 550 NM.
!       DENH2O   LAYER H2O DENSITY [GM/M3].
!       DENCO2   LAYER CO2 DENSITY [GM/M3].
!       DENO3    LAYER O3 DENSITY [GM/M3].
      REAL PRES,AER550,DENH2O,DENCO2,DENO3
      COMMON/M3DDEN/PRES(LAYTWO),AER550(LAYTWO),                        &
     &  DENH2O(LAYTWO),DENCO2(LAYTWO),DENO3(LAYTWO)
      SAVE /M3DDEN/,/CONSTN/
      EXTERNAL ATMCON

!     LOCAL VARIABLES:
!       IK       PATH SEGMENT INDEX.
!       CONVRT   ATM-CM TO [(KM K MOLE) / (ATM M3)] CONVERSION FACTOR.
!       CONLAY   COLUMN DENSITY [ATM-CM] TO DENSITY [MOLE/M3] CONVERSION
      INTEGER IK
      REAL CONVRT,CONLAY

!     CONVERSION FACTOR FROM ATM-CM TO [KM MOLE / M3]
!       CONVRT = (1.E6 CM3/M3) (1.E-5 KM/CM) (MOLE / 22413.83 CM3 ATM )
      CONVRT=10/STDVOL

!     LOOP OVER LAYERS:
      DO IK=1,IKMX
          PRES(IK)=WPTH(0,IK,1)*(TSEG(IK,1)/TZERO)/DRNG(IK,1)
          AER550(IK)=WPTH(7,IK,1)+WPTH(12,IK,1)+WPTH(13,IK,1)           &
     &      +WPTH(14,IK,1)+WPTH(68,IK,1)+WPTH(69,IK,1)+WPTH(70,IK,1)    &
     &      +WPTH(71,IK,1)+WPTH(72,IK,1)+WPTH(73,IK,1)+WPTH(74,IK,1)    &
     &      +WPTH(75,IK,1)+WPTH(76,IK,1)+WPTH(77,IK,1)

!         COLUMN DENSITY [ATM-CM] TO DENSITY [MOLE/M3] CONVERSION
!           CONLAY = CONVRT / DIST(KM)
          CONLAY=CONVRT/DRNG(IK,1)
          DENH2O(IK)=WPTH(17,IK,1)*CONLAY*AMWT(1)
          DENCO2(IK)=WPTH(36,IK,1)*CONLAY*AMWT(2)
          DENO3(IK)=WPTH(31,IK,1)*CONLAY*AMWT(3)
      ENDDO
      RETURN
      END
