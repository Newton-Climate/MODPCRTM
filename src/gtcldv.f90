      SUBROUTINE GTCLDV(ALAM)

!     THIS ROUTINE SPECTRALLY INTERPOLATES CLOUD
!     DATA FROM THE CLDMOD AND CIRMOD MODULES.

!     MODULES:
      USE CLDMOD
      USE CIRMOD

!     INPUT ARGUMENTS:
!       ALAM     SPECTRAL WAVELENGTH [MICRONS].
      REAL ALAM

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     COMMONS:

!     /AER/ THERE ARE "MAER=17" PARTICULATE COMPONENTS:
!           1       AEROSOL 1 (NOMINALLY, BOUNDARY LAYER AEROSOL).
!           2       AEROSOL 2 (NOMINALLY, TROPOSPHERIC AEROSOL).
!           3       AEROSOL 3 (NOMINALLY, STRATOSPHERIC AEROSOL).
!           4       AEROSOL 4 (NOMINALLY, VOLCANIC AEROSOL).
!           5       CIRRUS CLOUD.
!           6       CLOUD 1 (NOMINALLY, WATER CLOUD).
!           7       CLOUD 2 (NOMINALLY, ICE CLOUD).
!           8-17    NOVAM (NAVY OCEANIC VERTICAL AEROSOL MODEL) LAYERS.
!       NAER     NUMBER OF ACTIVE AEROSOLS.
!       EXTV     SPECTRAL EXTINCTION (NORMALIZED TO 1 AT 550 NM).
!       ABSV     SPECTRAL ABSORPTION (1-ABSV/EXTV=SCATTERING ALBEDO).
!       ASYV     SPECTRAL LEGENDRE MOMENT (DIVIDED BY 2N+1).
!       FRAC5    5 CM-1 GRID SPECTRAL INTERPOLATION FRACTION.
!       ASYVLO   ASYMMETRY FACTOR FROM PREVIOUS SPECTRAL FREQUENCY.
      INTEGER NAER
      REAL EXTV,ABSV,ASYV,FRAC5,ASYVLO
      COMMON/AER/NAER,EXTV(MAER),ABSV(MAER),ASYV(MXCMU,MAER),           &
     &  FRAC5,ASYVLO(MAER)

!     /DISRT/
!       UMU      MONOTONICALLY INCREASING LIST OF DISTINCT USER-PATH
!                COSINE POLAR ANGLES.
!       PHI      MONOTONICALLY INCREASING LIST OF DISTINCT RELATIVE
!                SOLAR AZIMUTH ANGLES [0 TO 180 DEG].
!       NSTR     NUMBER OF DISCRETE ORDINATE STREAMS.
!       NAZ      NUMBER OF DISORT AZIMUTH COMPONENTS.
!       N2GAUS   ORDER OF DOUBLE-GAUSS QUADRATURES.
!       NUMU     NUMBER OF DISTINCT USER LINE-OF-SIGHT POLAR ANGLES.
!       MAPUMU   MAPPING FROM LINE-OF-SIGHT INDEX TO UMU ARRAY ENTRY.
!       NPHI     NUMBER OF DISTINCT RELATIVE SOLAR AZIMUTH ANGLES.
!       MAPPHI   MAPPING FROM LINE-OF-SIGHT INDEX TO PHI ARRAY ENTRY.
!       DIS      LOGICAL FLAG, TRUE FOR DISORT MULTIPLE SCATTERING.
!       DISAZM   LOGICAL FLAG, TRUE FOR DISORT WITH AZIMUTH DEPENDENCE.
!       DISALB   LOGICAL FLAG, TRUE FOR DISORT SPHERICAL ALBEDO OPTION.
!       LDISCL   LOGICAL FLAG, TRUE FOR ISAACS SCALED TO DISORT.
      REAL UMU,PHI
      INTEGER NSTR,NAZ,N2GAUS,NUMU,MAPUMU,NPHI,MAPPHI
      LOGICAL DIS,DISAZM,DISALB,LDISCL
      COMMON/DISRT/UMU(MXUMU),PHI(MAXPHI),NSTR,NAZ,N2GAUS,NUMU,         &
     &  MAPUMU(MLOS),NPHI,MAPPHI(MLOS),DIS,DISAZM,DISALB,LDISCL
      SAVE /DISRT/

!     LOCAL VARIABLES:
!       IWAVLO   LOWER INDEX FOR CLOUD SPECTRAL GRID INTERPOLATION.
!       WAV      SPECTRAL WAVELENGTH [MICRONS].
!       FACTOR   INTERPOLATION FRACTION.
      INTEGER IWAVLO
      REAL WAV,FACTOR

!     DATA:
!       ICLDHI   SAVED UPPER INDEX FOR WATER CLOUD SPECTRAL GRID.
!       ICIRHI   SAVED UPPER INDEX FOR CIRRUS CLOUD SPECTRAL GRID.
      INTEGER ICLDHI,ICIRHI
      SAVE ICLDHI,ICIRHI
      DATA ICLDHI,ICIRHI/2,2/

!     SET UP SPECTRAL INTERPOLATION FOR WATER CLOUD DATA:
      WAV=ALAM
      IF(ICLDHI.GT.NCLDWV)ICLDHI=NCLDWV
      IF(CLDWAV(ICLDHI-1).GT.WAV .OR. WAV.GT.CLDWAV(ICLDHI))THEN

!         SAVED BOUNDS NO LONGER APPLICABLE - FIND NEW BOUNDS:
          IF(WAV.LE.CLDWAV(1))THEN
              ICLDHI=2
              WAV=CLDWAV(1)
          ELSEIF(WAV.GE.CLDWAV(NCLDWV))THEN
              ICLDHI=NCLDWV
              WAV=CLDWAV(NCLDWV)
          ELSE
              DO ICLDHI=2,NCLDWV
                  IF(WAV.LT.CLDWAV(ICLDHI))GOTO 10
              ENDDO
          ENDIF
   10     CONTINUE
      ENDIF
      IWAVLO=ICLDHI-1
      FACTOR=(WAV-CLDWAV(IWAVLO))/(CLDWAV(ICLDHI)-CLDWAV(IWAVLO))

!     SPECTRALLY INTERPOLATE WATER CLOUD DATA:
      EXTV(6)=CLDEXT(IWAVLO)+FACTOR*(CLDEXT(ICLDHI)-CLDEXT(IWAVLO))
      ABSV(6)=CLDABS(IWAVLO)+FACTOR*(CLDABS(ICLDHI)-CLDABS(IWAVLO))
      DO ICLDAN=1,NCLDAN
          CLDPFV(ICLDAN)=CLDPF(ICLDAN,IWAVLO)                           &
     &      +FACTOR*(CLDPF(ICLDAN,ICLDHI)-CLDPF(ICLDAN,IWAVLO))
      ENDDO
      DO ICLDLG=1,NSTR
          ASYV(ICLDLG,6)=CLDLEG(ICLDLG,IWAVLO)                          &
     &      +FACTOR*(CLDLEG(ICLDLG,ICLDHI)-CLDLEG(ICLDLG,IWAVLO))
      ENDDO

!     SET UP SPECTRAL INTERPOLATION FOR CIRRUS CLOUD DATA:
      WAV=ALAM
      IF(ICIRHI.GT.NCIRWV)ICIRHI=NCIRWV
      IF(CIRWAV(ICIRHI-1).GT.WAV .OR. WAV.GT.CIRWAV(ICIRHI))THEN

!         SAVED BOUNDS NO LONGER APPLICABLE - FIND NEW BOUNDS:
          IF(WAV.LE.CIRWAV(1))THEN
              ICIRHI=2
              WAV=CIRWAV(1)
          ELSEIF(WAV.GE.CIRWAV(NCIRWV))THEN
              ICIRHI=NCIRWV
              WAV=CIRWAV(NCIRWV)
          ELSE
              DO ICIRHI=2,NCIRWV
                  IF(WAV.LT.CIRWAV(ICIRHI))GOTO 20
              ENDDO
          ENDIF
   20     CONTINUE
      ENDIF
      IWAVLO=ICIRHI-1
      FACTOR=(WAV-CIRWAV(IWAVLO))/(CIRWAV(ICIRHI)-CIRWAV(IWAVLO))

!     SPECTRALLY INTERPOLATE CIRRUS CLOUD DATA:
      EXTV(7)=CIREXT(IWAVLO)+FACTOR*(CIREXT(ICIRHI)-CIREXT(IWAVLO))
      ABSV(7)=CIRABS(IWAVLO)+FACTOR*(CIRABS(ICIRHI)-CIRABS(IWAVLO))
      DO ICIRAN=1,NCIRAN
          CIRPFV(ICIRAN)=CIRPF(ICIRAN,IWAVLO)                           &
     &      +FACTOR*(CIRPF(ICIRAN,ICIRHI)-CIRPF(ICIRAN,IWAVLO))
      ENDDO
      DO ICIRLG=1,NSTR
          ASYV(ICIRLG,7)=CIRLEG(ICIRLG,IWAVLO)                          &
     &      +FACTOR*(CIRLEG(ICIRLG,ICIRHI)-CIRLEG(ICIRLG,IWAVLO))
      ENDDO
      RETURN
      END
