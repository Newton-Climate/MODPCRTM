      SUBROUTINE DOSLIT(NTRIAN,TRIDAT,SLIT)

!     DOSLIT CONVOLVES SPECTRAL DATA A TRIANGULAR SLIT FUNCTION.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     ARGUMENTS:
!       NTRIAN   NUMBER OF SPECTRAL DATA BEING CONVOLVED
!       TRIDAT   SPECTRAL DATA.
      INTEGER NTRIAN
      REAL TRIDAT(0:NTRIAN),SLIT(0:MWGT,0:NTRIAN)

!     COMMONS:
      INCLUDE 'BMHEAD.h'

!     /CARD4/
!       IV1      LOWEST SPECTRAL FREQUENCY OUTPUT [CM-1].
!       IV2      HIGHEST SPECTRAL FREQUENCY OUTPUT [CM-1].
!       IDV      PRINTOUT SPECTRAL FREQUENCY STEP SIZE [CM-1].
!       IFWHM    TRIANGULAR SLIT FULL-WIDTH-HALF-MAXIMUM [CM-1].
!       VBAND    CURRENT COMPUTATION BAND FREQUENCY [CM-1].
!                (EQUALS BAND CENTER FOR 1, 5 & 15 CM-1 BAND MODELS;
!                EQUALS THE MINIMUM BAND VALUE FOR 0.1 CM-1 BAND MODEL)
!       IBINPT   BIN NUMBER OF CURRENT SPECTRAL POINT.
!                (CENTER FREQUENCY = IBINPT * BNDWID + OSHIFT).
!       IBINLO   BIN NUMBER OF (PADDED) SPECTRAL RANGE LOWER BOUND.
!       IBINHI   BIN NUMBER OF (PADDED) SPECTRAL RANGE UPPER BOUND.
!       IBINMN   BIN NUMBER OF MINIMUM COMPUTATION SPECTRAL POINT.
!       IBINMX   BIN NUMBER OF MAXIMUM COMPUTATION SPECTRAL POINT.
!       IBINDL   BIN NUMBER INCREMENT FOR SPECTRAL PRINTOUT.
!       IBINRS   BIN NUMBER INCREMENT EQUAL TO SPECTRAL RESOLUTION.
!       IBINOS   BIN NUMBER OFFSET BETWEEN CURRENT & OUTPUT SPC POINTS.
!       IBINWR   BIN NUMBER OF NEXT SPECTRAL DATA WRITE.
!       MBINPT   BIN NUMBER MAXIMUM FOR CURRENT BAND MODEL RESOLUTION.
!       IDBIN5   SPECTRAL BIN NUMBER STEP SIZE FOR 5 CM-1 GRID.
!       ISTEP5   INCREMENT FOR RETRIEVING 5 CM-1 RESOLUTION DATA [CM-1].
!       NSPCDT   NUMBER OF OUTPUT SPECTRAL DATA POINTS.
      DOUBLE PRECISION IDV
      REAL IV1,IV2,IFWHM,VBAND
      INTEGER IBINPT,IBINLO,IBINHI,IBINMN,IBINMX,IBINDL,                &
     &  IBINRS,IBINOS,IBINWR,MBINPT,IDBIN5,ISTEP5,NSPCDT
      COMMON/CARD4/IDV,IV1,IV2,IFWHM,VBAND,IBINPT,IBINLO,IBINHI,IBINMN, &
     &  IBINMX,IBINDL,IBINRS,IBINOS,IBINWR,MBINPT,IDBIN5,ISTEP5,NSPCDT

!     /TRIANG/
!       NWGT     NUMBER OF SPECTRAL BINS CONTRIBUTING TO SLIT FUNCTION.
!       IBIN0    SPECTRAL BIN OFFSET (1 FOR .2 BAND MODEL, 0 OTHERWISE).
!       WNORM    TRIANGULAR SLIT FUNCTION NORMALIZATION FACTOR.
!       WGT      NORMALIZED WEIGHTS USED TO DEFINE THE SLIT FUNCTION.
!       SLIT     SAVED CONVOLVED SPECTRAL DATA, TRIANGULAR SLIT.
      INTEGER NWGT,IBIN0
      REAL WNORM,WGT
      COMMON/TRIANG/NWGT,IBIN0,WNORM,WGT(0:MWGT)
      SAVE /TRIANG/

!     LOCAL VARIABLES:
!       ITRIAN   LOOP INDEX FOR SPECTRAL DATA SOURCES.
!       IWGT     CONVOLUTION WEIGHT LOOP INDEX.
!       IWGTP1   IWGT PLUS ONE.
!       RENORM   RENORMALIZATION FOR SLIT FUNCTION CENTERED NEAR 0 CM-1.
!       HSPCDT   HALF THE TRIDAT VALUE.
!       WGTOFF   SPECTRAL BIN WEIGHT OFFSET FOR 0 CM-1 BINS.
      INTEGER IBIN,ITRIAN,IWGT,IWGTP1
      REAL RENORM,HSPCDT,WGTOFF

!     CONVOLVE DATA:
      IBIN=NINT(VBAND/BNDWID)+IBIN0
      IF(IBIN.GE.NWGT)THEN

!         SLIT DOES NOT EXTEND TO NEGATIVE FREQUENCIES:
          DO ITRIAN=0,NTRIAN
              IWGTP1=NWGT
              DO IWGT=NWGT-1,0,-1
                  SLIT(IWGTP1,ITRIAN)                                   &
     &              =SLIT(IWGT,ITRIAN)+WGT(IWGTP1)*TRIDAT(ITRIAN)
                  IWGTP1=IWGT
              ENDDO
              SLIT(0,ITRIAN)=WGT(0)*TRIDAT(ITRIAN)
              TRIDAT(ITRIAN)=SLIT(NWGT,ITRIAN)
          ENDDO
      ELSE

!         SLIT EXTENDS TO NEGATIVE FREQUENCIES:
          IF(IBIN.GE.IBINRS)THEN

!             AT LEAST HALF THE SLIT COVERED BY POSITIVE FREQUENCIES:
              RENORM=1-2*(1-FLOAT(IBIN)/NWGT)**2
              DO ITRIAN=0,NTRIAN
                  IWGTP1=NWGT
                  DO IWGT=NWGT-1,0,-1
                      SLIT(IWGTP1,ITRIAN)                               &
     &                  =SLIT(IWGT,ITRIAN)+WGT(IWGTP1)*TRIDAT(ITRIAN)
                      IWGTP1=IWGT
                  ENDDO
                  SLIT(0,ITRIAN)=WGT(0)*TRIDAT(ITRIAN)
                  TRIDAT(ITRIAN)=SLIT(NWGT,ITRIAN)/RENORM
              ENDDO
          ELSE

!             LESS THAN HALF THE SLIT COVERED BY POSITIVE FREQUENCIES
!             (NO NEED TO UPDATE TRIDAT ARRAY; IT IS NOT USED):
              IF(IBIN.GT.0)THEN

!                 NO ADJUSTMENT OF "WGT" ARRAY REQUIRED:
                  DO ITRIAN=0,NTRIAN
                      IWGTP1=NWGT
                      DO IWGT=NWGT-1,0,-1
                          SLIT(IWGTP1,ITRIAN)=                          &
     &                      SLIT(IWGT,ITRIAN)+WGT(IWGTP1)*TRIDAT(ITRIAN)
                          IWGTP1=IWGT
                      ENDDO
                      SLIT(0,ITRIAN)=WGT(0)*TRIDAT(ITRIAN)
                  ENDDO
              ELSE

!                 FOR VBAND=0 WITH THE 1, 5 OR 15 CM-1 BAND MODELS
!                 (I.E., IBIN=0) THE SPECTRAL BIN WEIGHTS MUST
!                 BE ADJUSTED TO ACCOUNT FOR A HALF BIN.
                  WGTOFF=.25/WNORM
                  DO ITRIAN=0,NTRIAN
                      HSPCDT=TRIDAT(ITRIAN)/2
                      SLIT(NWGT,ITRIAN)=WGT(NWGT)*TRIDAT(ITRIAN)
                      DO IWGT=NWGT-1,IBINRS+1,-1
                          SLIT(IWGT,ITRIAN)=(WGT(IWGT)-WGTOFF)*HSPCDT
                      ENDDO
                      SLIT(IBINRS,ITRIAN)=WGT(IBINRS)*HSPCDT
                      DO IWGT=IBINRS-1,1,-1
                          SLIT(IWGT,ITRIAN)=(WGT(IWGT)+WGTOFF)*HSPCDT
                      ENDDO
                      SLIT(0,ITRIAN)=WGT(0)*TRIDAT(ITRIAN)
                  ENDDO
              ENDIF
          ENDIF
      ENDIF

!     RETURN TO TRANS:
      RETURN
      END

      SUBROUTINE INSLIT(NEXTXY,NLOS,IBINRS,BNDWID)

!     INSLIT INITIALIZES ARRAYS FOR TRIANGULAR SLIT CONVOLUTION.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'
!       IOFF     INDEX OFFSET USED TO STORE TRANSMITTANCE DATA.
      INTEGER IOFF
      PARAMETER(IOFF=21-MEXT)

!     ARGUMENTS:
!       NEXTXY   NUMBER OF ACTIVE SPECIES.
!       NLOS     NUMBER OF LINE-OF-SIGHT PATHS.
!       IBINRS   FWHM IN BAND WIDTHS (=IFWHM/BNDWID).
!       BNDWID   SPECTRAL WIDTH OF BAND MODEL INTERVAL [CM-1].
      INTEGER NEXTXY,NLOS,IBINRS
      REAL BNDWID

!     COMMONS:

!     /TRIANG/
!       NWGT     NUMBER OF SPECTRAL BINS CONTRIBUTING TO SLIT FUNCTION.
!       IBIN0    SPECTRAL BIN OFFSET (1 FOR .2 BAND MODEL, 0 OTHERWISE).
!       WNORM    TRIANGULAR SLIT FUNCTION NORMALIZATION FACTOR.
!       WGT      NORMALIZED WEIGHTS USED TO DEFINE THE SLIT FUNCTION.
!       SLIT     SAVED CONVOLVED SPECTRAL DATA, TRIANGULAR SLIT.
      INTEGER NWGT,IBIN0
      REAL WNORM,WGT,SLIT
      COMMON/TRIANG/NWGT,IBIN0,WNORM,WGT(0:MWGT)
      COMMON/TRIAN2/SLIT(0:MWGT,0:MOUT,1:MLOS)
      SAVE /TRIANG/,/TRIAN2/

!     LOCAL VARIABLES:
!       ILOS     LOOP INDEX FOR LINE-OF-SIGHT PATHS.
!       IEXT     EXTINCTION SPECIES LOOP INDEX.
!       IWGT     CONVOLUTION WEIGHT LOOP INDEX.
      INTEGER ILOS,IEXT,IWGT

      DO ILOS=1,NLOS
          DO IEXT=0,NEXTXY+IOFF
              DO IWGT=0,MWGT
                  SLIT(IWGT,IEXT,ILOS)=0.
              ENDDO
          ENDDO
      ENDDO

!     COMPUTE NORMALIZATION AND WEIGHTS:
      WNORM=FLOAT(IBINRS*IBINRS)
      NWGT=2*IBINRS
      IF(BNDWID.GT..2)THEN

!         TRIANGULAR SLIT CENTERED ON SPECTRAL BIN CENTER FREQUENCY:
          IBIN0=0
          DO IWGT=1,IBINRS-1
              WGT(IWGT)=IWGT/WNORM
              WGT(NWGT-IWGT)=WGT(IWGT)
          ENDDO
          WGT(0)=.125/WNORM
          WGT(NWGT)=WGT(0)
          WGT(IBINRS)=(IBINRS-.25)/WNORM
      ELSE

!         TRIANGULAR SLIT CENTERED ON SPECTRAL BIN LEFT EDGE FREQUENCY:
          IBIN0=1
          DO IWGT=1,IBINRS
              WGT(IWGT)=(IWGT-.5)/WNORM
              WGT(NWGT+1-IWGT)=WGT(IWGT)
          ENDDO
          WGT(0)=0.
      ENDIF

!     RETURN TO TRANS:
      RETURN
      END

      SUBROUTINE SQSLIT

!     SQSLIT INITIALIZES ARRAYS FOR SQUARE SLIT.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     COMMONS:

!     /TRIANG/
!       NWGT     NUMBER OF SPECTRAL BINS CONTRIBUTING TO SLIT FUNCTION.
!       IBIN0    SPECTRAL BIN OFFSET (1 FOR .2 BAND MODEL, 0 OTHERWISE).
!       WNORM    TRIANGULAR SLIT FUNCTION NORMALIZATION FACTOR.
!       WGT      NORMALIZED WEIGHTS USED TO DEFINE THE SLIT FUNCTION.
!       SLIT     SAVED CONVOLVED SPECTRAL DATA, TRIANGULAR SLIT.
      INTEGER NWGT,IBIN0
      REAL WNORM,WGT,SLIT
      COMMON/TRIANG/NWGT,IBIN0,WNORM,WGT(0:MWGT)
      COMMON/TRIAN2/SLIT(0:MWGT,0:MOUT,1:MLOS)
      SAVE /TRIANG/,/TRIAN2/

!     WEIGHT REQUIRES FOR SPCFLX ROUTINE:
      NWGT=0
      WGT(0)=1.

!     RETURN TO TRANS:
      RETURN
      END

      SUBROUTINE T_SLIT(NEXTXY,VCEN,UNIF,TRACE)

!     T_SLIT STORES TRANSMITTANCE DATA FOR TRIANGULAR SLIT CONVOLUTION.
      IMPLICIT NONE

!     PARAMETERS:
!       IOFF     INDEX OFFSET USED TO STORE TRANSMITTANCE DATA.
!       MEXTP1   MEXT PLUS ONE.
      INCLUDE 'PARAMS.h'
      INTEGER IOFF,MEXTP1
      PARAMETER(IOFF=21-MEXT,MEXTP1=MEXT+1)

!     COMMONS:
!       SLIT     SAVED CONVOLVED SPECTRAL DATA, TRIANGULAR SLIT.
      REAL SLIT
      COMMON/TRIAN2/SLIT(0:MWGT,0:MOUT,1:MLOS)
      SAVE /TRIAN2/

!     INPUT ARGEMENTS:
!       NEXTXY   NUMBER OF ACTIVE SPECIES:
!       VCEN     SPECTRAL BAND CENTRAL FREQUENCY [CM-1].
!       UNIF     UNIFORMLY MIXED GASES SPECTRAL TRANSMITTANCE.
!       TRACE    TRACE GAS SPECTRAL TRANSMITTANCE.
      INTEGER NEXTXY
      REAL VCEN,UNIF,TRACE

!     COMMONS:
      INCLUDE 'BASE.h'

!     LOCAL VARIABLES:
!       IEXT     LOOP INDEX FOR X AND Y SPECIES.
      INTEGER IEXT

!     LOCAL ARRAYS:
!       TRIDAT   TRANSMITTANCE DATA TEMPORARY STORAGE.
      REAL TRIDAT(0:MOUT)

!     STORE DATA:
      TRIDAT(0)=VCEN
      TRIDAT(1)=TX(4)
      TRIDAT(2)=TX(5)
      TRIDAT(3)=TX(6)
      TRIDAT(4)=TX(7)
      TRIDAT(5)=TX(9)
      TRIDAT(6)=TX(10)
      TRIDAT(7)=TX(11)
      TRIDAT(8)=TX(16)
      TRIDAT(9)=TX(17)
      TRIDAT(10)=TX(31)
      TRIDAT(11)=TX(36)
      TRIDAT(12)=TX(44)
      TRIDAT(13)=TX(46)
      TRIDAT(14)=TX(47)
      TRIDAT(15)=TX(50)
      TRIDAT(16)=TX(52)
      TRIDAT(17)=TX(54)
      TRIDAT(18)=TX(64)
      TRIDAT(19)=TX(65)
      TRIDAT(20)=UNIF
      TRIDAT(21)=TRACE
      DO IEXT=MEXTP1,NEXTXY
          TRIDAT(IEXT+IOFF)=TX(IEXT)
      ENDDO

!     PROCESS DATA:
      CALL DOSLIT(NEXTXY+IOFF,TRIDAT,SLIT(0,0,1))

!     ASSIGNED CONVOLVED DATA:
      VCEN=TRIDAT(0)
      TX(4)=TRIDAT(1)
      TX(5)=TRIDAT(2)
      TX(6)=TRIDAT(3)
      TX(7)=TRIDAT(4)
      TX(9)=TRIDAT(5)
      TX(10)=TRIDAT(6)
      TX(11)=TRIDAT(7)
      TX(16)=TRIDAT(8)
      TX(17)=TRIDAT(9)
      TX(31)=TRIDAT(10)
      TX(36)=TRIDAT(11)
      TX(44)=TRIDAT(12)
      TX(46)=TRIDAT(13)
      TX(47)=TRIDAT(14)
      TX(50)=TRIDAT(15)
      TX(52)=TRIDAT(16)
      TX(54)=TRIDAT(17)
      TX(64)=TRIDAT(18)
      TX(65)=TRIDAT(19)
      UNIF=TRIDAT(20)
      TRACE=TRIDAT(21)
      DO IEXT=MEXT+1,NEXTXY
          TX(IEXT)=TRIDAT(IEXT+IOFF)
      ENDDO

!     RETURN TO TRANS:
      RETURN
      END

      SUBROUTINE S_SLIT(VCEN,TRNLOS,S0,TS0)

!     S_SLIT STORES SOLAR IRRADIANCE DATA FOR TRIANGULAR SLIT
!     CONVOLUTION.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     INPUT ARGEMENTS:
!       VCEN     SPECTRAL BAND CENTRAL FREQUENCY [CM-1].
!       TRNLOS   LINE-OF-SIGHT COMBINED SPECIES TRANSMITTANCE [TX(9)].
!       S0       TOP-OF-ATMOSPHERE SOLAR IRRADIANCE [W CM-2 / CM-1].
!       TS0      TRANSMITTED SOLAR IRRADIANCE [W CM-2 / CM-1].
      REAL VCEN,TRNLOS,S0,TS0

!     COMMONS:
!       SLIT     SAVED CONVOLVED SPECTRAL DATA, TRIANGULAR SLIT.
      REAL SLIT
      COMMON/TRIAN2/SLIT(0:MWGT,0:MOUT,1:MLOS)
      SAVE /TRIAN2/

!     LOCAL ARRAYS:
!       TRIDAT   TRANSMITTANCE DATA TEMPORARY STORAGE.
      REAL TRIDAT(0:3)

!     STORE DATA:
      TRIDAT(0)=VCEN
      TRIDAT(1)=TRNLOS
      TRIDAT(2)=S0
      TRIDAT(3)=TS0

!     PROCESS DATA:
      CALL DOSLIT(3,TRIDAT,SLIT(0,0,1))

!     ASSIGNED CONVOLVED DATA:
      VCEN=TRIDAT(0)
      TRNLOS=TRIDAT(1)
      S0=TRIDAT(2)
      TS0=TRIDAT(3)

!     RETURN TO TRANS:
      RETURN
      END

      SUBROUTINE E_SLIT(ILOS,VCEN,TRNLOS,DEMIS,THMCUM,SUMTMS,BBG,RFSURF)

!     E_SLIT STORES THERMAL EMISSION DATA FOR TRIANGULAR SLIT
!     CONVOLUTION.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     INPUT ARGEMENTS:
!       ILOS     LOOP INDEX FOR LINE-OF-SIGHT PATHS.
!       VCEN     SPECTRAL BAND CENTRAL FREQUENCY [CM-1].
!       TRNLOS   LINE-OF-SIGHT COMBINED SPECIES TRANSMITTANCE [TX(9)].
!       DEMIS    DIRECTIONAL EMISSIVITY TO SENSOR.
!       THMCUM   PATH & SCATTERED THERMAL EMISSION [W SR-1 CM-2 / CM-1].
!       SUMTMS   ISAACS SCATTERED THERMAL EMISSION [W SR-1 CM-2 / CM-1].
!       BBG      TRANSMITTED GROUND EMISSION [W SR-1 CM-2 / CM-1].
!       RFSURF   TRANSMITTED SURFACE REFLECTANCE [W SR-1 CM-2 / CM-1].
      INTEGER ILOS
      REAL VCEN,TRNLOS,DEMIS,THMCUM,SUMTMS,BBG,RFSURF

!     COMMONS:
!       SLIT     SAVED CONVOLVED SPECTRAL DATA, TRIANGULAR SLIT.
      REAL SLIT
      COMMON/TRIAN2/SLIT(0:MWGT,0:MOUT,1:MLOS)
      SAVE /TRIAN2/

!     LOCAL ARRAYS:
!       TRIDAT   TRANSMITTANCE DATA TEMPORARY STORAGE.
      REAL TRIDAT(0:6)

!     STORE DATA:
      TRIDAT(0)=VCEN
      TRIDAT(1)=TRNLOS
      TRIDAT(2)=DEMIS
      TRIDAT(3)=THMCUM
      TRIDAT(4)=SUMTMS
      TRIDAT(5)=BBG
      TRIDAT(6)=RFSURF

!     PROCESS DATA:
      CALL DOSLIT(6,TRIDAT,SLIT(0,0,ILOS))

!     ASSIGNED CONVOLVED DATA:
      VCEN=TRIDAT(0)
      TRNLOS=TRIDAT(1)
      DEMIS=TRIDAT(2)
      THMCUM=TRIDAT(3)
      SUMTMS=TRIDAT(4)
      BBG=TRIDAT(5)
      RFSURF=TRIDAT(6)

!     RETURN TO TRANS:
      RETURN
      END

      SUBROUTINE R_SLIT(ILOS,VCEN,TRNLOS,DEMIS,THMCUM,SUMTMS,BBG,       &
     &  SUMSSR,SUMSSS,RFSURF,RFLSS,TSNREF,TSNOBS,S0)

!     R_SLIT STORES THERMAL PLUS SOLAR DATA FOR TRIANGULAR SLIT
!     CONVOLUTION.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     INPUT ARGEMENTS:
!       ILOS     LOOP INDEX FOR LINE-OF-SIGHT PATHS.
!       VCEN     SPECTRAL BAND CENTRAL FREQUENCY [CM-1].
!       TRNLOS   LINE-OF-SIGHT COMBINED SPECIES TRANSMITTANCE [TX(9)].
!       DEMIS    DIRECTIONAL EMISSIVITY TO SENSOR.
!       THMCUM   PATH & SCATTERED THERMAL EMISSION [W SR-1 CM-2 / CM-1].
!       SUMTMS   ISAACS SCATTERED THERMAL EMISSION [W SR-1 CM-2 / CM-1].
!       BBG      TRANSMITTED GROUND EMISSION [W SR-1 CM-2 / CM-1].
!       SUMSSR   PATH SCATTERED SOLAR [W SR-1 CM-2 / CM-1].
!       SUMSSS   PATH SINGLE SCATTERED SOLAR [W SR-1 CM-2 / CM-1].
!       RFSURF   TRANSMITTED SURFACE REFLECTANCE [W SR-1 CM-2 / CM-1].
!       RFLSS    SUN-SURFACE-SENSOR DIRECT RADIANCE[W SR-1 CM-2 / CM-1].
!       TSNREF   SENSOR-FINAL_ALTITUDE-SUN TRANSMITTANCE CONVOLVED WITH
!                THE TOP-OF-ATMOSPHERE SOLAR IRRADIANCE [W CM-2 / CM-1].
!       TSNOBS   TRANSMITTED SOLAR IRRADIANCE AT SENSOR [W CM-2 / CM-1].
!       S0       TOP-OF-ATMOSPHERE SOLAR IRRADIANCE [W CM-2 / CM-1].
      INTEGER ILOS
      REAL VCEN,TRNLOS,DEMIS,THMCUM,SUMTMS,BBG,                         &
     &  SUMSSR,SUMSSS,RFSURF,RFLSS,TSNREF,TSNOBS,S0

!     COMMONS:
!       SLIT     SAVED CONVOLVED SPECTRAL DATA, TRIANGULAR SLIT.
      REAL SLIT
      COMMON/TRIAN2/SLIT(0:MWGT,0:MOUT,1:MLOS)
      SAVE /TRIAN2/

!     LOCAL ARRAYS:
!       TRIDAT   TRANSMITTANCE DATA TEMPORARY STORAGE.
      REAL TRIDAT(0:12)

!     STORE DATA:
      TRIDAT(0)=VCEN
      TRIDAT(1)=TRNLOS
      TRIDAT(2)=DEMIS
      TRIDAT(3)=THMCUM
      TRIDAT(4)=SUMTMS
      TRIDAT(5)=BBG
      TRIDAT(6)=SUMSSR
      TRIDAT(7)=SUMSSS
      TRIDAT(8)=RFSURF
      TRIDAT(9)=RFLSS
      TRIDAT(10)=TSNREF
      TRIDAT(11)=TSNOBS
      TRIDAT(12)=S0

!     PROCESS DATA:
      CALL DOSLIT(12,TRIDAT,SLIT(0,0,ILOS))

!     ASSIGNED CONVOLVED DATA:
      VCEN=TRIDAT(0)
      TRNLOS=TRIDAT(1)
      DEMIS=TRIDAT(2)
      THMCUM=TRIDAT(3)
      SUMTMS=TRIDAT(4)
      BBG=TRIDAT(5)
      SUMSSR=TRIDAT(6)
      SUMSSS=TRIDAT(7)
      RFSURF=TRIDAT(8)
      RFLSS=TRIDAT(9)
      TSNREF=TRIDAT(10)
      TSNOBS=TRIDAT(11)
      S0=TRIDAT(12)

!     RETURN TO TRANS:
      RETURN
      END
