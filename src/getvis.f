      REAL FUNCTION GETVIS(AOD,GNDALT,SUMMER,VOLCAN,IPR)

!     GETVIS RETURNS SURFACE METEOROLOGICAL RANGE (VISIBILITY) FOR THE
!     INPUT AEROSOL MODEL AND VERTICAL COLUMN 550 NM OPTICAL DEPTH.
      IMPLICIT NONE

!     PARAMETERS:
!       VIS1     FIRST  VISIBILITY GRID POINT [ 5KM].
!       VIS2     SECOND VISIBILITY GRID POINT [10KM].
!       VIS3     THIRD  VISIBILITY GRID POINT [23KM].
!       VIS4     MAXIMUM VISIBILITY (AEROSOL DENSITY<0 IF HIGHER) [KM].
!       OFF0S    UNDER 6KM SUMMER 550NM DEPTH OFFSET FOR      VIS<VIS1.
!       OFF0W    UNDER 6KM WINTER 550NM DEPTH OFFSET FOR      VIS<VIS1.
!       OFF1S    UNDER 6KM SUMMER 550NM DEPTH OFFSET FOR VIS1<VIS<VIS2.
!       OFF1W    UNDER 6KM WINTER 550NM DEPTH OFFSET FOR VIS1<VIS<VIS2.
!       OFF2S    UNDER 6KM SUMMER 550NM DEPTH OFFSET FOR VIS2<VIS<VIS3.
!       OFF2W    UNDER 6KM WINTER 550NM DEPTH OFFSET FOR VIS2<VIS<VIS3.
!       OFF3S    UNDER 6KM SUMMER 550NM DEPTH OFFSET FOR VIS3<VIS.
!       OFF3W    UNDER 6KM WINTER 550NM DEPTH OFFSET FOR VIS3<VIS.
!       HCOEF0   HALF UNDER 6KM 550NM DEPTH COEF FOR VIS<VIS1 [KM].
!       COEF1    UNDER 6KM 550NM DEPTH COEF FOR VIS1<VIS<VIS2 [KM].
!       COEF2    UNDER 6KM 550NM DEPTH COEF FOR VIS2<VIS<VIS3 [KM].
!       COEF3S   UNDER 6KM SUMMER 550NM DEPTH COEF FOR VIS3<VIS [KM].
!       COEF3W   UNDER 6KM WINTER 550NM DEPTH COEF FOR VIS3<VIS [KM].
!       BCKGND   INDEX FOR BACKGROUND STRATOSPHERIC VOLCANIC AEROSOLS.
!       MODER    INDEX FOR MODERATE STRATOSPHERIC VOLCANIC AEROSOLS.
!       HIGH     INDEX FOR HIGH STRATOSPHERIC VOLCANIC AEROSOLS.
!       XTREME   INDEX FOR EXTREME STRATOSPHERIC VOLCANIC AEROSOLS.
!       BCKSUM   ABOVE 6KM SUMMER BACKGROUND 550NM VERTICAL DEPTH.
!       MODSUM   ABOVE 6KM SUMMER MODERATE   550NM VERTICAL DEPTH.
!       HISUM    ABOVE 6KM SUMMER HIGH       550NM VERTICAL DEPTH.
!       XTRSUM   ABOVE 6KM SUMMER EXTREME    550NM VERTICAL DEPTH.
!       BCKWIN   ABOVE 6KM WINTER BACKGROUND 550NM VERTICAL DEPTH.
!       MODWIN   ABOVE 6KM WINTER MODERATE   550NM VERTICAL DEPTH.
!       HIWIN    ABOVE 6KM WINTER HIGH       550NM VERTICAL DEPTH.
!       XTRWIN   ABOVE 6KM WINTER EXTREME    550NM VERTICAL DEPTH.
      INTEGER BCKGND,MODER,HIGH,XTREME
      REAL VIS1,VIS2,VIS3,OFF0S,OFF0W,OFF1S,OFF1W,                      &
     &  OFF2S,OFF2W,OFF3S,OFF3W,HCOEF0,COEF1,COEF2,COEF3S,COEF3W,       &
     &  BCKSUM,MODSUM,HISUM,XTRSUM,BCKWIN,MODWIN,HIWIN,XTRWIN,          &
     &  VIS4,U6SUM4,U6WIN4,U6SUM3,U6WIN3,U6SUM2,U6WIN2,U6SUM1,U6WIN1
      PARAMETER(  OFF0S=.190806, OFF0W=.170150, HCOEF0=2.390760,        &
     &  VIS1= 5., OFF1S=.147658, OFF1W=.127002, COEF1=4.997261,         &
     &  VIS2=10., OFF2S=.035200, OFF2W=.014544, COEF2=6.121839,         &
     &  VIS3=23., OFF3S=-.002909594,            COEF3S=6.9983467,       &
     &            OFF3W=-.010791,               COEF3W=6.704554,        &
     &  BCKGND=1,MODER=2,HIGH=3,XTREME=8,                               &
     &  BCKSUM=.02337204,MODSUM=.044187,HISUM=.111495,XTRSUM=.280859,   &
     &  BCKWIN=.014820,MODWIN=.034590,HIWIN=.082906,XTRWIN=.266033,     &
     &  VIS4=323.5775,U6SUM4=OFF3S+COEF3S/VIS4,U6WIN4=OFF3W+COEF3W/VIS4,&
     &                U6SUM3=OFF3S+COEF3S/VIS3,U6WIN3=OFF3W+COEF3W/VIS3,&
     &                U6SUM2=OFF2S+COEF2 /VIS2,U6WIN2=OFF2W+COEF2 /VIS2,&
     &                U6SUM1=OFF1S+COEF1 /VIS1,U6WIN1=OFF1W+COEF1 /VIS1)

!     INPUT ARGUMENTS:
!       AOD      550 NM VERTICAL COLUMN AEROSOL OPTICAL DEPTH.
!       GNDALT   GROUND ALTITUDE (<6.) [KM].
!       SUMMER   TRUE FOR SPRING/SUMMER.
!       VOLCAN   VOLCANIC AEROSOL MODEL NUMBER.
!       IPR      FILE UNIT NUMBER FOR WRITING WARNINGS.
      REAL AOD
      DOUBLE PRECISION GNDALT
      LOGICAL SUMMER
      INTEGER VOLCAN,IPR

!     LOCAL VARIABLES:
!       UNDER6   GROUND TO 6KM 550NM VERTICAL AEROSOL OPTICAL DEPTH.
!       VIS0     LOWER BOUND OF VISIBILITY [KM].
!       AODMIN   MINIMUM 550 NM VERTICAL COLUMN AEROSOL OPTICAL DEPTH.
      REAL UNDER6,VIS0,AODMIN

!     FUNCTIONS:
!       BI6VIS   USES BISECTION METHOD TO FIND VISIBILITY MATCHING AOD.
      REAL BI6VIS

!     DATA:
!       VOLEXT   VOLCANIC AEROSOL EXTINCTION MODEL:
      INTEGER VOLEXT(0:8)
      DATA VOLEXT/BCKGND,BCKGND,MODER,HIGH,HIGH,MODER,MODER,HIGH,XTREME/

!     BRANCH BASED ON SEASON FOR HIGHER ALTITUDE AEROSOLS:
      IF(SUMMER)THEN

!         BRANCH BASED ON VOLCANIC AEROSOL CONTENT:
          IF(VOLEXT(VOLCAN).EQ.BCKGND)THEN
              UNDER6=6*(AOD-BCKSUM)/SNGL(6-GNDALT)
          ELSEIF(VOLEXT(VOLCAN).EQ.MODER)THEN
              UNDER6=6*(AOD-MODSUM)/SNGL(6-GNDALT)
          ELSEIF(VOLEXT(VOLCAN).EQ.HIGH)THEN
              UNDER6=6*(AOD-HISUM)/SNGL(6-GNDALT)
          ELSEIF(VOLEXT(VOLCAN).EQ.XTREME)THEN
              UNDER6=6*(AOD-XTRSUM)/SNGL(6-GNDALT)
          ELSE
              STOP 'BAD VOLCAN VALUE'
          ENDIF
          IF(UNDER6.GT.U6SUM2)THEN
              IF(UNDER6.GT.U6SUM1)THEN
                  VIS0=HCOEF0/(UNDER6-OFF0S)
                  GETVIS=BI6VIS(4,GNDALT,.TRUE.,UNDER6,VIS0,VIS1)
              ELSE
                  GETVIS=BI6VIS(3,GNDALT,.TRUE.,UNDER6,VIS1,VIS2)
              ENDIF
          ELSEIF(UNDER6.GT.U6SUM3)THEN
              GETVIS=BI6VIS(2,GNDALT,.TRUE.,UNDER6,VIS2,VIS3)
          ELSEIF(UNDER6.GE.U6SUM4)THEN
              GETVIS=BI6VIS(1,GNDALT,.TRUE.,UNDER6,VIS3,VIS4)
          ELSE
              IF(VOLEXT(VOLCAN).EQ.BCKGND)THEN
                  AODMIN=SNGL(6-GNDALT)*U6SUM4/6+BCKSUM
              ELSEIF(VOLEXT(VOLCAN).EQ.MODER)THEN
                  AODMIN=SNGL(6-GNDALT)*U6SUM4/6+MODSUM
              ELSEIF(VOLEXT(VOLCAN).EQ.HIGH)THEN
                  AODMIN=SNGL(6-GNDALT)*U6SUM4/6+HISUM
              ELSEIF(VOLEXT(VOLCAN).EQ.XTREME)THEN
                  AODMIN=SNGL(6-GNDALT)*U6SUM4/6+XTRSUM
              ENDIF
              WRITE(IPR,'(/A,F10.5,A,/(22X,A,F10.5,A))')                &
     &          ' Warning from GETVIS:  The input value for aerosol'    &
     &          //' optical depth (AOD =',AOD,') is less than the',     &
     &          ' minimum AOD (=',AODMIN,') for the given atmospheric ' &
     &          //'conditions.  The surface',' meteorological range'    &
     &          //' (visibility) is being set to its maximum value (',  &
     &          VIS4,'km);',' the assumed AOD is the minimum value.'
              GETVIS=VIS4
          ENDIF
      ELSE
          IF(VOLEXT(VOLCAN).EQ.BCKGND)THEN
              UNDER6=6*(AOD-BCKWIN)/SNGL(6-GNDALT)
          ELSEIF(VOLEXT(VOLCAN).EQ.MODER)THEN
              UNDER6=6*(AOD-MODWIN)/SNGL(6-GNDALT)
          ELSEIF(VOLEXT(VOLCAN).EQ.HIGH)THEN
              UNDER6=6*(AOD-HIWIN)/SNGL(6-GNDALT)
          ELSEIF(VOLEXT(VOLCAN).EQ.XTREME)THEN
              UNDER6=6*(AOD-XTRWIN)/SNGL(6-GNDALT)
          ELSE
              STOP 'BAD VOLCAN VALUE'
          ENDIF
          IF(UNDER6.GT.U6WIN2)THEN
              IF(UNDER6.GT.U6WIN1)THEN
                  VIS0=HCOEF0/(UNDER6-OFF0W)
                  GETVIS=BI6VIS(4,GNDALT,.FALSE.,UNDER6,VIS0,VIS1)
              ELSE
                  GETVIS=BI6VIS(3,GNDALT,.FALSE.,UNDER6,VIS1,VIS2)
              ENDIF
          ELSEIF(UNDER6.GT.U6WIN3)THEN
              GETVIS=BI6VIS(2,GNDALT,.FALSE.,UNDER6,VIS2,VIS3)
          ELSEIF(UNDER6.GE.U6WIN4)THEN
              GETVIS=BI6VIS(1,GNDALT,.FALSE.,UNDER6,VIS3,VIS4)
          ELSE
              IF(VOLEXT(VOLCAN).EQ.BCKGND)THEN
                  AODMIN=SNGL(6-GNDALT)*U6WIN4/6+BCKWIN
              ELSEIF(VOLEXT(VOLCAN).EQ.MODER)THEN
                  AODMIN=SNGL(6-GNDALT)*U6WIN4/6+MODWIN
              ELSEIF(VOLEXT(VOLCAN).EQ.HIGH)THEN
                  AODMIN=SNGL(6-GNDALT)*U6WIN4/6+HIWIN
              ELSEIF(VOLEXT(VOLCAN).EQ.XTREME)THEN
                  AODMIN=SNGL(6-GNDALT)*U6WIN4/6+XTRWIN
              ENDIF
              WRITE(IPR,'(/A,F10.5,A,/(22X,A,F10.5,A))')                &
     &          ' Warning from GETVIS:  The input value for aerosol'    &
     &          //' optical depth (AOD =',AOD,') is less than the',     &
     &          ' minimum AOD (=',AODMIN,') for the given atmospheric ' &
     &          //'conditions.  The surface',' meteorological range'    &
     &          //' (visibility) is being set to its maximum value (',  &
     &          VIS4,'km);',' the assumed AOD is the minimum value.'
              GETVIS=VIS4
          ENDIF
      ENDIF
      RETURN
      END

      REAL FUNCTION BI6VIS(IVIS,GNDALT,SUMMER,UNDER6,VISMIN,VISMAX)

!     BI6VIS USES THE BISECTION METHOD TO DETERMINE THE VISIBILITY
!     BETWEEN VISMIN AND VISMAX THAT PRODUCES A GROUND TO 6KM VERTICAL
!     550nm AEROSOL OPTICAL DEPTH (AOD) EQUAL TO THE "UNDER6" VALUE.
      IMPLICIT NONE

!     PARAMETERS:
!       VISTOL   VISIBILITY TOLERANCE [KM].
      REAL VISTOL
      PARAMETER(VISTOL=.0001)

!     INPUT ARGUMENTS:
!       IVIS     VISIBILITY RANGE INDEX (1 FOR VIS>23KM; 4 FOR VIS<5KM).
!       GNDALT   GROUND ALTITUDE (<6.) [KM].
!       SUMMER   TRUE FOR SPRING/SUMMER.
!       UNDER6   GROUND TO 6KM 550NM VERTICAL AEROSOL OPTICAL DEPTH.
!       VISMIN   VISIBILITY (SURFACE METEOROLOGICAL RANGE) MINIMUM [KM].
!       VISMAX   VISIBILITY (SURFACE METEOROLOGICAL RANGE) MAXIMUM [KM].
      INTEGER IVIS
      DOUBLE PRECISION GNDALT
      REAL UNDER6,VISMIN,VISMAX
      LOGICAL SUMMER

!     LOCAL VARIABLES:
!       VISMN    CURRENT VISIBILITY MINIMUM [KM].
!       VISMX    CURRENT VISIBILITY MAXIMUM [KM].
!       U6       UNDER 6KM 550NM AEROSOL OPTICAL DEPTH BISECTION VALUE.
      REAL VISMN,VISMX,U6

!     FUNCTIONS:
!       GTAOD6   RETURNS GROUND TO 6KM 550NM AEROSOL OPTICAL DEPTH.
      REAL GTAOD6

!     SET LOCAL VISIBILITY EXTREMA:
      VISMN=VISMIN
      VISMX=VISMAX

!     TOLERANCE TEST:
   10 CONTINUE
      BI6VIS=(VISMN+VISMX)/2
      IF(VISMX-VISMN.LT.VISTOL)RETURN

!     BISECTION VALUE FOR UNDER 6km 550nm AEROSOL OPTICAL DEPTH:
      U6=GTAOD6(IVIS,BI6VIS,GNDALT,SUMMER)

!     COMPARE U6 TO UNDER6, BRANCH ACCORDING & RETURN TO TOLERANCE TEST:
      IF(U6.GT.UNDER6)THEN
          VISMN=BI6VIS
      ELSE
          VISMX=BI6VIS
      ENDIF
      GOTO 10
      END

      REAL FUNCTION GTAOD6(IVIS,VIS,GNDALT,SUMMER)

!     GTAOD6 RETURNS THE VERTICAL AEROSOL OPTICAL DEPTH FROM THE
!     GROUND TO 6KM ALTITUDE FOR A GIVEN VISIBILITY AND SEASON.
      IMPLICIT NONE

!     INPUT ARGUMENTS:
!       IVIS     VISIBILITY RANGE INDEX (1 FOR VIS>23KM; 4 FOR VIS<5KM).
!       VIS      VISIBILITY (SURFACE METEOROLOGICAL RANGE) [KM].
!       GNDALT   GROUND ALTITUDE (<6.) [KM].
!       SUMMER   TRUE FOR SPRING/SUMMER.
      INTEGER IVIS
      REAL VIS
      DOUBLE PRECISION GNDALT
      LOGICAL SUMMER

!     COMMONS:

!     /PRFD/
!       ZHT     ALTITUDE GRID FOR AEROSOL PROFILE DATA [KM].
!       HZ2K    0-3KM HAZE W/ 50, 23, 10, 5 & 2 KM VIS [550NM EXT/KM].
!       FAWI50  2-11KM  FALL/WINTER  PROFILE W/ 50KM VIS [550NM EXT/KM].
!       FAWI23  2-11KM  FALL/WINTER  PROFILE W/ 23KM VIS [550NM EXT/KM].
!       SPSU50  2-11KM SPRING/SUMMER PROFILE W/ 50KM VIS [550NM EXT/KM].
!       SPSU23  2-11KM SPRING/SUMMER PROFILE W/ 23KM VIS [550NM EXT/KM].
!       BASTFW  10-35KM  FALL/WINTER  BACKGROUND [550NM EXT/KM].
!       VUMOFW  10-35KM  FALL/WINTER  MODERATE VOLCANIC [550NM EXT/KM].
!       HIVUFW  10-35KM  FALL/WINTER  HIGH VOLCANIC [550NM EXT/KM].
!       EXVUFW  10-35KM  FALL/WINTER  EXTREME VOLCANIC [550NM EXT/KM].
!       BASTSS  10-35KM SPRING/SUMMER BACKGROUND [550NM EXT/KM].
!       UPNATM  >30KM NORMAL UPPER ATMOSPHERIC PROFILE [550NM EXT/KM].
!       VUTONO  >30KM VOLCANIC TO NORMAL TRANSITION [550NM EXT/KM].
      REAL ZHT,HZ2K,FAWI50,FAWI23,SPSU50,SPSU23,BASTFW,VUMOFW,          &
     &  HIVUFW,EXVUFW,BASTSS,VUMOSS,HIVUSS,EXVUSS,UPNATM,VUTONO
      COMMON/PRFD/ZHT(34),HZ2K(34,5),                                   &
     &  FAWI50(34),FAWI23(34),SPSU50(34),SPSU23(34),                    &
     &  BASTFW(34),VUMOFW(34),HIVUFW(34),EXVUFW(34),BASTSS(34),         &
     &  VUMOSS(34),HIVUSS(34),EXVUSS(34),UPNATM(34),VUTONO(34)

!     LOCAL VARIABLES:
!       IVISP1   IVIS PLUS 1.
!       VISFAC   VISIBILITY INTERPOLATION FACTOR.
!       DEN0     BOUNDARY LAYER AEROSOL DENSITY NOMINALLY AT 0KM [KM-1].
!       DEN1     BOUNDARY LAYER AEROSOL DENSITY NOMINALLY AT 1KM [KM-1].
!       DEN2     BOUNDARY LAYER AEROSOL DENSITY NOMINALLY AT 2KM [KM-1].
!       DEN3     TROPOSPHERIC AEROSOL DENSITY NOMINALLY AT 3KM [KM-1].
!       DEN4     TROPOSPHERIC AEROSOL DENSITY NOMINALLY AT 4KM [KM-1].
!       DEN5     TROPOSPHERIC AEROSOL DENSITY NOMINALLY AT 5KM [KM-1].
!       DEN6     TROPOSPHERIC AEROSOL DENSITY NOMINALLY AT 6KM [KM-1].
      INTEGER IVISP1
      REAL VISFAC,DEN0,DEN1,DEN2,DEN3,DEN4,DEN5,DEN6

!     FUNCTIONS:
!       DENLAY   RETURNS VERTICAL LAYER AVERAGE COLUMN AMOUNT.
      REAL DENLAY

!     DATA [ AEROSOL DENSITY = ( ANUMER / VIS - AOFFST ) / ADENOM ]:
!       INUMER   AEROSOL DENSITY INTERPOLATION NUMERATOR [KM].
!       IOFFST   AEROSOL DENSITY INTERPOLATION OFFSET.
!       IDENOM   AEROSOL DENSITY INTERPOLATION DENOMINATION.
      INTEGER INUMER(4),IOFFST(4),IDENOM(4)
      DATA INUMER/1150,230,10,10/,IOFFST/23,10,1,2/,IDENOM/27,13,1,3/

!     VISIBILITY INTERPOLATION FACTOR:
      IVISP1=IVIS+1
      VISFAC=(INUMER(IVIS)/VIS-IOFFST(IVIS))/IDENOM(IVIS)

!     BOUNDARY LAYER AEROSOL DENSITIES:
      DEN0=HZ2K(1,IVIS)+(HZ2K(1,IVISP1)-HZ2K(1,IVIS))*VISFAC
      DEN1=HZ2K(2,IVIS)+(HZ2K(2,IVISP1)-HZ2K(2,IVIS))*VISFAC
      DEN2=HZ2K(3,IVIS)+(HZ2K(3,IVISP1)-HZ2K(3,IVIS))*VISFAC

!     BOUNDARY LAYER AEROSOL DENSITY INTEGRAL [KM-1]:
      GTAOD6=DENLAY(DEN0,DEN1)+DENLAY(DEN1,DEN2)+DEN2/2

!     TROPOSPHERIC AEROSOL DENSITIES:
      IF(SUMMER)THEN

!         SPRING/SUMMER:
          IF(IVIS.EQ.1)THEN

!             INTERPOLATE FOR ALTITUDES UP TO 4KM AND VISIBILITY > 23KM:
              DEN3=SPSU50(4)+(SPSU23(4)-SPSU50(4))*VISFAC
              DEN4=SPSU50(5)+(SPSU23(5)-SPSU50(5))*VISFAC
          ELSE
              DEN3=SPSU23(4)
              DEN4=SPSU23(5)
          ENDIF
          DEN5=SPSU23(6)
          DEN6=SPSU23(7)
      ELSE

!         FALL/WINTER
          IF(IVIS.EQ.1)THEN

!             INTERPOLATE FOR ALTITUDES UP TO 4KM AND VISIBILITY > 23KM:
              DEN3=FAWI50(4)+(FAWI23(4)-FAWI50(4))*VISFAC
              DEN4=FAWI50(5)+(FAWI23(5)-FAWI50(5))*VISFAC
          ELSE
              DEN3=FAWI23(4)
              DEN4=FAWI23(5)
          ENDIF
          DEN5=FAWI23(6)
          DEN6=FAWI23(7)
      ENDIF

!     GROUND TO 6KM VERTICAL AEROSOL OPTICAL DEPTH:
      GTAOD6=SNGL(1-GNDALT/6)*(GTAOD6                                   &
     &  +DEN3/2+DENLAY(DEN3,DEN4)+DENLAY(DEN4,DEN5)+DENLAY(DEN5,DEN6))
      RETURN
      END
