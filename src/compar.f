      LOGICAL FUNCTION COMPAR(IPARM,IPH,IDAY,ISOURC,PARM1,              &
     &  PARM2,SRCLAT,SRCLON,GMTIME,G,ANGLEM,IPRMSV,IPHSV,IDAYSV,        &
     &  ISRCSV,PRM1SV,PRM2SV,SLATSV,SLONSV,GMTSV,GSV,ANGMSV)

!     LOGICAL FUNCTION COMPAR IS TRUE IF SOLAR MULTIPLE
!     SCATTERING CALCULATIONS DO NOT HAVE TO BE REDONE.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     IPARM, IPH, ETC. ARE THE CURRENT SOLARPARAMETERS.  IPRMSV, IPHSV,
!     ETC. ARE THE SOLAR PARAMETERS OF THE IMMEDIATELY PRECEDING RUN.
      INTEGER IPARM,IDAY,IPH,ISOURC,IPRMSV,IPHSV,IDAYSV,ISRCSV
      REAL G,GSV
      DOUBLE PRECISION PARM1,PARM2,SRCLAT,SRCLON,GMTIME,ANGLEM,         &
     &  PRM1SV,PRM2SV,SLATSV,SLONSV,GMTSV,ANGMSV

!     INITIALIZE TO FALSE
      COMPAR=.FALSE.

!     CHECK PHASE FUNCTION TYPE, EXTRA-TERRESTRIAL SOURCE,
!     DAY OF YEAR AND SOLAR/LUNAR SPECIFICATION FLAG.
      IF(IPH.NE.IPHSV .OR. ISOURC.NE.ISRCSV .OR.                        &
     &  IDAYSV.NE.IDAY .OR. IPRMSV.NE.IPARM)RETURN

!     THE ASYMMETRY FACTOR G IS ONLY USED WHEN THE HENYEY-GREENSTEIN
!     SCATTERING PHASE FUNCTION IS SELECTED, IPH=0.
      IF(IPH.EQ.0 .AND. G.NE.GSV)RETURN

!     THE LUNAR PHASE IS ONLY USED WITH THE LUNAR SOURCE.
      IF(ISOURC.EQ.1 .AND. ANGMSV.NE.ANGLEM)RETURN

!     PARM2 IS ALWAYS USED:
      IF(PARM2.NE.PRM2SV)RETURN

!     CHECK FOR EPHERMERAL SPECIFICATION:
      IF(IPARM.NE.2 .AND. IPARM.NE.12)THEN

!         CHECK PARM1:
          IF(PARM1.NE.PRM1SV)RETURN

!         BRANCH BASED ON SOLAR/LUNAR SPECIFICATION:
          IF(IPARM.EQ.0 .OR. IPARM.EQ.10)THEN

!             CHECK SOLAR/LUNAR LATITUDE AND LONGITUDE:
              IF(SRCLAT.NE.SLATSV .OR. SRCLON.NE.SLONSV)RETURN
          ELSEIF(IPARM.EQ.1 .OR. IPARM.EQ.11)THEN

!             CHECK TIME:
              IF(GMTIME.NE.GMTSV)RETURN
          ELSE

!             INVALID IPARM
              IF(LJMASS)CALL WRTBUF(FATAL)
              STOP 'ERROR IN COMPAR:  Invalid IPARM'
          ENDIF
      ENDIF
      COMPAR=.TRUE.
      RETURN
      END
