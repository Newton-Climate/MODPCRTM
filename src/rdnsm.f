      SUBROUTINE RDNSM

!     RDNSM READS MODEL 7 DATA WHEN IVSA EQ 1
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     COMMONS:
      INCLUDE 'IFIL.h'

!     /CARD1/
!       MODEL    MODEL ATMOSPHERE INDEX.
!       ITYPE    SLANT PATH TYPE.
!       IEMSCT   RADIATIVE TRANSFER MODE.
!                  0 FOR TRANSMITTANCE
!                  1 FOR THERMAL EMISSION ONLY
!                  2 FOR THERMAL EMISSION PLUS SOLAR SCATTER
!                  3 FOR TRANSMITTED SOLAR IRRADIANCE
!                  4 FOR SOLAR SCATTER ONLY
!       M1       MODEL ATMOSPHERE FOR PRESSURE & TEMPERATURE PROFILES.
!       M2       MODEL ATMOSPHERE FOR H2O PROFILE.
!       M3       MODEL ATMOSPHERE FOR O3 PROFILE.
!       I_RD2C   READ CARD 2C, 2C1, ... IF EQUAL 1; SKIP IF EQUAL TO 0.
!       NOPRNT   PRINT FLAG.
!       MODTRN   MODTRAN BAND MODEL FLAG.
      INTEGER MODEL,ITYPE,IEMSCT,M1,M2,M3,I_RD2C,NOPRNT
      LOGICAL MODTRN
      COMMON/CARD1/MODEL,ITYPE,IEMSCT,M1,M2,M3,I_RD2C,NOPRNT,MODTRN

!     /CARD1A/
      INTEGER M4,M5,M6,MDEF,IRD1,IRD2
      COMMON/CARD1A/M4,M5,M6,MDEF,IRD1,IRD2

!     /CARD1B/
      INTEGER JUNITP,JUNITT,JUNIT,JLOW
      REAL WMOL
      COMMON/CARD1B/JUNITP,JUNITT,JUNIT(13),WMOL(12),JLOW

!     /CARD2/
!       IHAZE    BOUNDARY LAYER AEROSOL MODEL NUMBER.
!       ISEASN   SEASON NUMBER (1=SPRING-SUMMER, 2=FALL-WINTER).
!       IVULCN   VOLCANIC AEROSOL MODEL NUMBER.
!       ICSTL    COASTAL AIRMASS MODEL NUMBER.
!       ICLD     CLOUD MODEL NUMBER.
!       IVSA     VERTICAL STRUCTURE ALGORITHM (0=OFF, 1=ON).
!       VIS      SURFACE VISIBILITY (GROUND METEOROLOGICAL RANGE) [KM].
!       WSS      CURRENT WIND SPEED (M/S).
!       WHH      24-HOUR WIND SPEED (M/S).
!       RAINRT   RAIN RATE (MM/HR)
!       LSAP     LOGICAL FLAG FOR SPECTRAL AEROSOL PROFILES INPUT.
      INTEGER IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA
      REAL VIS,WSS,WHH,RAINRT
      LOGICAL LSAP
      COMMON/CARD2/IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA,                 &
     &  VIS,WSS,WHH,RAINRT,LSAP

!     /CNTRL/
!       NSEG     NUMBER OF PATH SEGMENTS ALONG LINE-OF-SIGHT.
!       ML       NUMBER OF ATMOSPHERIC PROFILE LEVELS.
!       MLFLX    NUMBER OF LEVELS FOR WHICH FLUX VALUES ARE WRITTEN.
!       IMULT    MULTIPLE SCATTERING FLAG
!                  (0=NONE, 1=AT SENSOR, -1=AT FINAL OR TANGENT POINT).
!       THERML   FLAG TO CALCULATE THERMAL SCATTER.
      INTEGER NSEG,ML,MLFLX,IMULT
      LOGICAL THERML
      COMMON/CNTRL/NSEG(0:MLOSP1),ML,MLFLX,IMULT,THERML

!     /NSINP/
      DOUBLE PRECISION ZNSINP
      REAL PNSINP,TNSINP,WNSINP
      COMMON/NSINP/ZNSINP(40),PNSINP(40),TNSINP(40),WNSINP(40,12)

!     FUNCTIONS:
!       JOU      TRANSLATE UNIT SPECIFIER CHARACTER INTO INTEGER LABEL.
      INTEGER JOU

!     LOCAL VARIABLES:
      INTEGER LEVEL,KM,IRD0,IHA1,ICLD1,IVUL1,ISEA1
      REAL AHAZE,EQLWCZ,RRATZ

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      EXTERNAL DEVCBD
      CHARACTER JCHAR*1
      DIMENSION  JCHAR(15)
      IF(ML.GT.24)THEN
          WRITE(IPR,'(A,I5,A)')'  ML = ',ML,'  GT 24 ML RESET TO 24'
          ML=24
      ENDIF
      JLOW=1
      DO LEVEL=1,ML
          DO 10 KM=1,15
              JCHAR(KM)=' '
              IF(KM.LE.12)WMOL(KM)=0.
   10     CONTINUE
          IRD0=1
          IF(MODEL.GT.0 .AND. MODEL.LT.7)IRD0=0
          IF(IRD0.EQ.1 .AND. IVSA.EQ.1)IRD0=0

!        PARAMETERS - JCHAR = INPUT KEY (SEE BELOW)

!     ***  ROUTINE ALSO ACCEPTS VARIABLE UNITS ON PRESS AND TEMP

!          SEE INPUT KEY BELOW

!     FOR MOLECULAR SPECIES ONLY

!       JCHAR   JUNIT

!     " ",A      10    VOLUME MIXING RATIO (PPMV)
!         B      11    NUMBER DENSITY (CM-3)
!         C      12    MASS MIXING RATIO (GM(K)/KG(AIR))
!         D      13    MASS DENSITY (GM M-3)
!         E      14    PARTIAL PRESSURE (MB)
!         F      15    DEW POINT TEMP (TD IN KELVIN) - H2O ONLY
!         G      16     "    "     "  (TD IN CELSIUS) - H2O ONLY
!         H      17    RELATIVE HUMIDITY (RH IN PERCENT) - H2O ONLY
!         I      18    AVAILABLE FOR USER DEFINITION
!        1-6    1-6    DEFAULT TO SPECIFIED MODEL ATMOSPHERE

!     ****************************************************************
!     ****************************************************************

!     ***** OTHER 'JCHAR' SPECIFICATIONS -

!       JCHAR   JUNIT

!      " ",A     10    PRESSURE IN (MB)
!          B     11       "     "  (ATM)
!          C     12       "     "  (TORR)
!         1-6   1-6    DEFAULT TO SPECIFIED MODEL ATMOSPHERE

!      " ",A     10    AMBIENT TEMPERATURE IN DEG(K)
!          B     11       "         "       "  " (C)
!          C     12       "         "       "  " (F)
!         1-6   1-6    DEFAULT TO SPECIFIED MODEL ATMOSPHERE

!     ***** DEFINITION OF "DEFAULT" CHOICES FOR PROFILE SELECTION *****

!      FOR THE USER WHO WISHES TO ENTER ONLY SELECTED ORIGINAL
!      VERTICAL PROFILES AND WANTS STANDARD ATMOSPHERE SPECIFICATIONS
!      FOR THE OTHERS, THE FOLLOWING OPTION IS AVAILABLE

!     *** JCHAR(P,T OR K) MUST = 1-6 (AS ABOVE)

!      FOR MOLECULES 8-35, ONLY US STD PROFILES ARE AVIALABLE
!      THEREFORE, WHEN  'JCHAR(K) = 1-5', JCHAR(K) WILL BE RESET TO 6

          IF(LJMASS)THEN
              CALL INITCD( 'CARD2C1' )
          ELSE
              READ(IRD,'(6F10.0,15A1)')                                 &
     &          ZNSINP(LEVEL),PNSINP(LEVEL),TNSINP(LEVEL),              &
     &          WMOL(1),WMOL(2),WMOL(3),(JCHAR(KM),KM=1,15)
              WRITE(IPR,'(F10.3,1P,5E10.3,10X,15A1)')                   &
     &          ZNSINP(LEVEL),PNSINP(LEVEL),TNSINP(LEVEL),              &
     &          WMOL(1),WMOL(2),WMOL(3),(JCHAR(KM),KM=1,15)
          ENDIF
          IF(ZNSINP(LEVEL).LE.2.D0)JLOW=LEVEL
          IF(IRD1.EQ.1)THEN
              IF(LJMASS)THEN
                  CALL INITCD( 'CARD2C2' )
              ELSE
                  READ(IRD,'((8F10.0))')(WMOL(KM),KM=4,12)
                  WRITE(IPR,'(1P,(8E10.3))')(WMOL(KM),KM=4,12)
              ENDIF
          ENDIF

!     AHAZE =  AEROSOL VISIBLE EXTINCTION COFF (KM-1)
!     AT A WAVELENGTH OF 0.55 MICROMETERS

!     EQLWCZ=LIQUID WATER CONTENT (PPMV) AT ALT Z
!            FOR AEROSOL, CLOUD OR FOG MODELS

!     RRATZ=RAIN RATE (MM/HR) AT ALT Z

!     IHA1 AEROSOL MODEL USED FOR SPECTRAL DEPENDENCE OF EXTINCTION

!     IVUL1 STRATOSPHERIC AEROSOL MODEL USED FOR SPECTRAL DEPENDENCE
!     OF EXT AT Z

!     ICLD1 CLOUD MODEL USED FOR SPECTRAL DEPENDENCE OF EXT AT Z

!     ONLY ONE OF IHA1,ICLD1  OR IVUL1 IS ALLOWED
!     IHA1 NE 0 OTHERS IGNORED
!     IHA1 EQ 0 AND ICLD1 NE 0 USE ICLD1

!     IF AHAZE AND EQLWCZ ARE BOTH ZERO
!        DEFAULT PROFILE ARE LOADED FROM IHA1,ICLD1,IVUL1
!     ISEA1 = AEROSOL SEASON CONTROL FOR ALTITUDE Z

          IF(IRD2.EQ.1)THEN
              IF(LJMASS)THEN
                  CALL INITCD( 'CARD2C3' )
              ELSE
                  READ(IRD,'(10X,3F10.0,4I5)')                          &
     &              AHAZE,EQLWCZ,RRATZ,IHA1,ICLD1,IVUL1,ISEA1
                  WRITE(IPR,'(10X,3F10.3,4I5)')                         &
     &              AHAZE,EQLWCZ,RRATZ,IHA1,ICLD1,IVUL1,ISEA1
             ENDIF
          ENDIF
          DO KM=1,13
              JUNIT(KM)=JOU(JCHAR(KM+2),6)
          ENDDO
          IF(M1.NE.0)THEN
              JUNITP=M1
              JUNITT=M1
          ELSE
              JUNITP=JOU(JCHAR(1),M1)
              JUNITT=JOU(JCHAR(2),M1)
          ENDIF
          CALL CHECKP(PNSINP(LEVEL),JUNITP)
          CALL CHECKT(TNSINP(LEVEL),JUNITT,M1)
          CALL DEFALT(ZNSINP(LEVEL),PNSINP(LEVEL),TNSINP(LEVEL))
          CALL JUBRAN(PNSINP(LEVEL),TNSINP(LEVEL))
          DO KM=1,12
              WNSINP(LEVEL,KM)=WMOL(KM)
          ENDDO
      ENDDO
      RETURN
      END
