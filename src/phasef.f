      REAL FUNCTION PHASEF(REGION,VCEN,PTHALT,SANGLE,RH)
      IMPLICIT NONE

!     INPUT ARGUMENTS:
!       VCEN     SPECTRAL BAND CENTRAL FREQUENCY [CM-1].
!       PTHALT   ALTITUDE ALONG LINE-OF-SIGHT [KM]
      DOUBLE PRECISION PTHALT
      REAL ALAM,ANG,COSANG,HENGNS,PF,PF11,PF12,PF21,PF22,               &
     &  PF_SAV,RH,RHPTS,SANGLE,VCEN,WAVE,YANG1,YANG2
      INTEGER I,I1,IANG1,IANG2,IRHHI,IRHLO,IWAV1,IWAV2,NN,NN0
      INCLUDE 'PARAMS.h'

!     RETURNS THE AEROSOL PHASE FUNCTION FROM THE STORED DATA BASE

!     THE TRUTH TABLE MNUM(27,26) STORED IN COMMON/MNMPHS/
!     IN SUBROUTINE PHSDTA IS QUERIED TO DETERMINE THE PROPER PHASE
!     FUNCTION NEEDED.
!     THE 27 POSITIONS REPRESENT THE 27 SPECIFIC FREQUENCIES SHOWN IN
!     DATA STATEMENT WAVE  .2-40 MICRONS.
!     THE NUMBERS STORED IN THESE 27 POSITIONS REPRESENT THE CORRECT
!     PHASE FUNCTIONS CHOSEN FROM THE DATA STATEMENT PHSFNC'S 1-70
!     POSSIBLE CHOICES.
!     THE 26 DATA STATEMENTS EACH HAVING 27 FREQUENCIES REPRESENT THE
!     FOLLOWING 26 MODELS;
!     1=RURAL     0%RH   2=RURAL    70%RH   3=RURAL    80%RH
!     4=RURAL    99%RH   5=MARITIME  0%RH   6=MARITIME 70%RH
!     7=MARITIME 80%RH   8=MARITIME 99%RH   9=URBAN     0%RH
!     10=URBAN    70%RH  11=URBAN    80%RH  12=URBAN    99%RH
!     13=OCEANIC   0%RH  14=OCEANIC  70%RH  15=OCEANIC  80%RH
!     16=OCEANIC  99%RH  17=TROPOSPH  0%RH  18=TROPOSPH 70%RH
!     19=TROPOSPH 80%RH  20=TROPOSPH 99%RH  21=STRATOSPHERIC
!     22=AGED VOLCANIC   23=FRESH VOLCANIC  24=RADIATION FOG
!     25=ADVECTIVE FOG   26=METEORIC DUST

!     IN THE PRESENT VERSION THE 4 OCEANIC MODELS 13-16
!     ARE NOT UTILIZED.

!     COMMONS:
      INCLUDE 'IFIL.h'

!     /RCNSTN/
!     PI       THE CONSTANT PI.
!     DEG      NUMBER OF DEGREES IN ONE RADIAN.
!     BIGNUM   MAXIMUM SINGLE PRECISION NUMBER.
!     BIGEXP   MAXIMUM EXPONENTIAL ARGUMENT WITHOUT OVERFLOW.
!     RRIGHT   SMALLEST SINGLE PRECISION REAL ADDED TO 1 EXCEEDS 1.
      REAL PI,DEG,BIGNUM,BIGEXP,RRIGHT
      COMMON/RCNSTN/PI,DEG,BIGNUM,BIGEXP,RRIGHT

!     REGION MOD:  REGION = 1,2,3 OR 4; DEFINES THE REGION OF THE AEROSOL
!     REGION MOD:  IS IT THE BOUNDARY LAYER? TROPOSPHERIC?
!     REGION MOD:  STRATOSPHERIC? MID-STRATO TO MESO TO THERMOSPHERIC?
!     REGION MOD:  THE ANSWER DEPENDS ON REGION.
      INTEGER REGION

!     /CARD2/
!       IHAZE    BOUNDARY LAYER AEROSOL MODEL NUMBER.
!       ISEASN   SEASON NUMBER (1=SPRING-SUMMER, 2=FALL-WINTER).
!       IVULCN   VOLCANIC AEROSOL MODEL NUMBER.
!       ICSTL    COASTAL AIRMASS MODEL NUMBER.
!       ICLD     CLOUD MODEL NUMBER.
!       IVSA     VERTICAL STRUCTURE ALGORITHM (0=OFF, 1=ON).
!       VIS      SURFACE VISIBILITY (GROUND METEOROLOGICAL RANGE) [KM].
!       WSS      CURRENT WIND SPEED (M/S).
!       WHH      24-HOUR WIND SPEED (M/S).
!       RAINRT   RAIN RATE (MM/HR)
!       LSAP     LOGICAL FLAG FOR SPECTRAL AEROSOL PROFILES INPUT.
      INTEGER IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA
      REAL VIS,WSS,WHH,RAINRT
      LOGICAL LSAP
      COMMON/CARD2/IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA,                 &
     &  VIS,WSS,WHH,RAINRT,LSAP

!     /CARD2D/
      INTEGER IREG,IREGC
      DOUBLE PRECISION ALTB
      COMMON/CARD2D/IREG(4),ALTB(4),IREGC(4)

!     /AER/ THERE ARE "MAER=17" PARTICULATE COMPONENTS:
!           1       AEROSOL 1 (NOMINALLY, BOUNDARY LAYER AEROSOL).
!           2       AEROSOL 2 (NOMINALLY, TROPOSPHERIC AEROSOL).
!           3       AEROSOL 3 (NOMINALLY, STRATOSPHERIC AEROSOL).
!           4       AEROSOL 4 (NOMINALLY, VOLCANIC AEROSOL).
!           5       CIRRUS CLOUD.
!           6       CLOUD 1 (NOMINALLY, WATER CLOUD).
!           7       CLOUD 2 (NOMINALLY, ICE CLOUD).
!           8-17    NOVAM (NAVY OCEANIC VERTICAL AEROSOL MODEL) LAYERS.
!       NAER     NUMBER OF ACTIVE AEROSOLS.
!       EXTV     SPECTRAL EXTINCTION (NORMALIZED TO 1 AT 550 NM).
!       ABSV     SPECTRAL ABSORPTION (1-ABSV/EXTV=SCATTERING ALBEDO).
!       ASYV     SPECTRAL LEGENDRE MOMENT (DIVIDED BY 2N+1).
!       FRAC5    5 CM-1 GRID SPECTRAL INTERPOLATION FRACTION.
!       ASYVLO   ASYMMETRY FACTOR FROM PREVIOUS SPECTRAL FREQUENCY.
      INTEGER NAER
      REAL EXTV,ABSV,ASYV,FRAC5,ASYVLO
      COMMON/AER/NAER,EXTV(MAER),ABSV(MAER),ASYV(MXCMU,MAER),           &
     &  FRAC5,ASYVLO(MAER)

!     FUNCTIONS:
!       INTEXP   EXPONENTIAL INTERPOLATION WITHOUT ARGUMENT CHECKING.
      REAL INTEXP

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      EXTERNAL DEVCBD
      DIMENSION RHPTS(4), WAVE(27), ANG(34)
      DATA ANG/0., 2., 4., 6., 8., 10., 12., 16., 20., 24., 28., 32.,   &
     &     36., 40., 50., 60., 70., 80., 90., 100., 110., 120., 125.,   &
     &     130., 135., 140., 145., 150., 155., 160., 165., 170., 175.,  &
     &     180./
      DATA WAVE/.2, .3, .55, .6943, 1.06, 1.536, 2.0, 2.5, 2.7, 3., 3.2,&
     &     3.39, 5., 6., 7.2, 7.9, 8.7, 9.2, 10.0, 10.59, 12.5, 15.0,   &
     &     17.2, 18.5, 21.3, 30., 40./
      DATA RHPTS/0., 70., 80., 99./
      PHASEF=0.
      ALAM=1.E4/(VCEN+1.E-5)
      IF(SANGLE.LT.0. .OR. SANGLE.GT.180.)THEN
          WRITE(IPR,'(/A,/A,F12.5,A)')                                  &
     &      ' Error in PHASEF:  Scattering angle is out of range.',     &
     &      '                   Scattering angle =',SANGLE,' degrees.'
          IF(LJMASS)CALL WRTBUF(FATAL)
          STOP ' Error in PHASEF:  Scattering angle is out of range.'
      ENDIF
      IF(ALAM.GT.WAVE(27))THEN
          COSANG=COS(SANGLE/DEG)
          PHASEF=HENGNS(ASYV(1,1),COSANG)
          IF(ABS(FRAC5).GT..001)                                        &
     &      PHASEF=PHASEF+FRAC5*(HENGNS(ASYVLO(1),COSANG)-PHASEF)
          RETURN
      ENDIF

!     REGION MOD:  THIS ROUTINE WAS MODIFIED SO THAT
!     REGION MOD:  PHASEF CALCULATED IS REGION-SPECIFIC
!     INITIALIZE NN:
      NN=-99

!     DETERMINE THE AEROSOL MODEL NUMBER:
      IF(REGION.EQ.1 .AND. PTHALT.LE.ALTB(1))THEN
          IF(IHAZE.EQ.0)RETURN
          IF(IHAZE.EQ.10)THEN
              COSANG=COS(SANGLE/DEG)
              PHASEF=HENGNS(ASYV(1,1),COSANG)
              IF(ABS(FRAC5).GT..001)                                    &
     &          PHASEF=PHASEF+FRAC5*(HENGNS(ASYVLO(1),COSANG)-PHASEF)
              RETURN
          ENDIF

!         CHECK IF CLOUD,RAIN OR DESERT MODEL IS REQUESTED:
          IF(IHAZE.EQ.7)THEN
              COSANG=COS(SANGLE/DEG)
              PHASEF=HENGNS(ASYV(1,1),COSANG)
              IF(ABS(FRAC5).GT..001)                                    &
     &          PHASEF=PHASEF+FRAC5*(HENGNS(ASYVLO(1),COSANG)-PHASEF)
              RETURN
          ENDIF
          IF(IHAZE.GE.8)THEN

!             NORMALLY 0-2KM FOG MODELS, NO RH DEPENDENCE:
              IF(IHAZE.EQ.8)NN=24
              IF(IHAZE.EQ.9)NN=25
              GOTO 40
          ELSE

!             NORMALLY 0-2KM BOUNDARY LAYER MODELS, RH DEPENDENT:
              DO I1=1, 4
                  I=I1
                  IF(RHPTS(I).EQ.RH)GOTO 20
                  IF(RHPTS(I).GT.RH)GOTO 10
              ENDDO
   10         IRHLO=I - 1
              IRHHI=I
              GOTO 30
          ENDIF
   20     CONTINUE
          IRHLO=I
          IRHHI=I

!         RURAL MODEL:
   30     CONTINUE
          IF(IHAZE.EQ.1 .OR. IHAZE.EQ.2)NN0=0

!         MARITIME MODEL:
          IF(IHAZE.EQ.3 .OR. IHAZE.EQ.4)NN0=4

!         URBAN MODEL:
          IF(IHAZE.EQ.5)NN0=8

!         TROPOSPHERIC MODEL:
          IF(IHAZE.EQ.6)NN0=16
          NN=NN0+IRHLO
      ELSEIF((REGION.EQ.4 .AND. PTHALT.LE.ALTB(4)) .OR.                 &
     &  (IHAZE.EQ.6 .AND. REGION.EQ.3 .AND. PTHALT.LE.ALTB(3)))THEN

!         NORMALLY 30-100KM METEORIC MODEL:
          NN=26
      ELSEIF((REGION.EQ.3 .AND. PTHALT.LE.ALTB(3)) .OR.                 &
     &  (IHAZE.EQ.6 .AND. REGION.EQ.2 .AND. PTHALT.LE.ALTB(2)))THEN

!         (NORMALLY 10-30KM) BACKGROUND STRATOSPHERIC MODEL:
          IF(IVULCN.EQ.0 .OR. IVULCN.EQ.1)NN=21

!         (NORMALLY 10-30KM) AGED VOLCANIC STRATOSPHERIC MODEL:
          IF(IVULCN.EQ.2 .OR. IVULCN.EQ.4)NN=22

!         (NORMALLY 10-30KM) FRESH VOLCANIC STRATOSPHERIC MODEL:
          IF(IVULCN.EQ.3 .OR. IVULCN.EQ.5 .OR. IVULCN.EQ.8)NN=23

!         (NORMALLY 10-30KM) BACKGROUND STRATOSPHERIC MODEL:
          IF(IVULCN.EQ.6 .OR. IVULCN.EQ.7)NN=21
      ELSEIF(REGION.EQ.2 .AND. PTHALT.LE.ALTB(2))THEN

!         NORMALLY 2-10KM TROPOSPHERIC MODEL:
          IF(IHAZE.EQ.7)THEN
              COSANG=COS(SANGLE/DEG)
              PHASEF=HENGNS(ASYV(1,2),COSANG)
              IF(ABS(FRAC5).GT..001)                                    &
     &          PHASEF=PHASEF+FRAC5*(HENGNS(ASYVLO(2),COSANG)-PHASEF)
              RETURN
          ENDIF
          NN=18
      ENDIF
   40 CONTINUE

!     DETERMINE THE BOUNDING ANGLE INDICES:
      DO I1=1,34
          I=I1
          IF(ANG(I).EQ.SANGLE)GOTO 60
          IF(ANG(I).GT.SANGLE)GOTO 50
      ENDDO
   50 CONTINUE
      IANG1=I-1
      IANG2=I
      GOTO 70
   60 CONTINUE
      IANG1=I
      IANG2=I

!     DETERMINE THE BOUNDING WAVELENGTH INDICES:
   70 CONTINUE
      DO I1=1,27
          I=I1
          IF(WAVE(I).EQ.ALAM)GOTO 90
          IF(WAVE(I).GT.ALAM)GOTO 80
      ENDDO
   80 CONTINUE
      IWAV1=I-1
      IWAV2=I
      IF(IWAV1.LT.1)IWAV1=1
      GOTO 100
   90 CONTINUE
      IWAV1=I
      IWAV2=I

!     FUNCTION PF CHOOSES DESIRED PHASE FUNCTION FROM LOOK UP TABLE
!     MNUM(IWAV,NN)  WHERE IWAV IS FREQ. AND NN IS MODEL NO.

!     WAVELENGTH INTERPOLATION ONLY USES PF11 AND PF21
!     ANGLE INTERPOLATION ONLY USES PF11 AND PF12
!     WAVELENGTH AND ANGLE INTERPOLATION USES PF11,PF21 AND PF12,PF22.

  100 CONTINUE
      IF(NN.EQ.-99)RETURN
      PF11=PF(NN,IWAV1,IANG1)
      PF21=PF(NN,IWAV2,IANG1)
      PF12=PF(NN,IWAV1,IANG2)
      PF22=PF(NN,IWAV2,IANG2)

!     INTERPOLATE IN WAVELENGTH THEN ANGLE
      IF(IWAV1.EQ.IWAV2)THEN
          IF(IANG1.EQ.IANG2)THEN

!             NO INTERPOLATION IS NECESSARY
              PHASEF=PF(NN,IWAV1,IANG1)
          ELSE

!             ONLY ANGLE INTERPOLATION IS NECESSARY
              PHASEF=INTEXP(SANGLE,ANG(IANG1),ANG(IANG2),PF11,PF12)
          ENDIF
      ELSEIF(IANG1.EQ.IANG2)THEN

!         ONLY WAVELENGTH INTERPOLATION IS NECESSARY
          PHASEF=INTEXP(ALAM,WAVE(IWAV1),WAVE(IWAV2),PF11,PF21)
      ELSE

!         BOTH INTERPOLATIONS ARE NECESSARY
          YANG1=INTEXP(ALAM,WAVE(IWAV1),WAVE(IWAV2),PF11,PF21)
          YANG2=INTEXP(ALAM,WAVE(IWAV1),WAVE(IWAV2),PF12,PF22)
          PHASEF=INTEXP(SANGLE,ANG(IANG1),ANG(IANG2),YANG1,YANG2)
      ENDIF

!     HUMIDITY DEPENDENCE - FIRST AEROSOL ONLY (REGION=1)
      IF(REGION.GT.1 .OR. PTHALT.GT.ALTB(1) .OR.                        &
     &  IRHLO.EQ.IRHHI .OR. NN.GT.20)RETURN
      IF(NN.NE.NN0+IRHHI)THEN
          NN=NN0+IRHHI
          PF_SAV=PHASEF
          GOTO 100
      ENDIF
      PHASEF=PF_SAV                                                     &
     &  +(RH-RHPTS(IRHLO))*(PHASEF-PF_SAV)/(RHPTS(IRHHI)-RHPTS(IRHLO))
      RETURN
      END
