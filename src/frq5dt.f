      SUBROUTINE FRQ5DT(FIRST,VBAND5)

!     FRQ5DT DEFINES THE LAYER INDEPENDENT 5 CM-1 SPECTRAL DATA.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     INPUT ARGUMENTS:
!       FIRST    LOGICAL FLAG, TRUE FOR FIRST PASS OF A LOWTRAN RUN.
!       VBAND5   5 CM-1 RESOLUTION DATA SPECTRAL FREQUENCY [CM-1].
      LOGICAL FIRST
      REAL VBAND5

!     COMMONS:

!     /CARD1/
!       MODEL    MODEL ATMOSPHERE INDEX.
!       ITYPE    SLANT PATH TYPE.
!       IEMSCT   RADIATIVE TRANSFER MODE.
!                  0 FOR TRANSMITTANCE
!                  1 FOR THERMAL EMISSION ONLY
!                  2 FOR THERMAL EMISSION PLUS SOLAR SCATTER
!                  3 FOR TRANSMITTED SOLAR IRRADIANCE
!                  4 FOR SOLAR SCATTER ONLY
!       M1       MODEL ATMOSPHERE FOR PRESSURE & TEMPERATURE PROFILES.
!       M2       MODEL ATMOSPHERE FOR H2O PROFILE.
!       M3       MODEL ATMOSPHERE FOR O3 PROFILE.
!       I_RD2C   READ CARD 2C, 2C1, ... IF EQUAL 1; SKIP IF EQUAL TO 0.
!       NOPRNT   PRINT FLAG.
!       MODTRN   MODTRAN BAND MODEL FLAG.
      INTEGER MODEL,ITYPE,IEMSCT,M1,M2,M3,I_RD2C,NOPRNT
      LOGICAL MODTRN
      COMMON/CARD1/MODEL,ITYPE,IEMSCT,M1,M2,M3,I_RD2C,NOPRNT,MODTRN

!     /AABBCC/
      REAL AA,BB,CC,QA,CPS
      INTEGER IBND
      COMMON/AABBCC/AA(11),BB(11),CC(11),IBND(11),QA(11),CPS(11)

!     /H2O/
      REAL CPH2O
      COMMON/H2O/CPH2O(3515)

!     /O3/
      REAL CPO3
      COMMON/O3/CPO3(447)

!     /UFMIX/
      REAL CPCO2,CPCO,CPCH4,CPN2O,CPO2
      COMMON/UFMIX/CPCO2(1219),CPCO(173),CPCH4(493),CPN2O(704),CPO2(382)

!     /TRACEG/
      REAL CPNH3,CPNO,CPNO2,CPSO2
      COMMON/TRACEG/CPNH3(431),CPNO(62),CPNO2(142),CPSO2(226)

!     /AER/ THERE ARE "MAER=17" PARTICULATE COMPONENTS:
!           1       AEROSOL 1 (NOMINALLY, BOUNDARY LAYER AEROSOL).
!           2       AEROSOL 2 (NOMINALLY, TROPOSPHERIC AEROSOL).
!           3       AEROSOL 3 (NOMINALLY, STRATOSPHERIC AEROSOL).
!           4       AEROSOL 4 (NOMINALLY, VOLCANIC AEROSOL).
!           5       CIRRUS CLOUD.
!           6       CLOUD 1 (NOMINALLY, WATER CLOUD).
!           7       CLOUD 2 (NOMINALLY, ICE CLOUD).
!           8-17    NOVAM (NAVY OCEANIC VERTICAL AEROSOL MODEL) LAYERS.
!       NAER     NUMBER OF ACTIVE AEROSOLS.
!       EXTV     SPECTRAL EXTINCTION (NORMALIZED TO 1 AT 550 NM).
!       ABSV     SPECTRAL ABSORPTION (1-ABSV/EXTV=SCATTERING ALBEDO).
!       ASYV     SPECTRAL LEGENDRE MOMENT (DIVIDED BY 2N+1).
!       FRAC5    5 CM-1 GRID SPECTRAL INTERPOLATION FRACTION.
!       ASYVLO   ASYMMETRY FACTOR FROM PREVIOUS SPECTRAL FREQUENCY.
      INTEGER NAER
      REAL EXTV,ABSV,ASYV,FRAC5,ASYVLO
      COMMON/AER/NAER,EXTV(MAER),ABSV(MAER),ASYV(MXCMU,MAER),           &
     &  FRAC5,ASYVLO(MAER)

!     /FRQ5/
!       N2CONT   N2 CONTINUUM ABS [KM-1 / (ATM-AIR ATM-N2) AT 296K].
!       O2HERZ   HERZBERG O2 ABSORPTION [CM-1/ATM AT 273.15].
!       O2C127   O2 1.27um CONT ABS [CM-1/(ATM-AIR ATM-O2) AT 273.15K].
!       O2C106   O2 1.06 MICRON BAND CONTINUUM ABSORPTION.
!       O2_O2V   O2 VISIBLE ABSORBANCE (89.5CM, 296K, 55ATM).
!       A_CH4    CH4 VIS/NIR ABSORPTION COEFFICIENT [CM-1 / ATM].
!       CRSNO2   NO2 ABSORPTION CROSS-SECTION [CM-1/ATM].
!       CRSSO2   SO2 ABSORPTION CROSS-SECTION [CM-1/ATM].
      REAL SIGO20,SIGO2A,SIGO2B,O3T0,O3T1,O3T2,RAYSCT,N2CONT,SH2OT0,    &
     &  SH2OT,FH2O,CHNO3,O2HERZ,O2C127,O2C106,O2_O2V,CRSNO2,CRSSO2,A_CH4
      COMMON/FRQ5/SIGO20,SIGO2A,SIGO2B,O3T0,O3T1,O3T2,                  &
     &  RAYSCT,N2CONT,SH2OT0,SH2OT,FH2O,CHNO3,O2HERZ,                   &
     &  O2C127,O2C106,O2_O2V,CRSNO2,CRSSO2,A_CH4

!     /CARD2/
!       IHAZE    BOUNDARY LAYER AEROSOL MODEL NUMBER.
!       ISEASN   SEASON NUMBER (1=SPRING-SUMMER, 2=FALL-WINTER).
!       IVULCN   VOLCANIC AEROSOL MODEL NUMBER.
!       ICSTL    COASTAL AIRMASS MODEL NUMBER.
!       ICLD     CLOUD MODEL NUMBER.
!       IVSA     VERTICAL STRUCTURE ALGORITHM (0=OFF, 1=ON).
!       VIS      SURFACE VISIBILITY (GROUND METEOROLOGICAL RANGE) [KM].
!       WSS      CURRENT WIND SPEED (M/S).
!       WHH      24-HOUR WIND SPEED (M/S).
!       RAINRT   RAIN RATE (MM/HR)
!       LSAP     LOGICAL FLAG FOR SPECTRAL AEROSOL PROFILES INPUT.
      INTEGER IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA
      REAL VIS,WSS,WHH,RAINRT
      LOGICAL LSAP
      COMMON/CARD2/IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA,                 &
     &  VIS,WSS,WHH,RAINRT,LSAP

!     /SAP/
!       NWVSAP   NUMBER OF AEROSOL SPECTRAL GRID POINTS.
!       NLGSAP   HIGHEST AEROSOL PHASE FUNCTION LEGENDRE MOMENT.
!       NANSAP   NUMBER OF AEROSOL PHASE FUNCTION ANGULAR POINTS.
!       LEVSAP   NUMBER OF AEROSOL ATMOSPHERIC LEVELS.
!       ANGSAP   AEROSOL PHASE FUNCTION ANGULAR GRID [DEGREES].
!       COSSAP   COSINE OF THE AEROSOL PHASE FUNCTION ANGLES.
!       WAVSAP   AEROSOL SPECTRAL GRID [MICRONS].
!       LEGSAP   AEROSOL LEGENDRE COEFFICIENTS.
!       PFSAP    AEROSOL PHASE FUNCTION [SR-1].
!       LOSSAP   LOGICAL FLAG, TRUE FOR LINE-OF-SIGHT PATH.
      INTEGER NWVSAP,NLGSAP,NANSAP,LEVSAP
      DOUBLE PRECISION ANGSAP,COSSAP,PFSAP
      REAL WAVSAP,LEGSAP
      LOGICAL LOSSAP
      COMMON/SAP/NWVSAP,NLGSAP,NANSAP,LEVSAP,ANGSAP(MANSAP),            &
     &  COSSAP(MANSAP),WAVSAP(MWVSAP),LEGSAP(MLGSAP,MWVSAP,LAYDIM),     &
     &  PFSAP(MANSAP,MWVSAP,LAYDIM),LOSSAP

!     /SAPV/
!       ILOSAP   SAP DATA LOWER WAVELENGTH INDEX AT CURRENT FREQUENCY.
!       IHISAP   SAP DATA HIGHER WAVELENGTH INDEX AT CURRENT FREQUENCY.
!       FRCSAP   SAP DATA INTERPOLATION FRACTION AT CURRENT FREQUENCY.
      INTEGER ILOSAP,IHISAP
      REAL FRCSAP
      COMMON/SAPV/ILOSAP,IHISAP,FRCSAP

!     LOCAL VARIABLES:
!       IAER     LOOP INDEX FOR PARTICULATE COMPONENTS:
!       ILOLIM   CURRENT UPPER BOUND OF ILOSAP
!       WAVLEN   SPECTRAL WAVELENGTH [MICRONS].
      INTEGER IAER,ILOLIM
      REAL WAVLEN

!     DATA:
!       IWLH2O   LOWTRAN H2O BAND MODELS LOWER SPECTRAL LIMITS [CM-1].
!       IWHH2O   LOWTRAN H2O BAND MODELS HIGHER SPECTRAL LIMITS [CM-1].
!       IWLO3    LOWTRAN O3  BAND MODELS LOWER SPECTRAL LIMITS [CM-1].
!       IWHO3    LOWTRAN O3  BAND MODELS HIGHER SPECTRAL LIMITS [CM-1].
!       IWLCO2   LOWTRAN CO2 BAND MODELS LOWER SPECTRAL LIMITS [CM-1].
!       IWHCO2   LOWTRAN CO2 BAND MODELS HIGHER SPECTRAL LIMITS [CM-1].
!       IWLCO    LOWTRAN CO  BAND MODELS LOWER SPECTRAL LIMITS [CM-1].
!       IWHCO    LOWTRAN CO  BAND MODELS HIGHER SPECTRAL LIMITS [CM-1].
!       IWLCH4   LOWTRAN CH4 BAND MODELS LOWER SPECTRAL LIMITS [CM-1].
!       IWHCH4   LOWTRAN CH4 BAND MODELS HIGHER SPECTRAL LIMITS [CM-1].
!       IWLN2O   LOWTRAN N2O BAND MODELS LOWER SPECTRAL LIMITS [CM-1].
!       IWHN2O   LOWTRAN N2O BAND MODELS HIGHER SPECTRAL LIMITS [CM-1].
!       IWLO2    LOWTRAN O2  BAND MODELS LOWER SPECTRAL LIMITS [CM-1].
!       IWHO2    LOWTRAN O2  BAND MODELS HIGHER SPECTRAL LIMITS [CM-1].
!       IWLNH3   LOWTRAN NH3 BAND MODELS LOWER SPECTRAL LIMITS [CM-1].
!       IWHNH3   LOWTRAN NH3 BAND MODELS HIGHER SPECTRAL LIMITS [CM-1].
!       IWLNO    LOWTRAN NO  BAND MODELS LOWER SPECTRAL LIMITS [CM-1].
!       IWHNO    LOWTRAN NO  BAND MODELS HIGHER SPECTRAL LIMITS [CM-1].
!       IWLNO2   LOWTRAN NO2 BAND MODELS LOWER SPECTRAL LIMITS [CM-1].
!       IWHNO2   LOWTRAN NO2 BAND MODELS HIGHER SPECTRAL LIMITS [CM-1].
!       IWLSO2   LOWTRAN SO2 BAND MODELS LOWER SPECTRAL LIMITS [CM-1].
!       IWHSO2   LOWTRAN SO2 BAND MODELS HIGHER SPECTRAL LIMITS [CM-1].
      INTEGER                IWLH2O(15),IWHH2O(15), IWLO3(6), IWHO3(6), &
     & IWLCO2(11),IWHCO2(11),  IWLCO(4),  IWHCO(4),IWLCH4(5),IWHCH4(5), &
     & IWLN2O(12),IWHN2O(12),  IWLO2(7),  IWHO2(7),IWLNH3(3),IWHNH3(3), &
     &   IWLNO(2),  IWHNO(2), IWLNO2(4), IWHNO2(4),IWLSO2(5),IWHSO2(5)
      DATA IWLH2O,IWHH2O/   0,  350, 1005, 1645, 2535, 3425, 4315, 6155,&
     &                         8005, 9620,11545,13075,14865,16340, -999,&
     &                    345, 1000, 1640, 2530, 3420, 4310, 6150, 8000,&
     &                         9615,11540,13070,14860,16045,17860, -999/
      DATA IWLO3,IWHO3/     0,  515, 1630, 2670, 2850, -999,            &
     &                    200, 1275, 2295, 2845, 3260, -999/
      DATA IWLCO2,IWHCO2/                                               &
     &  425,  840, 1805, 3070, 3760, 4530, 5905, 7395, 8030, 9340, -999,&
     &  835, 1440, 2855, 3755, 4065, 5380, 7025, 7785, 8335, 9670, -999/
      DATA IWLCO,IWHCO/     0, 1940, 4040, -999,  175, 2285, 4370, -999/
      DATA IWLCH4,IWHCH4/1065, 2345, 4110, 5865, -999,                  &
     &                   1775, 3230, 4690, 6135, -999/
      DATA IWLN2O,IWHN2O/   0,  490,  865, 1065, 1545, 2090, 2705, 3245,&
     &                                           4260, 4540, 4910, -999,&
     &                    120,  775,  995, 1385, 2040, 2655, 2865, 3925,&
     &                                           4470, 4785, 5165, -999/
      DATA IWLO2,IWHO2/     0, 7650, 9235,12850,14300,15695, -999,      &
     &                    265, 8080, 9490,13220,14600,15955, -999/
      DATA IWLNH3,IWHNH3/   0,  390, -999,  385, 2150, -999/
      DATA IWLNO,IWHNO/  1700, -999, 2005, -999/
      DATA IWLNO2,IWHNO2/ 580, 1515, 2800, -999,  925, 1695, 2970, -999/
      DATA IWLSO2,IWHSO2/   0,  400,  950, 2415, -999,                  &
     &                    185,  650, 1460, 2580, -999/
      SAVE IWLH2O,IWHH2O,IWLO3,IWHO3,IWLCO2,IWHCO2,IWLCO,IWHCO,         &
     &  IWLCH4,IWHCH4,IWLN2O,IWHN2O,IWLO2,IWHO2,IWLNH3,IWHNH3,          &
     &  IWLNO,IWHNO,IWLNO2,IWHNO2,IWLSO2,IWHSO2

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      SAVE /H2O/,/O3/,/TRACEG/,/UFMIX/
      EXTERNAL CPWAT,CPO3BD,CPTRCG,CPUMIX

!     FUNCTIONS:
!       N2DATA   N2 CONTINUUM DATA [KM-1 / (ATM-AIR ATM-N2) AT 296K].
!       C6DTA    RAYLEIGH SPECTRAL SCATTERING COEFFICIENTS [KM-1].
!       HERTDA   O2 HERZBERG BAND [CM-1/ATM AT 273.15].
!       O2MATE   O2 1.27 MICRON CONTINUUM ABSORPTION.
!       O2INF2   O2 1.06 MICRON CONTINUUM ABSORPTION.
!       O2_O2    O2 VISIBLE ABSORBANCE (89.5CM, 296K, 55ATM).
!       O3HHT0   O3 HARTLEY-HUGGINS DATA
!       O3HHT1   O3 HARTLEY-HUGGINS DATA
!       O3HHT2   O3 HARTLEY-HUGGINS DATA
!       NO2XS    NO2 SPECTRAL ABSORPTION CROSS-SECTION [CM-1/ATM].
!       SO2XS    SO2 SPECTRAL ABSORPTION CROSS-SECTION [CM-1/ATM].
!       SCHRUN   O2 SCHUMANN-RUNGE BAND MODEL DATA.
!       O3UV     O3 UV ABSORPTION COEFFICIENTS.
!       HNO3     HNO3 STATISTICAL BAND PARAMETERS [CM-1/ATM].
!       CH4HI    0.4NM CH4 ABSORPTION FROM 520.1 TO 995.0 NM [CM-1/ATM].
!       CH4LO    1 NM CH4 ABSORPTION FROM 400.4 TO 1050.0 NM [CM-1/ATM].
      REAL N2DATA,C6DTA,HERTDA,O2MATE,O2INF2,O2_O2,O3HHT0,O3HHT1,O3HHT2,&
     &  CXDTA,NO2XS,SO2XS,SCHRUN,O3UV,HNO3,CH4HI,CH4LO

!     SAVED VARIABLES:
      INTEGER INDH2O,INDO3,INDCO2,INDCO,INDCH4,INDN2O,                  &
     &  INDO2,INDNH3,INDNO,INDNO2,INDSO2,MNAER
      SAVE INDH2O,INDO3,INDCO2,INDCO,INDCH4,INDN2O,                     &
     &  INDO2,INDNH3,INDNO,INDNO2,INDSO2,MNAER
      DATA MNAER/1/
      IF(FIRST)THEN

!         INITIALIZE FREQUENCY REGION INDEX FOR EACH MOLECULE
          INDH2O=1
          INDO3 =1
          INDCO2=1
          INDCO =1
          INDCH4=1
          INDN2O=1
          INDO2 =1
          INDNH3=1
          INDNO =1
          INDNO2=1
          INDSO2=1
          RETURN
      ENDIF
      IF(MODTRN)THEN
          CHNO3=0.
      ELSE
          CPS( 1)=CXDTA(VBAND5,IWLH2O,IWHH2O,CPH2O,INDH2O)
          CPS( 2)=CXDTA(VBAND5,IWLCO2,IWHCO2,CPCO2,INDCO2)
          CPS( 3)=CXDTA(VBAND5,IWLO3 ,IWHO3 ,CPO3 ,INDO3 )
          CPS( 4)=CXDTA(VBAND5,IWLN2O,IWHN2O,CPN2O,INDN2O)
          CPS( 5)=CXDTA(VBAND5,IWLCO, IWHCO, CPCO, INDCO )
          CPS( 6)=CXDTA(VBAND5,IWLCH4,IWHCH4,CPCH4,INDCH4)
          CPS( 7)=CXDTA(VBAND5,IWLO2, IWHO2, CPO2, INDO2 )
          CPS( 8)=CXDTA(VBAND5,IWLNO, IWHNO, CPNO, INDNO )
          CPS( 9)=CXDTA(VBAND5,IWLSO2,IWHSO2,CPSO2,INDSO2)
          CPS(10)=CXDTA(VBAND5,IWLNO2,IWHNO2,CPNO2,INDNO2)
          CPS(11)=CXDTA(VBAND5,IWLNH3,IWHNH3,CPNH3,INDNH3)
          CALL ABCDTA(VBAND5)

!         HNO3 ABSORPTION CALCULATION:
          CHNO3=HNO3(VBAND5)
      ENDIF

!     N2 CONTINUUM:
      N2CONT=N2DATA(VBAND5)

!     H2O CONTINUUM:
      CALL CKD(VBAND5,SH2OT0,SH2OT,FH2O)
      IF(SH2OT0.GT.0.)THEN
          SH2OT=SH2OT/SH2OT0
      ELSE
          SH2OT=0.
      ENDIF

!     RAYLEIGH SCATTERING COEFFICIENT [KM-1]:
      RAYSCT=C6DTA(VBAND5)

!     AEROSOL OPTICAL PROPERTIES:
      DO IAER=1,MAER
          ASYVLO(IAER)=ASYV(1,IAER)
      ENDDO
      CALL AEREXT(VBAND5,MNAER)

!     O2 CONTINUUM CONTRIBUTIONS:
      O2HERZ=HERTDA(VBAND5)
      O2C127=O2MATE(VBAND5)
      O2C106=O2INF2(VBAND5)
      O2_O2V=O2_O2(VBAND5)
      CALL O2CONT(VBAND5,SIGO20,SIGO2A,SIGO2B)
      IF(VBAND5.GE.49600.)CPS(7)=SCHRUN(VBAND5)

!     DIFFUSE OZONE:
      IF(VBAND5.GT.40799.)THEN
          O3T0=O3UV(VBAND5)
          O3T1=0.
          O3T2=0.
      ELSEIF(VBAND5.GT.24569.)THEN
          O3T0=O3HHT0(VBAND5)
          O3T1=O3HHT1(VBAND5)
          O3T2=O3HHT2(VBAND5)
      ELSE
          CALL O3CHAP(VBAND5,O3T0,O3T1,O3T2)
      ENDIF

!     NO2 CROSS-SECTIONS (14000 CM-1 TO 50000 CM-1):
      CRSNO2=NO2XS(VBAND5)

!     SO2 CROSS-SECTIONS (14000 CM-1 TO 50000 CM-1):
      CRSSO2=SO2XS(VBAND5)

!     CH4 CROSS-SECTIONS (400.4 TO 1050.0 NM):
      IF(VBAND5.GE.10050.25 .AND. VBAND5.LE.19227.07)THEN
          A_CH4=CH4HI(VBAND5)
      ELSEIF(VBAND5.GE.9523.81 .AND. VBAND5.LE.24975.02)THEN
          A_CH4=CH4LO(VBAND5)
      ELSE
          A_CH4=0.
      ENDIF

!     SPECTRAL AEROSOL PROFILES (SAP) DATA INTERPOLATION:
      IF(.NOT.LSAP)RETURN
      WAVLEN=10000/VBAND5
      IF(WAVLEN.GE.WAVSAP(NWVSAP))THEN

!         NO EXTRAPOLATION ABOVE HIGHEST WAVELENGTH:
          ILOSAP=NWVSAP
          IHISAP=NWVSAP
          FRCSAP=0.
      ELSEIF(WAVLEN.LE.WAVSAP(1))THEN

!         NO EXTRAPOLATION BELOW LOWEST WAVELENGTH:
          ILOSAP=1
          IHISAP=1
          FRCSAP=0.
      ELSE

!         ASSUME SPECTRAL FREQUENCY INCREASES (WAVELENGTH DECREASES)
!         IN DETERMINING INTERPOLATION BOUNDS:
          ILOLIM=ILOSAP
          DO ILOSAP=ILOLIM,2,-1
              IF(WAVLEN.GE.WAVSAP(ILOSAP))THEN
                  IHISAP=ILOSAP+1
                  FRCSAP=(WAVLEN-WAVSAP(ILOSAP))                        &
     &              /(WAVSAP(IHISAP)-WAVSAP(ILOSAP))
                  RETURN
              ENDIF
          ENDDO

!         ILOSAP EQUALS ONE:
          IHISAP=2
          FRCSAP=(WAVLEN-WAVSAP(ILOSAP))/(WAVSAP(IHISAP)-WAVSAP(ILOSAP))
      ENDIF
      RETURN
      END
