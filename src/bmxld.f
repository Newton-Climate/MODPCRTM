      SUBROUTINE BMXLD

!     BMXLD IS THE EQUIVALENT OF BMLOAD BUT FOR THE X-MOLECULES.
!     BMXLD LOADS CROSS-SECTION DATA INTO THE SDTL ARRAY.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     COMMONS:
      INCLUDE 'BMDAT.h'

!     /BMDCMX/
!       TRANS VARIABLES
      INTEGER BINXTL,MOLXTL,ALFXTL
      REAL SDX1TL,SDX2TL
      COMMON/BMDCMX/BINXTL,MOLXTL,ALFXTL,SDX1TL(NTEMPX),SDX2TL(NTEMPX)

!     LOCAL VARIABLES:
      INTEGER ITEMP

!     FILL THE SDTL MATRICES:
      IF(ALFXTL.EQ.0)THEN

!         ONE TAIL:
          DO ITEMP=1,NTEMPX
              SDTL(0,ITEMP,1,MOLXTL)=SDX1TL(ITEMP)
          ENDDO
      ELSE

!         TWO TAILS:
          DO ITEMP=1,NTEMPX
              SDTL(0,ITEMP,1,MOLXTL)=SDX1TL(ITEMP)
              SDTL(0,ITEMP,1,ALFXTL)=SDX2TL(ITEMP)
          ENDDO
      ENDIF

!     RETURN TO BMOD0:
      RETURN
      END

      SUBROUTINE BMYLD(IFLAG,IMOLY)

!     BMYLD IS CALLED FOR THE Y-SPECIES.
!     IFLAG=1 MEANS IT IS CALLED FOR LINE CENTER PARAMETERS.
!     IFLAG=2 MEANS IT IS CALLED FOR LINE TAIL PARAMETERS.
!     IMOLY IDENTIFIES THE MOLECULE WHICH IS DIFFERENT FOR EACH RUN.

!     THIS ROUTINE IS THE EQUIVALENT OF BMLOAD/BMXLD, BUT FOR
!     THE Y-SPECIES.  THERE IS ONE DIFFERENCE W/ BMLOAD/BMXLD.
!     BECAUSE THE BAND MODEL PARAMETERS ARE KEPT IN A SEPARATE FILE FOR
!     EACH SPECIES, THE ARGUMENT TO THIS ROUTINE IS THE SPECIES NUMBER.

!     THIS SUBROUTINE (CALLED BY BMOD) LOADS DATA FOR
!     INTO THE MATRICES SDY, ODY AND ALF0Y (FOUND IN /BMDATY/
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     INPUT ARGUMENTS:
      INTEGER IMOLY,IFLAG

!     COMMONS:
      INCLUDE 'BMHEAD.h'
      INCLUDE 'BMDAT.h'

!     /BMDATY/
!       BINYCN   Y-MOLECULE LINE CENTER SPECTRAL BIN NUMBER.
!       SDYCN    Y-MOLECULE ABSORPTION COEFFICIENTS [CM-1 / ATM].
!       ALFYCN   Y-MOLECULE FOREIGN-BROADENED LORENTZ HALF WIDTH [CM-1].
!       ODYCN    Y-MOLECULE RECIPROCAL LINE SPACING PARAMETER [CM].
!       BINYTL   Y-MOLECULE LINE TAIL SPECTRAL BIN NUMBER.
!       SDY0TL   Y-MOLECULE PADE NUMERATOR ZERO-ORDER TERM [CM-1 / ATM].
!       SDY1TL   Y-MOLECULE PADE NUMERATOR 1ST-ORDER TERM [CM-1 / ATM].
!       SDY2TL   Y-MOLECULE PADE NUMERATOR 2ND-ORDER TERM [CM-1 / ATM].
!       SDY3TL   Y-MOLECULE PADE DENOMINATOR 1ST-ORDER TERM.
!       SDY4TL   Y-MOLECULE PADE DENOMINATOR 2ND-ORDER TERM.
      INTEGER BINYCN,ALFYCN,BINYTL
      REAL SDYCN,ODYCN,SDY0TL,SDY1TL,SDY2TL,SDY3TL,SDY4TL
      COMMON/BMDATY/BINYCN(MMOLY),SDYCN(MTEMP,MMOLY),ALFYCN(MMOLY),     &
     &  ODYCN(MTEMP,MMOLY),BINYTL(MMOLY),SDY0TL(MTEMP,MPRES,MMOLY),     &
     &         SDY1TL(MTEMP,MPRES,MMOLY),SDY2TL(MTEMP,MPRES,MMOLY),     &
     &         SDY3TL(MTEMP,MPRES,MMOLY),SDY4TL(MTEMP,MPRES,MMOLY)

!     LOCAL VARIABLES:
      INTEGER IMY,ITEMP,IP,ITLSUB

!     FILL THE SDTL MATRIX:
      IMY=NMOLXT+IMOLY
      IF(IFLAG.EQ.3)THEN

!         CROSS SECTIONS, NO BAND MODEL (LINE CENTER OR TAIL) PARAMETERS
          DO ITEMP=1,NTEMPX
              SDTL(0,ITEMP,1,IMY)=SDY1TL(ITEMP,1,IMOLY)*LOSCHM
          ENDDO
      ELSEIF(IFLAG.EQ.2)THEN

!         IFLAG = 2 OR 1 MEANS BAND MODEL PARAMETERS.
!         IFLAG = 2 MEANS YOU ARE LOADING LINE TAIL PARAMETERS.
          DO ITEMP=1,NTEMP
              DO IP=1,NPRESS
                  CALL SDTAIL(SDY0TL(ITEMP,IP,IMOLY),                   &
     &              SDY1TL(ITEMP,IP,IMOLY),SDY2TL(ITEMP,IP,IMOLY),      &
     &              SDY3TL(ITEMP,IP,IMOLY),SDY4TL(ITEMP,IP,IMOLY),      &
     &              NTLSUB,DELSUB(0),SDTL(0,ITEMP,IP,IMY))
              ENDDO
          ENDDO
      ELSEIF(IFLAG.EQ.1)THEN

!         IFLAG = 2 OR 1 MEANS BAND MODEL PARAMETERS.
!         IFLAG = 1 MEANS YOU ARE LOADING LINE CENTER PARAMETERS.
!         FILL THE SD, OD AND ALF0 MATRICES.
          DO ITEMP=NTEMP,1,-1
              SDCN(ITEMP,IMY)=SDYCN(ITEMP,IMOLY)
              IF(ODYCN(ITEMP,IMOLY).GT.0. .OR. ITEMP.EQ.NTEMP)THEN
                  ODCN(ITEMP,IMY)=ODYCN(ITEMP,IMOLY)
              ELSE

!                 ODYCN WILL BE ZERO IF SDYCN IS ZERO.  IN THAT CASE,
!                 JUST SET ODCN TO THE FIRST NON-ZERO HIGHER
!                 TEMPERATURE VALUE TO AVOID INTERPOLATION PROBLEMS.
                  ODCN(ITEMP,IMY)=ODCN(ITEMP+1,IMY)
              ENDIF
              DO IP=1,NPRESS
                  DO ITLSUB=0,NTLSUB
                      SDTL(ITLSUB,ITEMP,IP,IMY)=0.
                  ENDDO
              ENDDO
          ENDDO
          ALF0(IMY)=.0001*ALFYCN(IMOLY)
      ENDIF
      END
