      SUBROUTINE SUBSOL(THETAS,PHIS,GMTIME,IDAY)

!     SUBSOL CALCULATES THE SUBSOLAR POINT ANGLES BASED UPON DAY OF
!     YEAR AND GREENWICH MEAN TIME.  SINCE EACH YEAR IS 365.25 DAYS
!     LONG, THE EXACT VALUE OF THE DECLINATION ANGLE CHANGES FROM
!     YEAR TO YEAR.  FOR PRECISE VALUES CONSULT 'THE AMERICAN
!     EPHEMERIS AND NAUTICAL ALMANAC' PUBLISHED YEARLY BY THE U.S.
!     GOVT. PRINTING OFFICE.  THE SOLAR POSITION IS CHARACTERIZED
!     BY 25 POINTS.  THIS SHOULD PREDICT THE SUBSOLAR ANGLES WITHIN
!     ONE DEGREE.  FOR INCREASED ACCURACY ADD MORE DATA POINTS.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     INPUT ARGUMENTS:
!       IDAY     DAY OF YEAR.
!       GMTIME   GREENWICH MEAN TIME.
      INTEGER IDAY
      DOUBLE PRECISION GMTIME

!     OUTPUT ARGUMENTS:
!       THETAS   SUBSOLAR LATITUDE [DEGREES NORTH OF EQUATOR].
!       PHIS     SUBSOLAR LONGITUDE [DEGREES WEST OF GREENWICH].
      DOUBLE PRECISION THETAS,PHIS

!     COMMONS:
      INCLUDE 'IFIL.h'

!     LOCAL VARIABLES
!       ILO      LOWER ARRAY INTERPOLATION INDEX.
!       IHI      HIGHER ARRAY INTERPOLATION INDEX.
!       FNTERP   INTERPOLATION FRACTION.
      INTEGER ILO,IHI
      DOUBLE PRECISION FNTERP

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      EXTERNAL DEVCBD

!     DATA:
!       NDAY     DAY OF YEAR GRID.
!       EQT      EQUATION OF TIME [MINUTES].
!       DEC      DECLINATION ANGLE [DEG].
      INTEGER NDAY(25)
      DOUBLE PRECISION EQT(25),DEC(25)
      DATA NDAY/                          1,       9,      21,      32, &
     &        44,      60,      91,     121,     141,     152,     160, &
     &       172,     182,     190,     202,     213,     244,     274, &
     &       305,     309,     325,     335,     343,     355,     366/
      DATA DEC/                    -23.07D0,-22.22D0,-20.08D0,-17.32D0, &
     &  -13.62D0, -7.88D0,  4.23D0, 14.83D0, 20.03D0, 21.95D0, 22.87D0, &
     &   23.45D0, 23.17D0, 22.47D0, 20.63D0, 18.23D0,  8.58D0, -2.88D0, &
     &  -14.18D0,-15.45D0,-19.75D0,-21.68D0,-22.75D0,-23.43D0,-23.07D0/
      DATA EQT/                     -3.23D0, -6.83D0,-11.17D0,-13.57D0, &
     &  -14.33D0,-12.63D0, -4.20D0,  2.83D0,  3.57D0,  2.45D0,  1.10D0, &
     &   -1.42D0, -3.52D0, -4.93D0, -6.25D0, -6.28D0, -0.25D0, 10.02D0, &
     &   16.35D0, 16.38D0, 14.30D0, 11.27D0,  8.02D0,  2.32D0, -3.23D0/
      IF(IDAY.LT.1.OR.IDAY.GT.366)THEN
          WRITE(IPR,'(/A,I6)')' From SUBSOL: IDAY out of range, =',IDAY
          IF(LJMASS)RETURN
          STOP ' From SUBSOL: IDAY out of range'
      ENDIF
      IF(GMTIME.LT.0D0 .OR. GMTIME.GT.24.D0)THEN
          WRITE(IPR,'(/A,E12.5)')                                       &
     &      ' From SUBSOL: TIME out of range, =',GMTIME
          IF(LJMASS)RETURN
          STOP ' From SUBSOL: TIME out of range'
      ENDIF
      DO IHI=1,25
          IF(NDAY(IHI).EQ.IDAY)THEN
              THETAS=DEC(IHI)
              PHIS=15*(GMTIME+EQT(IHI)/60)-180
              IF(PHIS.LT.0.)PHIS=PHIS+360
              RETURN
          ENDIF
          IF(NDAY(IHI).GT.IDAY)THEN
              ILO=IHI-1
              FNTERP=(IDAY-NDAY(ILO))/DBLE(NDAY(IHI)-NDAY(ILO))
              THETAS=DEC(ILO)+FNTERP*(DEC(IHI)-DEC(ILO))
              PHIS=15*GMTIME+(EQT(ILO)+FNTERP*(EQT(IHI)-EQT(ILO)))/4-180
              IF(PHIS.LT.0.D0)PHIS=PHIS+360
              RETURN
          ENDIF
      ENDDO
      END
