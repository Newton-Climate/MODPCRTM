      REAL FUNCTION O2INF2(V)

!     THIS ROUTINE WAS CREATED FROM ROUTINES IN LBLRTM

!     O2 CONTINUUM FORMULATED BY MLAWER ET AL. OVER THE SPECTRAL REGION
!     9100-11000 CM-1. REFER TO THE PAPER "OBSERVED  ATMOSPHERIC
!     COLLISION-INDUCED ABSORPTION IN NEAR INFRARED OXYGEN BANDS",
!     MLAWER, CLOUGH, BROWN, STEPHEN, LANDRY, GOLDMAN, & MURCRAY,
!     JOURNAL OF GEOPHYSICAL RESEARCH, V103, 3859-3863 (1998).

!     PARAMETERS:
      REAL V1S,V2S,V1OSC,HW1,V2OSC,HW2,COEF1,COEF2
      PARAMETER(V1S=9100.,V2S=11000.,                                   &
     &  V1OSC=9375.,HW1=58.96,COEF1=.31831*1.166E-4*1.054/HW1,          &
     &  V2OSC=9439.,HW2=45.04,COEF2=.31831*3.086E-5*1.054/HW2)

!     ARGUMENTS:
!       V        SPECTRAL FREQUENCY [CM-1].
      REAL V

!     LOCAL VARIABLES:
      REAL DV1,DV2

!     BRANCH BASED ON SPECTRAL FREQUENCY:
      IF(V.LE.V1S .OR. V.GE.V2S)THEN
          O2INF2=0.
      ELSE
          DV1=V-V1OSC
          DV2=V-V2OSC
          IF(DV1.LT.0.)THEN
              O2INF2=COEF1*EXP(DV1/176.1)/(1+(DV1/HW1)**2)              &
     &              +COEF2*EXP(DV2/176.1)/(1+(DV2/HW2)**2)
          ELSEIF(DV2.LT.0.)THEN
              O2INF2=COEF1               /(1+(DV1/HW1)**2)              &
     &              +COEF2*EXP(DV2/176.1)/(1+(DV2/HW2)**2)
          ELSE
              O2INF2=COEF1/(1+(DV1/HW1)**2)+COEF2/(1+(DV2/HW2)**2)
          ENDIF
      ENDIF
      RETURN
      END
