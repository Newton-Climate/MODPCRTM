      SUBROUTINE DESATT(WSPD,VIS)

!     THIS SUBROUTINE CALCULATES THE ATTENUATION COEFFICIENTS AND
!     ASYMMETRY PARAMETER FOR THE DESERT AEROSOL BASED ON THE WIND
!     SPEED AND METEOROLOGICAL RANGE.

!     PROGRAMMED BY:  D. R. LONGTIN         OPTIMETRICS, INC.
!                                           BURLINGTON, MASSACHUSETTS
!                                           JULY 1987

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     INPUT ARGUMENTS:
!       WSPD     WIND SPEED (IN M/S) AT 10 M
!       VIS      METEOROLOGICAL RANGE (KM)
      REAL WSPD,VIS

!     OUTPUTS: EXTC(1,*)  EXTINCTION COEFFICIENT AT "NWAVLN" WAVELENGTHS
!              ABSC(1,*)  ABSORPTION COEFFICIENT AT "NWAVLN" WAVELENGTHS
!              ASYM(1,*)  ASYMMETRY PARAMETER AT "NWAVLN" WAVELENGTHS

!     COMMONS:
      INCLUDE 'BASE.h'
      INCLUDE 'IFIL.h'

!     /DESAER/
      REAL DSEXT,DSABS,DSASYM
      COMMON/DESAER/DSEXT(NWAVLN,4),DSABS(NWAVLN,4),DSASYM(NWAVLN,4)

!     /EXTWAV/
      REAL VX0
      COMMON/EXTWAV/VX0(NWAVLN)

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      SAVE /DESAER/,/EXTWAV/
      EXTERNAL DEVCBD,DSTDTA,EXTDT1,EXTDT2

!     LOCAL VARIABLES:
      INTEGER NWSPD,WAVEL,IWAV55
      REAL SLOPE,EXT55

!     DATA:
      REAL RAYSCT,WIND(4)
      DATA RAYSCT/.01159/,WIND/0.,10.,20.,30./

!     CHECK WIND SPEED:
      IF(WSPD.LT.0.)WSPD=10.
      NWSPD=INT(WSPD/10)+1
      IF(NWSPD.GE.4)THEN
          WRITE(IPR,'(/2A,/22X,A)')' WARNING from DESATT:  WIND',       &
     &      ' SPEED IS BEYOND 30 M/S; RADIATIVE PROPERTIES',            &
     &      ' OF THE DESERT AEROSOL HAVE BEEN EXTRAPOLATED.'
          NWSPD=3
      ENDIF
      SLOPE=(WSPD-WIND(NWSPD))/(WIND(NWSPD+1)-WIND(NWSPD))

!     INTERPOLATE THE RADIATIVE PROPERTIES AT WIND SPEED WSPD
      IWAV55=0
      DO WAVEL=1,NWAVLN
          IF(VX0(WAVEL).LE.0.55)IWAV55=WAVEL

!         EXTINCTION COEFFICIENT:
          EXTC(1,WAVEL)=DSEXT(WAVEL,NWSPD)                              &
     &      *(DSEXT(WAVEL,NWSPD+1)/DSEXT(WAVEL,NWSPD))**SLOPE

!         ABSORPTION COEFFICIENT:
          ABSC(1,WAVEL)=DSABS(WAVEL,NWSPD)                              &
     &      *(DSABS(WAVEL,NWSPD+1)/DSABS(WAVEL,NWSPD))**SLOPE

!         ASYMMETRY PARAMETER:
          ASYM(1,WAVEL)=DSASYM(WAVEL,NWSPD)                             &
     &      *(DSASYM(WAVEL,NWSPD+1)/DSASYM(WAVEL,NWSPD))**SLOPE
      ENDDO

!     DETERMINE METEROLOGICAL RANGE FROM 0.55 EXTINCTION
!     AND KOSCHMIEDER FORMULA:
      IF(IWAV55.EQ.0)THEN
          EXT55=EXTC(1,1)
      ELSEIF(IWAV55.EQ.NWAVLN)THEN
          EXT55=EXTC(1,NWAVLN)
      ELSE
          SLOPE=(.55-VX0(IWAV55))/(VX0(IWAV55+1)-VX0(IWAV55))
          EXT55=EXTC(1,IWAV55)
          IF(EXT55.GT.0. .AND. EXTC(1,IWAV55+1).GT.0.)THEN
              EXT55=EXT55*(EXTC(1,IWAV55+1)/EXT55)**SLOPE
          ELSE
              EXT55=EXT55+SLOPE*(EXTC(1,IWAV55+1)-EXT55)
          ENDIF
      ENDIF
      IF(VIS.LE.0.)VIS=3.912/(EXT55+RAYSCT)

!     RENORMALIZE ATTENUATION COEFFICIENTS TO 1.0 KM-1
!     AT 0.55 MICRONS FOR COMPATIBILITY WITH LOWTRAN:
      DO WAVEL=1,NWAVLN
          EXTC(1,WAVEL)=EXTC(1,WAVEL)/EXT55
          ABSC(1,WAVEL)=ABSC(1,WAVEL)/EXT55
      ENDDO
      IF(.NOT.LJMASS)WRITE(IPR,'(/3(A,F11.3))')                         &
     &  ' METEOROLOGICAL RANGE =',VIS,' KM;  WIND =',WSPD,' M/S.'
      RETURN
      END
