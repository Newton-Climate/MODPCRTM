      SUBROUTINE PSIECA(OLAT,OLONG,SLAT,SLONG,PSI,ECA)

!     THIS ROUTINE CALCULATES THE EARTH CENTER ANGLE AND THE PATH
!     AZIMUTH FROM AN OBSERVER TO A SOURCE POINT GIVEN LATITUDE AND
!     LONGITUDE INFORMATION.

!     NOTE:  OLONG AND PSI ARE NOT WELL DEFINED AT THE NORTH (OLAT=90)
!            AND SOUTH (OLAT=-90) POLES.  THIS ROUTINE RETURNS THE
!            VALUE OF PATH AZIMUTH OBTAINED IN THE LIMIT AS |OLAT|
!            APPROACHES 90 FROM BELOW.
      IMPLICIT NONE

!     INPUT ARGUMENTS:
!       OLAT     OBSERVER LATITUDE [DEG NORTH, -90 TO 90]
!       OLONG    OBSERVER LONGITUDE [DEG WEST, 0 TO 360]
!       SLAT     SOURCE POINT LATITUDE [DEG NORTH, -90 TO 90]
!       SLONG    SOURCE POINT LONGITUDE [DEG WEST, 0 TO 360]
      DOUBLE PRECISION OLAT,OLONG,SLAT,SLONG

!     OUTPUT ARGUMENTS:
!       PSI      PATH AZIMUTH @ SENSOR [DEG EAST OF NORTH, -180 TO 180].
!       ECA      EARTH CENTER ANGLE [DEG, 0 TO 180]
      DOUBLE PRECISION PSI,ECA

!     COMMONS:

!     /DCNSTN/
!       DRIGHT   SMALLEST DOUBLE PRECISION REAL ADDED TO 1 EXCEEDS 1.
!       DPDEG    NUMBER OF DEGREES IN ONE RADIAN IN DOUBLE PRECISION
      DOUBLE PRECISION DRIGHT,DPDEG
      COMMON/DCNSTN/DRIGHT,DPDEG

!     LOCAL VARIABLES:
!       OLATR    OBSERVER LATITUDE [RADIANS].
!       SLATR    SOURCE POINT LATITUDE [RADIANS].
      DOUBLE PRECISION OLATR,SLATR,SOLAT,COLAT,SSLAT,CSLAT,             &
     &  DLONG,SDLONG,CDLONG,COSECA,X,Y

!     TREAT SAME LONGITUDE AS SPECIAL CASE:
      IF(OLONG.EQ.SLONG)THEN
          IF(OLAT.LE.SLAT)THEN
              ECA=SLAT-OLAT
              PSI=0.D0
          ELSE
              ECA=OLAT-SLAT
              PSI=180.D0
          ENDIF
          RETURN
      ENDIF
      OLATR=DBLE(OLAT)/DPDEG
      SOLAT=SIN(OLATR)
      COLAT=COS(OLATR)
      SLATR=SLAT/DPDEG
      SSLAT=SIN(SLATR)
      CSLAT=COS(SLATR)
      DLONG=DBLE(OLONG-SLONG)/DPDEG
      SDLONG=SIN(DLONG)
      CDLONG=COS(DLONG)
      COSECA=COLAT*CSLAT*CDLONG+SOLAT*SSLAT
      IF(COSECA.GE.1.D0)THEN
          ECA=0.D0
      ELSEIF(COSECA.GT.-1.D0)THEN
          ECA=DPDEG*ACOS(COSECA)
      ELSE
          ECA=180.D0
      ENDIF
      X=COLAT*SSLAT-SOLAT*CSLAT*CDLONG
      Y=CSLAT*SDLONG
      IF(X.EQ.0.D0)THEN
          PSI=90.D0
          IF(Y.LT.0.D0)PSI=-90.D0
      ELSE
          PSI=DPDEG*ATAN(Y/X)
          IF(X.LT.0.D0)PSI=PSI+180
          IF(PSI.GT.180.D0)PSI=PSI-360
      ENDIF
      RETURN
      END
