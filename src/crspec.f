      SUBROUTINE CRSPEC(CTHICK,CWDCOL,CIPCOL)

!     CRSPEC DEFINES CLOUD PARTICLE SPECTRAL DATA:
!       EXTC(6,IWAV)   WATER DROPLET EXTINCTION COEFS [KM-1 M3/GM].
!       ABSC(6,IWAV)   WATER DROPLET ABSORPTION COEFS [KM-1 M3/GM].
!       ASYM(6,IWAV)   WATER DROPLET PHASE FUNCTION ASYMMETRY FACTORS.
!       EXTC(7,IWAV)   ICE PARTICLE EXTINCTION COEFS [KM-1 M3/GM].
!       ABSC(7,IWAV)   ICE PARTICLE ABSORPTION COEFS [KM-1 M3/GM].
!       ASYM(7,IWAV)   ICE PARTICLE PHASE FUNCTION ASYMMETRY FACTORS.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     ARGUMENTS:
!     CTHICK   INPUT CLOUD VERTICAL THICKNESS [KM]
!     CWDCOL   INPUT WATER DROPLET VERTICAL COLUMN DENSITY [KM GM/M3]
!     CIPCOL   INPUT ICE PARTICLE VERTICAL COLUMN DENSITY [KM GM/M3]
      REAL CTHICK,CWDCOL,CIPCOL

!     COMMONS:
      INCLUDE 'BASE.h'
      INCLUDE 'IFIL.h'
      REAL VX0
      COMMON/EXTWAV/VX0(NWAVLN)
      REAL CLDSPC
      COMMON/CLDDAT/CLDSPC(NWAVLN,3,NCLDS)
      REAL CIRSPC
      COMMON/CIRR/CIRSPC(NWAVLN,3,NCIRS)

!     /CARD2/
!       IHAZE    BOUNDARY LAYER AEROSOL MODEL NUMBER.
!       ISEASN   SEASON NUMBER (1=SPRING-SUMMER, 2=FALL-WINTER).
!       IVULCN   VOLCANIC AEROSOL MODEL NUMBER.
!       ICSTL    COASTAL AIRMASS MODEL NUMBER.
!       ICLD     CLOUD MODEL NUMBER.
!       IVSA     VERTICAL STRUCTURE ALGORITHM (0=OFF, 1=ON).
!       VIS      SURFACE VISIBILITY (GROUND METEOROLOGICAL RANGE) [KM].
!       WSS      CURRENT WIND SPEED (M/S).
!       WHH      24-HOUR WIND SPEED (M/S).
!       RAINRT   RAIN RATE (MM/HR)
!       LSAP     LOGICAL FLAG FOR SPECTRAL AEROSOL PROFILES INPUT.
      INTEGER IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA
      REAL VIS,WSS,WHH,RAINRT
      LOGICAL LSAP
      COMMON/CARD2/IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA,                 &
     &  VIS,WSS,WHH,RAINRT,LSAP

!     /CARD2A/
      INTEGER NCRALT,NCRSPC
      DOUBLE PRECISION CTHIK,CALT
      REAL CEXT,CWAVLN,CCOLWD,CCOLIP,CHUMID,ASYMWD,ASYMIP
      COMMON/CARD2A/CTHIK,CALT,CEXT,NCRALT,NCRSPC,CWAVLN,CCOLWD,        &
     &  CCOLIP,CHUMID,ASYMWD,ASYMIP

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      SAVE /CLDDAT/,/CIRR/,/EXTWAV/
      EXTERNAL DEVCBD,CLDDTA,EXTDT1,EXTDT2

!     LIST LOCAL VARIABLES AND ARRAYS
      INTEGER ICLD0,ICIR0,IWAV,IWAVM1
      REAL REFWAV,REFDEP,FACTOR,EXTWD,EXTIP,XNRMWD,XNRMIP,RATDEP

!     DATA:
!       MDLCLD    MAPS CLOUD/RAIN MODEL INDICES TO CLOUD MODEL INDICES.
!       MDLCIR    MAPS CLOUD/RAIN MODEL INDICES TO CIRRUS MODEL INDICES.
!       CLDX55    550 NM CLOUD MODEL EXTINCTION COEFS [KM-1 M3/GM].
!       CIRX55    550 NM CIRRUS MODEL EXTINCTION COEFS [KM-1 M3/GM].
      INTEGER MDLCLD(NMDLS),MDLCIR(NMDLS)
      REAL CLDX55(NCLDS),CIRX55(NCIRS)
      DATA MDLCLD/1,2,3,4,5,3,5,5,1,1/,MDLCIR/NMDLS*1/
      DATA CLDX55/130.16,222.78,189.68,239.41,133.01/
      DATA CIRX55/ 17.21,290.19/

!     RETURN IF CLOUD/RAIN MODEL 1 THROUGH NMDLS WAS NOT SELECTED
      IF(ICLD.LT.1 .OR. ICLD.GT.NMDLS)RETURN

!     CHECK INPUT WATER DROPLET AND ICE PARTICLE COLUMN DENSITIES
      IF(CWDCOL.LE.0.)CWDCOL=0.
      IF(CIPCOL.LE.0.)CIPCOL=0.
      IF(CWDCOL.EQ.0. .AND. CIPCOL.EQ.0.)THEN

!         FATAL ERROR:  NO CLOUD VERTICAL COLUMN DENSITY
          WRITE(IPR,'(/2A)')' FATAL ERROR:  CLOUD WATER DROPLET AND',   &
     &      ' ICE PARTICLE VERTICAL COLUMN DENSITIES ARE BOTH ZERO.'
          IF(LJMASS)CALL WRTBUF(FATAL)
          STOP 'FATAL ERROR:  NO CLOUD VERTICAL COLUMN DENSITY'
      ENDIF

!     ICLD0    INDEX OF CLOUD WATER DROPLET MODEL
!     ICIR0    INDEX OF CLOUD ICE PARTICLE MODEL
      ICLD0=MDLCLD(ICLD)
      ICIR0=MDLCIR(ICLD)

!     FOR THE CLOUD MODELS, THE EXTINCTION COEFFICIENT NORMALIZATION
!     IS INCORPORATED INTO THE SPECTRAL DATA INSTEAD OF THE COLUMN
!     DENSITY.  THEREFORE, THE AWCCON INPUTS TO GAMFOG ARE ONE.
      AWCCON(6)=1.
      AWCCON(7)=1.

!     XNRMWD   WATER DROPLET EXTINCTION COEFFICIENT NORMALIZATION
!     XNRMIP   ICE PARTICLE EXTINCTION COEFFICIENT NORMALIZATION
      XNRMWD=CLDX55(ICLD0)
      XNRMIP=CIRX55(ICIR0)

!     REFWAV    REFERENCE WAVELENGTH (MICRONS)
      REFWAV=CWAVLN
      IF(CWAVLN.LT.VX0(1) .OR. CWAVLN.GT.VX0(NWAVLN-1))REFWAV=.55

!     THE REFERENCE VERTICAL CLOUD DEPTH (REFDEP) IS THE
!     PRODUCT OF THE VERTICAL EXTINCTION (CEXT) [KM-1]
!     AND THE CLOUD THICKNESS (CTHICK) [KM].

!     REFDEP    VERTICAL CLOUD DEPTH AT REFWAV
      REFDEP=0.
      IF(CEXT.GT.0.)THEN
          REFDEP=CEXT*CTHICK

!         ANNOUNCE CLOUD SPECTRAL DATA SCALING:
          IF(.NOT.LJMASS)WRITE(IPR,'(/2A,/A,3(F9.4,A))')                &
     &      ' CLOUD SPECTRAL DATA IS BEING SCALED',                     &
     &      ' TO MAKE THE CLOUD VERTICAL EXTINCTION',                   &
     &      ' AT',REFWAV,' MICRONS EQUAL TO',CEXT,                      &
     &      ' KM-1 FOR THE',CTHICK,' KM THICK CLOUD.'
      ENDIF

!     KEY ON NCRSPC TO DETERMINE IF USER-DEFINED
!     SPECTRAL DATA IS TO BE USED.
      IF(NCRSPC.EQ.1)THEN

!         CLOUD SPECTRAL DATA IN EXTERNAL FILES:
          CALL CRFILE(REFWAV,REFDEP,CWDCOL,CIPCOL)
          RETURN
      ELSEIF(NCRSPC.GE.2)THEN

!         USER-DEFINED SPECTRAL DATA:
          CALL CRUSPC(REFWAV,REFDEP,                                    &
     &      ICLD0,ICIR0,XNRMWD,XNRMIP,CWDCOL,CIPCOL,CTHICK)
          RETURN
      ENDIF
      IF(REFDEP.GT.0.)THEN

!         DETERMINE BRACKETING WAVELENGTHS FOR MODEL DATA
          IWAVM1=1
          DO 10 IWAV=2,NWAVLN-2
              IF(REFWAV.LE.VX0(IWAV))GOTO 20
              IWAVM1=IWAV
   10     CONTINUE
   20     CONTINUE

!         DETERMINE DEFAULT EXTINCTION COEFFICIENTS AT REFWAV
          FACTOR=(REFWAV-VX0(IWAVM1))/(VX0(IWAV)-VX0(IWAVM1))
          EXTWD=CLDSPC(IWAVM1,1,ICLD0)
          EXTWD=XNRMWD*(EXTWD+FACTOR*(CLDSPC(IWAV,1,ICLD0)-EXTWD))
          EXTIP=CIRSPC(IWAVM1,1,ICIR0)
          EXTIP=XNRMIP*(EXTIP+FACTOR*(CIRSPC(IWAV,1,ICIR0)-EXTIP))

!         DETERMINE RATIO OF INPUT TO CURRENT CLOUD DEPTH
          RATDEP=REFDEP/(EXTWD*CWDCOL+EXTIP*CIPCOL)

!         SCALE EXTINCTION COEFFICIENT NORMALIZATION CONSTANTS
!         SO THAT INPUT CLOUD EXTINCTION RESULTS
          XNRMWD=RATDEP*XNRMWD
          XNRMIP=RATDEP*XNRMIP
      ENDIF

!     STORE THE CLOUD PARTICLE SPECTRAL DATA
      DO 30 IWAV=1,NWAVLN-1
          EXTC(6,IWAV)=XNRMWD*CLDSPC(IWAV,1,ICLD0)
          ABSC(6,IWAV)=XNRMWD*CLDSPC(IWAV,2,ICLD0)
          IF(ABS(ASYMWD).LT.1.)THEN
              ASYM(6,IWAV)=ASYMWD
          ELSE
              ASYM(6,IWAV)=CLDSPC(IWAV,3,ICLD0)
          ENDIF
          EXTC(7,IWAV)=XNRMIP*CIRSPC(IWAV,1,ICIR0)
          ABSC(7,IWAV)=XNRMIP*CIRSPC(IWAV,2,ICIR0)
          IF(ABS(ASYMIP).LT.1.)THEN
              ASYM(7,IWAV)=ASYMIP
          ELSE
              ASYM(7,IWAV)=CIRSPC(IWAV,3,ICIR0)
          ENDIF
   30 CONTINUE

!     RETURN IF SPECTRAL DATA IS NOT TO BE OUTPUT.
      IF(NPR.GE.0)RETURN

!     WRITE SPECTRAL DATA HEADER
      IF(.NOT.LJMASS)WRITE(IPR,'(A,/A,//54X,A,34X,A,/38X,A,2X,A,/(3A))')&
     &  '1',' CLOUD SPECTRAL DATA','WATER DROPLETS','ICE PARTICLES',    &
     &  '----------------------------------------------',               &
     &  '----------------------------------------------',               &
     &  ' IWAV   WAVLEN       FREQ   VERT EXT',                         &
     &  '  EXT COEF  ABS COEF  SCT COEF     ASYM  SCT ALB',             &
     &  '  EXT COEF  ABS COEF  SCT COEF     ASYM  SCT ALB',             &
     &  '      (MICRON)     (CM-1)     (KM-1)',                         &
     &  '  (        KM-1 M3/GM        )                  ',             &
     &  '  (        KM-1 M3/GM        )'

!     WRITE SPECTRAL DATA
      IF(.NOT.LJMASS) THEN
          WRITE(IPR,'((I4,F10.4,F11.3,F11.5,2(3F10.5,2F9.5)))')         &
     &      (IWAV,VX0(IWAV),10000./VX0(IWAV),                           &
     &      (EXTC(6,IWAV)*CWDCOL+EXTC(7,IWAV)*CIPCOL)/CTHICK,           &
     &      EXTC(6,IWAV),ABSC(6,IWAV),EXTC(6,IWAV)-ABSC(6,IWAV),        &
     &      ASYM(6,IWAV),1.-ABSC(6,IWAV)/EXTC(6,IWAV),                  &
     &      EXTC(7,IWAV),ABSC(7,IWAV),EXTC(7,IWAV)-ABSC(7,IWAV),        &
     &      ASYM(7,IWAV),1.-ABSC(7,IWAV)/EXTC(7,IWAV),IWAV=1,NWAVLN-1)
          WRITE(IPR,'(/A,//)')' END OF CLOUD PARTICLE SPECTRAL DATA'
      ENDIF
      RETURN
      END
