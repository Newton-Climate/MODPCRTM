      SUBROUTINE DPFISH(ALT,RSCLHT,GAMMA0)

!     DPFISH CALCULATES THE RECIPROCAL OF THE REFRACTIVITY
!     SCALE HEIGHT AT THE INPUT ALTITUDE ALONG WITH THE
!     REFRACTIVITY EXTRAPOLATED TO 0 KM ALTITUDE.

!     INPUT ARGUMENTS:
!       ALT      ALTITUDE [KM].

!     OUTPUT ARGUMENTS:
!       RSCLHT   RECIPROCAL OF THE REFRACTIVITY SCALE HEIGHT [KM-1].
!       GAMMA0   REFRACTIVITY EXTRAPOLATED DOWN TO 0 KM ALTITUDE.
      DOUBLE PRECISION ALT,RSCLHT,GAMMA0

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     COMMONS:

!     /CNTRL/
!       NSEG     NUMBER OF PATH SEGMENTS ALONG LINE-OF-SIGHT.
!       ML       NUMBER OF ATMOSPHERIC PROFILE LEVELS.
!       MLFLX    NUMBER OF LEVELS FOR WHICH FLUX VALUES ARE WRITTEN.
!       IMULT    MULTIPLE SCATTERING FLAG
!                  (0=NONE, 1=AT SENSOR, -1=AT FINAL OR TANGENT POINT).
!       THERML   FLAG TO CALCULATE THERMAL SCATTER.
      INTEGER NSEG,ML,MLFLX,IMULT
      LOGICAL THERML
      COMMON/CNTRL/NSEG(0:MLOSP1),ML,MLFLX,IMULT,THERML

!     /MPROF/
!       ZM       PROFILE LEVEL ALTITUDES [KM].
!       PM       PROFILE LEVEL PRESSURES [MBAR].
!       TM       PROFILE LEVEL TEMPERATURES [K].
!       RFNDX    PROFILE LEVEL REFRACTIVITIES.
!       LRHSET   FLAG, .TRUE. IF RELATIVE HUMIDITY IS NOT TO BE SCALED.
      DOUBLE PRECISION ZM
      REAL PM,TM,RFNDX
      LOGICAL LRHSET
      COMMON/MPROF/ZM(LAYDIM),PM(LAYDIM),TM(LAYDIM),                    &
     &  RFNDX(LAYDIM),LRHSET(LAYDIM)

!     LOCAL VARIABLES:
!       I1      INDEX OF LAYER LOWER BOUNDARY.
!       I2      INDEX OF LAYER UPPER BOUNDARY.
      INTEGER I1,I2

!     LOOP OVER LAYERS:
      I1=1
      DO I2=2,ML
          IF(ZM(I2).GE.ALT)THEN
              CALL DPSCHT(ZM(I1),ZM(I2),                                &
     &          DBLE(RFNDX(I1)),DBLE(RFNDX(I2)),RSCLHT,GAMMA0)
              RETURN
          ENDIF
          I1=I2
      ENDDO
      RSCLHT=DBLE(0.)
      GAMMA0=DBLE(RFNDX(ML))
      RETURN
      END
