      SUBROUTINE GEOPTH(H1ALT,H2ALT,OBSZEN,HRANGE,BETA,REARTH,          &
     &  HMIN,HMAX,CKRANG,BCKZEN,BEND)

!     GEOPTH DRIVES COLUMN DENSITY CALCULATIONS FOR USER-DEFINED PATHS.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     INPUT ARGUMENTS:
!       H1ALT    OBSERVER (SENSOR) ALTITUDE [KM].
!       H2ALT    FINAL (TANGENT FOR LIMB PATH) ALTITUDE [KM].
!       OBSZEN   OBSERVER ZENITH ANGLE (H1ALT TO H2ALT) [DEG].
!       HRANGE   DISTANCE FROM H1ALT TO H2ALT [KM].
!       BETA     EARTH CENTER ANGLE BETWEEN H1ALT AND H2ALT [DEG].
!       REARTH   RADIUS OF THE EARTH [KM].
!       HMIN     PATH MINIMUM ALTITUDE [KM].
!       HMAX     PATH MAXIMUM ALTITUDE [KM].
!       CKRANG   MAXIMUM PATH RANGE FOR K-DISTRIBUTION OUTPUT
!                (=0. FOR TOTAL PATH ONLY; <0. FOR ALL RANGES).
!       BCKZEN   ZENITH ANGLE FOR BACKWARD (H2ALT TO H1ALT) PATH [DEG].
      DOUBLE PRECISION H1ALT,H2ALT,OBSZEN,HRANGE,BETA,REARTH,HMIN,HMAX, &
     &  CKRANG,BCKZEN

!     OUTPUT ARGUMENTS:
!       BEND     PATH BENDING FROM REFRACTION [DEG].
      DOUBLE PRECISION BEND

!     COMMONS:
      INCLUDE 'SOLS.h'

!     /PATH/
!       PTHCOS   COSINE OF PATH ZENITH AT PATH BOUNDARIES.
!       PTHZEN   PATH ZENITH AT PATH BOUNDARIES [DEG].
!       PTHECA   SENSOR TO PATH EARTH CENTER ANGLE [DEG].
!       PTHALT   ALTITUDES AT PATH BOUNDARIES [KM].
!       PTH_MS   ALTITUDES AT PATH BOUNDARIES FOR THE MS PATH.
!       PTHSEG   PATH SEGMENT LENGTH [KM].
!       PTHRNG   SENSOR TO PATH BOUNDARY RANGE [KM].
!       JMAX     NUMBER OF DISTINCT LOS PATH SEGMENT ENDPOINT ALTITUDES.
!       IKHMIN   PATH BOUNDARY INDEX OF PATH MINIMUM ALTITUDE.
!       IKHMAX   PATH BOUNDARY INDEX OF PATH MAXIMUM ALTITUDE.
!       IKOUT    NUMBER OF PATH BOUNDARIES K DATA IS OUTPUT.
!       NTKDIS   RECORD NUMBER FOR K-DISTRIBUTION TRANSMITTANCE FILE.
!       NRKDIS   RECORD NUMBER FOR K-DISTRIBUTION RADIANCE FILE.
!       MAPPTH   MAPPING FROM PATH SEGMENT MIDPOINT TO VERTICAL LAYER.
!       IPTHHT   ALTITUDES (HEIGHTS) AT PATH BOUNDARIES [M].
!       LOWALT   VERTICAL LAYER BOUNDARY INDEX AT OR JUST BELOW PTHALT.
!       FACALT   ALTITUDE INTERPOLATION FRACTION FOR PTHALT
!       PATH_T   TEMPERATURE AT PATH BOUNDARIES [K].
!       PATH_P   PRESSURE AT PATH BOUNDARIES [ATM].
!       PTHRH    RELATIVE HUMIDITY AT PATH BOUNDARIES [K].
!       LSSGEO   LOGICAL FLAG, .TRUE. FOR SOLAR PATHS.
!       LTANMX   LOGICAL FLAG, .TRUE. IF PATH HAS A TANGENT MAXIMUM.
      DOUBLE PRECISION PTHCOS,PTHZEN,PTHECA,PTHALT,PTH_MS,PTHSEG,PTHRNG
      INTEGER JMAX,IKHMIN,IKHMAX,IKOUT,NTKDIS,NRKDIS,MAPPTH,            &
     &  IPTHHT,LOWALT
      REAL FACALT,PATH_T,PATH_P,PTHRH
      LOGICAL LSSGEO,LTANMX
      COMMON/PATH/PTHCOS(0:LAYTWO),PTHZEN(0:LAYTWO),PTHECA(0:LAYTWO),   &
     &  PTHALT(0:LAYTWO,1:MLOS),PTH_MS(0:LAYDIM),PTHSEG(LAYTWO),        &
     &  PTHRNG(0:LAYTWO,1:MLOS),JMAX,IKHMIN(MLOS),IKHMAX(MLOS),         &
     &  IKOUT(MLOS),MAPPTH(LAYTWO,1:MLOS),IPTHHT(0:LAYTWO),NTKDIS,      &
     &  NRKDIS,LOWALT(0:LAYTWO,1:MLOS),FACALT(0:LAYTWO,1:MLOS),         &
     &  PATH_T(0:LAYTWO,1:MLOS),PATH_P(0:LAYTWO,1:MLOS),                &
     &  PTHRH(0:LAYTWO,1:MLOS),LSSGEO,LTANMX

!     /CNTRL/
!       NSEG     NUMBER OF PATH SEGMENTS ALONG LINE-OF-SIGHT.
!       ML       NUMBER OF ATMOSPHERIC PROFILE LEVELS.
!       MLFLX    NUMBER OF LEVELS FOR WHICH FLUX VALUES ARE WRITTEN.
!       IMULT    MULTIPLE SCATTERING FLAG
!                  (0=NONE, 1=AT SENSOR, -1=AT FINAL OR TANGENT POINT).
!       THERML   FLAG TO CALCULATE THERMAL SCATTER.
      INTEGER NSEG,ML,MLFLX,IMULT
      LOGICAL THERML
      COMMON/CNTRL/NSEG(0:MLOSP1),ML,MLFLX,IMULT,THERML

!     LOCAL VARIABLES:
!       JSTART   PATH SEGMENT STARTING ALTITUDE INDEX.
      INTEGER JSTART

!     CHECK FOR SPECIAL K-DATA OUTPUT INDEX CONDITIONS:
      IF(CKRANG.LT.0.D0 .OR. CKRANG.GE.HRANGE)THEN

!         WRITE K DATA AT ALL LEVELS.
          IKOUT(1)=NSEG(1)
          CKRANG=HRANGE
      ELSEIF(CKRANG.GT.0.D0)THEN

!         FLAG A POSITIVE RANGE BY SETTING IKOUT(1) TO NSEG(1)+1:
          IKOUT(1)=NSEG(1)+1
      ELSE

!         CKRANG IS ZERO:  WRITE K DATA FOR TOTAL PATH ONLY.
          IKOUT(1)=0
      ENDIF

!     USER-SPECIFIED PATH:
      JMAX=1
      JTURN=0
      IF(H1ALT.LT.H2ALT)THEN

!         CHECK FOR TANGENT MAXIMUM:
          IF(HMAX.GT.H2ALT)THEN

!             SET JTURN:
              JTURN=IKHMAX(1)+1

!             FETCH DENSITIES FROM H1ALT UP TO THE H2ALT ALTITUDE:
              CALL FILL(H1ALT,H2ALT,JMAX)
              JSTART=JMAX

!             FETCH DENSITIES FROM H2ALT UP TO THE HMAX ALTITUDE:
              CALL FILL(H2ALT,HMAX,JMAX)

!             COLUMN DENSITIES FROM H1ALT FORWARD TO HMAX:
              CALL H1_HMX(REARTH,CKRANG,1)

!             COLUMN DENSITIES FROM H2ALT BACK TO HMAX (VALUES ARE
!             RECOMPUTED TO HANDLE POSSIBLE ASYMMETRIC PATHS):
              CALL H2_HMX(REARTH,CKRANG,JSTART)
          ELSE

!             CHECK FOR TANGENT MINIMUM:
              IF(HMIN.LT.H1ALT)THEN

!                 SET JTURN:
                  JTURN=IKHMIN(1)+1

!                 FETCH DENSITIES FROM HMIN UP TO THE H1ALT ALTITUDE:
                  CALL FILL(HMIN,H1ALT,JMAX)

!                 COLUMN DENSITIES FROM HMIN BACK TO H1ALT:
                  CALL HMN_H1(REARTH,CKRANG)
              ENDIF

!             FETCH DENSITIES FROM H1ALT UP TO THE H2ALT ALTITUDE:
              CALL FILL(H1ALT,H2ALT,JMAX)

!             COLUMN DENSITIES FROM HMIN FORWARD TO H2ALT (VALUES ARE
!             RECOMPUTED BETWEEN HMIN & H1ALT FOR ASYMMETRIC PATHS):
              CALL HMN_H2(REARTH,CKRANG)
          ENDIF
      ELSEIF(H2ALT.LT.H1ALT)THEN

!         CHECK FOR TANGENT MAXIMUM:
          IF(HMAX.GT.H1ALT)THEN

!             SET JTURN:
              JTURN=IKHMAX(1)+1

!             FETCH DENSITIES FROM H2ALT UP TO THE H1ALT ALTITUDE:
              CALL FILL(H2ALT,H1ALT,JMAX)
              JSTART=JMAX

!             FETCH DENSITIES FROM H1ALT UP TO THE HMAX ALTITUDE:
              CALL FILL(H1ALT,HMAX,JMAX)

!             COLUMN DENSITIES FROM H1ALT FORWARD TO HMAX:
              CALL H1_HMX(REARTH,CKRANG,JSTART)

!             COLUMN DENSITIES FROM H2ALT BACK TO HMAX (VALUES ARE
!             RECOMPUTED TO HANDLE POSSIBLE ASYMMETRIC PATHS):
              CALL H2_HMX(REARTH,CKRANG,1)
          ELSE

!             CHECK FOR TANGENT PATH:
              IF(HMIN.LT.H2ALT)THEN

!                 SET JTURN:
                  JTURN=IKHMIN(1)+1

!                 FETCH DENSITIES UP TO THE H2ALT ALTITUDE:
                  CALL FILL(HMIN,H2ALT,JMAX)

!                 COMPUTES HMIN TO H1ALT SEGMENT COLUMN DENSITIES:
                  CALL HMN_H2(REARTH,CKRANG)
              ENDIF

!             FETCH DENSITIES UP TO THE H1ALT ALTITUDE:
              CALL FILL(H2ALT,H1ALT,JMAX)

!             COLUMN DENSITIES FROM HMIN BACK TO H1ALT (VALUES ARE
!             RECOMPUTED BETWEEN HMIN & H2ALT FOR ASYMMETRIC PATHS):
              CALL HMN_H1(REARTH,CKRANG)
          ENDIF
      ELSEIF(HMAX.GT.H1ALT)THEN

!         SET JTURN:
          JTURN=IKHMAX(1)+1

!         FETCH DENSITIES FROM H1ALT=H2ALT UP TO THE HMAX ALTITUDE:
          CALL FILL(H1ALT,HMAX,JMAX)

!         COLUMN DENSITIES FROM H1ALT FORWARD TO HMAX:
          CALL H1_HMX(REARTH,CKRANG,1)

!         COLUMN DENSITIES FROM H2ALT BACK TO HMAX (VALUES ARE
!         RECOMPUTED TO HANDLE POSSIBLE ASYMMETRIC PATHS):
          CALL H2_HMX(REARTH,CKRANG,1)
      ELSE

!         SET JTURN:
          JTURN=IKHMIN(1)+1

!         FETCH DENSITIES UP TO THE H1ALT=H2ALT ALTITUDE:
          CALL FILL(HMIN,H1ALT,JMAX)

!         COLUMN DENSITIES FROM HMIN BACK TO H1ALT:
          CALL HMN_H1(REARTH,CKRANG)

!         COLUMN DENSITIES FROM HMIN FORWARD TO H2ALT:
          CALL HMN_H2(REARTH,CKRANG)
      ENDIF

!     COMPUTE PATH BENDING FOR OUTPUT:
      BEND=BETA-OBSZEN+180-BCKZEN

!     RETURN TO GEO:
      RETURN
      END

      SUBROUTINE HMN_H1(REARTH,CKRANG)

!     HMN_H1 COMPUTES HMIN TO H1ALT COLUMN DENSITIES FOR
!     USER-DEFINED PATHS.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     INPUT ARGUMENTS:
!       REARTH   RADIUS OF THE EARTH [KM].
!       CKRANG   MAXIMUM PATH RANGE FOR K-DISTRIBUTION OUTPUT
!                (=0. FOR TOTAL PATH ONLY; <0. FOR ALL RANGES).
      DOUBLE PRECISION REARTH,CKRANG

!     COMMONS:
      INCLUDE 'SEGDAT.h'
      INCLUDE 'YPROP.h'

!     /PATH/
!       PTHCOS   COSINE OF PATH ZENITH AT PATH BOUNDARIES.
!       PTHZEN   PATH ZENITH AT PATH BOUNDARIES [DEG].
!       PTHECA   SENSOR TO PATH EARTH CENTER ANGLE [DEG].
!       PTHALT   ALTITUDES AT PATH BOUNDARIES [KM].
!       PTH_MS   ALTITUDES AT PATH BOUNDARIES FOR THE MS PATH.
!       PTHSEG   PATH SEGMENT LENGTH [KM].
!       PTHRNG   SENSOR TO PATH BOUNDARY RANGE [KM].
!       JMAX     NUMBER OF DISTINCT LOS PATH SEGMENT ENDPOINT ALTITUDES.
!       IKHMIN   PATH BOUNDARY INDEX OF PATH MINIMUM ALTITUDE.
!       IKHMAX   PATH BOUNDARY INDEX OF PATH MAXIMUM ALTITUDE.
!       IKOUT    NUMBER OF PATH BOUNDARIES K DATA IS OUTPUT.
!       NTKDIS   RECORD NUMBER FOR K-DISTRIBUTION TRANSMITTANCE FILE.
!       NRKDIS   RECORD NUMBER FOR K-DISTRIBUTION RADIANCE FILE.
!       MAPPTH   MAPPING FROM PATH SEGMENT MIDPOINT TO VERTICAL LAYER.
!       IPTHHT   ALTITUDES (HEIGHTS) AT PATH BOUNDARIES [M].
!       LOWALT   VERTICAL LAYER BOUNDARY INDEX AT OR JUST BELOW PTHALT.
!       FACALT   ALTITUDE INTERPOLATION FRACTION FOR PTHALT
!       PATH_T   TEMPERATURE AT PATH BOUNDARIES [K].
!       PATH_P   PRESSURE AT PATH BOUNDARIES [ATM].
!       PTHRH    RELATIVE HUMIDITY AT PATH BOUNDARIES [K].
!       LSSGEO   LOGICAL FLAG, .TRUE. FOR SOLAR PATHS.
!       LTANMX   LOGICAL FLAG, .TRUE. IF PATH HAS A TANGENT MAXIMUM.
      DOUBLE PRECISION PTHCOS,PTHZEN,PTHECA,PTHALT,PTH_MS,PTHSEG,PTHRNG
      INTEGER JMAX,IKHMIN,IKHMAX,IKOUT,NTKDIS,NRKDIS,MAPPTH,            &
     &  IPTHHT,LOWALT
      REAL FACALT,PATH_T,PATH_P,PTHRH
      LOGICAL LSSGEO,LTANMX
      COMMON/PATH/PTHCOS(0:LAYTWO),PTHZEN(0:LAYTWO),PTHECA(0:LAYTWO),   &
     &  PTHALT(0:LAYTWO,1:MLOS),PTH_MS(0:LAYDIM),PTHSEG(LAYTWO),        &
     &  PTHRNG(0:LAYTWO,1:MLOS),JMAX,IKHMIN(MLOS),IKHMAX(MLOS),         &
     &  IKOUT(MLOS),MAPPTH(LAYTWO,1:MLOS),IPTHHT(0:LAYTWO),NTKDIS,      &
     &  NRKDIS,LOWALT(0:LAYTWO,1:MLOS),FACALT(0:LAYTWO,1:MLOS),         &
     &  PATH_T(0:LAYTWO,1:MLOS),PATH_P(0:LAYTWO,1:MLOS),                &
     &  PTHRH(0:LAYTWO,1:MLOS),LSSGEO,LTANMX

!     /CARD2/
!       IHAZE    BOUNDARY LAYER AEROSOL MODEL NUMBER.
!       ISEASN   SEASON NUMBER (1=SPRING-SUMMER, 2=FALL-WINTER).
!       IVULCN   VOLCANIC AEROSOL MODEL NUMBER.
!       ICSTL    COASTAL AIRMASS MODEL NUMBER.
!       ICLD     CLOUD MODEL NUMBER.
!       IVSA     VERTICAL STRUCTURE ALGORITHM (0=OFF, 1=ON).
!       VIS      SURFACE VISIBILITY (GROUND METEOROLOGICAL RANGE) [KM].
!       WSS      CURRENT WIND SPEED (M/S).
!       WHH      24-HOUR WIND SPEED (M/S).
!       RAINRT   RAIN RATE (MM/HR)
!       LSAP     LOGICAL FLAG FOR SPECTRAL AEROSOL PROFILES INPUT.
      INTEGER IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA
      REAL VIS,WSS,WHH,RAINRT
      LOGICAL LSAP
      COMMON/CARD2/IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA,                 &
     &  VIS,WSS,WHH,RAINRT,LSAP

!     /SAP/
!       NWVSAP   NUMBER OF AEROSOL SPECTRAL GRID POINTS.
!       NLGSAP   HIGHEST AEROSOL PHASE FUNCTION LEGENDRE MOMENT.
!       NANSAP   NUMBER OF AEROSOL PHASE FUNCTION ANGULAR POINTS.
!       LEVSAP   NUMBER OF AEROSOL ATMOSPHERIC LEVELS.
!       ANGSAP   AEROSOL PHASE FUNCTION ANGULAR GRID [DEGREES].
!       COSSAP   COSINE OF THE AEROSOL PHASE FUNCTION ANGLES.
!       WAVSAP   AEROSOL SPECTRAL GRID [MICRONS].
!       LEGSAP   AEROSOL LEGENDRE COEFFICIENTS.
!       PFSAP    AEROSOL PHASE FUNCTION [SR-1].
!       LOSSAP   LOGICAL FLAG, TRUE FOR LINE-OF-SIGHT PATH.
      INTEGER NWVSAP,NLGSAP,NANSAP,LEVSAP
      DOUBLE PRECISION ANGSAP,COSSAP,PFSAP
      REAL WAVSAP,LEGSAP
      LOGICAL LOSSAP
      COMMON/SAP/NWVSAP,NLGSAP,NANSAP,LEVSAP,ANGSAP(MANSAP),            &
     &  COSSAP(MANSAP),WAVSAP(MWVSAP),LEGSAP(MLGSAP,MWVSAP,LAYDIM),     &
     &  PFSAP(MANSAP,MWVSAP,LAYDIM),LOSSAP

!     /FILLP/
!       ZP       ALTITUDE AT REFRACTED PATH LEVELS [KM].
!       RANGEP   SEGMENT RANGE [KM].
!       PP       PRESSURE AT REFRACTED PATH LEVELS [MBAR].
!       TP       TEMPERATURE AT REFRACTED PATH LEVELS [K].
!       RHP      RELATIVE HUMIDITY AT REFRACTED PATH LEVELS [%].
!       RFNDXP   REFRACTIVITIES AT REFRACTED PATH LEVELS.
!       DENPTH   ATMOSPHERIC LEVEL DENSITIES
!                [UNITS VARY WITH SPECIES BUT MOST OFTEN ATM CM / KM].
      DOUBLE PRECISION ZP,RANGEP,PP,RFNDXP,DENPTH
      REAL TP,RHP
      COMMON/FILLP/ZP(LAYDM1),RANGEP(LAYDM1),PP(LAYDM1),                &
     &  TP(LAYDM1),RHP(LAYDM1),RFNDXP(LAYDM1),DENPTH(0:MEXTXY,1:LAYDM1)

!     /SAPDEP/
!       XSAP     LINE-OF-SIGHT PATH AEROSOL EXTINCTION OPTICAL DEPTHS.
!       XSAPM    VERTICAL MS PATH AEROSOL EXTINCTION OPTICAL DEPTHS.
!       ASAP     LINE-OF-SIGHT PATH AEROSOL ABSORPTION OPTICAL DEPTHS.
!       ASAPM    VERTICAL MS PATH AEROSOL ABSORPTION OPTICAL DEPTHS.
!       XSAPS    SOLAR PATH AEROSOL EXTINCTION OPTICAL DEPTHS FROM LOS.
!       XSAPSM   SOLAR PATH AEROSOL EXTINCTION OPTICAL DEPTHS FOR MS.
!       ASAPS    SOLAR PATH AEROSOL ABSORPTION OPTICAL DEPTHS FROM LOS.
!       ASAPSM   SOLAR PATH AEROSOL ABSORPTION OPTICAL DEPTHS FOR MS.
      REAL XSAP,XSAPM,ASAP,ASAPM,XSAPS,XSAPSM,ASAPS,ASAPSM
      COMMON/SAPDEP/                                                    &
     &  XSAP(1:MWVSAP,0:LAYTWO,0:MLOS),XSAPM(1:MWVSAP,0:LAYDIM),        &
     &  ASAP(1:MWVSAP,0:LAYTWO,0:MLOS),ASAPM(1:MWVSAP,0:LAYDIM),        &
     &  XSAPS(MWVSAP,LAYTWO,MLOS),XSAPSM(MWVSAP,LAYDIM),                &
     &  ASAPS(MWVSAP,LAYTWO,MLOS),ASAPSM(MWVSAP,LAYDIM)

!     LOCAL VARIABLES:
!       IK       PATH SEGMENT INDEX.
!       IKM1     IK MINUS 1
!       JBEG     INDEX FOR BEGINNING OF REFRACTED PATH SEGMENT ALTITUDE.
!       JEND     INDEX FOR END OF REFRACTED PATH SEGMENT ALTITUDE.
!       KSPEC    SPECIES LOOP INDEX.
!       IWVSAP   SPECTRAL GRID INDEX FOR SPECTRAL AEROSOL PROFILES.
!       RHODUM   UNUSED COLUMN DENSITY
!       TLAYC    DENSITY WEIGHTED LAYER TEMPERATURE [C]
!       COSBEG   COSINE OF PATH ZENITH AT BEGINNING OF PATH SEGMENT.
!       COSEND   COSINE OF PATH ZENITH AT END OF PATH SEGMENT.
      INTEGER IK,IKM1,JBEG,JEND,KSPEC,IWVSAP
      REAL RHODUM,TLAYC
      DOUBLE PRECISION COSBEG,COSEND

!     TEMPERATURE AND ALTITUDE AT HMIN:
      IK=IKHMIN(1)
      JEND=1
      IPTHHT(IK)=NINT(1000*PTHALT(IK,1))
      PATH_T(IK,1)=TP(JEND)
      PATH_P(IK,1)=PP(JEND)
      PTHRH(IK,1)=RHP(JEND)

!     COLUMN DENSITIES FROM HMIN BACK TO H1ALT:
      DO IKM1=IKHMIN(1)-1,0,-1

!         PATH RANGE DATA:
          DRNG(IK,1)=SNGL(PTHSEG(IK))
          IF(PTHRNG(IK,1).GE.CKRANG .AND. IK.LT.IKOUT(1))IKOUT(1)=IK

!         BOUNDARY ALTITUDE, TEMPERATURE AND RELATIVE HUMIDITY:
          JBEG=JEND
          JEND=JBEG+1
          IPTHHT(IKM1)=NINT(1000*PTHALT(IKM1,1))
          PATH_T(IKM1,1)=TP(JEND)
          PATH_P(IKM1,1)=PP(JEND)
          PTHRH(IKM1,1)=RHP(JEND)

!         BACKWARD PATH:
          COSBEG=-PTHCOS(IKM1)
          COSEND=-PTHCOS(IK)

!         REFRACTIVE PATH COLUMN DENSITIES AND PATH AVERAGES:
          CALL SEGMNT(JBEG,JEND,REARTH,PTHSEG(IK),COSBEG,COSEND,        &
     &      PATM(IK,1),TSEG(IK,1),RHODUM,WPTH(0,IK,1),                  &
     &      XSAP(1,IK,1),ASAP(1,IK,1))
          PATM(IK,1)=PATM(IK,1)/PZERO

!         H2O CONTINUUM BAND MODEL COLUMN DENSITY DATA:
          WPTH(9,IK,1)=WPTH(5,IK,1)*(296-TSEG(IK,1))/36

!         OZONE CONTINUUM BAND MODEL COLUMN DENSITY DATA:
          TLAYC=TSEG(IK,1)-TZERO
          WPTH(59,IK,1)=.269*WPTH(8,IK,1)*TLAYC
          WPTH(60,IK,1)=WPTH(59,IK,1)*TLAYC

!         CUMULATIVE COLUMN DENSITIES:
          DO KSPEC=0,MEXTX+NMOLY
              WTOTAL(KSPEC)=WTOTAL(KSPEC)+WPTH(KSPEC,IK,1)
          ENDDO
          IF(LSAP)THEN
              DO IWVSAP=1,NWVSAP
                  XSAP(IWVSAP,0,1)=XSAP(IWVSAP,0,1)+XSAP(IWVSAP,IK,1)
                  ASAP(IWVSAP,0,1)=ASAP(IWVSAP,0,1)+ASAP(IWVSAP,IK,1)
              ENDDO
          ENDIF
          IK=IKM1
      ENDDO

!     RETURN TO GEOPTH:
      RETURN
      END

      SUBROUTINE HMN_H2(REARTH,CKRANG)

!     HMN_H2 COMPUTES HMIN TO H2ALT COLUMN DENSITIES FOR
!     USER-DEFINED PATHS.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     INPUT ARGUMENTS:
!       REARTH   RADIUS OF THE EARTH [KM].
!       CKRANG   MAXIMUM PATH RANGE FOR K-DISTRIBUTION OUTPUT
!                (=0. FOR TOTAL PATH ONLY; <0. FOR ALL RANGES).
      DOUBLE PRECISION REARTH,CKRANG

!     COMMONS:
      INCLUDE 'SEGDAT.h'
      INCLUDE 'YPROP.h'

!     /PATH/
!       PTHCOS   COSINE OF PATH ZENITH AT PATH BOUNDARIES.
!       PTHZEN   PATH ZENITH AT PATH BOUNDARIES [DEG].
!       PTHECA   SENSOR TO PATH EARTH CENTER ANGLE [DEG].
!       PTHALT   ALTITUDES AT PATH BOUNDARIES [KM].
!       PTH_MS   ALTITUDES AT PATH BOUNDARIES FOR THE MS PATH.
!       PTHSEG   PATH SEGMENT LENGTH [KM].
!       PTHRNG   SENSOR TO PATH BOUNDARY RANGE [KM].
!       JMAX     NUMBER OF DISTINCT LOS PATH SEGMENT ENDPOINT ALTITUDES.
!       IKHMIN   PATH BOUNDARY INDEX OF PATH MINIMUM ALTITUDE.
!       IKHMAX   PATH BOUNDARY INDEX OF PATH MAXIMUM ALTITUDE.
!       IKOUT    NUMBER OF PATH BOUNDARIES K DATA IS OUTPUT.
!       NTKDIS   RECORD NUMBER FOR K-DISTRIBUTION TRANSMITTANCE FILE.
!       NRKDIS   RECORD NUMBER FOR K-DISTRIBUTION RADIANCE FILE.
!       MAPPTH   MAPPING FROM PATH SEGMENT MIDPOINT TO VERTICAL LAYER.
!       IPTHHT   ALTITUDES (HEIGHTS) AT PATH BOUNDARIES [M].
!       LOWALT   VERTICAL LAYER BOUNDARY INDEX AT OR JUST BELOW PTHALT.
!       FACALT   ALTITUDE INTERPOLATION FRACTION FOR PTHALT
!       PATH_T   TEMPERATURE AT PATH BOUNDARIES [K].
!       PATH_P   PRESSURE AT PATH BOUNDARIES [ATM].
!       PTHRH    RELATIVE HUMIDITY AT PATH BOUNDARIES [K].
!       LSSGEO   LOGICAL FLAG, .TRUE. FOR SOLAR PATHS.
!       LTANMX   LOGICAL FLAG, .TRUE. IF PATH HAS A TANGENT MAXIMUM.
      DOUBLE PRECISION PTHCOS,PTHZEN,PTHECA,PTHALT,PTH_MS,PTHSEG,PTHRNG
      INTEGER JMAX,IKHMIN,IKHMAX,IKOUT,NTKDIS,NRKDIS,MAPPTH,            &
     &  IPTHHT,LOWALT
      REAL FACALT,PATH_T,PATH_P,PTHRH
      LOGICAL LSSGEO,LTANMX
      COMMON/PATH/PTHCOS(0:LAYTWO),PTHZEN(0:LAYTWO),PTHECA(0:LAYTWO),   &
     &  PTHALT(0:LAYTWO,1:MLOS),PTH_MS(0:LAYDIM),PTHSEG(LAYTWO),        &
     &  PTHRNG(0:LAYTWO,1:MLOS),JMAX,IKHMIN(MLOS),IKHMAX(MLOS),         &
     &  IKOUT(MLOS),MAPPTH(LAYTWO,1:MLOS),IPTHHT(0:LAYTWO),NTKDIS,      &
     &  NRKDIS,LOWALT(0:LAYTWO,1:MLOS),FACALT(0:LAYTWO,1:MLOS),         &
     &  PATH_T(0:LAYTWO,1:MLOS),PATH_P(0:LAYTWO,1:MLOS),                &
     &  PTHRH(0:LAYTWO,1:MLOS),LSSGEO,LTANMX

!     /CNTRL/
!       NSEG     NUMBER OF PATH SEGMENTS ALONG LINE-OF-SIGHT.
!       ML       NUMBER OF ATMOSPHERIC PROFILE LEVELS.
!       MLFLX    NUMBER OF LEVELS FOR WHICH FLUX VALUES ARE WRITTEN.
!       IMULT    MULTIPLE SCATTERING FLAG
!                  (0=NONE, 1=AT SENSOR, -1=AT FINAL OR TANGENT POINT).
!       THERML   FLAG TO CALCULATE THERMAL SCATTER.
      INTEGER NSEG,ML,MLFLX,IMULT
      LOGICAL THERML
      COMMON/CNTRL/NSEG(0:MLOSP1),ML,MLFLX,IMULT,THERML

!     /CARD2/
!       IHAZE    BOUNDARY LAYER AEROSOL MODEL NUMBER.
!       ISEASN   SEASON NUMBER (1=SPRING-SUMMER, 2=FALL-WINTER).
!       IVULCN   VOLCANIC AEROSOL MODEL NUMBER.
!       ICSTL    COASTAL AIRMASS MODEL NUMBER.
!       ICLD     CLOUD MODEL NUMBER.
!       IVSA     VERTICAL STRUCTURE ALGORITHM (0=OFF, 1=ON).
!       VIS      SURFACE VISIBILITY (GROUND METEOROLOGICAL RANGE) [KM].
!       WSS      CURRENT WIND SPEED (M/S).
!       WHH      24-HOUR WIND SPEED (M/S).
!       RAINRT   RAIN RATE (MM/HR)
!       LSAP     LOGICAL FLAG FOR SPECTRAL AEROSOL PROFILES INPUT.
      INTEGER IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA
      REAL VIS,WSS,WHH,RAINRT
      LOGICAL LSAP
      COMMON/CARD2/IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA,                 &
     &  VIS,WSS,WHH,RAINRT,LSAP

!     /SAP/
!       NWVSAP   NUMBER OF AEROSOL SPECTRAL GRID POINTS.
!       NLGSAP   HIGHEST AEROSOL PHASE FUNCTION LEGENDRE MOMENT.
!       NANSAP   NUMBER OF AEROSOL PHASE FUNCTION ANGULAR POINTS.
!       LEVSAP   NUMBER OF AEROSOL ATMOSPHERIC LEVELS.
!       ANGSAP   AEROSOL PHASE FUNCTION ANGULAR GRID [DEGREES].
!       COSSAP   COSINE OF THE AEROSOL PHASE FUNCTION ANGLES.
!       WAVSAP   AEROSOL SPECTRAL GRID [MICRONS].
!       LEGSAP   AEROSOL LEGENDRE COEFFICIENTS.
!       PFSAP    AEROSOL PHASE FUNCTION [SR-1].
!       LOSSAP   LOGICAL FLAG, TRUE FOR LINE-OF-SIGHT PATH.
      INTEGER NWVSAP,NLGSAP,NANSAP,LEVSAP
      DOUBLE PRECISION ANGSAP,COSSAP,PFSAP
      REAL WAVSAP,LEGSAP
      LOGICAL LOSSAP
      COMMON/SAP/NWVSAP,NLGSAP,NANSAP,LEVSAP,ANGSAP(MANSAP),            &
     &  COSSAP(MANSAP),WAVSAP(MWVSAP),LEGSAP(MLGSAP,MWVSAP,LAYDIM),     &
     &  PFSAP(MANSAP,MWVSAP,LAYDIM),LOSSAP

!     /FILLP/
!       ZP       ALTITUDE AT REFRACTED PATH LEVELS [KM].
!       RANGEP   SEGMENT RANGE [KM].
!       PP       PRESSURE AT REFRACTED PATH LEVELS [MBAR].
!       TP       TEMPERATURE AT REFRACTED PATH LEVELS [K].
!       RHP      RELATIVE HUMIDITY AT REFRACTED PATH LEVELS [%].
!       RFNDXP   REFRACTIVITIES AT REFRACTED PATH LEVELS.
!       DENPTH   ATMOSPHERIC LEVEL DENSITIES
!                [UNITS VARY WITH SPECIES BUT MOST OFTEN ATM CM / KM].
      DOUBLE PRECISION ZP,RANGEP,PP,RFNDXP,DENPTH
      REAL TP,RHP
      COMMON/FILLP/ZP(LAYDM1),RANGEP(LAYDM1),PP(LAYDM1),                &
     &  TP(LAYDM1),RHP(LAYDM1),RFNDXP(LAYDM1),DENPTH(0:MEXTXY,1:LAYDM1)

!     /SAPDEP/
!       XSAP     LINE-OF-SIGHT PATH AEROSOL EXTINCTION OPTICAL DEPTHS.
!       XSAPM    VERTICAL MS PATH AEROSOL EXTINCTION OPTICAL DEPTHS.
!       ASAP     LINE-OF-SIGHT PATH AEROSOL ABSORPTION OPTICAL DEPTHS.
!       ASAPM    VERTICAL MS PATH AEROSOL ABSORPTION OPTICAL DEPTHS.
!       XSAPS    SOLAR PATH AEROSOL EXTINCTION OPTICAL DEPTHS FROM LOS.
!       XSAPSM   SOLAR PATH AEROSOL EXTINCTION OPTICAL DEPTHS FOR MS.
!       ASAPS    SOLAR PATH AEROSOL ABSORPTION OPTICAL DEPTHS FROM LOS.
!       ASAPSM   SOLAR PATH AEROSOL ABSORPTION OPTICAL DEPTHS FOR MS.
      REAL XSAP,XSAPM,ASAP,ASAPM,XSAPS,XSAPSM,ASAPS,ASAPSM
      COMMON/SAPDEP/                                                    &
     &  XSAP(1:MWVSAP,0:LAYTWO,0:MLOS),XSAPM(1:MWVSAP,0:LAYDIM),        &
     &  ASAP(1:MWVSAP,0:LAYTWO,0:MLOS),ASAPM(1:MWVSAP,0:LAYDIM),        &
     &  XSAPS(MWVSAP,LAYTWO,MLOS),XSAPSM(MWVSAP,LAYDIM),                &
     &  ASAPS(MWVSAP,LAYTWO,MLOS),ASAPSM(MWVSAP,LAYDIM)

!     LOCAL VARIABLES:
!       IK       PATH SEGMENT INDEX.
!       IKM1     IK MINUS 1
!       JBEG     INDEX FOR BEGINNING OF REFRACTED PATH SEGMENT ALTITUDE.
!       JEND     INDEX FOR END OF REFRACTED PATH SEGMENT ALTITUDE.
!       KSPEC    SPECIES LOOP INDEX.
!       IWVSAP   SPECTRAL GRID INDEX FOR SPECTRAL AEROSOL PROFILES.
!       RHODUM   UNUSED COLUMN DENSITY
!       TLAYC    DENSITY WEIGHTED LAYER TEMPERATURE [C]
!       COSBEG   COSINE OF PATH ZENITH AT BEGINNING OF PATH SEGMENT.
!       COSEND   COSINE OF PATH ZENITH AT END OF PATH SEGMENT.
      INTEGER IK,IKM1,JBEG,JEND,KSPEC,IWVSAP
      REAL RHODUM,TLAYC
      DOUBLE PRECISION COSBEG,COSEND

!     TEMPERATURE AND ALTITUDE AT HMIN:
      IKM1=IKHMIN(1)
      JEND=1
      IPTHHT(IKM1)=NINT(1000*PTHALT(IKM1,1))
      PATH_T(IKM1,1)=TP(JEND)
      PATH_P(IKM1,1)=PP(JEND)
      PTHRH(IKM1,1)=RHP(JEND)

!     COLUMN DENSITIES FROM HMIN FORWARD TO H2ALT:
      DO IK=IKHMIN(1)+1,NSEG(1)

!         PATH RANGE DATA:
          DRNG(IK,1)=SNGL(PTHSEG(IK))
          IF(PTHRNG(IK,1).GE.CKRANG .AND. IK.LT.IKOUT(1))IKOUT(1)=IK

!         BOUNDARY ALTITUDE, TEMPERATURE AND RELATIVE HUMIDITY:
          JBEG=JEND
          JEND=JBEG+1
          IPTHHT(IK)=NINT(1000*PTHALT(IK,1))
          PATH_T(IK,1)=TP(JEND)
          PATH_P(IK,1)=PP(JEND)
          PTHRH(IK,1)=RHP(JEND)

!         FORWARD PATH:
          COSBEG=PTHCOS(IKM1)
          COSEND=PTHCOS(IK)

!         REFRACTIVE PATH COLUMN DENSITIES AND PATH AVERAGES:
          CALL SEGMNT(JBEG,JEND,REARTH,PTHSEG(IK),COSBEG,COSEND,        &
     &      PATM(IK,1),TSEG(IK,1),RHODUM,WPTH(0,IK,1),                  &
     &      XSAP(1,IK,1),ASAP(1,IK,1))
          PATM(IK,1)=PATM(IK,1)/PZERO

!         H2O CONTINUUM BAND MODEL COLUMN DENSITY DATA:
          WPTH(9,IK,1)=WPTH(5,IK,1)*(296-TSEG(IK,1))/36

!         OZONE CONTINUUM BAND MODEL COLUMN DENSITY DATA:
          TLAYC=TSEG(IK,1)-TZERO
          WPTH(59,IK,1)=.269*WPTH(8,IK,1)*TLAYC
          WPTH(60,IK,1)=WPTH(59,IK,1)*TLAYC

!         CUMULATIVE COLUMN DENSITIES:
          DO KSPEC=0,MEXTX+NMOLY
              WTOTAL(KSPEC)=WTOTAL(KSPEC)+WPTH(KSPEC,IK,1)
          ENDDO
          IF(LSAP)THEN
              DO IWVSAP=1,NWVSAP
                  XSAP(IWVSAP,0,1)=XSAP(IWVSAP,0,1)+XSAP(IWVSAP,IK,1)
                  ASAP(IWVSAP,0,1)=ASAP(IWVSAP,0,1)+ASAP(IWVSAP,IK,1)
              ENDDO
          ENDIF
          IKM1=IK
      ENDDO

!     RETURN TO GEOPTH:
      RETURN
      END

      SUBROUTINE H2_HMX(REARTH,CKRANG,JSTART)

!     H2_HMX COMPUTES H2ALT TO HMAX COLUMN DENSITIES FOR
!     USER-DEFINED PATHS.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     INPUT ARGUMENTS:
!       REARTH   RADIUS OF THE EARTH [KM].
!       CKRANG   MAXIMUM PATH RANGE FOR K-DISTRIBUTION OUTPUT
!                (=0. FOR TOTAL PATH ONLY; <0. FOR ALL RANGES).
!       JSTART   ALTITUDE H2ALT PATH SEGMENT INDEX (=1 IF H2ALT=HMIN).
      DOUBLE PRECISION REARTH,CKRANG
      INTEGER JSTART

!     COMMONS:
      INCLUDE 'SEGDAT.h'
      INCLUDE 'YPROP.h'

!     /PATH/
!       PTHCOS   COSINE OF PATH ZENITH AT PATH BOUNDARIES.
!       PTHZEN   PATH ZENITH AT PATH BOUNDARIES [DEG].
!       PTHECA   SENSOR TO PATH EARTH CENTER ANGLE [DEG].
!       PTHALT   ALTITUDES AT PATH BOUNDARIES [KM].
!       PTH_MS   ALTITUDES AT PATH BOUNDARIES FOR THE MS PATH.
!       PTHSEG   PATH SEGMENT LENGTH [KM].
!       PTHRNG   SENSOR TO PATH BOUNDARY RANGE [KM].
!       JMAX     NUMBER OF DISTINCT LOS PATH SEGMENT ENDPOINT ALTITUDES.
!       IKHMIN   PATH BOUNDARY INDEX OF PATH MINIMUM ALTITUDE.
!       IKHMAX   PATH BOUNDARY INDEX OF PATH MAXIMUM ALTITUDE.
!       IKOUT    NUMBER OF PATH BOUNDARIES K DATA IS OUTPUT.
!       NTKDIS   RECORD NUMBER FOR K-DISTRIBUTION TRANSMITTANCE FILE.
!       NRKDIS   RECORD NUMBER FOR K-DISTRIBUTION RADIANCE FILE.
!       MAPPTH   MAPPING FROM PATH SEGMENT MIDPOINT TO VERTICAL LAYER.
!       IPTHHT   ALTITUDES (HEIGHTS) AT PATH BOUNDARIES [M].
!       LOWALT   VERTICAL LAYER BOUNDARY INDEX AT OR JUST BELOW PTHALT.
!       FACALT   ALTITUDE INTERPOLATION FRACTION FOR PTHALT
!       PATH_T   TEMPERATURE AT PATH BOUNDARIES [K].
!       PATH_P   PRESSURE AT PATH BOUNDARIES [ATM].
!       PTHRH    RELATIVE HUMIDITY AT PATH BOUNDARIES [K].
!       LSSGEO   LOGICAL FLAG, .TRUE. FOR SOLAR PATHS.
!       LTANMX   LOGICAL FLAG, .TRUE. IF PATH HAS A TANGENT MAXIMUM.
      DOUBLE PRECISION PTHCOS,PTHZEN,PTHECA,PTHALT,PTH_MS,PTHSEG,PTHRNG
      INTEGER JMAX,IKHMIN,IKHMAX,IKOUT,NTKDIS,NRKDIS,MAPPTH,            &
     &  IPTHHT,LOWALT
      REAL FACALT,PATH_T,PATH_P,PTHRH
      LOGICAL LSSGEO,LTANMX
      COMMON/PATH/PTHCOS(0:LAYTWO),PTHZEN(0:LAYTWO),PTHECA(0:LAYTWO),   &
     &  PTHALT(0:LAYTWO,1:MLOS),PTH_MS(0:LAYDIM),PTHSEG(LAYTWO),        &
     &  PTHRNG(0:LAYTWO,1:MLOS),JMAX,IKHMIN(MLOS),IKHMAX(MLOS),         &
     &  IKOUT(MLOS),MAPPTH(LAYTWO,1:MLOS),IPTHHT(0:LAYTWO),NTKDIS,      &
     &  NRKDIS,LOWALT(0:LAYTWO,1:MLOS),FACALT(0:LAYTWO,1:MLOS),         &
     &  PATH_T(0:LAYTWO,1:MLOS),PATH_P(0:LAYTWO,1:MLOS),                &
     &  PTHRH(0:LAYTWO,1:MLOS),LSSGEO,LTANMX

!     /CNTRL/
!       NSEG     NUMBER OF PATH SEGMENTS ALONG LINE-OF-SIGHT.
!       ML       NUMBER OF ATMOSPHERIC PROFILE LEVELS.
!       MLFLX    NUMBER OF LEVELS FOR WHICH FLUX VALUES ARE WRITTEN.
!       IMULT    MULTIPLE SCATTERING FLAG
!                  (0=NONE, 1=AT SENSOR, -1=AT FINAL OR TANGENT POINT).
!       THERML   FLAG TO CALCULATE THERMAL SCATTER.
      INTEGER NSEG,ML,MLFLX,IMULT
      LOGICAL THERML
      COMMON/CNTRL/NSEG(0:MLOSP1),ML,MLFLX,IMULT,THERML

!     /CARD2/
!       IHAZE    BOUNDARY LAYER AEROSOL MODEL NUMBER.
!       ISEASN   SEASON NUMBER (1=SPRING-SUMMER, 2=FALL-WINTER).
!       IVULCN   VOLCANIC AEROSOL MODEL NUMBER.
!       ICSTL    COASTAL AIRMASS MODEL NUMBER.
!       ICLD     CLOUD MODEL NUMBER.
!       IVSA     VERTICAL STRUCTURE ALGORITHM (0=OFF, 1=ON).
!       VIS      SURFACE VISIBILITY (GROUND METEOROLOGICAL RANGE) [KM].
!       WSS      CURRENT WIND SPEED (M/S).
!       WHH      24-HOUR WIND SPEED (M/S).
!       RAINRT   RAIN RATE (MM/HR)
!       LSAP     LOGICAL FLAG FOR SPECTRAL AEROSOL PROFILES INPUT.
      INTEGER IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA
      REAL VIS,WSS,WHH,RAINRT
      LOGICAL LSAP
      COMMON/CARD2/IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA,                 &
     &  VIS,WSS,WHH,RAINRT,LSAP

!     /SAP/
!       NWVSAP   NUMBER OF AEROSOL SPECTRAL GRID POINTS.
!       NLGSAP   HIGHEST AEROSOL PHASE FUNCTION LEGENDRE MOMENT.
!       NANSAP   NUMBER OF AEROSOL PHASE FUNCTION ANGULAR POINTS.
!       LEVSAP   NUMBER OF AEROSOL ATMOSPHERIC LEVELS.
!       ANGSAP   AEROSOL PHASE FUNCTION ANGULAR GRID [DEGREES].
!       COSSAP   COSINE OF THE AEROSOL PHASE FUNCTION ANGLES.
!       WAVSAP   AEROSOL SPECTRAL GRID [MICRONS].
!       LEGSAP   AEROSOL LEGENDRE COEFFICIENTS.
!       PFSAP    AEROSOL PHASE FUNCTION [SR-1].
!       LOSSAP   LOGICAL FLAG, TRUE FOR LINE-OF-SIGHT PATH.
      INTEGER NWVSAP,NLGSAP,NANSAP,LEVSAP
      DOUBLE PRECISION ANGSAP,COSSAP,PFSAP
      REAL WAVSAP,LEGSAP
      LOGICAL LOSSAP
      COMMON/SAP/NWVSAP,NLGSAP,NANSAP,LEVSAP,ANGSAP(MANSAP),            &
     &  COSSAP(MANSAP),WAVSAP(MWVSAP),LEGSAP(MLGSAP,MWVSAP,LAYDIM),     &
     &  PFSAP(MANSAP,MWVSAP,LAYDIM),LOSSAP

!     /FILLP/
!       ZP       ALTITUDE AT REFRACTED PATH LEVELS [KM].
!       RANGEP   SEGMENT RANGE [KM].
!       PP       PRESSURE AT REFRACTED PATH LEVELS [MBAR].
!       TP       TEMPERATURE AT REFRACTED PATH LEVELS [K].
!       RHP      RELATIVE HUMIDITY AT REFRACTED PATH LEVELS [%].
!       RFNDXP   REFRACTIVITIES AT REFRACTED PATH LEVELS.
!       DENPTH   ATMOSPHERIC LEVEL DENSITIES
!                [UNITS VARY WITH SPECIES BUT MOST OFTEN ATM CM / KM].
      DOUBLE PRECISION ZP,RANGEP,PP,RFNDXP,DENPTH
      REAL TP,RHP
      COMMON/FILLP/ZP(LAYDM1),RANGEP(LAYDM1),PP(LAYDM1),                &
     &  TP(LAYDM1),RHP(LAYDM1),RFNDXP(LAYDM1),DENPTH(0:MEXTXY,1:LAYDM1)

!     /SAPDEP/
!       XSAP     LINE-OF-SIGHT PATH AEROSOL EXTINCTION OPTICAL DEPTHS.
!       XSAPM    VERTICAL MS PATH AEROSOL EXTINCTION OPTICAL DEPTHS.
!       ASAP     LINE-OF-SIGHT PATH AEROSOL ABSORPTION OPTICAL DEPTHS.
!       ASAPM    VERTICAL MS PATH AEROSOL ABSORPTION OPTICAL DEPTHS.
!       XSAPS    SOLAR PATH AEROSOL EXTINCTION OPTICAL DEPTHS FROM LOS.
!       XSAPSM   SOLAR PATH AEROSOL EXTINCTION OPTICAL DEPTHS FOR MS.
!       ASAPS    SOLAR PATH AEROSOL ABSORPTION OPTICAL DEPTHS FROM LOS.
!       ASAPSM   SOLAR PATH AEROSOL ABSORPTION OPTICAL DEPTHS FOR MS.
      REAL XSAP,XSAPM,ASAP,ASAPM,XSAPS,XSAPSM,ASAPS,ASAPSM
      COMMON/SAPDEP/                                                    &
     &  XSAP(1:MWVSAP,0:LAYTWO,0:MLOS),XSAPM(1:MWVSAP,0:LAYDIM),        &
     &  ASAP(1:MWVSAP,0:LAYTWO,0:MLOS),ASAPM(1:MWVSAP,0:LAYDIM),        &
     &  XSAPS(MWVSAP,LAYTWO,MLOS),XSAPSM(MWVSAP,LAYDIM),                &
     &  ASAPS(MWVSAP,LAYTWO,MLOS),ASAPSM(MWVSAP,LAYDIM)

!     LOCAL VARIABLES:
!       IK       PATH SEGMENT INDEX.
!       IKM1     IK MINUS 1
!       JBEG     INDEX FOR BEGINNING OF REFRACTED PATH SEGMENT ALTITUDE.
!       JEND     INDEX FOR END OF REFRACTED PATH SEGMENT ALTITUDE.
!       KSPEC    SPECIES LOOP INDEX.
!       IWVSAP   SPECTRAL GRID INDEX FOR SPECTRAL AEROSOL PROFILES.
!       RHODUM   UNUSED COLUMN DENSITY
!       TLAYC    DENSITY WEIGHTED LAYER TEMPERATURE [C]
!       COSBEG   COSINE OF PATH ZENITH AT BEGINNING OF PATH SEGMENT.
!       COSEND   COSINE OF PATH ZENITH AT END OF PATH SEGMENT.
      INTEGER IK,IKM1,JBEG,JEND,KSPEC,IWVSAP
      REAL RHODUM,TLAYC
      DOUBLE PRECISION COSBEG,COSEND

!     TEMPERATURE AND ALTITUDE AT H2ALT:
      IK=NSEG(1)
      JEND=JSTART
      IPTHHT(IK)=NINT(1000*PTHALT(IK,1))
      PATH_T(IK,1)=TP(JEND)
      PATH_P(IK,1)=PP(JEND)
      PTHRH(IK,1)=RHP(JEND)

!     COLUMN DENSITIES FROM H2ALT BACK TO HMAX:
      DO IKM1=NSEG(1)-1,IKHMAX(1),-1

!         PATH RANGE DATA:
          DRNG(IK,1)=SNGL(PTHSEG(IK))
          IF(PTHRNG(IK,1).GE.CKRANG .AND. IK.LT.IKOUT(1))IKOUT(1)=IK

!         BOUNDARY ALTITUDE, TEMPERATURE AND RELATIVE HUMIDITY:
          JBEG=JEND
          JEND=JBEG+1
          IPTHHT(IKM1)=NINT(1000*PTHALT(IKM1,1))
          PATH_T(IKM1,1)=TP(JEND)
          PATH_P(IKM1,1)=PP(JEND)
          PTHRH(IKM1,1)=RHP(JEND)

!         BACKWARD PATH:
          COSBEG=-PTHCOS(IKM1)
          COSEND=-PTHCOS(IK)

!         REFRACTIVE PATH COLUMN DENSITIES AND PATH AVERAGES:
          CALL SEGMNT(JBEG,JEND,REARTH,PTHSEG(IK),COSBEG,COSEND,        &
     &      PATM(IK,1),TSEG(IK,1),RHODUM,WPTH(0,IK,1),                  &
     &      XSAP(1,IK,1),ASAP(1,IK,1))
          PATM(IK,1)=PATM(IK,1)/PZERO

!         H2O CONTINUUM BAND MODEL COLUMN DENSITY DATA:
          WPTH(9,IK,1)=WPTH(5,IK,1)*(296-TSEG(IK,1))/36

!         OZONE CONTINUUM BAND MODEL COLUMN DENSITY DATA:
          TLAYC=TSEG(IK,1)-TZERO
          WPTH(59,IK,1)=.269*WPTH(8,IK,1)*TLAYC
          WPTH(60,IK,1)=WPTH(59,IK,1)*TLAYC

!         CUMULATIVE COLUMN DENSITIES:
          DO KSPEC=0,MEXTX+NMOLY
              WTOTAL(KSPEC)=WTOTAL(KSPEC)+WPTH(KSPEC,IK,1)
          ENDDO
          IF(LSAP)THEN
              DO IWVSAP=1,NWVSAP
                  XSAP(IWVSAP,0,1)=XSAP(IWVSAP,0,1)+XSAP(IWVSAP,IK,1)
                  ASAP(IWVSAP,0,1)=ASAP(IWVSAP,0,1)+ASAP(IWVSAP,IK,1)
              ENDDO
          ENDIF
          IK=IKM1
      ENDDO

!     RETURN TO GEOPTH:
      RETURN
      END

      SUBROUTINE H1_HMX(REARTH,CKRANG,JSTART)

!     H1_HMX COMPUTES H1ALT TO HMAX COLUMN DENSITIES
!     FOR USER-DEFINED PATHS.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     INPUT ARGUMENTS:
!       REARTH   RADIUS OF THE EARTH [KM].
!       CKRANG   MAXIMUM PATH RANGE FOR K-DISTRIBUTION OUTPUT
!                (=0. FOR TOTAL PATH ONLY; <0. FOR ALL RANGES).
!       JSTART   ALTITUDE H1ALT PATH SEGMENT INDEX (=1 IF H1ALT=HMIN).
      DOUBLE PRECISION REARTH,CKRANG
      INTEGER JSTART

!     COMMONS:
      INCLUDE 'SEGDAT.h'
      INCLUDE 'YPROP.h'

!     /PATH/
!       PTHCOS   COSINE OF PATH ZENITH AT PATH BOUNDARIES.
!       PTHZEN   PATH ZENITH AT PATH BOUNDARIES [DEG].
!       PTHECA   SENSOR TO PATH EARTH CENTER ANGLE [DEG].
!       PTHALT   ALTITUDES AT PATH BOUNDARIES [KM].
!       PTH_MS   ALTITUDES AT PATH BOUNDARIES FOR THE MS PATH.
!       PTHSEG   PATH SEGMENT LENGTH [KM].
!       PTHRNG   SENSOR TO PATH BOUNDARY RANGE [KM].
!       JMAX     NUMBER OF DISTINCT LOS PATH SEGMENT ENDPOINT ALTITUDES.
!       IKHMIN   PATH BOUNDARY INDEX OF PATH MINIMUM ALTITUDE.
!       IKHMAX   PATH BOUNDARY INDEX OF PATH MAXIMUM ALTITUDE.
!       IKOUT    NUMBER OF PATH BOUNDARIES K DATA IS OUTPUT.
!       NTKDIS   RECORD NUMBER FOR K-DISTRIBUTION TRANSMITTANCE FILE.
!       NRKDIS   RECORD NUMBER FOR K-DISTRIBUTION RADIANCE FILE.
!       MAPPTH   MAPPING FROM PATH SEGMENT MIDPOINT TO VERTICAL LAYER.
!       IPTHHT   ALTITUDES (HEIGHTS) AT PATH BOUNDARIES [M].
!       LOWALT   VERTICAL LAYER BOUNDARY INDEX AT OR JUST BELOW PTHALT.
!       FACALT   ALTITUDE INTERPOLATION FRACTION FOR PTHALT
!       PATH_T   TEMPERATURE AT PATH BOUNDARIES [K].
!       PATH_P   PRESSURE AT PATH BOUNDARIES [ATM].
!       PTHRH    RELATIVE HUMIDITY AT PATH BOUNDARIES [K].
!       LSSGEO   LOGICAL FLAG, .TRUE. FOR SOLAR PATHS.
!       LTANMX   LOGICAL FLAG, .TRUE. IF PATH HAS A TANGENT MAXIMUM.
      DOUBLE PRECISION PTHCOS,PTHZEN,PTHECA,PTHALT,PTH_MS,PTHSEG,PTHRNG
      INTEGER JMAX,IKHMIN,IKHMAX,IKOUT,NTKDIS,NRKDIS,MAPPTH,            &
     &  IPTHHT,LOWALT
      REAL FACALT,PATH_T,PATH_P,PTHRH
      LOGICAL LSSGEO,LTANMX
      COMMON/PATH/PTHCOS(0:LAYTWO),PTHZEN(0:LAYTWO),PTHECA(0:LAYTWO),   &
     &  PTHALT(0:LAYTWO,1:MLOS),PTH_MS(0:LAYDIM),PTHSEG(LAYTWO),        &
     &  PTHRNG(0:LAYTWO,1:MLOS),JMAX,IKHMIN(MLOS),IKHMAX(MLOS),         &
     &  IKOUT(MLOS),MAPPTH(LAYTWO,1:MLOS),IPTHHT(0:LAYTWO),NTKDIS,      &
     &  NRKDIS,LOWALT(0:LAYTWO,1:MLOS),FACALT(0:LAYTWO,1:MLOS),         &
     &  PATH_T(0:LAYTWO,1:MLOS),PATH_P(0:LAYTWO,1:MLOS),                &
     &  PTHRH(0:LAYTWO,1:MLOS),LSSGEO,LTANMX

!     /CARD2/
!       IHAZE    BOUNDARY LAYER AEROSOL MODEL NUMBER.
!       ISEASN   SEASON NUMBER (1=SPRING-SUMMER, 2=FALL-WINTER).
!       IVULCN   VOLCANIC AEROSOL MODEL NUMBER.
!       ICSTL    COASTAL AIRMASS MODEL NUMBER.
!       ICLD     CLOUD MODEL NUMBER.
!       IVSA     VERTICAL STRUCTURE ALGORITHM (0=OFF, 1=ON).
!       VIS      SURFACE VISIBILITY (GROUND METEOROLOGICAL RANGE) [KM].
!       WSS      CURRENT WIND SPEED (M/S).
!       WHH      24-HOUR WIND SPEED (M/S).
!       RAINRT   RAIN RATE (MM/HR)
!       LSAP     LOGICAL FLAG FOR SPECTRAL AEROSOL PROFILES INPUT.
      INTEGER IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA
      REAL VIS,WSS,WHH,RAINRT
      LOGICAL LSAP
      COMMON/CARD2/IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA,                 &
     &  VIS,WSS,WHH,RAINRT,LSAP

!     /SAP/
!       NWVSAP   NUMBER OF AEROSOL SPECTRAL GRID POINTS.
!       NLGSAP   HIGHEST AEROSOL PHASE FUNCTION LEGENDRE MOMENT.
!       NANSAP   NUMBER OF AEROSOL PHASE FUNCTION ANGULAR POINTS.
!       LEVSAP   NUMBER OF AEROSOL ATMOSPHERIC LEVELS.
!       ANGSAP   AEROSOL PHASE FUNCTION ANGULAR GRID [DEGREES].
!       COSSAP   COSINE OF THE AEROSOL PHASE FUNCTION ANGLES.
!       WAVSAP   AEROSOL SPECTRAL GRID [MICRONS].
!       LEGSAP   AEROSOL LEGENDRE COEFFICIENTS.
!       PFSAP    AEROSOL PHASE FUNCTION [SR-1].
!       LOSSAP   LOGICAL FLAG, TRUE FOR LINE-OF-SIGHT PATH.
      INTEGER NWVSAP,NLGSAP,NANSAP,LEVSAP
      DOUBLE PRECISION ANGSAP,COSSAP,PFSAP
      REAL WAVSAP,LEGSAP
      LOGICAL LOSSAP
      COMMON/SAP/NWVSAP,NLGSAP,NANSAP,LEVSAP,ANGSAP(MANSAP),            &
     &  COSSAP(MANSAP),WAVSAP(MWVSAP),LEGSAP(MLGSAP,MWVSAP,LAYDIM),     &
     &  PFSAP(MANSAP,MWVSAP,LAYDIM),LOSSAP

!     /FILLP/
!       ZP       ALTITUDE AT REFRACTED PATH LEVELS [KM].
!       RANGEP   SEGMENT RANGE [KM].
!       PP       PRESSURE AT REFRACTED PATH LEVELS [MBAR].
!       TP       TEMPERATURE AT REFRACTED PATH LEVELS [K].
!       RHP      RELATIVE HUMIDITY AT REFRACTED PATH LEVELS [%].
!       RFNDXP   REFRACTIVITIES AT REFRACTED PATH LEVELS.
!       DENPTH   ATMOSPHERIC LEVEL DENSITIES
!                [UNITS VARY WITH SPECIES BUT MOST OFTEN ATM CM / KM].
      DOUBLE PRECISION ZP,RANGEP,PP,RFNDXP,DENPTH
      REAL TP,RHP
      COMMON/FILLP/ZP(LAYDM1),RANGEP(LAYDM1),PP(LAYDM1),                &
     &  TP(LAYDM1),RHP(LAYDM1),RFNDXP(LAYDM1),DENPTH(0:MEXTXY,1:LAYDM1)

!     /SAPDEP/
!       XSAP     LINE-OF-SIGHT PATH AEROSOL EXTINCTION OPTICAL DEPTHS.
!       XSAPM    VERTICAL MS PATH AEROSOL EXTINCTION OPTICAL DEPTHS.
!       ASAP     LINE-OF-SIGHT PATH AEROSOL ABSORPTION OPTICAL DEPTHS.
!       ASAPM    VERTICAL MS PATH AEROSOL ABSORPTION OPTICAL DEPTHS.
!       XSAPS    SOLAR PATH AEROSOL EXTINCTION OPTICAL DEPTHS FROM LOS.
!       XSAPSM   SOLAR PATH AEROSOL EXTINCTION OPTICAL DEPTHS FOR MS.
!       ASAPS    SOLAR PATH AEROSOL ABSORPTION OPTICAL DEPTHS FROM LOS.
!       ASAPSM   SOLAR PATH AEROSOL ABSORPTION OPTICAL DEPTHS FOR MS.
      REAL XSAP,XSAPM,ASAP,ASAPM,XSAPS,XSAPSM,ASAPS,ASAPSM
      COMMON/SAPDEP/                                                    &
     &  XSAP(1:MWVSAP,0:LAYTWO,0:MLOS),XSAPM(1:MWVSAP,0:LAYDIM),        &
     &  ASAP(1:MWVSAP,0:LAYTWO,0:MLOS),ASAPM(1:MWVSAP,0:LAYDIM),        &
     &  XSAPS(MWVSAP,LAYTWO,MLOS),XSAPSM(MWVSAP,LAYDIM),                &
     &  ASAPS(MWVSAP,LAYTWO,MLOS),ASAPSM(MWVSAP,LAYDIM)

!     LOCAL VARIABLES:
!       IK       PATH SEGMENT INDEX.
!       IKM1     IK MINUS 1
!       JBEG     INDEX FOR BEGINNING OF REFRACTED PATH SEGMENT ALTITUDE.
!       JEND     INDEX FOR END OF REFRACTED PATH SEGMENT ALTITUDE.
!       KSPEC    SPECIES LOOP INDEX.
!       IWVSAP   SPECTRAL GRID INDEX FOR SPECTRAL AEROSOL PROFILES.
!       RHODUM   UNUSED COLUMN DENSITY
!       TLAYC    DENSITY WEIGHTED LAYER TEMPERATURE [C]
!       COSBEG   COSINE OF PATH ZENITH AT BEGINNING OF PATH SEGMENT.
!       COSEND   COSINE OF PATH ZENITH AT END OF PATH SEGMENT.
      INTEGER IK,IKM1,JBEG,JEND,KSPEC,IWVSAP
      REAL RHODUM,TLAYC
      DOUBLE PRECISION COSBEG,COSEND

!     TEMPERATURE AND ALTITUDE AT H1ALT:
      IKM1=0
      JEND=JSTART
      IPTHHT(IKM1)=NINT(1000*PTHALT(IKM1,1))
      PATH_T(IKM1,1)=TP(JEND)
      PATH_P(IKM1,1)=PP(JEND)
      PTHRH(IKM1,1)=RHP(JEND)

!     COLUMN DENSITIES FROM H1ALT FORWARD TO HMAX:
      DO IK=1,IKHMAX(1)

!         PATH RANGE DATA:
          DRNG(IK,1)=SNGL(PTHSEG(IK))
          IF(PTHRNG(IK,1).GE.CKRANG .AND. IK.LT.IKOUT(1))IKOUT(1)=IK

!         BOUNDARY ALTITUDE, TEMPERATURE AND RELATIVE HUMIDITY:
          JBEG=JEND
          JEND=JBEG+1
          IPTHHT(IK)=NINT(1000*PTHALT(IK,1))
          PATH_T(IK,1)=TP(JEND)
          PATH_P(IK,1)=PP(JEND)
          PTHRH(IK,1)=RHP(JEND)

!         FORWARD PATH:
          COSBEG=PTHCOS(IKM1)
          COSEND=PTHCOS(IK)

!         REFRACTIVE PATH COLUMN DENSITIES AND PATH AVERAGES:
          CALL SEGMNT(JBEG,JEND,REARTH,PTHSEG(IK),COSBEG,COSEND,        &
     &      PATM(IK,1),TSEG(IK,1),RHODUM,WPTH(0,IK,1),                  &
     &      XSAP(1,IK,1),ASAP(1,IK,1))
          PATM(IK,1)=PATM(IK,1)/PZERO

!         H2O CONTINUUM BAND MODEL COLUMN DENSITY DATA:
          WPTH(9,IK,1)=WPTH(5,IK,1)*(296-TSEG(IK,1))/36

!         OZONE CONTINUUM BAND MODEL COLUMN DENSITY DATA:
          TLAYC=TSEG(IK,1)-TZERO
          WPTH(59,IK,1)=.269*WPTH(8,IK,1)*TLAYC
          WPTH(60,IK,1)=WPTH(59,IK,1)*TLAYC

!         CUMULATIVE COLUMN DENSITIES:
          DO KSPEC=0,MEXTX+NMOLY
              WTOTAL(KSPEC)=WTOTAL(KSPEC)+WPTH(KSPEC,IK,1)
          ENDDO
          IF(LSAP)THEN
              DO IWVSAP=1,NWVSAP
                  XSAP(IWVSAP,0,1)=XSAP(IWVSAP,0,1)+XSAP(IWVSAP,IK,1)
                  ASAP(IWVSAP,0,1)=ASAP(IWVSAP,0,1)+ASAP(IWVSAP,IK,1)
              ENDDO
          ENDIF
          IKM1=IK
      ENDDO

!     RETURN TO GEOPTH:
      RETURN
      END
