      REAL FUNCTION SBRDF(CVIEW, CSOURC, PHI, PARAMS)

C     COMPUTES THE SPECULAR COMPONENT OF THE BIDIRECTIONNAL
C     REFLECTANCE FUNCTION OF THE SEA SURFACE ACCORDING TO:

C     V. ROSS, D. DION, AND G. POTVIN,
C     "DETAILED ANALYTICAL APPROACH TO THE GAUSSIAN SURFACE
C     BIDIRECTIONNAL REFLECTANCE DISTRIBUTION FUNCTION
C     SPECULAR COMPONENT APPLIED TO THE SEA SURFACE",
C     J. OPT. SOC. AM. A 22, 2442-2453 (2005)

C     WITH AN EMPIRICAL CORRECTION OF SLOPE STATISTICS DUE
C     TO SHADOWING NEAR THE HORIZON AS DESCRIBED IN:

C     V. ROSS AND D. DION,
C     "SEA SURFACE SLOPE STATISTICS DERIVED FROM SUN GLINT 
C     RADIANCE MEASUREMENTS AND THEIR APPARENT DEPENDENCE ON 
C     SENSOR ELEVATION",
C     J. GEOPHYS. RES., 112, C09015, DOI:10.1029/2007JC004137

      IMPLICIT NONE

C     INPUT:
C     CVIEW       COSINE OF THE SURFACE-RECEIVER ZENITH ANGLE
C     CSOURC      COSINE OF THE SURFACE-SOURCE ZENITH ANGLE
C     PHI         SOURCE-RECEIVER RELATIVE ANGLE
C                 (PI=SOURCE IN FRONT OF RECEIVER)
C     PARAMS(1)   REAL PART OF COMPLEX REFRACTION INDEX
C     PARAMS(2)   IMAGINARY PART OF COMPLEX REFRACTION INDEX
C     PARAMS(3)   UPWIND SLOPE RMS (SIGMA_U), NEGATIVE VALUE FOR
C                 HORIZON CORRECTION (*)
C     PARAMS(4)   CROSSWIND SLOPE RMS (SIGMA_C), NEGATIVE VALUE 
C                 FOR HORIZON CORRECTION (*)
C     PARAMS(5)   AZIMUTH ANGLE BETWEEN SURFACE-RECEIVER
C                 VECTOR AND WIND VECTOR [RAD]
C     PARAMS(6)   WHITECAP COVERAGE FRACTION
C     PARAMS(7)   WHITECAP HEMISPHERICAL ALBEDO
C
C     THESE ARE NOT USED YET, BUT ARE EVENTUALLY PLANNED
C     PARAMS(8)   MEDIUM ABSORPTION COEFFICIENT  [M-1]
C     PARAMS(9)   MEDIUM BACKSCATTERING COEFFICIENT  [M-1]
C     PARAMS(10)  CASE (LAMBERT SUBSURFACE = 0,
C                 CASE I WATERS = 1, CASE II WATERS = 2)
C
C     (*) ONLY ONE NEGATIVE VALUE IS NECESSARY (UPWIND OR 
C         CROSSWIND) FOR HORIZON CORRECTION TO OCCURE, BUT THERE
C         THERE IS NO HARM IN SETTING BOTH TO EITHER POSITIVE
C         OR NEGATIVE VALUES.

      REAL CVIEW,CSOURC,PHI,PARAMS(*)

C     EXTERNALS
      EXTERNAL ERF,FRESNEL
      DOUBLE PRECISION ERF,FRESNEL

C     INTERNAL VARIABLES
C     (ALL INTERNAL CALCULATIONS ARE DONE IN DOUBLE PRECISION)
      DOUBLE PRECISION PI,SQTPI,SQTWO
      DOUBLE PRECISION THER,PHIR,THES,PHIS
      DOUBLE PRECISION STHER,SPHIR,STHES,SPHIS
      DOUBLE PRECISION CTHER,CPHIR,CTHES,CPHIS
      DOUBLE PRECISION SPS2,CPR2
      DOUBLE PRECISION XR,YR,ZR,XS,YS,ZS,XN,YN,ZN
      DOUBLE PRECISION SX,SY,N,K
      DOUBLE PRECISION XIX,XIY
      DOUBLE PRECISION NORM,RDOTN
      DOUBLE PRECISION WFUNC,HSLOP
      DOUBLE PRECISION SIG,VS,VR,LAMBR,LAMBS,SX2,SY2,SFRAC,SCOEF
      DOUBLE PRECISION WS,SPDF,X,Y,X2,Y2,ONETF
      DOUBLE PRECISION C21,C03,C40,C22,C04
      DOUBLE PRECISION QVN,FREF
	LOGICAL EFFVAR

      PARAMETER(PI=3.141592653589793D0,SQTPI=1.772453850905516D0,
     1          SQTWO=1.414213562373095D0)

      IF(CVIEW.GT.0D0) THEN

C         PREVENT SINGULARITIES          
          IF(CVIEW.LT.1.0D0) THEN
              CTHER=CVIEW
          ELSE
              CTHER=1.0D0-EPSILON(0.0D0)
          ENDIF
          IF(CSOURC.LT.1.0D0) THEN
              CTHES=CSOURC
          ELSE
              CTHES=1.0D0-EPSILON(0.0D0)
          ENDIF

          THES = ACOS(CTHES)
          PHIS = PHI-PARAMS(5)
          THER = ACOS(CTHER)
          PHIR = -PARAMS(5)
          N=PARAMS(1)
          K=PARAMS(2)
	    IF(PARAMS(3).LT.0.OR.PARAMS(4).LT.0)EFFVAR=.TRUE.
          SX=ABS(PARAMS(3))
          SY=ABS(PARAMS(4))


C         TRIGONOMETRIC VALUES      
C         SIN(THER)
          STHER=SQRT(1.0D0-CTHER*CTHER)
C         SIN(THES)
          STHES=SQRT(1.0D0-CTHES*CTHES)
          CPHIS=COS(PHIS)
          CPHIR=COS(PHIR)
          SPHIS=SIN(PHIS)
          SPHIR=SIN(PHIR)

C         RECEIVER AND SOURCE CARTESIAN VECTORS
          XR=STHER*CPHIR
          YR=STHER*SPHIR
          ZR=CTHER

          XS=STHES*CPHIS
          YS=STHES*SPHIS
          ZS=CTHES

C         FACET SLOPES
          NORM=ZR+ZS
          XIX = -(XR+XS)/NORM
          XIY = -(YR+YS)/NORM

C         FACET NORMAL CARTESIAN VECTOR
          NORM=SQRT(1.0D0 + XIX*XIX + XIY*XIY)
          XN=-XIX/NORM
          YN=-XIY/NORM
          ZN=1.0D0/NORM

C         COSINE OF HALF REFLECTION ANGLE
          RDOTN = XR*XN + YR*YN + ZR*ZN

	    IF(RDOTN.GT.0.0D0) THEN

C         FACET WEIGHING FUNCTION
C         (EQUATION 19 OF ROSS ET AL.)
          WFUNC = RDOTN/ZN

C         SLOPE HIDING FUNCTION
C         (EQUATION 21 OF ROSS ET AL.)
          IF(RDOTN.GT.0D0) THEN
              HSLOP = 1.0D0
          ELSE
              HSLOP = 0.0D0
          ENDIF

C         WAVE HIDING AND SHADOWING EXPONENT TERMS (LAMBDA R AND S)
C         (EQUATIONS 23B AND 25B IN ROSS ET AL.)
          SX2=PARAMS(3)*PARAMS(3)
          SY2=PARAMS(4)*PARAMS(4)

          SIG = SQRT(SX2*CPHIR*CPHIR + SY2*SPHIR*SPHIR)
          IF(THER.NE.0.0D0) THEN
              VR=1.0D0/(STHER/CTHER*SQTWO*SIG)
          ELSE 
C             JUST A VERY LARGE NUMBER
              VR=HUGE(1.0D0)
          ENDIF

C         (EQUATION 23B IN ROSS ET AL.)
          LAMBR=(EXP(-VR*VR)-VR*SQTPI*ERF(VR,1))/(2.0D0*VR*SQTPI)

          SIG = SQRT(SX2*CPHIS*CPHIS + SY2*SPHIS*SPHIS)

          IF(THES.NE.0D0) THEN
              VS=1.0D0/(STHES/CTHES*SQTWO*SIG)
          ELSE 
C                 JUST A VERY LARGE NUMBER
              VS=HUGE(1.0D0)
          ENDIF

C           (EQUATION 25B IN ROSS ET AL.)
          LAMBS=(EXP(-VS*VS)-VS*SQTPI*ERF(VS,1))/(2.0D0*VS*SQTPI)

	    SFRAC = 1.0D0/(1.0D0+LAMBR+LAMBS)

C         WE STILL NEED TO APPROXIMATE WIND SPEED FOR SKEWNESS
C         CALCULATIONS; THIS IS DONE USING COX & MUNK
          WS = 0.5D0*(SX2/0.00316D0 + (SY2-0.003D0)/0.00192D0)

C         EMPIRICAL CORRECTION TO SLOPE VARIANCES FROM SHADOWING AND
C         HEIGHT/SLOPE CORRELATION
          IF(EFFVAR) THEN 
	        SCOEF = 1.5D0*(1.0D0-SFRAC)
              SX2 = SCOEF*SX2 + SFRAC*SX2
	        SY2 = SCOEF*SY2 + SFRAC*SY2
	        SX = SQRT(SX2)
	        SY = SQRT(SY2)
	    ENDIF

C         SLOPE PROBABILITY DENSITY FUNCTION
          X = XIX/SX;
          Y = XIY/SY;
          X2 = X*X
          Y2 = Y*Y
          ONETF = 1.0D0/24.0D0;

C         SKEWNESS PARAMETERS
          C21 = 0.01D0-0.0086D0*WS;
          C03 = 0.04D0-0.033D0*WS;

C         PEAKEDNESS PARAMETERS
          C40 = 0.40D0;
          C22 = 0.12D0;
          C04 = 0.23D0;

C         GRAMM-CHARLIER DISTRIBUTION
          SPDF = 0.5D0/(PI*SX*SY) * EXP(-0.5D0*(X2 + Y2)) * 
     1    (1.0D0 - 0.5D0*C21*(Y2-1.0D0)*X 
     2    - 1.0D0/6.0D0*C03*(X2*X - 3.0D0*X)
     3    + ONETF*C40*(Y2*Y2 - 6.0D0*Y2 + 3.0D0) 
     4    + 0.250D0*C22*(Y2-1.0D0)*(X2-1.0D0)
     5    + ONETF*C04*(X2*X2 - 6.0D0*X2 + 3.0D0));

C         SINCE THE EXPANSION IS NOT COMPLETE, SOME VALUES MAY
C         DIP UNDER 0 (THESE ARE USUALLY RATHER SMALL)
          IF(SPDF.LT.0.0D0)SPDF=0D0;

C         NORMALIZED VISIBLE INTERACTION PROBABILITY
C         (EQUATION 33B IN ROSS ET AL.)
          QVN=SPDF*WFUNC*HSLOP*SFRAC/CTHER

C         FRESNEL REFLECTANCE
          FREF=FRESNEL(N,K,RDOTN)

C         (EQUATION 36 OF ROSS ET AL. GENERALIZED TO ANY POINT SOURCE
C         AND CONVERTED TO A BRDF EQUATION ACCORDING TO THE MODTRAN
C         CONVENTION)
          SBRDF = PI*FREF*QVN/(4.0D0*ZN*ZN*ZN*RDOTN*CTHES)

C         ADD FOAM LAMBERTIAN COMPONENT
	    SBRDF=PARAMS(6)*PARAMS(7)/PI+(1.0D0-PARAMS(6))*SBRDF

	    ELSE
	        SBRDF=0
	    ENDIF

      ELSE
          SBRDF = 0.0D0
      ENDIF


      RETURN
      END


      DOUBLE PRECISION FUNCTION ERF(ARG,JINT)

      IMPLICIT NONE

C------------------------------------------------------------------
C
C THIS FUNCTION EVALUATES  ERF(X),  ERFC(X)
C   FOR A REAL ARGUMENT  X.
C
C   THE MAIN COMPUTATION EVALUATES NEAR-MINIMAX APPROXIMATIONS
C   FROM "RATIONAL CHEBYSHEV APPROXIMATIONS FOR THE ERROR FUNCTION"
C   BY W. J. CODY, MATH. COMP., 1969, PP. 631-638.  THIS
C   TRANSPORTABLE PROGRAM USES RATIONAL FUNCTIONS THAT THEORETICALLY
C   APPROXIMATE  ERF(X)  AND  ERFC(X)  TO AT LEAST 18 SIGNIFICANT
C   DECIMAL DIGITS.  THE ACCURACY ACHIEVED DEPENDS ON THE ARITHMETIC
C   SYSTEM, THE COMPILER, THE INTRINSIC FUNCTIONS, AND PROPER
C   SELECTION OF THE MACHINE-DEPENDENT CONSTANTS.
C
C*******************************************************************
C*******************************************************************
C
C EXPLANATION OF MACHINE-DEPENDENT CONSTANTS
C
C   XMIN   = THE SMALLEST POSITIVE FLOATING-POINT NUMBER.
C   XINF   = THE LARGEST POSITIVE FINITE FLOATING-POINT NUMBER.
C            THE NEGATIVE OF THE SOLUTION TO THE EQUATION
C            2*EXP(X*X) = XINF.
C   XSMALL = ARGUMENT BELOW WHICH ERF(X) MAY BE REPRESENTED BY
C            2*X/SQRT(PI)  AND ABOVE WHICH  X*X  WILL NOT UNDERFLOW.
C            A CONSERVATIVE VALUE IS THE LARGEST MACHINE NUMBER X
C            SUCH THAT   1.0 + X = 1.0   TO MACHINE PRECISION.
C   XBIG   = LARGEST ARGUMENT ACCEPTABLE TO ERFC;  SOLUTION TO
C            THE EQUATION:  W(X) * (1-0.5/X**2) = XMIN,  WHERE
C            W(X) = EXP(-X*X)/[X*SQRT(PI)].
C   XHUGE  = ARGUMENT ABOVE WHICH  1.0 - 1/(2*X*X) = 1.0  TO
C            MACHINE PRECISION.  A CONSERVATIVE VALUE IS
C            1/[2*SQRT(XSMALL)]
C   XMAX   = LARGEST ACCEPTABLE ARGUMENT; THE MINIMUM
C            OF XINF AND 1/[SQRT(PI)*XMIN].
C
C   APPROXIMATE VALUES FOR SOME IMPORTANT MACHINES ARE:
C
C                          XMIN       XINF        XSMALL
C
C  IEEE (IBM/XT,
C    SUN, ETC.)  (D.P.)  2.23D-308   1.79D+308   1.11D-16
C  IBM 195       (D.P.)  5.40D-79    7.23E+75    1.39D-17
C  UNIVAC 1108   (D.P.)  2.78D-309   8.98D+307   1.73D-18
C  VAX D-FORMAT  (D.P.)  2.94D-39    1.70D+38    1.39D-17
C  VAX G-FORMAT  (D.P.)  5.56D-309   8.98D+307   1.11D-16
C
C
C                          XBIG       XHUGE       XMAX
C
C  IEEE (IBM/XT,
C    SUN, ETC.)  (D.P.)  26.543      6.71D+7     2.53D+307
C  IBM 195       (D.P.)  13.306      1.90D+8     7.23E+75
C  UNIVAC 1108   (D.P.)  26.582      5.37D+8     8.98D+307
C  VAX D-FORMAT  (D.P.)   9.269      1.90D+8     1.70D+38
C  VAX G-FORMAT  (D.P.)  26.569      6.71D+7     8.98D+307
C
C*******************************************************************
C*******************************************************************
C
C INTRINSIC FUNCTIONS REQUIRED ARE:
C
C     ABS, AINT, EXP
C
C  ADAPTED (BARELY) FROM A PUBLIC DOMAIN (NETLIB/SPECFUN) CODE BY:
C          W. J. CODY
C          MATHEMATICS AND COMPUTER SCIENCE DIVISION
C          ARGONNE NATIONAL LABORATORY
C          ARGONNE, IL 60439
C          (1990)
C
C------------------------------------------------------------------
      INTEGER I,JINT
      DOUBLE PRECISION
     1     A,ARG,B,C,D,DEL,FOUR,HALF,P,ONE,Q,SIXTEN,SQRPI,
     2     TWO,THRESH,X,XBIG,XDEN,XHUGE,XINF,XMAX,XNUM,XSMALL,
     3     Y,YSQ,ZERO
      DIMENSION A(5),B(4),C(9),D(8),P(6),Q(5)
C------------------------------------------------------------------
C  MATHEMATICAL CONSTANTS
C------------------------------------------------------------------
      DATA FOUR,ONE,HALF,TWO,ZERO/4.0D0,1.0D0,0.5D0,2.0D0,0.0D0/,
     1     SQRPI/5.6418958354775628695D-1/,THRESH/0.46875D0/,
     2     SIXTEN/16.0D0/
C------------------------------------------------------------------
C  MACHINE-DEPENDENT CONSTANTS
C------------------------------------------------------------------
      DATA XINF,XSMALL/1.79D308,1.11D-16/,
     1     XBIG,XHUGE,XMAX/26.543D0,6.71D7,2.53D307/
C------------------------------------------------------------------
C  COEFFICIENTS FOR APPROXIMATION TO  ERF  IN FIRST INTERVAL
C------------------------------------------------------------------
      DATA A/3.16112374387056560D00,1.13864154151050156D02,
     1       3.77485237685302021D02,3.20937758913846947D03,
     2       1.85777706184603153D-1/
      DATA B/2.36012909523441209D01,2.44024637934444173D02,
     1       1.28261652607737228D03,2.84423683343917062D03/
C------------------------------------------------------------------
C  COEFFICIENTS FOR APPROXIMATION TO  ERFC  IN SECOND INTERVAL
C------------------------------------------------------------------
      DATA C/5.64188496988670089D-1,8.88314979438837594D0,
     1       6.61191906371416295D01,2.98635138197400131D02,
     2       8.81952221241769090D02,1.71204761263407058D03,
     3       2.05107837782607147D03,1.23033935479799725D03,
     4       2.15311535474403846D-8/
      DATA D/1.57449261107098347D01,1.17693950891312499D02,
     1       5.37181101862009858D02,1.62138957456669019D03,
     2       3.29079923573345963D03,4.36261909014324716D03,
     3       3.43936767414372164D03,1.23033935480374942D03/
C------------------------------------------------------------------
C  COEFFICIENTS FOR APPROXIMATION TO  ERFC  IN THIRD INTERVAL
C------------------------------------------------------------------
      DATA P/3.05326634961232344D-1,3.60344899949804439D-1,
     1       1.25781726111229246D-1,1.60837851487422766D-2,
     2       6.58749161529837803D-4,1.63153871373020978D-2/
      DATA Q/2.56852019228982242D00,1.87295284992346047D00,
     1       5.27905102951428412D-1,6.05183413124413191D-2,
     2       2.33520497626869185D-3/
C------------------------------------------------------------------
      X = ARG
      Y = ABS(X)
      IF (Y .LE. THRESH) THEN
C------------------------------------------------------------------
C  EVALUATE  ERF  FOR  |X| <= 0.46875
C------------------------------------------------------------------
            YSQ = ZERO
            IF (Y .GT. XSMALL) YSQ = Y * Y
            XNUM = A(5)*YSQ
            XDEN = YSQ
            DO 20 I = 1, 3
               XNUM = (XNUM + A(I)) * YSQ
               XDEN = (XDEN + B(I)) * YSQ
   20       CONTINUE
            ERF = X * (XNUM + A(4)) / (XDEN + B(4))
            IF (JINT .NE. 0) ERF = ONE - ERF
            GO TO 800
C------------------------------------------------------------------
C  EVALUATE  ERFC  FOR 0.46875 <= |X| <= 4.0
C------------------------------------------------------------------
         ELSE IF (Y .LE. FOUR) THEN
            XNUM = C(9)*Y
            XDEN = Y
            DO 120 I = 1, 7
               XNUM = (XNUM + C(I)) * Y
               XDEN = (XDEN + D(I)) * Y
  120       CONTINUE
            ERF = (XNUM + C(8)) / (XDEN + D(8))
            YSQ = AINT(Y*SIXTEN)/SIXTEN
            DEL = (Y-YSQ)*(Y+YSQ)
            ERF = EXP(-YSQ*YSQ) * EXP(-DEL) * ERF
C------------------------------------------------------------------
C  EVALUATE  ERFC  FOR |X| > 4.0
C------------------------------------------------------------------
         ELSE
            ERF = ZERO
            IF (Y .GE. XBIG) THEN
               IF ((Y .GE. XMAX)) GO TO 300
               IF (Y .GE. XHUGE) THEN
                  ERF = SQRPI / Y
                  GO TO 300
               END IF
            END IF
            YSQ = ONE / (Y * Y)
            XNUM = P(6)*YSQ
            XDEN = YSQ
            DO 240 I = 1, 4
               XNUM = (XNUM + P(I)) * YSQ
               XDEN = (XDEN + Q(I)) * YSQ
  240       CONTINUE
            ERF = YSQ *(XNUM + P(5)) / (XDEN + Q(5))
            ERF = (SQRPI -  ERF) / Y
            YSQ = AINT(Y*SIXTEN)/SIXTEN
            DEL = (Y-YSQ)*(Y+YSQ)
            ERF = EXP(-YSQ*YSQ) * EXP(-DEL) * ERF
      END IF
C------------------------------------------------------------------
C  FIX UP FOR NEGATIVE ARGUMENT, ERF, ETC.
C------------------------------------------------------------------
  300 IF (JINT .EQ. 0) THEN
            ERF = (HALF - ERF) + HALF
            IF (X .LT. ZERO) ERF = -ERF
      ELSE
            IF (X .LT. ZERO) ERF = TWO - ERF
      ENDIF

  800 RETURN
      END


      DOUBLE PRECISION FUNCTION FRESNEL(N,K,COSW)

C     COMPUTE FRESNEL REFLECTANCE BETWEEN EMPTY SPACE (OR AIR)
C     AND A SURFACE OF COMPLEX REFRACTION INDEX N + KI

      IMPLICIT NONE

C     INPUT:
C     N     REAL PART OF COMPLEX REFRACTION INDEX
C     K     IMAGINARY PART OF COMPLEX REFRACTION INDEX
C     COSW  COSINE OF INCIDENCE ANGLE (FROM SURFACE NORMAL)
      DOUBLE PRECISION N,K,COSW

C     OUTPUT:
C     FRESNEL REFLECTANCE COEFFICIENT

C     INTERNAL VARIABLES
      DOUBLE PRECISION N2,K2,TKN,TKN2,N2MK2,COSW2,SINW2
      DOUBLE PRECISION G,H,P,P2,Q
      DOUBLE PRECISION QMCW,QPCW,C,D,RT,N2K2C,TKNCW,E,F
      DOUBLE PRECISION T,U,RP

      N2 = N*N
      K2 = K*K

      TKN = 2.0D0*K*N
      TKN2 = TKN*TKN
      N2MK2 = N2-K2

      COSW2 = COSW*COSW
      SINW2 = 1.0D0-COSW2


      G = N2MK2 - SINW2
      H = SQRT(TKN2 + G*G)
      P = SQRT(0.5D0*(H - G))
      P2 = P*P
      Q = SQRT(0.5D0*(H + G))


C     STRATTON, EQ. 74, P. 505: PERPENDICULLAR COMPONENT
      QMCW = Q-COSW
      QPCW = Q+COSW
      C  = QMCW*QMCW + P2
      D  = QPCW*QPCW + P2
      RT = C/D

C     STRATTON, EQ. 77, P. 506: PARALLEL COMPONENT
      N2K2C = (N2MK2)*COSW
      E  = (N2K2C - Q)
      F  = (N2K2C + Q)
      TKNCW = TKN*COSW
      T  = (TKNCW - P)
      U  = (TKNCW + P)
      RP = (E*E + T*T)/(F*F + U*U)

      FRESNEL = 0.5D0*(RT+RP);
      RETURN
      END
