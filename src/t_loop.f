      SUBROUTINE T_LOOP(VBAND5,IBINPT,VCEN,LVBND5,UNIF,TRACE)

!     TLOOP DRIVES THE TRANSMITTANCE ONLY AND THE TRANSMITTED SOLAR
!     IRRADIANCE RUNS.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     INPUT ARGUMENTS:
!       IBINPT   BIN NUMBER OF CURRENT SPECTRAL POINT.
!                (CENTER FREQUENCY = IBINPT * BNDWID + OSHIFT).
!       VBAND5   5 CM-1 RESOLUTION DATA SPECTRAL FREQUENCY [CM-1].
!       VCEN     SPECTRAL-BIN CENTRAL FREQUENCY [CM-1].
!       LVBND5   LOGICAL FLAG, TRUE WHEN 5 CM-1 DATA IS TO BE OBTAINED.
      INTEGER IBINPT
      REAL VBAND5,VCEN
      LOGICAL LVBND5

!     OUTPUT ARGUMENTS:
!       UNIF     COMBINED TRANSMITTANCE OF UNIFORMLY MIXED GASES.
!       TRACE    COMBINED TRANSMITTANCE OF TRACE MOLECULES.
      REAL UNIF,TRACE

!     COMMONS:
      INCLUDE 'BASE.h'
      INCLUDE 'SEGDAT.h'
      INCLUDE 'YPROP.h'

!     /CNTRL/
!       NSEG     NUMBER OF PATH SEGMENTS ALONG LINE-OF-SIGHT.
!       ML       NUMBER OF ATMOSPHERIC PROFILE LEVELS.
!       MLFLX    NUMBER OF LEVELS FOR WHICH FLUX VALUES ARE WRITTEN.
!       IMULT    MULTIPLE SCATTERING FLAG
!                  (0=NONE, 1=AT SENSOR, -1=AT FINAL OR TANGENT POINT).
!       THERML   FLAG TO CALCULATE THERMAL SCATTER.
      INTEGER NSEG,ML,MLFLX,IMULT
      LOGICAL THERML
      COMMON/CNTRL/NSEG(0:MLOSP1),ML,MLFLX,IMULT,THERML

!     /AER/ THERE ARE "MAER=17" PARTICULATE COMPONENTS:
!           1       AEROSOL 1 (NOMINALLY, BOUNDARY LAYER AEROSOL).
!           2       AEROSOL 2 (NOMINALLY, TROPOSPHERIC AEROSOL).
!           3       AEROSOL 3 (NOMINALLY, STRATOSPHERIC AEROSOL).
!           4       AEROSOL 4 (NOMINALLY, VOLCANIC AEROSOL).
!           5       CIRRUS CLOUD.
!           6       CLOUD 1 (NOMINALLY, WATER CLOUD).
!           7       CLOUD 2 (NOMINALLY, ICE CLOUD).
!           8-17    NOVAM (NAVY OCEANIC VERTICAL AEROSOL MODEL) LAYERS.
!       NAER     NUMBER OF ACTIVE AEROSOLS.
!       EXTV     SPECTRAL EXTINCTION (NORMALIZED TO 1 AT 550 NM).
!       ABSV     SPECTRAL ABSORPTION (1-ABSV/EXTV=SCATTERING ALBEDO).
!       ASYV     SPECTRAL LEGENDRE MOMENT (DIVIDED BY 2N+1).
!       FRAC5    5 CM-1 GRID SPECTRAL INTERPOLATION FRACTION.
!       ASYVLO   ASYMMETRY FACTOR FROM PREVIOUS SPECTRAL FREQUENCY.
      INTEGER NAER
      REAL EXTV,ABSV,ASYV,FRAC5,ASYVLO
      COMMON/AER/NAER,EXTV(MAER),ABSV(MAER),ASYV(MXCMU,MAER),           &
     &  FRAC5,ASYVLO(MAER)

!     /SAPDEP/
!       XSAP     LINE-OF-SIGHT PATH AEROSOL EXTINCTION OPTICAL DEPTHS.
!       XSAPM    VERTICAL MS PATH AEROSOL EXTINCTION OPTICAL DEPTHS.
!       ASAP     LINE-OF-SIGHT PATH AEROSOL ABSORPTION OPTICAL DEPTHS.
!       ASAPM    VERTICAL MS PATH AEROSOL ABSORPTION OPTICAL DEPTHS.
!       XSAPS    SOLAR PATH AEROSOL EXTINCTION OPTICAL DEPTHS FROM LOS.
!       XSAPSM   SOLAR PATH AEROSOL EXTINCTION OPTICAL DEPTHS FOR MS.
!       ASAPS    SOLAR PATH AEROSOL ABSORPTION OPTICAL DEPTHS FROM LOS.
!       ASAPSM   SOLAR PATH AEROSOL ABSORPTION OPTICAL DEPTHS FOR MS.
      REAL XSAP,XSAPM,ASAP,ASAPM,XSAPS,XSAPSM,ASAPS,ASAPSM
      COMMON/SAPDEP/                                                    &
     &  XSAP(1:MWVSAP,0:LAYTWO,0:MLOS),XSAPM(1:MWVSAP,0:LAYDIM),        &
     &  ASAP(1:MWVSAP,0:LAYTWO,0:MLOS),ASAPM(1:MWVSAP,0:LAYDIM),        &
     &  XSAPS(MWVSAP,LAYTWO,MLOS),XSAPSM(MWVSAP,LAYDIM),                &
     &  ASAPS(MWVSAP,LAYTWO,MLOS),ASAPSM(MWVSAP,LAYDIM)

!     /SEG5DT/
!       SPCHI    LINE-OF-SIGHT SPECTRAL DATA @ HIGHER 5 CM-1 GRID POINT.
!       SPCMHI   VERTICAL PATH SPECTRAL DATA @ HIGHER 5 CM-1 GRID POINT.
!       SPCLO    LINE-OF-SIGHT SPECTRAL DATA @ LOWER 5 CM-1 GRID POINT.
!       SPCMLO   VERTICAL PATH SPECTRAL DATA @ LOWER 5 CM-1 GRID POINT.
!            1   SEGMENT SCATTERING OD WEIGHTED ASYMMETRY FACTOR
!            2   SEGMENT AEROSOL SCATTERING OPTICAL DEPTH
!            3   TOTAL O2 CONTINUUM TRANSMITTANCE.
!            4   N2 CONTINUUM TRANSMITTANCE.
!            5   TOTAL H2O CONTINUUM TRANSMITTANCE.
!            6   RAYLEIGH MOLECULAR SCATTERED TRANSMITTANCE.
!            7   TRANSMITTANCE FROM AEROSOL EXTINCTION.
!            8   TOTAL OZONE CONTINUUM TRANSMITTANCE.
!            9   ALL CONTINUUM TRANSMITTANCES EXCEPT O2 AND HNO3.
!           10   TRANSMITTANCE FROM AEROSOL ABSORPTION.
!           11   HNO3 TRANSMITTANCE.
!           12   MOLECULAR CONTINUUM OPTICAL DEPTH
!           13   SEGMENT AEROSOL EXTINCTION OPTICAL DEPTH
!           14   COMBINED SPECIES INCREMENTAL CONTINUUM OPTICAL DEPTH
!           15   RAYLEIGH MOLECULAR SCATTERING OPTICAL DEPTH
!           16   CIRRUS CLOUD TRANSMITTANCE (ICLD=20 ONLY)
!           17   UV/VIS NO2 TRANSMISSION
!           18   UV/VIS SO2 TRANSMISSION
!           19   SEGMENT WATER DROPLET SCATTERING OPTICAL DEPTH
!           20   SEGMENT ICE PARTICLE SCATTERING OPTICAL DEPTH
!           21   SEGMENT BOUNDARY LAYER AEROSOL SCATTERING OPTICAL DEPTH
!           22   SEGMENT TROPOSPHERIC AEROSOL SCATTERING OPTICAL DEPTH
!           23   SEGMENT STRATOSPHERIC AEROSOL SCATTERING OPTICAL DEPTH
!           24   SEGMENT MESOSPHERIC+ AEROSOL SCATTERING OPTICAL DEPTH
!           25   SEGMENT NOVAM AEROSOL SCATTERING OPTICAL DEPTH
!           26   SEGMENT NOVAM AEROSOL ASYMMETRY FACTOR
!           27   SEGMENT STD OR SUB-VIS CIRRUS SCATTERING OPTICAL DEPTH
!           28   SEGMENT CLOUD EXTINCTION OPTICAL DEPTH
!           29   SEGMENT CLOUD SCATTERING OD WEIGHTED ASYMMETRY FACTOR
!           30   SEGMENT RAIN EXTINCTION OPTICAL DEPTH
!           31   SEGMENT RAIN SCATTERING OPTICAL DEPTH
!           32   SEGMENT RAIN ASYMMETRY FACTOR
!           33   "14" WITHOUT RAIN, CLOUD AND AEROSOL.
!      NSEG5-2   VIS/NIR CH4 OPTICAL DEPTH
!      NSEG5-1   H2-H2 DIMER OPTICAL DEPTH
!        NSEG5   H2-HE DIMER OPTICAL DEPTH
!       SSAPH    LOS SAP SCATTERING OPTICAL DEPTH @ HIGHER 5 CM-1 POINT.
!       SSAPHM   MS SAP SCATTERING OPTICAL DEPTH @ HIGHER 5 CM-1 POINT.
!       SSAPL    LOS SAP SCATTERING OPTICAL DEPTH @ LOWER 5 CM-1 POINT.
!       SSAPLM   MS SAP SCATTERING OPTICAL DEPTH @ LOWER 5 CM-1 POINT.
!       TXLEG    SPECTRALLY INTERPOLATED LEGENDRE COEFFICIENTS FOR THE
!                SCATTERING PHASE FUNCTION (OPTICAL DEPTH WEIGHTED SUM).
      REAL SPCHI,SPCMHI,SPCLO,SPCMLO,SSAPH,SSAPHM,SSAPL,SSAPLM,TXLEG
      COMMON/SEG5DT/SPCHI(NSEG5,LAYTWO,MLOS,3),SPCMHI(NSEG5,LAYDIM,3),  &
     &  SPCLO(NSEG5,LAYTWO,MLOS,3),SPCMLO(NSEG5,LAYDIM,3),              &
     &  SSAPH(LAYTWO,MLOS,3),SSAPHM(LAYDIM,3),SSAPL(LAYTWO,MLOS,3),     &
     &  SSAPLM(LAYDIM,3),TXLEG(2:MXCMU,1:LAYDIM,0:1)
      SAVE/SEG5DT/

!     /CARD1/
!       MODEL    MODEL ATMOSPHERE INDEX.
!       ITYPE    SLANT PATH TYPE.
!       IEMSCT   RADIATIVE TRANSFER MODE.
!                  0 FOR TRANSMITTANCE
!                  1 FOR THERMAL EMISSION ONLY
!                  2 FOR THERMAL EMISSION PLUS SOLAR SCATTER
!                  3 FOR TRANSMITTED SOLAR IRRADIANCE
!                  4 FOR SOLAR SCATTER ONLY
!       M1       MODEL ATMOSPHERE FOR PRESSURE & TEMPERATURE PROFILES.
!       M2       MODEL ATMOSPHERE FOR H2O PROFILE.
!       M3       MODEL ATMOSPHERE FOR O3 PROFILE.
!       I_RD2C   READ CARD 2C, 2C1, ... IF EQUAL 1; SKIP IF EQUAL TO 0.
!       NOPRNT   PRINT FLAG.
!       MODTRN   MODTRAN BAND MODEL FLAG.
      INTEGER MODEL,ITYPE,IEMSCT,M1,M2,M3,I_RD2C,NOPRNT
      LOGICAL MODTRN
      COMMON/CARD1/MODEL,ITYPE,IEMSCT,M1,M2,M3,I_RD2C,NOPRNT,MODTRN

!     LOCAL VARIABLES:
!       LBMCEN   FLAG, TRUE IF S/d & 1/d DATA FOR CURRENT FREQUENCY BIN.
!       LBMFLG   FLAG, TRUE IF CENTER OR TAIL DATA FOR CURRENT FREQ BIN.
!       P2RAY    SECOND LEGENDRE EXPANSION COEFFICIENT OVER 5 (= 2N+1).
!       SSAP     SCATTERING OPTICAL DEPTH OF SPECTRAL AEROSOL PROFILE.
!       STORE    TEMPORARY MEMORY FOR INTERPOLATIONS.
!       TRANXY   COMBINED TRANSMITTANCE OF X AND Y MOLECULES.
!       T_CH4    METHANE SHORTER WAVE TRANSMITTANCE.
!       IEXT     LOOP INDEX OF EXTINCTION SOURCES.
      LOGICAL LBMCEN,LBMFLG
      REAL P2RAY,SSAP,STORE,TRANXY,T_CH4
      INTEGER IEXT

!     DEFINE THE LAYER INDEPENDENT 5 CM-1 DATA
      IF(LVBND5)CALL FRQ5DT(.FALSE.,VBAND5)

!     FOR EACH WAVENUMBER, BMOD0 PERFORMS INITIALIZATIONS FOR BMOD:
      IF(MODTRN)CALL BMOD0(IBINPT,VCEN,.TRUE.,LBMCEN,LBMFLG)

!     DEFINE OPTICAL PATH ALTITUDE DEPENDENT 5 CM-1 DATA:
      IF(LVBND5)CALL SEG5(VBAND5,1,1,NSEG(1),1,                         &
     &  WTOTAL,XSAP(1,0,1),ASAP(1,0,1),.TRUE.)

!     INTERPOLATE COARSE SPECTRAL RESOLUTION DATA:
      IF(ABS(FRAC5).LT..001)THEN

!         GRID POINT: NO SPECTRAL INTERPOLATION:
          DO IEXT=1,16
              TX(IEXT)=SPCHI(IEXT,1,1,1)
          ENDDO
          DO IEXT=17,NSEG5-3
              TX(47+IEXT)=SPCHI(IEXT,1,1,1)
          ENDDO

!         VIS/NIR CH4:
          T_CH4=EXP(-SPCHI(NSEG5-2,1,1,1))

!         H2-H2 AND H2-HE DIMERS:
          TX(MEXTX-1)=EXP(-SPCHI(NSEG5-1,1,1,1))
          TX(MEXTX)=EXP(-SPCHI(NSEG5,1,1,1))

!         SAP AEROSOLS:
          SSAP=SSAPH(1,1,1)
      ELSE

!         PERFORM SPECTRAL INTERPOLATION:
          DO IEXT=1,16
              STORE=SPCHI(IEXT,1,1,1)
              TX(IEXT)=STORE+FRAC5*(SPCLO(IEXT,1,1,1)-STORE)
          ENDDO
          DO IEXT=17,NSEG5-2
              STORE=SPCHI(IEXT,1,1,1)
              TX(47+IEXT)=STORE+FRAC5*(SPCLO(IEXT,1,1,1)-STORE)
          ENDDO

!         VIS/NIR CH4:
          STORE=SPCHI(NSEG5-2,1,1,1)
          T_CH4=EXP(-(STORE+FRAC5*(SPCLO(NSEG5-2,1,1,1)-STORE)))

!         H2-H2 AND H2-HE DIMERS:
          STORE=SPCHI(NSEG5-1,1,1,1)
          TX(MEXTX-1)=EXP(-(STORE+FRAC5*(SPCLO(NSEG5-1,1,1,1)-STORE)))
          STORE=SPCHI(NSEG5,1,1,1)
          TX(MEXTX)=EXP(-(STORE+FRAC5*(SPCLO(NSEG5,1,1,1)-STORE)))

!         SAP AEROSOLS:
          SSAP=SSAPH(1,1,1)
          SSAP=SSAP+FRAC5*(SSAPL(1,1,1)-SSAP)
      ENDIF

!     BAND MODEL TRANSMITTANCES:
      IF(MODTRN)CALL BMOD(1,1,NSEG(1),1,IBINPT,LBMCEN,LBMFLG)

!     EXCLUDE H2-H2 & H2-HE DIMERS FROM XY TRANSM [IT IS IN TX(14)]:
      TRANXY=TX(MEXT+1)
      DO IEXT=MEXT+2,MEXTX-2
          TRANXY=TRANXY*TX(IEXT)
      ENDDO
      DO IEXT=MEXTX+1,MEXTX+NMOLY
          TRANXY=TRANXY*TX(IEXT)
      ENDDO

!     COMBINE TRANSMISSIONS:
      UNIF=TX(36)*TX(44)*TX(46)*TX(47)*TX(50)
      TRACE=TX(52)*TX(54)*TX(55)*TX(56)*TX(11)

!     DEFINE SPECIES DEPENDENT TRANSMITTANCES:
      TX(9)=TX(17)*UNIF*TX(31)*TRACE*TRANXY*EXP(-TX(14))
      UNIF=UNIF*TX(3)*T_CH4
      TRACE=TRACE*TX(64)*TX(65)

!     COMBINE IR AND UV/VIS CONTRIBUTIONS FOR CH4, NO2, SO2 AND O2:
      TX(46)=T_CH4*TX(46)
      TX(64)=TX(64)*TX(55)
      TX(65)=TX(65)*TX(56)
      TX(50)=TX(50)*TX(3)

!     OZONE CONTRIBUTIONS ARE MODELED WITH THE MODTRAN BAND MODEL
!     BELOW 9170 CM-1 AND AS A CONTINUUM ABOVE 9170 CM-1:
      IF(VBAND5.GE.9169.)TX(31)=TX(8)

!     RETURN TO TRANS:
      RETURN
      END
