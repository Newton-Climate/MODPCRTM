      FUNCTION GAMFOG(MR,FREQ,T,RHO)

!     COMPUTES ATTENUATION OF EQUIVALENT LIQUID WATER CONTENT
!     IN CLOUDS OR FOG IN DB/KM
!     CONVERTED TO NEPERS BY NEW CONSTANT 1.885

!       FREQ = WAVENUMBER (INVERSE CM)
!       T    = TEMPERATURE (DEGREES KELVIN)
!       RHO  = EQUIVALENT LIQUID CONTENT  (G/CUBIC METER)
!     CINDEX=COMPLEX DIELECTRIC CONSTANT M  FROM INDEX
!     WAVL = WAVELENGTH IN CM

      COMPLEX CINDEX
      IF(RHO.LE.0.)THEN
          GAMFOG=0.
          RETURN
      ENDIF
      WAVL=1.0/FREQ
      IF(T.GT.253.2)THEN
          TC=T-273.2
      ELSE

!         CHANGE TEMP SO THAT MINIMUM IS -20.0 CENT.
          TC=-20.
      ENDIF
      IF(MR.GE.5)THEN
          CALL INDXRF(WAVL,TC,0,REIL,AIMAK)
      ELSE
          CALL INDXRF(WAVL,TC,1,REIL,AIMAK)
      ENDIF
      CINDEX=CMPLX(REIL,AIMAK)

!     ATTENUATION = 6.0*PI*FREQ*RHO*IMAG(-K)
!      6.0*PI/10. = 1.885 (THE FACTOR OF 10 IS FOR UNITS CONVERSION)
      GAMFOG=1.885*FREQ*RHO*AIMAG((1-CINDEX**2)/(2+CINDEX**2))
      RETURN
      END
