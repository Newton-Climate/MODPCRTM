      DOUBLE PRECISION FUNCTION ZENSUN(REL_AZ,DELO,BETA,IARBO)

!     FUNCTION ZENSUN RETURNS THE VALUE OF THE SUN'S ZENITH ANGLE
!     AT ANY POINT ALONG THE OPTICAL PATH BASED UPON STRAIGHT
!     LINE GEOMETRY (NO REFRACTION). THIS ANGLE IS USED TO SPECIFY
!     THE SCATTERING POINT TO SUN PATHS. THE BENDING DUE TO REFRACTION
!     ALONG THIS PATH IS DETERMINED BY THE GEO ROUTINES. IF THE BENDING
!     IS GREATER THAN ONE DEGREE THE ZENITH ANGLE IS CORRECTED ACCORDING
!     AND THE PATH CALCULATION IS REPEATED.
      IMPLICIT NONE

!     INPUT ARGUMENTS:
!       REL_AZ   RELATIVE SOLAR AZIMUTH ANGLE AT SENSOR [DEG].
!       DELO     OBSERVER-TO-SUN EARTH CENTERED ANGLE [DEG].
!       BETA     OBSERVER-TO-SCATTER POINT EARTH CENTERED ANGLE [DEG].
!       IARBO    INITIAL GUESS AT CASE INDEX.
      DOUBLE PRECISION REL_AZ,DELO,BETA
      INTEGER IARBO

!     COMMONS:

!     /DCNSTN/
!       DRIGHT   SMALLEST DOUBLE PRECISION REAL ADDED TO 1 EXCEEDS 1.
!       DPDEG    NUMBER OF DEGREES IN ONE RADIAN IN DOUBLE PRECISION
      DOUBLE PRECISION DRIGHT,DPDEG
      COMMON/DCNSTN/DRIGHT,DPDEG

!     LOCAL VARIABLES:
!       DELOR    OBSERVER-TO-SUN EARTH CENTERED ANGLE [RAD].
!       BETAR    OBSERVER-TO-SCATTER POINT EARTH CENTERED ANGLE [RAD].
      DOUBLE PRECISION DELOR,BETAR

!     BRANCH BASED ON CASE:
      IF(IARBO.EQ.0)THEN

!         GENERAL CASE
          DELOR=DELO/DPDEG
          BETAR=BETA/DPDEG
          ZENSUN=DPDEG*ACOS(COS(DELOR)*COS(BETAR)                       &
     &                     +SIN(DELOR)*SIN(BETAR)*COS(REL_AZ/DPDEG))

!     SPECIAL CASES IF REL_AZ IS ARBITRARY:
      ELSEIF(IARBO.EQ.1)THEN
          ZENSUN=DELO
      ELSEIF(IARBO.EQ.2)THEN
          ZENSUN=BETA
      ELSEIF(IARBO.EQ.3)THEN
          ZENSUN=0.D0
      ENDIF
      RETURN
      END
