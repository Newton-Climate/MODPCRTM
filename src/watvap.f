      SUBROUTINE WATVAP(P,T)

!        WRITTEN APR, 1985 TO ACCOMMODATE 'JCHAR' DEFINITIONS FOR
!        UNIFORM DATA INPUT -
!        RE-WRITTEN JULY, 1996 TO CORRECT COMMENTS, CORRECT RELATIVE
!        HUMIDITY CHECK, SIMPLIFY

!     JCHAR    JUNIT(1)

!     " ",A       10    VOLUME MIXING RATIO (PPMV)
!       B         11    NUMBER DENSITY (CM-3)
!       C         12    MASS MIXING RATIO (GM(K)/KG(AIR))
!       D         13    MASS DENSITY (GM M-3)
!       E         14    PARTIAL PRESSURE (MB)
!       F         15    DEW POINT TEMP (TD IN T(K)) - H2O ONLY
!       G         16     "    "     "  (TD IN T(C)) - H2O ONLY
!       H         17    RELATIVE HUMIDITY (RH IN PERCENT) - H2O ONLY
!       I         18    AVAILABLE FOR USER DEFINITION
!       J         19    REQUEST DEFAULT TO SPECIFIED MODEL ATMOSPHERE

!     THIS SUBROUTINE COMPUTES THE WATER VAPOR MASS DENSITY (GM M-3)
!     GIVEN HUMIDITY  # TD = DEW POINT TEMP(K,C), RH = RELATIVE
!     (PERCENT), PPH2O = WATER VAPOR PARTIAL PRESSURE (MB), DENH2O =
!     WATER VAPOR MASS DENSITY (GM M-3),AMSMIX = MASS MIXING RATIO
!     (GM/KG).
!                     THE FUNCTION DENSAT FOR THE SATURATION
!     WATER VAPOR DENSITY OVER WATER IS ACCURATE TO BETTER THAN 1
!     PERCENT FROM -50 TO +50 DEG C. (SEE THE LOWTRAN3 OR 5 REPORT)

!       'JUNIT(1)' GOVERNS CHOICE OF UNITS -
      REAL P,T

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     COMMONS:
!     /CONSTN/
!       AMWT     MOLECULAR WEIGHTS [GM/MOLE].
!                  1  H2O   2  CO2   3  O3    4  N2O   5  CO    6  CH4
!                  7  O2    8  NO    9  SO2   10 NO2   11 NH3   12 HNO3
      REAL AMWT
      COMMON/CONSTN/AMWT(12)

      INCLUDE 'IFIL.h'
      INTEGER JUNITP,JUNITT,JUNIT,JLOW
      REAL WMOL
      COMMON/CARD1B/JUNITP,JUNITT,JUNIT(13),WMOL(12),JLOW

!     LOCAL VARIABLES:
!       RH       RELATIVE HUMIDITY [PERCENT].
      REAL RH

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      SAVE /CONSTN/
      EXTERNAL DEVCBD,ATMCON

!     FUNCTIONS:
!       DENSAT   MAPS TEMPERATURE RATIO TO SATURATION DENSITY [G/M3]:
      REAL TRATIO,DENSAT
      DENSAT(TRATIO)=TRATIO*EXP(18.9766-(14.9595+2.43882*TRATIO)*TRATIO)

!     BRANCH BASED ON UNIT SPECIFIER:
      IF(JUNIT(1).EQ.10)THEN

!         GIVEN VOLUME MIXING RATIO [PPMV]:
          WMOL(1)=WMOL(1)*(P/PZERO)*(TZERO/T)*AMWT(1)/STDVOL
      ELSEIF(JUNIT(1).EQ.11)THEN

!         GIVEN NUMBER DENSITY [MOLECULES/CM3]:
          WMOL(1)=1.E6*WMOL(1)*AMWT(1)/AVOGAD
      ELSEIF(JUNIT(1).EQ.12)THEN

!         GIVEN MASS MIXING RATIO [GM(H2O)/KG(AIR)]:
          WMOL(1)=1.E3*WMOL(1)*(P/PZERO)*(TZERO/T)*REAL(AIRMWT)/STDVOL
      ELSEIF(JUNIT(1).EQ.14)THEN

!         GIVEN WATER VAPOR PARTIAL PRESSURE [MB]:
          WMOL(1)=1.E6*(TZERO/T)*WMOL(1)*AMWT(1)/(STDVOL*PZERO)
      ELSEIF(JUNIT(1).EQ.15)THEN

!         GIVEN DEWPOINT TEMPERATURE [DEG K]:
          WMOL(1)=DENSAT(TZERO/WMOL(1))*WMOL(1)/T
      ELSEIF(JUNIT(1).EQ.16)THEN

!         GIVEN DEWPOINT TEMPERATURE [DEG C]:
          WMOL(1)=DENSAT(TZERO/(TZERO+WMOL(1)))*(TZERO+WMOL(1))/T
      ELSEIF(JUNIT(1).EQ.17)THEN

!         GIVEN RELATIVE HUMIDITY [PERCENT]:
          WMOL(1)=DENSAT(TZERO/T)*WMOL(1)/100
      ELSEIF(JUNIT(1).NE.13)THEN

!         GIVEN INVALID JUNIT(1):
          WRITE(IPR,'(/A,I5)')' Error in WATVAP:  JUNIT(1) =',JUNIT(1)
          IF(LJMASS)CALL WRTBUF(FATAL)
          STOP '*** JUNIT(1) Error in WATVAP ***'
      ENDIF

!     INSURE THAT RELATIVE HUMIDITY IS VALID
      RH=100*WMOL(1)/DENSAT(TZERO/T)
      IF(RH.GT.100.01)WRITE(IPR,'(/2A,F10.3,A)')' WARNING from',        &
     &  ' WATVAP:  RELATIVE HUMIDITY (=',RH,'%) EXCEEDS 100%.'
      RETURN
      END
