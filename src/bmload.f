      SUBROUTINE BMLOAD(IFLAG)

!     IFLAG=1 MEANS BMLOAD IS CALLED FOR LINE CENTER PARAMETERS
!     IFLAG=2 MEANS BMLOAD IS CALLED FOR LINE TAIL PARAMETERS

!     BMLOAD IS CALLED BY BMOD TO LOAD BAND MODEL DATA FOR A
!     SINGLE PARAMETER SET INTO THE MATRICES SD, OD AND ALF0.

!     ORIGINALLY, THE BAND MODEL PARAMETERS ARE STORED IN
!     COMPACT PARAMETER SETS.  THERE ARE TWO TYPES OF PARAMETER
!     SETS CORRESPONDING TO LINE CENTER AND LINE TAIL
!     CONTRIBUTIONS.  IF IMOL IS 12 OR LESS, THE PARAMETER SET
!     CONTAINS LINE CENTER MOLECULAR ABSORPTION INFORMATION:

!      IBIN = THE BIN NUMBER (IBIN*BNDWID = BIN CENTER FREQUENCY)
!      IMOL =  1 FOR H2O
!           =  2 FOR CO2
!           =  3 FOR O3
!           =  4 FOR N2O
!           =  5 FOR CO
!           =  6 FOR CH4
!           =  7 FOR O2
!           =  8 FOR NO
!           =  9 FOR SO2
!           = 10 FOR NO2
!           = 11 FOR NH3
!           = 12 FOR HNO3

!        SDZ(IT)= AVERAGE MOLECULAR ABSORPTION COEFFICIENT PARAMETER
!                 FOR THE IT'TH TEMPERATURE (CM-1/AMAGAT)
!          IALF = LORENTZ LINE WIDTH PARAMETER AT STANDARD PRESSURE AND
!                 TEMPERATURE (1.E-04 CM-1/ATM)
!        ODZ(IT)= LINE DENSITY PARAMETER FOR THE TEMPERATURE IT (CM-1)
!                 (ONLY FOR LINE CENTERS, NOT A LINE TAIL PARAMETER)

!     IF IMOL IS 12 OR MORE, THE PARAMETER SET CONTAINS CONTINUUM
!     ABSORPTION INFORMATION AND

!                  IBIN = THE BIN NUMBER
!             IMOL,IALF = 13 FOR  H2O TAIL CONTRIBUTION
!                       = 14 FOR  CO2 TAIL CONTRIBUTION
!                       = 15 FOR   O3 TAIL CONTRIBUTION
!                       = 16 FOR  N2O TAIL CONTRIBUTION
!                       = 17 FOR   CO TAIL CONTRIBUTION
!                       = 18 FOR  CH4 TAIL CONTRIBUTION
!                       = 19 FOR   O2 TAIL CONTRIBUTION
!                       = 20 FOR   NO TAIL CONTRIBUTION
!                       = 21 FOR  SO2 TAIL CONTRIBUTION
!                       = 22 FOR  NO2 TAIL CONTRIBUTION
!                       = 23 FOR  NH3 TAIL CONTRIBUTION
!                       = 24 FOR HNO3 TAIL CONTRIBUTION
!               SDZ(IT) = MOLECULAR LINE WING CONTINUUM ABSORPTION
!                         COEFFICIENT FOR SPECIES IMOL AT THE
!                         IT'TH TEMPERATURE (CM-1/AMAGAT)
!               ODZ(IT) = MOLECULAR LINE WING CONTINUUM ABSORPTION
!                         COEFFICIENT FOR SPECIES IALF AT THE
!                         IT'TH TEMPERATURE (CM-1/AMAGAT)
      IMPLICIT NONE

!     INPUT ARGUMENTS:
      INTEGER IFLAG

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     COMMONS:
      INCLUDE 'BMHEAD.h'
      INCLUDE 'BMDAT.h'

!     LOCAL VARIABLES:
      INTEGER ITLSUB,IM,IT,IP

      IF(IFLAG.EQ.1)THEN

!         FILL THE SD, OD AND ALF0 MATRICES.
!         LINE CENTER CONTRIBUTIONS
          IM=IMOLCN
          DO IT=NTEMP,1,-1
              SDCN(IT,IM)=SDZCN(IT)
              IF(ODZCN(IT).GT.0. .OR. IT.EQ.NTEMP)THEN
                  ODCN(IT,IM)=ODZCN(IT)
              ELSE

!                 ODZCN WILL BE ZERO IF SDZCN IS ZERO.  IN THAT CASE,
!                 JUST SET ODZCN TO THE FIRST NON-ZERO HIGHER
!                 TEMPERATURE VALUE TO AVOID INTERPOLATION PROBLEMS.
                  ODCN(IT,IM)=ODCN(IT+1,IM)
              ENDIF
              DO IP=1,NPRESS
                  DO ITLSUB=0,NTLSUB
                      SDTL(ITLSUB,IT,IP,IM)=0.
                  ENDDO
              ENDDO
          ENDDO
          ALF0(IM)=.0001*IALFCN

      ELSE

!         IFLAG.EQ.2 HERE.  LINE TAIL CONTRIBUTIONS
          IM=IMOLTL

!         ONE TAIL IN THE RECORD
          DO IT=1,NTEMP
              DO IP=1,NPRESS
                  CALL SDTAIL(SDZ0TL(IT,IP),SDZ1TL(IT,IP),              &
     &              SDZ2TL(IT,IP),SDZ3TL(IT,IP),SDZ4TL(IT,IP),          &
     &              NTLSUB,DELSUB(0),SDTL(0,IT,IP,IM))
              ENDDO
          ENDDO
      ENDIF
      RETURN
      END

      SUBROUTINE SDTAIL(SDZ0TL,SDZ1TL,SDZ2TL,SDZ3TL,SDZ4TL,             &
     &  NTLSUB,DELSUB,SDTL)

!     EVALUATES LINE TAIL PAED APPROXIMATE FITS.

!     INPUT ARGUMENTS:
!       SDZ0TL   PADE NUMERATOR CONSTANT TERM [???].
!       SDZ1TL   PADE NUMERATOR LINEAR TERM [???].
!       SDZ2TL   PADE NUMERATOR QUADRATICE TERM [???].
!       SDZ3TL   PADE DENOMINATOR LINEAR TERM.
!       SDZ4TL   PADE DENOMINATOR QUADRATICE TERM.
!       NTLSUB   NUMBER OF EVALUATION POINTS LESS 1.
!       DELSUB   LINE TAIL EVLAUATION POINTS, NORMALIZED ONTO [-1,1].

!     OUTPUT ARGUMENTS:
!       SDTL     LINE TAIL ABSORPTION COEFFICIENTS [???].
      INTEGER NTLSUB
      REAL SDZ0TL,SDZ1TL,SDZ2TL,SDZ3TL,SDZ4TL,DELSUB(0:NTLSUB),         &
     &  SDTL(0:NTLSUB)
      DO ITLSUB=0,NTLSUB
          SDTL(ITLSUB)                                                  &
     &      =(SDZ0TL+DELSUB(ITLSUB)*(SDZ1TL+DELSUB(ITLSUB)*SDZ2TL))     &
     &      /(1+DELSUB(ITLSUB)*(SDZ3TL+DELSUB(ITLSUB)*SDZ4TL))
      ENDDO
      RETURN
      END
