      SUBROUTINE M3D(JNTRVL,IK,IKMAX,SUBINT,WPATH)

!     INPUT ARGUMENTS:
!       JNTRVL   NUMBER OF K-DISTRIBUTION INTERVALS FOR CURRENT FREQ.
!       IK       PATH SEGMENT INDEX.
!       IKMAX    NUMBER OF PATH SEGMENTS.
!       SUBINT   SPECTRAL BIN "K" SUB-INTERVAL FRACTIONAL WIDTHS.
!       WPATH    PATH COLUMN AMOUNTS.
      INTEGER JNTRVL,IK,IKMAX
      REAL SUBINT(*),WPATH(0:*)

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     COMMONS:
      INCLUDE 'IFIL.h'

!     /BASE/
!         TX ARRAY
!           1  AEROSOL SCATTERING DEPTH WEIGHTED ASYMMETRY PARAMETER.
!           2  INCREMENTAL AEROSOL SCATTERING OPTICAL DEPTH.
!           3  TOTAL O2 CONTINUUM TRANSMITTANCE.
!           4  N2 CONTINUUM TRANSMITTANCE.
!           5  TOTAL H2O CONTINUUM TRANSMITTANCE.
!           6  RAYLEIGH MOLECULAR SCATTERED TRANSMITTANCE.
!           7  AEROSOL EXTINCTION.
!           8  TOTAL OZONE CONTINUUM TRANSMITTANCE.
!           9  PRODUCT OF ALL CONTINUUM TRANSMITTANCES EXCEPT O2 & HNO3.
!          10  AEROSOL ABSORPTION.
!          11  HNO3 TRANSMITTANCE.
!          12  MOLECULAR CONTINUUM OPTICAL DEPTH.
!          13  INCREMENTAL AEROSOL EXTINCTION OPTICAL DEPTH.
!          14  TOTAL CONTINUUM OPTICAL DEPTH.
!          15  LAYER RAYLEIGH MOLECULAR SCATTERING OPTICAL DEPTH.
!          16  CIRRUS CLOUD TRANSMITTANCE (ICLD=20 ONLY).
!          64  UV/VIS NO2 TRANSMITTANCE.
!          65  UV/VIS SO2 TRANSMITTANCE.
!          66  INCREMENTAL WATER DROPLET SCATTERING OPTICAL DEPTH.
!          67  INCREMENTAL ICE PARTICLE SCATTERING OPTICAL DEPTH.
!          74  INCREMENTAL STD/SUB-VIS CIRRUS SCATTERING OPTICAL DEPTH.
!          75  INCREMENTAL CLOUD (WATER+ICE) EXTINCTION OPTICAL DEPTH.
!          76  CLOUD ASYMMETRY PARAMETER WEIGHTED BY SCATTERING DEPTH.
!          77  INCREMENTAL RAIN EXTINCTION OPTICAL DEPTH
!          78  INCREMENTAL RAIN SCATTERING OPTICAL DEPTH
!          79  INCREMENTAL RAIN ASYMMETRY PARAMETER.
!          --  MOLECULAR LINE CENTER TRANSMITTANCE  --
!          17=H2O  36=CO2  31=O3   47=N2O  44=CO   46=CH4
!          50=O2   54=NO   56=SO2  55=NO2  52=NH3  11=HNO3
      INCLUDE 'BASE.h'

!     /CARD4/
!       IV1      LOWEST SPECTRAL FREQUENCY OUTPUT [CM-1].
!       IV2      HIGHEST SPECTRAL FREQUENCY OUTPUT [CM-1].
!       IDV      PRINTOUT SPECTRAL FREQUENCY STEP SIZE [CM-1].
!       IFWHM    TRIANGULAR SLIT FULL-WIDTH-HALF-MAXIMUM [CM-1].
!       VBAND    CURRENT COMPUTATION BAND FREQUENCY [CM-1].
!                (EQUALS BAND CENTER FOR 1, 5 & 15 CM-1 BAND MODELS;
!                EQUALS THE MINIMUM BAND VALUE FOR 0.1 CM-1 BAND MODEL)
!       IBINPT   BIN NUMBER OF CURRENT SPECTRAL POINT.
!                (CENTER FREQUENCY = IBINPT * BNDWID + OSHIFT).
!       IBINLO   BIN NUMBER OF (PADDED) SPECTRAL RANGE LOWER BOUND.
!       IBINHI   BIN NUMBER OF (PADDED) SPECTRAL RANGE UPPER BOUND.
!       IBINMN   BIN NUMBER OF MINIMUM COMPUTATION SPECTRAL POINT.
!       IBINMX   BIN NUMBER OF MAXIMUM COMPUTATION SPECTRAL POINT.
!       IBINDL   BIN NUMBER INCREMENT FOR SPECTRAL PRINTOUT.
!       IBINRS   BIN NUMBER INCREMENT EQUAL TO SPECTRAL RESOLUTION.
!       IBINOS   BIN NUMBER OFFSET BETWEEN CURRENT & OUTPUT SPC POINTS.
!       IBINWR   BIN NUMBER OF NEXT SPECTRAL DATA WRITE.
!       MBINPT   BIN NUMBER MAXIMUM FOR CURRENT BAND MODEL RESOLUTION.
!       IDBIN5   SPECTRAL BIN NUMBER STEP SIZE FOR 5 CM-1 GRID.
!       ISTEP5   INCREMENT FOR RETRIEVING 5 CM-1 RESOLUTION DATA [CM-1].
!       NSPCDT   NUMBER OF OUTPUT SPECTRAL DATA POINTS.
      DOUBLE PRECISION IDV
      REAL IV1,IV2,IFWHM,VBAND
      INTEGER IBINPT,IBINLO,IBINHI,IBINMN,IBINMX,IBINDL,                &
     &  IBINRS,IBINOS,IBINWR,MBINPT,IDBIN5,ISTEP5,NSPCDT
      COMMON/CARD4/IDV,IV1,IV2,IFWHM,VBAND,IBINPT,IBINLO,IBINHI,IBINMN, &
     &  IBINMX,IBINDL,IBINRS,IBINOS,IBINWR,MBINPT,IDBIN5,ISTEP5,NSPCDT

!     /CORKDT/
!       WTKSUB   SPECTRAL BIN SUB-INTERVAL FRACTIONAL WIDTHS.
!       WTKSAV   SAVED SPECTRAL BIN SUB-INTERVAL FRACTIONAL WIDTHS.
!       DEPLAY   INCREMENTAL OPTICAL DEPTHS.
!       TRNLAY   INCREMENTAL TRANSMITTANCES.
!       TRNCUM   CUMULATIVE TRANSMITTANCES.
!       K2TAIL   POINTER FROM K BIN TO LINE TAIL SUB-BIN
!                (=0 IF MULTIPLE LINE TAIL SUB-BINS CONTRIBUTE).
!       CONTWT   WEIGHTS FOR PARTITIONING LINE TAILS INTO K'S
!                (ONLY USED IF K2TAIL IS 0).
      REAL WTKSUB(MXKSUB),WTKSAV(NTLSUB),DEPLAY(MXKSUB),                &
     &  TRNLAY(MXKSUB),TRNCUM(MXKSUB),CONTWT(NTLSUB,MXKSUB)
      INTEGER K2TAIL(MXKSUB)
      COMMON/CORKDT/K2TAIL,WTKSUB,WTKSAV,DEPLAY,TRNLAY,TRNCUM,CONTWT
      SAVE /CORKDT/

!     /CARD2/
!       IHAZE    BOUNDARY LAYER AEROSOL MODEL NUMBER.
!       ISEASN   SEASON NUMBER (1=SPRING-SUMMER, 2=FALL-WINTER).
!       IVULCN   VOLCANIC AEROSOL MODEL NUMBER.
!       ICSTL    COASTAL AIRMASS MODEL NUMBER.
!       ICLD     CLOUD MODEL NUMBER.
!       IVSA     VERTICAL STRUCTURE ALGORITHM (0=OFF, 1=ON).
!       VIS      SURFACE VISIBILITY (GROUND METEOROLOGICAL RANGE) [KM].
!       WSS      CURRENT WIND SPEED (M/S).
!       WHH      24-HOUR WIND SPEED (M/S).
!       RAINRT   RAIN RATE (MM/HR)
!       LSAP     LOGICAL FLAG FOR SPECTRAL AEROSOL PROFILES INPUT.
      INTEGER IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA
      REAL VIS,WSS,WHH,RAINRT
      LOGICAL LSAP
      COMMON/CARD2/IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA,                 &
     &  VIS,WSS,WHH,RAINRT,LSAP

!     /M3DNAM/
!       M3DGEN   NAME OF GENERAL INFORMATION FILE FOR MOD3D.
!       M3DCNT   NAME OF CONTINUA DATA FILE FOR MOD3D.
!       M3DDAT   NAME OF MOLECULAR EXTINCTION FILE SORTED BY ATMOSPHERE.
!       M3DMOL   NAME OF MOLECULAR EXTINCTION DATA FILE FOR MOD3D.
      CHARACTER M3DGEN*(NAMLEN),M3DCNT*(NAMLEN),                        &
     &  M3DDAT*(NAMLEN),M3DMOL*(NAMLEN)
      COMMON/M3DNAM/M3DGEN,M3DCNT,M3DDAT,M3DMOL
      INCLUDE 'BMHEAD.h'

!     /M3DSPC/
!       LASCII   ASCII FILE OUTPUT FLAG FOR MOD3D FILES.
!       NATM     COUNTER FOR NUMBER OF DIFFERENT MOLECULAR ATMOSPHERES.
      LOGICAL LASCII
      INTEGER NATM
      COMMON/M3DSPC/LASCII,NATM

!     /CARD1/
!       MODEL    MODEL ATMOSPHERE INDEX.
!       ITYPE    SLANT PATH TYPE.
!       IEMSCT   RADIATIVE TRANSFER MODE.
!                  0 FOR TRANSMITTANCE
!                  1 FOR THERMAL EMISSION ONLY
!                  2 FOR THERMAL EMISSION PLUS SOLAR SCATTER
!                  3 FOR TRANSMITTED SOLAR IRRADIANCE
!                  4 FOR SOLAR SCATTER ONLY
!       M1       MODEL ATMOSPHERE FOR PRESSURE & TEMPERATURE PROFILES.
!       M2       MODEL ATMOSPHERE FOR H2O PROFILE.
!       M3       MODEL ATMOSPHERE FOR O3 PROFILE.
!       I_RD2C   READ CARD 2C, 2C1, ... IF EQUAL 1; SKIP IF EQUAL TO 0.
!       NOPRNT   PRINT FLAG.
!       MODTRN   MODTRAN BAND MODEL FLAG.
      INTEGER MODEL,ITYPE,IEMSCT,M1,M2,M3,I_RD2C,NOPRNT
      LOGICAL MODTRN
      COMMON/CARD1/MODEL,ITYPE,IEMSCT,M1,M2,M3,I_RD2C,NOPRNT,MODTRN

!     /M3DDEN/
!       PRES     LAYER PRESSURE [ATM].
!       AER550   AEROSOL EXTINCTION OPTICAL DEPTH AT 550 NM.
!       DENH2O   LAYER H2O DENSITY [GM/M3].
!       DENCO2   LAYER CO2 DENSITY [GM/M3].
!       DENO3    LAYER O3 DENSITY [GM/M3].
      REAL PRES,AER550,DENH2O,DENCO2,DENO3
      COMMON/M3DDEN/PRES(LAYTWO),AER550(LAYTWO),                        &
     &  DENH2O(LAYTWO),DENCO2(LAYTWO),DENO3(LAYTWO)

      SAVE /M3DDEN/
      EXTERNAL DEVCBD,ATMCON

!     LOCAL VARIABLES:
!       INTRVL   ABSORPTION COEFFICIENT (K) SUB-INTERVAL INDEX.
!       GAER     AEROSOL ASYMMETRY FACTOR.
!       CLDSCT   CLOUD SCATTERING OPTICAL DEPTH.
!       GCLD     CLOUD ASYMMETRY FACTOR.
!       COLDEN   EFFECTIVE COLUMN DENSITY [ATM KM / K].
!       LFIRST   LOGICAL INDICATING FIRST ENTRY WITH NEW ATMOSPHERE.
      INTEGER INTRVL
      REAL CLDSCT,COLDEN,XMOL(MXKSUB),SAER(LAYTWO),XAER(LAYTWO),        &
     &  GAER(LAYTWO),SCLD(LAYTWO),XCLD(LAYTWO),GCLD(LAYTWO),            &
     &  SRAIN(LAYTWO),XRAIN(LAYTWO),GRAIN(LAYTWO),SMOL(LAYTWO)
      SAVE SAER,XAER,GAER,SCLD,XCLD,GCLD,SRAIN,XRAIN,GRAIN,SMOL
      LOGICAL LFIRST
      LFIRST=IK.EQ.1 .AND. IBINPT.EQ.IBINMN
      IF(TX(2).GT.0.)THEN
          GAER(IK)=TX(1)/TX(2)
      ELSE
          GAER(IK)=0.
      ENDIF
      CLDSCT=TX(66)+TX(67)+TX(74)
      IF(CLDSCT.GT.0.)THEN
          GCLD(IK)=TX(76)/CLDSCT
      ELSE
          GCLD(IK)=0.
      ENDIF
      COLDEN=WPATH(0)/TZERO
!old  IF(IK.EQ.1)THEN
!old      WRITE(IDBOUT,'(6X,A,F9.2,A,/F10.7,3(2(1X,1,PE13.7),           &
!old &      1X,0P,F7.5),35(1X,1P,E13.7))')' (FREQ =',IVX,' CM-1)',      &
!old &      PRES(IK),TX(2),TX(13),GAER(IK),CLDSCT,TX(75),GCLD(IK),      &
!old &      TX(78),TX(77),TX(79),TX(15),(DEPLAY(INTRVL),INTRVL=1,JNTRVL)
!old  ELSE
!old      WRITE(IDBOUT,                                                 &
!old &      '(F10.7,3(2(1X,1P,E13.7),1X,0P,F7.5),35(1X,1P,E13.7))')     &
!old &      PRES(IK),TX(2),TX(13),GAER(IK),CLDSCT,TX(75),GCLD(IK),      &
!old &      TX(78),TX(77),TX(79),TX(15),(DEPLAY(INTRVL),INTRVL=1,JNTRVL)
!old  ENDIF
      IF(TX(13).GT.0.)THEN
          SAER(IK)=TX(2)/AER550(IK)
          XAER(IK)=TX(13)/AER550(IK)
      ELSE
          SAER(IK)=0.
          XAER(IK)=0.
      ENDIF
      IF(TX(75).LE.0.)THEN
          SCLD(IK)=0.
          XCLD(IK)=0.
      ELSEIF(ICLD.EQ.18)THEN
          SCLD(IK)=17.21*CLDSCT/WPATH(16)
          XCLD(IK)=17.21*TX(75)/WPATH(16)
      ELSEIF(ICLD.EQ.19)THEN
          SCLD(IK)=290.19*CLDSCT/WPATH(16)
          XCLD(IK)=290.19*TX(75)/WPATH(16)
      ELSE
          SCLD(IK)=CLDSCT/(WPATH(66)+WPATH(67))
          XCLD(IK)=TX(75)/(WPATH(66)+WPATH(67))
      ENDIF
      IF(WPATH(3).GT.0.)THEN
          SRAIN(IK)=TX(78)/WPATH(3)
          XRAIN(IK)=TX(77)/WPATH(3)
      ELSE
          SRAIN(IK)=0.
          XRAIN(IK)=0.
      ENDIF
      GRAIN(IK)=TX(79)
      SMOL(IK)=TX(15)/COLDEN
      DO INTRVL=1,JNTRVL
          XMOL(INTRVL)=DEPLAY(INTRVL)/COLDEN
      ENDDO
      CALL M3DWRT(SMOL,I3DGEN,I3DCNT,I3DDAT,IHAZE,ICLD,M1,IBINMN,IBINMX,&
     &  BNDWID,JNTRVL,SUBINT,IK,IKMAX,NATM,PRES,DENH2O,DENCO2,DENO3,    &
     &  SAER,XAER,GAER,SCLD,XCLD,GCLD,SRAIN,XRAIN,GRAIN,                &
     &  XMOL,LASCII,LFIRST,M3DGEN,M3DCNT,M3DDAT,M3DMOL)
      RETURN
      END
