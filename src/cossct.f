      DOUBLE PRECISION FUNCTION COSSCT(ANGLST,THTST,PSIST,IARB)

!     COSSCT RETURNS THE COSINE OF THE SCATTERING ANGLE.
      IMPLICIT NONE

!     INPUT ARGUMENTS:
!       ANGLST  SOLAR ZENITH ANGLE [DEG].
!       THTST   LINE-OF-SIGHT ZENITH ANGLE [DEG].
!       PSIST   RELATIVE AZIMUTH ANGLE [DEG].
!       IARB    VERTICAL/NADIR LINE-OF-SIGHT FLAG
!               (=1 FOR VERTICAL/NADIR, =0 OTHERWISE).
      DOUBLE PRECISION ANGLST,THTST,PSIST
      INTEGER IARB

!     COMMONS:

!     /DCNSTN/
!       DRIGHT   SMALLEST DOUBLE PRECISION REAL ADDED TO 1 EXCEEDS 1.
!       DPDEG    NUMBER OF DEGREES IN ONE RADIAN IN DOUBLE PRECISION
      DOUBLE PRECISION DRIGHT,DPDEG
      COMMON/DCNSTN/DRIGHT,DPDEG

!     LOCAL VARIABLES:
!       SUNZEN  SOLAR ZENITH ANGLE [RAD].
!       LOSZEN  LINE-OF-SIGHT ZENITH ANGLE [RAD].
      DOUBLE PRECISION SUNZEN,LOSZEN

!     CONVERT FROM DEGREES TO RADIANS:
      SUNZEN=ANGLST/DPDEG
      LOSZEN=THTST/DPDEG
      COSSCT=COS(SUNZEN)*COS(LOSZEN)

!     FOR VERTICAL/NADIR LINE-OF-SIGHT (IARB=1), SIN(LOSZEN)=0.
      IF(IARB.EQ.0)                                                     &
     &  COSSCT=COSSCT+SIN(SUNZEN)*SIN(LOSZEN)*COS(PSIST/DPDEG)

!     PROTECT AGAINST ROUNDOFF:
      IF(COSSCT.GT. 1.D0)THEN
          COSSCT= 1.D0
      ELSEIF(COSSCT.LT.-1.D0)THEN
          COSSCT=-1.D0
      ENDIF
      RETURN
      END
