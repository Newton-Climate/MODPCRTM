      SUBROUTINE SMGEO(H1,H2,HRANGE,REE,                                &
     &  ANGLE,BETA,PHI,DHALFR,DPRNG2,BEND,LENN,HMIN)
      IMPLICIT NONE

!     INPUT ARGUMENTS:
!       H1       OBSERVER (SENSOR) ALTITUDE [KM].
!       H2       FINAL (TARGET) ALTITUDE [KM].
!       HRANGE   DISTANCE FROM H1 TO H2 [KM].
!       REE      RADIUS OF THE EARTH [KM].
      DOUBLE PRECISION H1,H2,HRANGE,REE

!     OUTPUT ARGUMENTS:
!       ANGLE    ZENITH ANGLE FROM H1 TO H2 [DEG].
!       BETA     EARTH CENTER ANGLE BETWEEN H1 AND H2 [DEG].
      DOUBLE PRECISION ANGLE,BETA,PHI,DHALFR,DPRNG2,BEND,HMIN
      INTEGER LENN

!     COMMONS:

!     AORIG IS THE ORIGINAL INPUT ANGLE IN SINGLE PRECISION
      REAL AORIG
      COMMON/SMALL5/AORIG

!     /DCNSTN/
!       DRIGHT   SMALLEST DOUBLE PRECISION REAL ADDED TO 1 EXCEEDS 1.
!       DPDEG    NUMBER OF DEGREES IN ONE RADIAN IN DOUBLE PRECISION
      DOUBLE PRECISION DRIGHT,DPDEG
      COMMON/DCNSTN/DRIGHT,DPDEG

!     LOCAL VARIABLES:
      DOUBLE PRECISION X,Y,COSPHI,COSANG
      X=H2-H1
      Y=(HRANGE+X)*(HRANGE-X)
      IF(Y.LT.0.)Y=0.D0
      BETA=2*DPDEG*ASIN(SQRT(Y/((H1+REE)*(H2+REE)))/2)

!     IF H2 > H1, CALCULATE PHI (>90 DEG) FROM THE COSINE LAW
!     AND ANGLE FROM SUBTRACTION;  IF H1 > H2, CALCULATE ANGLE
!     (>90 DEG) FROM THE COSINE LAW AND PHI FROM SUBTRACTION.
      IF(X.GE.0.)THEN
          COSPHI=-(HRANGE**2+X*(H1+H2+2*REE))/(2*HRANGE*(H2+REE))
          PHI=180.D0
          IF(COSPHI.GT.-1.)PHI=DPDEG*ACOS(COSPHI)
          ANGLE=180.D0+BETA-PHI
      ELSE
          COSANG=-(HRANGE**2-X*(H1+H2+2*REE))/(2*HRANGE*(H1+REE))
          ANGLE=180.D0
          IF(COSANG.GT.-1.)ANGLE=DPDEG*ACOS(COSANG)
          PHI=180.D0+BETA-ANGLE
      ENDIF
      BEND=0.D0
      IF(ANGLE.GT.90.D0 .AND. PHI.GT.90.D0 .AND.                        &
     &  (AORIG.GT.90. .OR. AORIG.EQ.0.))THEN
!         THE CONDITION ON AORIG IS TO MAKE SURE THAT PATHS WHOSE
!         ORIGINAL INPUT ZENITH IS 90 DEGREES OR LESS ARE TREATED AS
!         LENN=0 PATHS.  CALCULATED ANGLE WILL LIKELY
!         DIFFER FROM AORIG.
          LENN=1
          IF(H1.LE.H2)THEN
              DHALFR=(H1+REE)*COS((180.D0-ANGLE)/DPDEG)
          ELSE
              DHALFR=(H2+REE)*COS((180.D0-PHI)/DPDEG)
          ENDIF
          DPRNG2=HRANGE-2*DHALFR
          IF(DPRNG2.LE.1.D-6)THEN
              DPRNG2=0.D0
              DHALFR=HRANGE/2
          ENDIF
          HMIN=(REE+H1)*SIN((180.D0-ANGLE)/DPDEG)-REE
      ELSE
          LENN=0
          DHALFR=0.D0
          DPRNG2=HRANGE
          HMIN=MIN(H1,H2)
      ENDIF
      RETURN
      END
