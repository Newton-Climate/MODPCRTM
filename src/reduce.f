      SUBROUTINE REDUCE(REARTH,ZMAX,H1ALT,H2ALT,OBSZEN,BCKZEN)

!     ZMAX IS THE HIGHEST LEVEL IN THE ATMOSPHERIC PROFILE STORED IN
!     COMMON /MODEL/.  IF H1ALT AND/OR H2ALT ARE GREATER THAN ZMAX, THIS
!     SUBROUTINE REDUCES THEM TO ZMAX AND RESETS OBSZEN AND/OR BCKZEN
!     AS NECESSARY.  THIS REDUCTION IS NECESSARY, FOR EXAMPLE, FOR
!     SATELLITE ALTITUDES, BECAUSE (1) THE DENSITY PROFILES ARE POORLY
!     DEFINED ABOVE ZMAX AND (2) THE CALCULATION TIME FOR PATHS ABOVE
!     ZMAX CAN BE EXCESSIVE (E.G. FOR GEOSYNCHRONOUS ALTITUDES).
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     ARGUMENTS:
!       REARTH   RADIUS OF THE EARTH [KM].
!       ZMAX     MAXIMUM ATMOSPHERIC PROFILE ALTITUDE [KM].
      DOUBLE PRECISION REARTH,ZMAX,H1ALT,H2ALT,OBSZEN,BCKZEN

!     COMMONS:
      INCLUDE 'IFIL.h'

!     /DCNSTN/
!       DRIGHT   SMALLEST DOUBLE PRECISION REAL ADDED TO 1 EXCEEDS 1.
!       DPDEG    NUMBER OF DEGREES IN ONE RADIAN IN DOUBLE PRECISION
      DOUBLE PRECISION DRIGHT,DPDEG
      COMMON/DCNSTN/DRIGHT,DPDEG

!     BLOCK DATA ROUTINES EXTERNAL:
      EXTERNAL DEVCBD

!     FUNCTIONS:
      DOUBLE PRECISION DPANDX

!     LOCAL VARIABLES:
      DOUBLE PRECISION CPATH,CZMAX,ANGMAX,RSCLHT,GAMMA0

!     DATA:
      CALL DPFISH(H1ALT,RSCLHT,GAMMA0)
      CPATH=DPANDX(REARTH+H1ALT,H1ALT,RSCLHT,GAMMA0)*SIN(OBSZEN/DPDEG)
      CALL DPFISH(ZMAX,RSCLHT,GAMMA0)
      CZMAX=DPANDX(REARTH+ZMAX,ZMAX,RSCLHT,GAMMA0)
      ANGMAX=180-DPDEG*ASIN(CPATH/CZMAX)
      IF(H1ALT.GT.ZMAX)THEN
          H1ALT=ZMAX
          OBSZEN=ANGMAX
      ENDIF
      IF(H2ALT.GT.ZMAX)THEN
          H2ALT=ZMAX
          BCKZEN=ANGMAX
      ENDIF
      IF(.NOT.LJMASS .AND. NPR.LE.1)WRITE(IPR,'(/A,F15.5,A,2(/22X,A),   &
     &  F15.5,A))')'Warning from REDUCE:  H1ALT and/or H2ALT were'//    &
     &  ' above the Top-Of-Atmosphere altitude, ZMAX =',ZMAX,' km.',    &
     &  '(H1ALT, OBSZEN) and/or (H2ALT, BCKZEN) have been reset'        &
     &  //' to (ZMAX, ANGMAX),','where ANGMAX =',ANGMAX,' deg.'
      RETURN
      END
