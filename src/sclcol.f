      SUBROUTINE SCLCOL(H2OSTR,O3STR,H2OCOL,O3COL)

!     THIS ROUTINE SCALES WATER AND OZONE DENSITY PROFILES TO CONFORM
!     TO INPUT COLUMN DENSITIES.  THREE RESTRICTIONS COMPLICATE THE
!     SCALING OF THE WATER DENSITIES:  (1) RELATIVE HUMIDITY WITHIN
!     CLOUDS IS FIXED [INPUT CHUMID ON CARD 2A], (2) RELATIVE
!     HUMIDITY OUTSIDE OF CLOUD IS NOT PERMITTED TO EXCEED 100%, AND
!     (3) NO DEFAULT WATER DENSITY IS PERMITTED TO INCREASE BY MORE
!     THAN A FACTOR OF 5 [DATA SCLMAX].  THE THIRD RESTRICTION IS
!     USED TO GUARD AGAINST UNREASONABLY LARGE WATER DENSITIES BEING
!     ASSIGNED TO UPPER ALTITUDES IN ORDER TO MATCH UNREALISTIC
!     INPUT COLUMNS.  USERS CAN INCREASE THE VALUE OF SCLMAX IF
!     THEY FIND THE CURRENT VALUE TO RESTRICTIVE.  RECOMMENDATIONS
!     FOR ALTERNATIVE WATER SCALING TECHNIQUES SHOULD BE FORWARDED
!     TO LEX@SPECTRAL.COM AND/OR GANDERSON@PLH.AF.MIL
      IMPLICIT NONE

!     INPUT ARGUMENTS:
!       H2OSTR   VERTICAL WATER COLUMN CHARACTER STRING (IF THE
!                FIRST NON-BLANK CHARACTER IS A "G", THE WATER
!                COLUMN IN GM/CM2 FOLLOWS "G"; IF THE FIRST
!                NON-BLANK CHARACTER IS AN "A", THE WATER COLUMN
!                IN ATM-CM AT 273.15K FOLLOWS "A"; OTHERWISE THE
!                STRING CONTAINS A WATER COLUMN SCALING FACTOR).
!       O3STR    VERTICAL OZONE COLUMN CHARACTER STRING (IF THE
!                FIRST NON-BLANK CHARACTER IS A "G", THE OZONE
!                COLUMN IN GM/CM2 FOLLOWS "G"; IF THE FIRST
!                NON-BLANK CHARACTER IS AN "A", THE OZONE COLUMN
!                IN ATM-CM AT 273.15K FOLLOWS "A"; OTHERWISE THE
!                STRING CONTAINS AN OZONE COLUMN SCALING FACTOR).
      CHARACTER H2OSTR*10,O3STR*10

!     OUTPUT ARGUMENTS:
!       H2OCOL   WATER COLUMN [KM GM /M3].
!       O3COL    OZONE COLUMN [KM GM /M3].
      REAL H2OCOL,O3COL

!     PARAMETERS:
!       SCLMAX   MAXIMUM H2O DENSITY SCALING VALUE.
!       LN2      NATURAL LOGARITHM OF 2.
!       H2OATM   CONVERSION FACTOR FROM ATM-CM WATER TO KM GM / M3

!                1E6 CM3    KM    18.015 GM        MOLE
!                -------  ------  ---------  ----------------
!                   M3    1E5 CM     MOLE    22413.83 CM3 ATM

!       O3ATM    CONVERSION FACTOR FROM ATM-CM OZONE TO KM GM / M3

!                1E6 CM3    KM    47.998 GM        MOLE
!                -------  ------  ---------  ----------------
!                   M3    1E5 CM     MOLE    22413.83 CM3 ATM

!       O3CONV   OZONE PPMV TO GM/M3 CONVERSION FACTOR AT STANDARD
!                PRESSURE AND 273.15K [(GM K) / (M3 MB PPMV)]:

!      1E-6 1E6 CM3         1 ATM    273.15K 47.998 GM       MOLE
!      ---- ------- P[MB] ---------- ------- --------- ----------------
!      PPMV    M3         1013.25 MB   T[K]     MOLE   22413.83 CM3 ATM

!       (22413.83 CM3 ATM / MOLE = IDEAL GAS STANDARD VOLUME AT 273.15K)

      REAL SCLMAX,LN2,H2OATM,O3ATM,O3CONV
      PARAMETER(SCLMAX=5.,LN2=.6931472,                                 &
     &  H2OATM=8.037448E-3,O3ATM=2.141446E-2,O3CONV=5.772868E-4)
      INCLUDE 'PARAMS.h'

!     COMMONS:
      INCLUDE 'BASE.h'
      INCLUDE 'IFIL.h'

!     /CNTRL/
!       NSEG     NUMBER OF PATH SEGMENTS ALONG LINE-OF-SIGHT.
!       ML       NUMBER OF ATMOSPHERIC PROFILE LEVELS.
!       MLFLX    NUMBER OF LEVELS FOR WHICH FLUX VALUES ARE WRITTEN.
!       IMULT    MULTIPLE SCATTERING FLAG
!                  (0=NONE, 1=AT SENSOR, -1=AT FINAL OR TANGENT POINT).
!       THERML   FLAG TO CALCULATE THERMAL SCATTER.
      INTEGER NSEG,ML,MLFLX,IMULT
      LOGICAL THERML
      COMMON/CNTRL/NSEG(0:MLOSP1),ML,MLFLX,IMULT,THERML

!     /M_PTWO/
!       PPROF    PRESSURE PROFILE [MB].
!       TPROF    TEMPERATURE PROFILE [K].
!       WH2O     H2O VOLUME MIXING RATIO PROFILE [PPMV].
!       WO3      O3 VOLUME MIXING RATIO PROFILE [PPMV].
      REAL PPROF,TPROF,WH2O,WO3
      COMMON/M_PTWO/PPROF(LAYDIM),TPROF(LAYDIM),WH2O(LAYDIM),WO3(LAYDIM)

!     /CARD1/
!       MODEL    MODEL ATMOSPHERE INDEX.
!       ITYPE    SLANT PATH TYPE.
!       IEMSCT   RADIATIVE TRANSFER MODE.
!                  0 FOR TRANSMITTANCE
!                  1 FOR THERMAL EMISSION ONLY
!                  2 FOR THERMAL EMISSION PLUS SOLAR SCATTER
!                  3 FOR TRANSMITTED SOLAR IRRADIANCE
!                  4 FOR SOLAR SCATTER ONLY
!       M1       MODEL ATMOSPHERE FOR PRESSURE & TEMPERATURE PROFILES.
!       M2       MODEL ATMOSPHERE FOR H2O PROFILE.
!       M3       MODEL ATMOSPHERE FOR O3 PROFILE.
!       I_RD2C   READ CARD 2C, 2C1, ... IF EQUAL 1; SKIP IF EQUAL TO 0.
!       NOPRNT   PRINT FLAG.
!       MODTRN   MODTRAN BAND MODEL FLAG.
      INTEGER MODEL,ITYPE,IEMSCT,M1,M2,M3,I_RD2C,NOPRNT
      LOGICAL MODTRN
      COMMON/CARD1/MODEL,ITYPE,IEMSCT,M1,M2,M3,I_RD2C,NOPRNT,MODTRN

!     /MPROF/
!       ZM       PROFILE LEVEL ALTITUDES [KM].
!       PM       PROFILE LEVEL PRESSURES [MBAR].
!       TM       PROFILE LEVEL TEMPERATURES [K].
!       RFNDX    PROFILE LEVEL REFRACTIVITIES.
!       LRHSET   FLAG, .TRUE. IF RELATIVE HUMIDITY IS NOT TO BE SCALED.
      DOUBLE PRECISION ZM
      REAL PM,TM,RFNDX
      LOGICAL LRHSET
      COMMON/MPROF/ZM(LAYDIM),PM(LAYDIM),TM(LAYDIM),                    &
     &  RFNDX(LAYDIM),LRHSET(LAYDIM)

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      EXTERNAL DEVCBD

!     LOCAL VARIABLES:
!       RHTEST   LARGEST RELATIVE HUMIDITY THAT CAN BE SCALED BY SCLMAX.
!       OSUM0    INITIAL VERTICAL OZONE COLUMN [KM GM / M3].
!       WSUM0    INITIAL VERTICAL WATER COLUMN [KM GM / M3].
!       WSUMMN   MINIMUM ALLOWED VERTICAL WATER COLUMN [KM GM / M3].
!       WSUMMX   MAXIMUM ALLOWED VERTICAL WATER COLUMN [KM GM / M3].
!       WSUM     FINAL VERTICAL WATER COLUMN [KM GM / M3].
!       WSUMLO   LOWER BRACKETING VALUE FOR WSUM [KM GM / M3].
!       WSUMHI   HIGHER BRACKETING VALUE FOR WSUM [KM GM / M3].
!       OSCALE   SCALING FACTOR FOR OZONE PROFILE.
!       WSCALE   SCALING FACTOR FOR WATER PROFILE.
!       SCALLO   LOWER BRACKETING VALUE FOR WSCALE.
!       SCALHI   HIGHER BRACKETING VALUE FOR WSCALE.
!       OBOT     OZONE DENSITY AT BOTTOM OF CURRENT LAYER [GM / M3].
!       OTOP     OZONE DENSITY AT TOP OF CURRENT LAYER [GM / M3].
!       WBOT     WATER DENSITY AT BOTTOM OF CURRENT LAYER [GM / M3].
!       WTOP     WATER DENSITY AT TOP OF CURRENT LAYER [GM / M3].
!       LAY      INDEX FOR LAYER OR LAYER BOTTOM.
!       LAYTOP   INDEX FOR LAYER TOP.
!       NITER    NUMBER OF ITERATIONS USED IN BISECTION METHOD.
!       ITER     ITERATION INDEX USED IN BISECTION METHOD.
      REAL RHTEST,OSUM0,WSUM0,WSUMMN,WSUMMX,WSUM,WSUMLO,WSUMHI,         &
     &  OSCALE,WSCALE,SCALLO,SCALHI,OBOT,OTOP,WBOT,WTOP
      INTEGER LAY,LAYTOP,NITER,ITER

!     LOCAL ARRAYS:
!       WATMN    MINIMUM WATER DENSITY AT EACH ALTITUDE [GM / M3].
!       WATMX    MAXIMUM WATER DENSITY AT EACH ALTITUDE [GM / M3].
!       DZ       LAYER VERTICAL WIDTHS [KM].
      REAL WATMN(LAYDIM),WATMX(LAYDIM),DZ(2:LAYDIM)

!     FUNCTIONS:
!       LENSTR   RETURNS STRING LENGTH AFTER TRIMMING LEADING BLANKS.
!       DENLAY   DEFINES VERTICAL PATH AVERAGE DENSITY WITHIN A LAYER.
      INTEGER LENSTR
      REAL DENLAY

!     DATA:
!       H2ODFT   MODEL PROFILE DEFAULT WATER COLUMN [KM GM / M3].
!       O3DFT    MODEL PROFILE DEFAULT OZONE COLUMN [KM GM / M3].
      REAL H2ODFT(0:6),O3DFT(0:6)
      DATA H2ODFT/0.,41.1477,29.2234,8.51728,20.8130,4.16131,14.1644/,  &
     &  O3DFT/.0,.00593759,.00710446,.00806918,                         &
     &           .00738627,.00804113,.00735715/
      SAVE H2ODFT,O3DFT

!     INITIALIZATIONS:
      RHTEST=100/SCLMAX
      OSUM0=0.
      WSUM0=0.
      WSUMMN=0.
      WSUMMX=0.

!     IF H2OSTR BEGINS WITH '+', DO NOT USE RELATIVE HUMIDITY LIMIT:
      IF(LENSTR(H2OSTR).GT.0)THEN
          IF(H2OSTR(1:1).EQ.'+')THEN
              RHTEST=100.1
              H2OSTR(1:1)=' '
          ENDIF
      ENDIF

!     BOTTOM LAYER BOUNDARY:  CONVERT O3 DENSITY FROM PPMV
!     TO GM / M3 DEFINE WATER DENSITY MINIMUM AND MAXIMUM.
      OBOT=O3CONV*WO3(1)*PPROF(1)/TPROF(1)
      IF(LRHSET(1))THEN
          WATMN(1)=WH2O(1)
          WATMX(1)=WH2O(1)
      ELSE
          WATMN(1)=0.
          IF(RELHUM(1).LT.RHTEST)THEN
              WATMX(1)=SCLMAX*WH2O(1)
          ELSE
              WATMX(1)=100*WH2O(1)/RELHUM(1)
          ENDIF
      ENDIF

!     PROFILE OR SINGLE ALTITUDE LEVEL:
      IF(ML.GT.1)THEN

!         LOOP OVER LAYERS TO DETERMINE CURRENT H2O AND O3 COLUMNS,
!         AND ALSO MINIMUM AND MAXIMUM ALLOWED H2O COLUMNS.
          LAY=1
          DO LAYTOP=2,ML
              DZ(LAYTOP)=SNGL(ZM(LAYTOP)-ZM(LAY))
              OTOP=O3CONV*WO3(LAYTOP)*PPROF(LAYTOP)/TPROF(LAYTOP)
              OSUM0=OSUM0+DZ(LAYTOP)*DENLAY(OBOT,OTOP)
              WSUM0=WSUM0+DZ(LAYTOP)*DENLAY(WH2O(LAY),WH2O(LAYTOP))
              IF(LRHSET(LAYTOP))THEN
                  WATMN(LAYTOP)=WH2O(LAYTOP)
                  WATMX(LAYTOP)=WH2O(LAYTOP)
              ELSE
                  WATMN(LAYTOP)=0.
                  IF(RELHUM(LAYTOP).LT.RHTEST)THEN
                      WATMX(LAYTOP)=SCLMAX*WH2O(LAYTOP)
                  ELSE
                      WATMX(LAYTOP)=100*WH2O(LAYTOP)/RELHUM(LAYTOP)
                  ENDIF
              ENDIF
              WSUMMN=WSUMMN+DZ(LAYTOP)*DENLAY(WATMN(LAY),WATMN(LAYTOP))
              WSUMMX=WSUMMX+DZ(LAYTOP)*DENLAY(WATMX(LAY),WATMX(LAYTOP))

!             STORE TOP VALUES FOR NEXT LAYER:
              LAY=LAYTOP
              OBOT=OTOP
          ENDDO
      ELSE

!         SINGLE ALTITUDE LEVEL:
          WSUM0=H2ODFT(ABS(M2))
          OSUM0=O3DFT(M3)
      ENDIF

!     PARSE O3STR TO DETERMINE DESIRED OZONE COLUMN IN KM GM / M3:
      OSCALE=0.
      IF(LENSTR(O3STR).LE.0)THEN

!         O3STR IS BLANK:  OZONE COLUMN IS NOT SCALED.
          O3COL=OSUM0
      ELSEIF(O3STR(1:1).EQ.'G' .OR. O3STR(1:1).EQ.'g')THEN

!         OZONE COLUMN INPUT IN GM / CM2:  CONVERT TO KM GM / M3.
          READ(O3STR(2:10),'(F9.0)')O3COL
          O3COL=10*O3COL
      ELSEIF(O3STR(1:1).EQ.'A' .OR. O3STR(1:1).EQ.'a')THEN

!         OZONE COLUMN INPUT IN ATM CM:  CONVERT TO KM GM / M3.
          READ(O3STR(2:10),'(F9.0)')O3COL
          O3COL=O3ATM*O3COL
      ELSE

!         SCALE INITIAL COLUMN AMOUNT BY O3COL:
          READ(O3STR(1:10),'(F10.0)')O3COL
          IF(O3COL.GT.0.)THEN
              OSCALE=O3COL
              O3COL=O3COL*OSUM0
          ELSE
              OSCALE=1.
              O3COL=OSUM0
          ENDIF
      ENDIF

!     DETERMINE DENSITY SCALING FOR OZONE:
      IF(O3COL.LT.0.)THEN
          OSCALE=1.
          O3COL=OSUM0
          IF(.NOT.LJMASS)WRITE(IPR,'(/2A)')' No ozone profile scaling', &
     &      ' because the CARD1A input O3COL is negative.'
      ELSEIF(OSCALE.GT.0.)THEN
          IF(.NOT.LJMASS)WRITE(IPR,'(/A,F10.5,/(A,F10.5,A))')           &
     &      ' *** OZONE DENSITIES ARE BEING SCALED BY',OSCALE,          &
     &      '     THE ORIGINAL VERTICAL COLUMN CONTAINED',              &
     &      1000*OSUM0,' GM / M2 OF OZONE',                             &
     &      '     THE CURRENT  VERTICAL COLUMM CONTAINS ',              &
     &      1000*O3COL,' GM / M2 OF OZONE'
      ELSEIF(OSUM0.LE.0.)THEN
          OSCALE=1.
          WRITE(IPR,'(/2A,/10X,A)')' WARNING: ',                        &
     &      ' Unable to scale ozone profile to input column amount',    &
     &      ' because no default ozone profile was defined.'
      ELSE
          OSCALE=O3COL/OSUM0
          IF(.NOT.LJMASS)WRITE(IPR,'(/A,F10.5,/(A,F10.5,A))')           &
     &      ' *** OZONE DENSITIES ARE BEING SCALED BY',OSCALE,          &
     &      '     THE ORIGINAL VERTICAL COLUMN CONTAINED',              &
     &      1000*OSUM0,' GM / M2 OF OZONE',                             &
     &      '     THE CURRENT  VERTICAL COLUMM CONTAINS ',              &
     &      1000*O3COL,' GM / M2 OF OZONE'
      ENDIF

!     PARSE H2OSTR TO DETERMINE DESIRED WATER COLUMN IN KM GM / M3:
      WSCALE=0.
      IF(LENSTR(H2OSTR).LE.0)THEN

!         H2OSTR IS BLANK:  WATER COLUMN IS NOT SCALED.
          H2OCOL=WSUM0
      ELSEIF(H2OSTR(1:1).EQ.'G' .OR. H2OSTR(1:1).EQ.'g')THEN

!         WATER COLUMN INPUT IN GM / CM2:  CONVERT TO KM GM / M3.
          READ(H2OSTR(2:10),'(F9.0)')H2OCOL
          H2OCOL=10*H2OCOL
      ELSEIF(H2OSTR(1:1).EQ.'A' .OR. H2OSTR(1:1).EQ.'a')THEN

!         WATER COLUMN INPUT IN ATM CM:  CONVERT TO KM GM / M3.
          READ(H2OSTR(2:10),'(F9.0)')H2OCOL
          H2OCOL=H2OATM*H2OCOL
      ELSE

!         SCALE INITIAL COLUMN AMOUNT BY H2OCOL:
          READ(H2OSTR(1:10),'(F10.0)')H2OCOL
          IF(H2OCOL.GT.0.)THEN
              WSCALE=H2OCOL
              H2OCOL=H2OCOL*WSUM0
          ELSE
              WSCALE=1.
              H2OCOL=WSUM0
          ENDIF
      ENDIF

!     BRANCH BASED ON INPUT WATER COLUMN:
      IF(H2OCOL.LT.0.)THEN

!         NO SCALING OF THE H2O COLUMN.  SCALE THE OZONE PROFILE.
          IF(.NOT.LJMASS)WRITE(IPR,'(/A)')' No water profile scaling'   &
     &      //' because the CARD1A input H2OCOL is not positive.'
          DO LAY=1,ML
              WO3(LAY)=OSCALE*WO3(LAY)
          ENDDO
      ELSEIF(ML.EQ.1 .AND. WSCALE.GT.0. .AND. WH2O(1).GT.0.)THEN
          WO3(1)=OSCALE*WO3(1)
          IF(WSCALE*WH2O(1).LT.WATMN(1))THEN
              WSCALE=WATMN(1)/WH2O(1)
          ELSEIF(WSCALE*WH2O(1).GT.WATMX(1))THEN
              WSCALE=WATMX(1)/WH2O(1)
          ENDIF
          IF(.NOT.LJMASS)WRITE(IPR,'(/A,F10.5,A,/2(F12.6,A))')          &
     &      ' The water density is being scaled by',WSCALE,', from',    &
     &      WH2O(1),' G/M3 to',WSCALE*WH2O(1),' G/M3.'
          WH2O(1)=WSCALE*WH2O(1)
      ELSEIF(H2OCOL.LE.WSUMMN)THEN

!         INPUT WATER COLUMN IS BELOW MINIMUM:
          IF(H2OCOL.LT.WSUMMN)                                          &
     &      WRITE(IPR,'(/A,F10.5,A,/10X,A,F10.5,A)')                    &
     &      ' WARNING:  INPUT WATER COLUMN,',.1*H2OCOL,                 &
     &      ' GM / CM2, IS BELOW MINIMUM ALLOWED.',                     &
     &      ' THE WATER COLUMN IS BEING SET TO THE MINIMUM,',           &
     &      .1*WSUMMN,' GM / CM2.'

!         LOOP OVER LAYERS SCALING OZONE PROFILE
!         AND SETTING WATER PROFILE TO MINIMUM:
          DO LAY=1,ML
              WO3(LAY)=OSCALE*WO3(LAY)
              WH2O(LAY)=WATMN(LAY)
          ENDDO
      ELSEIF(H2OCOL.LE.WSUM0)THEN

!         DEFAULT WATER COLUMN MUST BE DECREASED.  USE 10 BISECTIONS
!         TO REDUCE SCALING FACTOR TO LESS THAN 0.001.
          SCALLO=0.
          SCALHI=1.
          WSUMLO=WSUMMN
          WSUMHI=WSUM0
          NITER=10
          DO ITER=1,NITER
              WSCALE=(SCALLO+SCALHI)/2
              WSUM=0.
              WBOT=WATMN(1)+WSCALE*(WH2O(1)-WATMN(1))
              DO LAYTOP=2,ML
                  WTOP=WATMN(LAYTOP)+WSCALE*(WH2O(LAYTOP)-WATMN(LAYTOP))
                  WSUM=WSUM+DZ(LAYTOP)*DENLAY(WBOT,WTOP)
                  WBOT=WTOP
              ENDDO
              IF(WSUM.LT.H2OCOL)THEN
                  SCALLO=WSCALE
                  WSUMLO=WSUM
              ELSE
                  SCALHI=WSCALE
                  WSUMHI=WSUM
              ENDIF
          ENDDO

!         INTERPOLATE TO DETERMINE FINAL WATER SCALING AND SCALE OZONE.
          WSCALE=SCALLO+(SCALHI-SCALLO)*(H2OCOL-WSUMLO)/(WSUMHI-WSUMLO)
          WO3(1)=OSCALE*WO3(1)
          WH2O(1)=WATMN(1)+WSCALE*(WH2O(1)-WATMN(1))
          WSUM=0.
          LAY=1
          DO LAYTOP=2,ML
              WO3(LAYTOP)=OSCALE*WO3(LAYTOP)
              WH2O(LAYTOP)=WATMN(LAYTOP)                                &
     &          +WSCALE*(WH2O(LAYTOP)-WATMN(LAYTOP))
              WSUM=WSUM+DZ(LAYTOP)*DENLAY(WH2O(LAY),WH2O(LAYTOP))
              LAY=LAYTOP
          ENDDO
          IF(.NOT.LJMASS)WRITE(IPR,'(/2A,/(A,F10.5,A))')                &
     &      ' *** THE WATER PROFILE WAS DECREASED',                     &
     &      ' TO FIT THE INPUT WATER COLUMN VALUE',                     &
     &      '     INITIAL:',.1*WSUM0,' GM / CM2',                       &
     &      '     INPUT:  ',.1*H2OCOL,' GM / CM2',                      &
     &      '     FINAL:  ',.1*WSUM,' GM / CM2'
      ELSEIF(H2OCOL.LT.WSUMMX)THEN

!         DEFAULT WATER COLUMN MUST BE INCREASED.  USE THE BISECTION
!         METHOD TO REDUCE SCALING FACTOR TO LESS THAN 0.001.
          SCALLO=1.
          SCALHI=SCLMAX
          WSUMLO=WSUM0
          WSUMHI=WSUMMX
          NITER=INT(10.5+LOG(SCLMAX-1)/LN2)
          DO ITER=1,NITER
              WSCALE=(SCALLO+SCALHI)/2
              WSUM=0.
              WBOT=WSCALE*WH2O(1)
              IF(WBOT.GT.WATMX(1))WBOT=WATMX(1)
              DO LAYTOP=2,ML
                  WTOP=WSCALE*WH2O(LAYTOP)
                  IF(WTOP.GT.WATMX(LAYTOP))WTOP=WATMX(LAYTOP)
                  WSUM=WSUM+DZ(LAYTOP)*DENLAY(WBOT,WTOP)
                  WBOT=WTOP
              ENDDO
              IF(WSUM.LT.H2OCOL)THEN
                  SCALLO=WSCALE
                  WSUMLO=WSUM
              ELSE
                  SCALHI=WSCALE
                  WSUMHI=WSUM
              ENDIF
          ENDDO

!         INTERPOLATE TO DETERMINE FINAL WATER SCALING AND SCALE OZONE.
          WSCALE=SCALLO+(SCALHI-SCALLO)*(H2OCOL-WSUMLO)/(WSUMHI-WSUMLO)
          WO3(1)=OSCALE*WO3(1)
          WH2O(1)=WSCALE*WH2O(1)
          IF(WH2O(1).GT.WATMX(1))WH2O(1)=WATMX(1)
          WSUM=0.
          LAY=1
          DO LAYTOP=2,ML
              WO3(LAYTOP)=OSCALE*WO3(LAYTOP)
              WH2O(LAYTOP)=WSCALE*WH2O(LAYTOP)
              IF(WH2O(LAYTOP).GT.WATMX(LAYTOP))                         &
     &          WH2O(LAYTOP)=WATMX(LAYTOP)
              WSUM=WSUM+DZ(LAYTOP)*DENLAY(WH2O(LAY),WH2O(LAYTOP))
              LAY=LAYTOP
          ENDDO
          IF(.NOT.LJMASS)WRITE(IPR,'(/2A,/(A,F10.5,A))')                &
     &      ' *** THE WATER PROFILE WAS INCREASED',                     &
     &      ' TO FIT THE INPUT WATER COLUMN VALUE',                     &
     &      '     INITIAL:',.1*WSUM0,' GM / CM2',                       &
     &      '     INPUT:  ',.1*H2OCOL,' GM / CM2',                      &
     &      '     FINAL:  ',.1*WSUM,' GM / CM2'
      ELSE

!         INPUT WATER COLUMN IS ABOVE MAXIMUM:
          IF(H2OCOL.GT.WSUMMX)                                          &
     &      WRITE(IPR,'(/A,F10.5,A,/10X,A,F10.5,A)')                    &
     &      ' WARNING:  INPUT WATER COLUMN,',.1*H2OCOL,                 &
     &      ' GM / CM2, IS ABOVE MAXIMUM ALLOWED.',                     &
     &      ' THE WATER COLUMN IS BEING SET TO THE MAXIMUM,',           &
     &      .1*WSUMMX,' GM / CM2.'

!         LOOP OVER LAYERS SCALING OZONE PROFILE
!         AND SETTING WATER PROFILE TO MAXIMUM:
          DO LAY=1,ML
              WO3(LAY)=OSCALE*WO3(LAY)
              WH2O(LAY)=WATMX(LAY)
          ENDDO
      ENDIF
      RETURN
      END
