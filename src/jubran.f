      SUBROUTINE JUBRAN(P,T)

!     UNIT CONVERSION BRANCHING BASED ON JUNIT VALUES.

!     JCHAR    JUNIT
!     -----    -----
!     " ",A      10    VOLUME MIXING RATIO [PPMV]
!       B        11    NUMBER DENSITY [MOLECULES/CM3]
!       C        12    MASS MIXING RATIO [GM(MOLECULE)/KG(AIR)]
!       D        13    MASS DENSITY [GM/M3]
!       E        14    PARTIAL PRESSURE [MB]
!       F        15    DEW POINT TEMPERATURE [K] - H2O ONLY
!       G        16    DEW POINT TEMPERATURE [C] - H2O ONLY
!       H        17    RELATIVE HUMIDITY [PERCENT] - H2O ONLY

!     ARGUMENTS:
!       P        PRESSURE [MB].
!       T        TEMPERATURE [K].
      REAL P,T

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     /CONSTN/
!       AMWT     MOLECULAR WEIGHTS [GM/MOLE].
!                  1  H2O   2  CO2   3  O3    4  N2O   5  CO    6  CH4
!                  7  O2    8  NO    9  SO2   10 NO2   11 NH3   12 HNO3
      REAL AMWT
      COMMON/CONSTN/AMWT(12)

!     COMMONS:
      INCLUDE 'IFIL.h'
      INTEGER JUNITP,JUNITT,JUNIT,JLOW
      REAL WMOL
      COMMON/CARD1B/JUNITP,JUNITT,JUNIT(13),WMOL(12),JLOW

      INCLUDE 'YPROP.h'
      REAL AMWTX
      COMMON/ATMWTX/AMWTX(NMOLX)

!     LOCAL VARIABLES:
!       KSPC     MOLECULAR SPECIES INDEX.
!       VOLAIR   VOLUME OF AIR [CM3 ATM / MOLE].
      INTEGER KSPC
      REAL VOLAIR

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      SAVE /CONSTN/,/ATMWTX/
      EXTERNAL DEVCBD,ATMCON,XMLATM

!     /CD1BXY/
!       JUNITX   INPUT UNIT FLAG FOR CROSS-SECTION MOLECULES.
!       WMOLX    X-SECTION MOLECULAR DENSITY [PPMV AFTER JUBRAN CALL].
!       JUNITY   INPUT UNIT FLAG FOR AUXILIARY (Y) MOLECULES.
!       WMOLY    AUXILIARY MOLECULAR DENSITY [PPMV AFTER JUBRAN CALL].
      INTEGER JUNITX,JUNITY
      REAL WMOLX,WMOLY
      COMMON/CD1BXY/JUNITX,WMOLX(NMOLX),JUNITY,WMOLY(MMOLY)

!     VARIABLES ENDING WITH "X" OR "Y" ARE INTRODUCED TO DEAL WITH THE
!     NMOLX EXTRA X-MOLECULES, AND NMOLY EXTRA Y-MOLECULES.

!     DENSITY OF AIR:
      VOLAIR=STDVOL*(PZERO/P)*(T/TZERO)

!     BAND MODEL SPECIES LOOP:
      CALL WATVAP(P,T)
      DO KSPC=2,NMOL
          IF(JUNIT(KSPC).EQ.11)THEN

!             CONVERT NUMBER DENSITY (MOLECULES/CM3) TO PPMV:
              WMOL(KSPC)=1.E6*WMOL(KSPC)*VOLAIR/AVOGAD
          ELSEIF(JUNIT(KSPC).EQ.12)THEN

!             CONVERT MASS MIXING RATIO (GM MOLECULE/KG AIR) TO PPMV:
              WMOL(KSPC)=1000*WMOL(KSPC)*REAL(AIRMWT)/AMWT(KSPC)
          ELSEIF(JUNIT(KSPC).EQ.13)THEN

!             CONVERT MASS DENSITY (GM/M3) TO PPMV:
              WMOL(KSPC)=WMOL(KSPC)*VOLAIR/AMWT(KSPC)
          ELSEIF(JUNIT(KSPC).EQ.14)THEN

!             CONVERT PARTIAL PRESSURE (MB) TO PPMV:
              WMOL(KSPC)=1.E6*WMOL(KSPC)/P
          ELSEIF(JUNIT(KSPC).GE.15)THEN
              WRITE(IPR,*)' Error in JUBRAN:  JUNIT(KSPC) =',JUNIT(KSPC)
          ENDIF
      ENDDO

!     CROSS-SECTION SPECIES:
      IF(JUNITX.EQ.11)THEN

!         CONVERT NUMBER DENSITY (MOLECULES/CM3) TO PPMV:
          DO KSPC=1,NMOLX
              WMOLX(KSPC)=1.E6*WMOLX(KSPC)*VOLAIR/AVOGAD
          ENDDO
      ELSEIF(JUNITX.EQ.12)THEN

!         CONVERT MASS MIXING RATIO (GM MOLECULE/KG AIR) TO PPMV:
          DO KSPC=1,NMOLX
              WMOLX(KSPC)=1000*WMOLX(KSPC)*REAL(AIRMWT)/AMWTX(KSPC)
          ENDDO
      ELSEIF(JUNITX.EQ.13)THEN

!         CONVERT MASS DENSITY (GM/M3) TO PPMV:
          DO KSPC=1,NMOLX
              WMOLX(KSPC)=WMOLX(KSPC)*VOLAIR/AMWTX(KSPC)
          ENDDO
      ELSEIF(JUNITX.EQ.14)THEN

!         CONVERT PARTIAL PRESSURE (MB) TO PPMV:
          DO KSPC=1,NMOLX
              WMOLX(KSPC)=1.E6*WMOLX(KSPC)/P
          ENDDO
      ELSEIF(JUNITX.GE.15)THEN
          WRITE(IPR,*)' Error in JUBRAN:  JUNITX =',JUNITX
      ENDIF

!     Y SPECIES:
      IF(JUNITY.EQ.11 .OR. JUNITY.EQ.2)THEN

!         CONVERT NUMBER DENSITY (MOLECULES/CM3) TO PPMV:
          DO KSPC=1,NMOLY
              WMOLY(KSPC)=1.E6*WMOLY(KSPC)*VOLAIR/AVOGAD
          ENDDO
      ELSEIF(JUNITY.EQ.12 .OR. JUNITY.EQ.3)THEN

!         CONVERT MASS MIXING RATIO (GM MOLECULE/KG AIR) TO PPMV:
          DO KSPC=1,NMOLY
              WMOLY(KSPC)=1000*WMOLY(KSPC)*REAL(AIRMWT)/AMWTY(KSPC)
          ENDDO
      ELSEIF(JUNITY.EQ.13 .OR. JUNITY.EQ.4)THEN

!         CONVERT MASS DENSITY (GM/M3) TO PPMV:
          DO KSPC=1,NMOLY
              WMOLY(KSPC)=WMOLY(KSPC)*VOLAIR/AMWTY(KSPC)
          ENDDO
      ELSEIF(JUNITY.EQ.14 .OR. JUNITY.EQ.5)THEN

!         CONVERT PARTIAL PRESSURE (MB) TO PPMV:
          DO KSPC=1,NMOLY
              WMOLY(KSPC)=1.E6*WMOLY(KSPC)/P
          ENDDO
      ELSEIF(JUNITY.GE.15)THEN
          WRITE(IPR,*)' Error in JUBRAN:  JUNITY =',JUNITY
      ENDIF
      RETURN
      END
