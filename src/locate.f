      SUBROUTINE LOCATE(LATI,LONI,AZM,ECA,LATF,LONF)

!     LOCATE CALCULATES THE FINAL "F" LATITUDE AND LONGITUDE FOR A PATH.
      IMPLICIT NONE

!     INPUT ARGUMENTS:
!       LATI     INITIAL LATITUDE [DEG, -90 TO 90].
!       LONI     INITIAL LONGITUDE [DEG WEST, 0 TO 360].
!       AZM      PATH AZIMUTH FROM INITIAL TO FINAL
!                ALTITUDE [DEG EAST OF NORTH, 0 TO 360].
!       ECA      PATH EARTH CENTERED ANGLE [DEG, 0 TO 180].
      DOUBLE PRECISION LATI,LONI,AZM,ECA

!     OUTPUT ARGUMENTS:
!       LATF     FINAL LATITUDE [DEG NORTH, -90 TO 90].
!       LONF     FINAL LONGITUDE [DEG WEST, 0 TO 360].
      DOUBLE PRECISION LATF,LONF

!     CAUTION:  LONI AND AZM ARE NOT WELL DEFINED AT THE NORTH
!               (LATI=90) AND SOUTH (LATI=-90) POLES.  THIS ROUTINE
!               RETURNS THE VALUES OF LATITUDE AND LONGITUDE OBTAINED
!               IN THE LIMIT AS LATI APPROACHES + OR - 90.

!     COMMONS:

!     /DCNSTN/
!       DRIGHT   SMALLEST DOUBLE PRECISION REAL ADDED TO 1 EXCEEDS 1.
!       DPDEG    NUMBER OF DEGREES IN ONE RADIAN IN DOUBLE PRECISION
      DOUBLE PRECISION DRIGHT,DPDEG
      COMMON/DCNSTN/DRIGHT,DPDEG

!     LOCAL VARIABLES:
!       ECA_R    PATH EARTH CENTERED ANGLE [RADIANS, 0 TO PI].
!       AZM_R    PATH AZIMUTH FROM INITIAL TO FINAL
!                ALTITUDE [RADIANS EAST OF NORTH, 0 TO 2 PI].
!       LONI_R   INITIAL LONGITUDE [RADIANS WEST, 0 TO 2 PI].
!       SNLATI   SINE OF THE INITIAL LATITUDE.
!       CSLATI   COSINE OF THE INITIAL LATITUDE.
!       SNLONI   SINE OF THE INITIAL LONGITUDE.
!       CSLONI   COSINE OF THE INITIAL LONGITUDE.
!       SN_ECA   SINE OF PATH EARTH CENTERED ANGLE.
!       CS_ECA   COSINE OF PATH EARTH CENTERED ANGLE.
!       COEF     PRODUCT OF SN_ECA AND COSINE OF AZM.
!       SNLATF   SINE OF THE FINAL LATITUDE.
!       XP       X-PRIME, TERM USED IN FINAL LONGITUDE SOLUTION.
!       YP       Y-PRIME, TERM USED IN FINAL LONGITUDE SOLUTION.
!       X        DENOMINATOR IN TANGENT OF FINAL LONGITUDE.
!       Y        NUMERATOR IN TANGENT OF FINAL LONGITUDE.
      DOUBLE PRECISION ECA_R,AZM_R,LONI_R,SN_ECA,CS_ECA,SNLONI,CSLONI,  &
     &  SNLATI,CSLATI,COEF,SNLATF,XP,YP,X,Y

!     ANGLES IN RADIANS:
      ECA_R=ECA/DPDEG
      AZM_R=AZM/DPDEG
      LONI_R=LONI/DPDEG

!     SINE AND COSINES:
      SN_ECA=SIN(ECA_R)
      CS_ECA=COS(ECA_R)
      SNLONI=SIN(LONI_R)
      CSLONI=COS(LONI_R)
      SNLATI=SIN(LATI/DPDEG)
      CSLATI=SQRT(1-SNLATI**2)

!     DETERMINE FINAL LATITUDE:
      COEF=SN_ECA*COS(AZM_R)
      SNLATF=SNLATI*CS_ECA+CSLATI*COEF
      IF(SNLATF.GE.1.D0)THEN
          LATF=90.D0
      ELSEIF(SNLATF.GT.-1.D0)THEN
          LATF=DPDEG*ASIN(SNLATF)
      ELSE
          LATF=-90.D0
      ENDIF

!     DETERMINE FINAL LONGITUDE:
      XP=CSLATI*CS_ECA-SNLATI*COEF
      YP=SN_ECA*SIN(AZM_R)
      X=CSLONI*XP+SNLONI*YP
      Y=SNLONI*XP-CSLONI*YP
      IF(X.LT.0.D0)THEN
          LONF=DPDEG*ATAN(Y/X)+180
      ELSEIF(X.GT.0.D0)THEN
          LONF=DPDEG*ATAN(Y/X)
          IF(LONF.LT.0.)LONF=LONF+360
      ELSEIF(Y.GE.0.D0)THEN
          LONF=90.D0
      ELSE
          LONF=270.D0
      ENDIF
      RETURN
      END

      LOGICAL FUNCTION LOCAT2(LATF,LONF,AZM,ECA,LATI,LONI,NSOLN)

!     LOCAT2 DETERMINES THE INITIAL "I" LATITUDE AND LONGITUDE GIVEN
!     A PATH FINAL "F" LATITUDE AND LONGITUDE, A PATH EARTH CENTERED
!     ANGLE AND AN INITIAL PATH TRUE AZIMUTH ANGLE.  NO ANGLES ARE
!     RETURNED IF NO SOLUTION EXISTS OR THERE ARE TWO VALID SOLUTIONS.
      IMPLICIT NONE

!     INPUT ARGUMENTS:
!       LATF     FINAL LATITUDE [DEG NORTH, -90 TO 90].
!       LONF     FINAL LONGITUDE [DEG WEST, 0 TO 360].
!       AZM      PATH AZIMUTH FROM INITIAL TO FINAL
!                ALTITUDE [DEG EAST OF NORTH, 0 TO 360].
!       ECA      PATH EARTH CENTERED ANGLE [DEG, 0 TO 180].
      DOUBLE PRECISION LATF,LONF,AZM,ECA

!     OUTPUT ARGUMENTS:
!       LATI     INITIAL LATITUDE [DEG, -90 TO 90].
!       LONI     INITIAL LONGITUDE [DEG WEST, 0 TO 360].
!       NSOLN    NUMBER OF SOLUTIONS.
      DOUBLE PRECISION LATI,LONI
      INTEGER NSOLN

!     THE SOLUTIONS FOR THE INITIAL LATITUDE HAVE THE FORM:
!                                                                    1/2
!                                       /          2      2        2\
!          snLATF csECA +/- snECA csAZM | 1 - snECA  snAZM - snLATF |
!                                       \                           /
! snLATI = -------------------------------------------------------------
!                                        2      2
!                               1 - snECA  snAZM

!     AND MUST ALSO SATIFY THE EQUATION:

!              snLATF = snLATI csECA + csLATI snECA csAZM

!     THE LONGITUDE IS THEN DETERMINED FROM:

!            tnLONF (csLATI csECA - snLATI snECA csAZM) + snECA snAZM
!   tnLONI = --------------------------------------------------------
!            (csLATI csECA - snLATI snECA csAZM) - tnLONF snECA snAZM

!     COMMONS:

!     /DCNSTN/
!       DRIGHT   SMALLEST DOUBLE PRECISION REAL ADDED TO 1 EXCEEDS 1.
!       DPDEG    NUMBER OF DEGREES IN ONE RADIAN IN DOUBLE PRECISION
      DOUBLE PRECISION DRIGHT,DPDEG
      COMMON/DCNSTN/DRIGHT,DPDEG

!     LOCAL VARIABLES:
!       ECA_R    PATH EARTH CENTERED ANGLE [RADIANS, 0 TO PI].
!       AZM_R    PATH AZIMUTH FROM INITIAL TO FINAL
!                ALTITUDE [RADIANS EAST OF NORTH, 0 TO 2 PI].
!       LONF_R   FINAL LONGITUDE [RADIANS WEST, 0 TO 2 PI].
!       SN_ECA   SINE OF PATH EARTH CENTERED ANGLE.
!       COEF     COEFFICIENT OF THE DISCRIMINANT IN QUADRATIC SOLUTION.
!       DENOM    DENOMINATOR IN QUADRATIC SOLUTION.
!       SNLATF   SINE OF THE FINAL LATITUDE.
!       DISCRM   DISCRIMINANT IN QUADRADTIC SOLUTION.
!       CS_ECA   COSINE OF PATH EARTH CENTERED ANGLE.
!       HALFB    NEGATIVE OF B OVER 2 IN QUADRATIC SOLUTION.
!       SNLATI   SINE OF THE INITIAL LATITUDE.
!       CSLATI   COSINE OF THE INITIAL LATITUDE.
!       S_LATI   SECOND SOLUTION FOR SINE OF THE INITIAL LATITUDE.
!       C_LATI   SECOND SOLUTION FOR COSINE OF THE INITIAL LATITUDE.
!       XP       X-PRIME, TERM USED IN INITIAL LONGITUDE SOLUTION.
!       YP       Y-PRIME, TERM USED IN INITIAL LONGITUDE SOLUTION.
!       SNLONF   SINE OF THE FINAL LONGITUDE.
!       CSLONF   COSINE OF THE FINAL LONGITUDE.
!       X        DENOMINATOR IN TANGENT OF INITIAL LONGITUDE.
!       Y        NUMERATOR IN TANGENT OF INITIAL LONGITUDE.
      DOUBLE PRECISION ECA_R,AZM_R,LONF_R,SN_ECA,COEF,DENOM,SNLATF,     &
     &  DISCRM,CS_ECA,HALFB,SNLATI,XP,YP,SNLONF,CSLONF,Y,X,CSLATI,      &
     &  S_LATI,C_LATI

!     CONVERT TO RADIANS:
      ECA_R=ECA/DPDEG
      AZM_R=AZM/DPDEG
      LONF_R=LONF/DPDEG

!     TEST FOR A POSITIVE DISCRIMINANT FOR THE QUADRATIC EQUATION:
      SN_ECA=SIN(ECA_R)
      COEF=SN_ECA*COS(AZM_R)
      DENOM=1-SN_ECA**2+COEF**2
      SNLATF=SIN(LATF/DPDEG)
      DISCRM=DENOM-SNLATF**2
      IF(DISCRM.LT.0.D0)THEN

!         DISCRIMINANT IS NEGATIVE:
          NSOLN=0
          LOCAT2=.FALSE.
          RETURN
      ENDIF
      DISCRM=COEF*SQRT(DISCRM)
      CS_ECA=COS(ECA_R)
      HALFB=SNLATF*CS_ECA

!     FIND SOLUTIONS:
      IF(ABS(HALFB+DISCRM).GT.DENOM)THEN
          SNLATI=(HALFB-DISCRM)/DENOM
          IF(ABS(SNLATI).GT.1.)THEN

!             NO SOLUTIONS:
              NSOLN=0
              LOCAT2=.FALSE.
              RETURN
          ELSE
              CSLATI=SQRT(1-SNLATI**2)
              IF(ABS(CS_ECA*SNLATI+COEF*CSLATI-SNLATF).GT..0001)THEN

!                 NO SOLUTIONS:
                  NSOLN=0
                  LOCAT2=.FALSE.
                  RETURN
              ENDIF
          ENDIF
      ELSE
          SNLATI=(HALFB+DISCRM)/DENOM
          CSLATI=SQRT(1-SNLATI**2)
          IF(ABS(CS_ECA*SNLATI+COEF*CSLATI-SNLATF).GT..0001)THEN

!             FIRST SOLUTION DOES NOT WORK:  CHECK SECOND SOLUTION.
              IF(ABS(HALFB-DISCRM).GT.DENOM)THEN

!                 NO SOLUTIONS:
                  NSOLN=0
                  LOCAT2=.FALSE.
                  RETURN
              ELSE
                  SNLATI=(HALFB-DISCRM)/DENOM
                  CSLATI=SQRT(1-SNLATI**2)
                  IF(ABS(CS_ECA*SNLATI+COEF*CSLATI-SNLATF).GT..0001)THEN

!                     NO SOLUTIONS:
                      NSOLN=0
                      LOCAT2=.FALSE.
                      RETURN
                  ENDIF
              ENDIF
          ELSE

!             FIRST SOLUTION WORKS:  CHECK SECOND SOLUTION.
              IF(ABS(HALFB-DISCRM).LE.DENOM)THEN
                  S_LATI=(HALFB-DISCRM)/DENOM
                  C_LATI=SQRT(1-S_LATI**2)
                  IF(ABS(CS_ECA*S_LATI+COEF*C_LATI-SNLATF).LE..0001)THEN

!                     TWO SOLUTIONS:
                      NSOLN=2
                      LOCAT2=.FALSE.
                      RETURN
                  ENDIF
              ENDIF
          ENDIF
      ENDIF

!     ONE SOLUTION:
      NSOLN=1
      LATI=DPDEG*ASIN(SNLATI)
      LOCAT2=.TRUE.

!     DETERMINE LONGITUDE:
      XP=CSLATI*CS_ECA-SNLATI*COEF
      YP=SN_ECA*SIN(AZM_R)
      SNLONF=SIN(LONF_R)
      CSLONF=COS(LONF_R)
      X=CSLONF*XP-SNLONF*YP
      Y=SNLONF*XP+CSLONF*YP

!     BRANCH TO AVOID ZERO DIVIDE:
      IF(X.GT.0.D0)THEN
          LONI=DPDEG*ATAN(Y/X)
          IF(LONI.LT.0.D0)LONI=LONI+360
      ELSEIF(X.LT.0.D0)THEN
          LONI=DPDEG*ATAN(Y/X)+180
      ELSEIF(Y.GE.0.D0)THEN
          LONI=90.D0
      ELSE
          LONI=270.D0
      ENDIF
      RETURN
      END
