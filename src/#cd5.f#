      SUBROUTINE CD5(FLRT,LNFLRT,H2OCOL,O3COL,TPTMPS)
      
      

!     WRITE HEADER FOR SPECTRAL OUTPUT FILES AND READ TAPE5:
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     INPUT ARGUMENTS:
!       FLRT     ROOT NAME FOR ALL I/O FILES.
!       LNFLRT   LENGTH OF ROOT NAME FOR ALL I/O FILES.
!       H2OCOL   WATER COLUMN [KM GM /M3].
!       O3COL    OZONE COLUMN [KM GM /M3].
!       TPTMPS   INPUT TARGET-PIXEL SURFACE TEMPERATURES [K].
      CHARACTER FLRT*(NAMLEN-4)
      INTEGER LNFLRT
      LOGICAL LOPEN
      REAL H2OCOL,O3COL,TPTMPS
      COMMON /PRONUM/LABEL
      CHARACTER LABEL*6

!     COMMONS:
      INCLUDE 'IFIL.h'

!     /JM1/
!       CODE     CODE INPUT FLAG: 'F' OR 'L' FOR LOWTRAN BAND MODEL.
!                                 'T' OR 'M' FOR MODTRAN BAND MODEL.
!                                 'C' OR 'K' FOR CORRELATED-K MODEL.
!       SPEED    COMPUTATIONAL SPEED FLAG USED WITH CK MODEL ['F' OR '2'
!                FOR FAST (12 K'S), 'M' OR '1' FOR MODERATE (17 K'S),
!                AND 'S' OR '0' FOR SLOW (33 K'S); 'S' IS DEFAULT]
!       SURREF   SURFACE REFLECTANCE CHARACTER STRING.
!                IF FIRST NON-BLANK CHARACTER IS "B",
!                  BI-DIRECTIONAL REFLECTANCE DISTRIBUTION
!                  FUNCTION (BRDF) DATA IS READ IN.
!                IF FIRST NON-BLANK CHARACTER IS "L" OR "-"
!                  SURFACE IS MODELED AS A LAMBERTIAN REFLECTOR AND
!                  SPECTRAL ALBEDO IS READ FROM FILE "DATA/spec_alb.dat"
!                OTHERWISE, THE CHARACTER STRING IS ASSUMED TO CONTAIN
!                  A SPECTRALLY INDEPENDENT VALUE FOR SURFACE ALBEDO.
      CHARACTER CODE*1,SPEED*1,SURREF*7
      COMMON/JM1/CODE,SPEED,SURREF

!     /CARD1/
!       MODEL    MODEL ATMOSPHERE INDEX.
!       ITYPE    SLANT PATH TYPE.
!       IEMSCT   RADIATIVE TRANSFER MODE.
!                  0 FOR TRANSMITTANCE
!                  1 FOR THERMAL EMISSION ONLY
!                  2 FOR THERMAL EMISSION PLUS SOLAR SCATTER
!                  3 FOR TRANSMITTED SOLAR IRRADIANCE
!                  4 FOR SOLAR SCATTER ONLY
!       M1       MODEL ATMOSPHERE FOR PRESSURE & TEMPERATURE PROFILES.
!       M2       MODEL ATMOSPHERE FOR H2O PROFILE.
!       M3       MODEL ATMOSPHERE FOR O3 PROFILE.
!       I_RD2C   READ CARD 2C, 2C1, ... IF EQUAL 1; SKIP IF EQUAL TO 0.
!       NOPRNT   PRINT FLAG.
!       MODTRN   MODTRAN BAND MODEL FLAG.
      INTEGER MODEL,ITYPE,IEMSCT,M1,M2,M3,I_RD2C,NOPRNT
      LOGICAL MODTRN
      COMMON/CARD1/MODEL,ITYPE,IEMSCT,M1,M2,M3,I_RD2C,NOPRNT,MODTRN

!     /CNTRL/
!       NSEG     NUMBER OF PATH SEGMENTS ALONG LINE-OF-SIGHT.
!       ML       NUMBER OF ATMOSPHERIC PROFILE LEVELS.
!       MLFLX    NUMBER OF LEVELS FOR WHICH FLUX VALUES ARE WRITTEN.
!       IMULT    MULTIPLE SCATTERING FLAG
!                  (0=NONE, 1=AT SENSOR, -1=AT FINAL OR TANGENT POINT).
!       THERML   FLAG TO CALCULATE THERMAL SCATTER.
      INTEGER NSEG,ML,MLFLX,IMULT
      LOGICAL THERML
      COMMON/CNTRL/NSEG(0:MLOSP1),ML,MLFLX,IMULT,THERML

!     /CARD1A/
      INTEGER M4,M5,M6,MDEF,IRD1,IRD2
      COMMON/CARD1A/M4,M5,M6,MDEF,IRD1,IRD2

!     /CARD2/
!       IHAZE    BOUNDARY LAYER AEROSOL MODEL NUMBER.
!       ISEASN   SEASON NUMBER (1=SPRING-SUMMER, 2=FALL-WINTER).
!       IVULCN   VOLCANIC AEROSOL MODEL NUMBER.
!       ICSTL    COASTAL AIRMASS MODEL NUMBER.
!       ICLD     CLOUD MODEL NUMBER.
!       IVSA     VERTICAL STRUCTURE ALGORITHM (0=OFF, 1=ON).
!       VIS      SURFACE VISIBILITY (GROUND METEOROLOGICAL RANGE) [KM].
!       WSS      CURRENT WIND SPEED (M/S).
!       WHH      24-HOUR WIND SPEED (M/S).
!       RAINRT   RAIN RATE (MM/HR)
!       LSAP     LOGICAL FLAG FOR SPECTRAL AEROSOL PROFILES INPUT.
      INTEGER IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA
      REAL VIS,WSS,WHH,RAINRT
      LOGICAL LSAP
      COMMON/CARD2/IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA,                 &
     &  VIS,WSS,WHH,RAINRT,LSAP

!     /GRND/
!       GNDALT   GROUND ALTITUDE, ABOVE SEA LEVEL [KM].
      DOUBLE PRECISION GNDALT
      COMMON/GRND/GNDALT

!     /CARD2A/
      INTEGER NCRALT,NCRSPC
      DOUBLE PRECISION CTHIK,CALT
      REAL CEXT,CWAVLN,CCOLWD,CCOLIP,CHUMID,ASYMWD,ASYMIP
      COMMON/CARD2A/CTHIK,CALT,CEXT,NCRALT,NCRSPC,CWAVLN,CCOLWD,        &
     &  CCOLIP,CHUMID,ASYMWD,ASYMIP

!     /JM1A1/
!       RESCHR   BAND MODEL RESOLUTION CHARACTER STRING.
!       DISSTR   CHARACTER STRING USED TO READ IN DISORT LOGICALS.
!       H2OSTR   VERTICAL WATER COLUMN CHARACTER STRING (IF THE
!                FIRST NON-BLANK CHARACTER IS A "G", THE WATER
!                COLUMN IN GM/CM2 FOLLOWS "G"; IF THE FIRST
!                NON-BLANK CHARACTER IS AN "A", THE WATER COLUMN
!                IN ATM-CM AT 273.15K FOLLOWS "A"; OTHERWISE THE
!                STRING CONTAINS A WATER COLUMN SCALING FACTOR).
!       O3STR    VERTICAL OZONE COLUMN CHARACTER STRING (IF THE
!                FIRST NON-BLANK CHARACTER IS A "G", THE OZONE
!                COLUMN IN GM/CM2 FOLLOWS "G"; IF THE FIRST
!                NON-BLANK CHARACTER IS AN "A", THE OZONE COLUMN
!                IN ATM-CM AT 273.15K FOLLOWS "A"; OTHERWISE THE
!                STRING CONTAINS AN OZONE COLUMN SCALING FACTOR).
!       USRSUN   USER-SPECIFIED TOP-OF-ATMOSPHERE SOLAR IRRADIANCE FILE.
!       BMROOT   PREFIX OF MOLECULAR BAND MODEL PARAMETERS FILE.
!       FILTNM   NAME OF FILTER RESPONSE FUNCTION FILE.
!       H2OAER   FLAG, TRUE IF DEFAULT AEROSOL PROPERTIES ARE REVISED
!                BASED ON WATER COLUMN SCALING.
!       DATDIR   NAME OF THE MODTRAN DATA DIRECTORY.
      CHARACTER RESCHR*2,DISSTR*3,H2OSTR*10,O3STR*10,USRSUN*(NAMLEN),   &
     &  FILTNM*(NAMLEN),BMROOT*(NAMLEN),H2OAER*1,DATDIR*(NAMLEN-LENSUN)
      COMMON/JM1A1/RESCHR,DISSTR,H2OSTR,O3STR,USRSUN,                   &
     &  FILTNM,BMROOT,H2OAER,DATDIR

!     /CARD2B/
      REAL ZCVSA,ZTVSA,ZINVSA
      COMMON/CARD2B/ZCVSA,ZTVSA,ZINVSA

!     /TITL/
      CHARACTER HHAZE(16)*20,HSEASN(2)*20,HVULCN(8)*20,                 &
     &  HMET(2)*20,HMODEL(0:8)*20
      COMMON/TITL/HHAZE,HSEASN,HVULCN,HMET,HMODEL

!     /MDLZ/
      REAL HMDLZ
      COMMON/MDLZ/HMDLZ(8)

!     /CARD3/
!       H1ALT    OBSERVER (SENSOR) ALTITUDE [KM].
!       H2ALT    FINAL (TANGENT FOR LIMB PATH) ALTITUDE [KM].
!       OBSZEN   OBSERVER ZENITH ANGLE (H1ALT TO H2ALT) [DEG].
!       HRANGE   DISTANCE FROM H1ALT TO H2ALT [KM].
!       BETA     EARTH CENTER ANGLE BETWEEN H1ALT AND H2ALT [DEG].
!       REARTH   RADIUS OF THE EARTH [KM].
!       HMIN     PATH MINIMUM ALTITUDE [KM].
!       HMAX     PATH MAXIMUM ALTITUDE [KM].
!       CKRANG   MAXIMUM PATH RANGE FOR K-DISTRIBUTION OUTPUT
!                (=0. FOR TOTAL PATH ONLY; <0. FOR ALL RANGES).
!       BCKZEN   ZENITH ANGLE FOR BACKWARD (H2ALT TO H1ALT) PATH [DEG].
!       REARTH   EARTH RADIUS [KM].
!       ANGLEM   LUNAR PHASE ANGLE [0 TO 180 DEG].
!       LENN     PATH LENGTH SWITCH (0=SHORT, 1=LONG).
!       IDAY     DAY OF YEAR [0-366, DEFAULT (0) IS DAY 91].
!       ISOURC   SOURCE FLAG [0=SUN AND 1=MOON].
!       NLOS     NUMBER OF LINE-OF-SIGHT PATHS.
      INTEGER LENN,IDAY,ISOURC,NLOS
      DOUBLE PRECISION H1ALT,H2ALT,OBSZEN,HRANGE,BETA,REARTH,           &
     &  HMIN,HMAX,CKRANG,BCKZEN,ANGLEM
      COMMON/CARD3/H1ALT(MLOS),H2ALT(MLOS),OBSZEN(MLOS),HRANGE(MLOS),   &
     &  BETA(MLOS),HMIN(MLOS),HMAX(MLOS),CKRANG(MLOS),BCKZEN(MLOS),     &
     &  REARTH,ANGLEM,LENN(MLOS),IDAY,ISOURC,NLOS

!     /JM3A1/
      INTEGER IPARM
      COMMON/JM3A1/IPARM

!     /CARD3A/
!       G        SCATTERING FUNCTION ASYMMETRY FACTOR.
      INTEGER IPH
      REAL G
      COMMON/CARD3A/IPH,G

!     /JM3A2/
!       IPRMSV   SAVED VALUE OF IPARM INPUT.
!       PARM1    SENSOR LATITUDE IF IPARM=0 OR =1 [DEG N OF EQUATOR].
!                TARGET LATITUDE IF IPARM=10 OR =11 [DEG N OF EQUATOR].
!                SOLAR REL AZIMUTH AT SENSOR IF IPARM=2 [DEG E OF N].
!                SOLAR REL AZIMUTH AT TARGET IF IPARM=12 [DEG E OF N].
!       PARM2    SENSOR LONGITUDE IF IPARM=0 OR =1 [DEG W OF GRNWCH].
!                TARGET LONGITUDE IF IPARM=10 OR =11 [DEG W OF GRNWCH].
!                SOLAR ZENITH AT SENSOR IF IPARM IS 2 [DEG].
!                SOLAR ZENITH AT TARGET IF IPARM IS 12 [DEG].
!       SRCLAT   SOURCE (SUN OR MOON) LATITUDE [DEG NORTH OF EQUATOR].
!       SRCLON   SOURCE (SUN OR MOON) LONGITUDE [DEG WEST OF GREENWICH].
!       GMTIME   GREENWICH MEAN TIME [DECIMAL HOURS].
!       TRUEAZ   PATH TRUE AZIMUTH ANGLE [DEG EAST OF NORTH].
      INTEGER IPRMSV
      DOUBLE PRECISION PARM1,PARM2,SRCLAT,SRCLON,GMTIME,TRUEAZ
      COMMON/JM3A2/PARM1,PARM2,SRCLAT,SRCLON,GMTIME,TRUEAZ,IPRMSV

!     /CARD4/
!       IV1      LOWEST SPECTRAL FREQUENCY OUTPUT [CM-1].
!       IV2      HIGHEST SPECTRAL FREQUENCY OUTPUT [CM-1].
!       IDV      PRINTOUT SPECTRAL FREQUENCY STEP SIZE [CM-1].
!       IFWHM    TRIANGULAR SLIT FULL-WIDTH-HALF-MAXIMUM [CM-1].
!       VBAND    CURRENT COMPUTATION BAND FREQUENCY [CM-1].
!                (EQUALS BAND CENTER FOR 1, 5 & 15 CM-1 BAND MODELS;
!                EQUALS THE MINIMUM BAND VALUE FOR 0.1 CM-1 BAND MODEL)
!       IBINPT   BIN NUMBER OF CURRENT SPECTRAL POINT.
!                (CENTER FREQUENCY = IBINPT * BNDWID + OSHIFT).
!       IBINLO   BIN NUMBER OF (PADDED) SPECTRAL RANGE LOWER BOUND.
!       IBINHI   BIN NUMBER OF (PADDED) SPECTRAL RANGE UPPER BOUND.
!       IBINMN   BIN NUMBER OF MINIMUM COMPUTATION SPECTRAL POINT.
!       IBINMX   BIN NUMBER OF MAXIMUM COMPUTATION SPECTRAL POINT.
!       IBINDL   BIN NUMBER INCREMENT FOR SPECTRAL PRINTOUT.
!       IBINRS   BIN NUMBER INCREMENT EQUAL TO SPECTRAL RESOLUTION.
!       IBINOS   BIN NUMBER OFFSET BETWEEN CURRENT & OUTPUT SPC POINTS.
!       IBINWR   BIN NUMBER OF NEXT SPECTRAL DATA WRITE.
!       MBINPT   BIN NUMBER MAXIMUM FOR CURRENT BAND MODEL RESOLUTION.
!       IDBIN5   SPECTRAL BIN NUMBER STEP SIZE FOR 5 CM-1 GRID.
!       ISTEP5   INCREMENT FOR RETRIEVING 5 CM-1 RESOLUTION DATA [CM-1].
!       NSPCDT   NUMBER OF OUTPUT SPECTRAL DATA POINTS.
      DOUBLE PRECISION IDV
      REAL IV1,IV2,IFWHM,VBAND
      INTEGER IBINPT,IBINLO,IBINHI,IBINMN,IBINMX,IBINDL,                &
     &  IBINRS,IBINOS,IBINWR,MBINPT,IDBIN5,ISTEP5,NSPCDT
      COMMON/CARD4/IDV,IV1,IV2,IFWHM,VBAND,IBINPT,IBINLO,IBINHI,IBINMN, &
     &  IBINMX,IBINDL,IBINRS,IBINOS,IBINWR,MBINPT,IDBIN5,ISTEP5,NSPCDT

!     /CFLAGS/
!       YFLAG    Y COORDINATE FLAG FOR plot.dat FILE
!                  = "T" FOR TRANSMITTANCE
!                  = "R" FOR RADIANCE (IRRADIANCE FOR IEMSCT=3)
!                  = "N" FOR NO plot.dat OUTPUT
!       XFLAG    X COORDINATE FLAG FOR plot.dat FILE
!                  = "W" FOR FREQUENCY IN WAVENUMBERS (CM-1) AND
!                        RADIANCE IN W SR-1 CM-2 / CM-1
!                  = "M" FOR WAVELENGTH IN MICRONS AND
!                        RADIANCE IN W SR-1 CM-2 / MICRON
!                  = "N" FOR WAVELENGTH IN NANOMETERS AND
!                        RADIANCE IN MICRO-WATTS SR-1 CM-2 / NANOMETER
!       DLIMIT   DELIMITER CHARACTER STRING BETWEEN MODTRAN RUNS
!       FLAGS    SCANNING FUNCTION FLAGS.
      CHARACTER YFLAG*1,XFLAG*1,DLIMIT*8,FLAGS*7
      COMMON/CFLAGS/YFLAG,XFLAG,DLIMIT,FLAGS

!     /JM5/
!       IRPT     REPEAT INPUT FLAG (0=NONE, 1=ALL, 3=GEOM, 4=SPEC).
!       IFAC     CURRENT COLUMN SCALING FACTOR INDEX.
!       NFACMN   NUMBER OF COLUMN SCALING FACTOR LESS THAN 1.
!       NFACMX   NUMBER OF COLUMN SCALING FACTOR GREATER THAN 1.
!       FACMC    CURRENT COLUMN SCALING FACTOR.
!       SCALMN   MINIMUM COLUMN SCALING FACTOR.
!       SCALMX   MAXIMUM COLUMN SCALING FACTOR.
!       LBMRES   LOGICAL FLAG, .TRUE. FOR BAND MODEL AND .FALSE.
!                FOR 1 NM SPECTRAL RESOLUTION OUTPUT.
      INTEGER IRPT,IFAC,NFACMN,NFACMX
      REAL FACMC
      DOUBLE PRECISION SCALMN,SCALMX
      LOGICAL LBMRES
      COMMON/JM5/SCALMN,SCALMX,IRPT,IFAC,NFACMN,NFACMX,FACMC,LBMRES

!     /CJM5/
!       AMOD3D   FLAG INDICATING OUTPUT DATABASE FILE TYPE:
      CHARACTER AMOD3D*1
      COMMON/CJM5/AMOD3D

!     /SPCCTL/
!       DODRGD   .TRUE. FOR NON-DEFAULT SPECTRAL ANALYSIS.
      LOGICAL DODGRD
      COMMON/SPCCTL/DODGRD

!     /TP8CTL/
!       LTAPE8   .TRUE. IF TAPE8 IS BEING WRITTEN.
      LOGICAL LTAPE8
      COMMON/TP8CTL/LTAPE8

!     SAVED COMMONS:
      SAVE /TITL/

!     LOCAL VARIABLES:
!       ILOS     LOOP INDEX FOR OBSERVER ZENITH ANGLES.
!       NFUN     NUMBER OF FILE UNIT NUMBERS.
!       KFUN     ARRAY OF FILE UNIT NUMBERS.
!       IFUN     LOOP INDEX FOR FILE UNIT NUMBERS.
      INTEGER I,ILOS,NFUN,IFUN,KFUN(3)
      CHARACTER CBNOUT*1


      IF(LJMASS)THEN

!         CARD 5
          CALL INITCD('CARD5')
          LTAPE8=.FALSE.
          RETURN
      ENDIF

      IF(MODEL.EQ.0)HMDLZ(8)=SNGL(HRANGE(1))

!     READ CARD 5
      CALL CARD5(LNFLRT,FLRT,LABEL)

      KFUN(1)=IPU
      NFUN=1
      IF(DODGRD)THEN

!         OPEN IPUSCN IF NOT ALREADY OPENED.
          NFUN=NFUN+1
          KFUN(NFUN)=IPUSCN
          INQUIRE(IPUSCN,OPENED=LOPEN)
          IF(.NOT.LOPEN)THEN
              IF(LNFLRT.LE.0)THEN
                  CALL OPNFL(IPUSCN,0,'tape7.scn','UNKNOWN','FORMATTED',&
     &              'CD5')
              ELSE
                  CALL OPNFL(IPUSCN,0,FLRT(1:LNFLRT)//LABEL//'.7sc',    &
 &               'UNKNOWN', 'FORMATTED','CD5')
              ENDIF
          ENDIF

!         OPEN SCRATCH FILE IF NOT ALREADY OPENED.
          INQUIRE(JPUSCR,OPENED=LOPEN)
          IF(.NOT.LOPEN)THEN
              IF(LNFLRT.LE.0)THEN
                  CALL OPNFL(JPUSCR,0,'tape7b.scr','UNKNOWN',           &
     &              'UNFORMATTED','CD5')
              ELSE
                  CALL OPNFL(JPUSCR,0,FLRT(1:LNFLRT)//LABEL//'b.7sr',   &
     &               'UNKNOWN','UNFORMATTED','CD5')
              ENDIF
          ENDIF
      ENDIF

!     OPEN TAPE8 IF NEEDED.
      LTAPE8=(NOPRNT.LT.0 .AND. IEMSCT.NE.0 .AND. IEMSCT.NE.3) .OR.     &
     &  AMOD3D.EQ.'C'
      IF(BINOUT)THEN
          CBNOUT='T'
          IF(LTAPE8)THEN
              INQUIRE(JPR1,OPENED=LOPEN)
              IF(.NOT.LOPEN)THEN

!                 OPEN tape8b OR *b.tp8 SINCE NOT ALREADY OPENED.
                  IF(LNFLRT.GT.0)THEN
                      CALL OPNFL(JPR1,0,FLRT(1:LNFLRT)//'b.tp8',        &
     &                  'UNKNOWN','UNFORMATTED','CD5')
                  ELSE
                      CALL OPNFL(JPR1,0,'tape8b','UNKNOWN',             &
     &                  'UNFORMATTED','CD5')
                  ENDIF
              ENDIF
          ENDIF
      ELSE
          CBNOUT='F'

!         NO OUTPUT TO TAPE8 IF TRANSMITTANCE OR MINIMAL OUTPUT:
          IF(LTAPE8)THEN
              NFUN=NFUN+1
              KFUN(NFUN)=IPR1
              INQUIRE(IPR1,OPENED=LOPEN)
              IF(.NOT.LOPEN)THEN

!                 OPEN tape8 or ..tp8 SINCE NOT ALREADY OPENED.
                  IF(LNFLRT.GT.0)THEN
                      CALL OPNFL(IPR1,0,FLRT(1:LNFLRT)//'.tp8',         &
     &                  'UNKNOWN','FORMATTED','CD5')
                  ELSE
                      CALL OPNFL(IPR1,0,'tape8','UNKNOWN','FORMATTED',  &
     &                  'CD5')
                  ENDIF
              ENDIF
          ENDIF
      ENDIF

!     LOOP OVER OUTPUT FILES:
      DO IFUN=1,NFUN

!         WRITE HEADER DATA:
          WRITE(KFUN(IFUN),'(3A1,I2,12I5,F8.3,A7)')                     &
     &      CODE,SPEED,CBNOUT,MODEL,ITYPE,IEMSCT,IMULT,                 &
     &      M1,M2,M3,M4,M5,M6,MDEF,I_RD2C,NOPRNT,TPTMPS,SURREF
          WRITE(KFUN(IFUN),'(6I5,5F10.5)')IHAZE,ISEASN,IVULCN,          &
     &      ICSTL,ICLD,IVSA,VIS,WSS,WHH,RAINRT,GNDALT
          IF(ICLD.LT.1 .OR. ICLD.GT.10)THEN
              WRITE(KFUN(IFUN),'(3F8.3)')CTHIK,CALT,CEXT
          ELSE
              WRITE(KFUN(IFUN),'(3F8.3,2I4,6F8.3)')CTHIK,CALT,CEXT,     &
     &          NCRALT,NCRSPC,CWAVLN,CCOLWD,CCOLIP,CHUMID,ASYMWD,ASYMIP
          ENDIF
          IF(H2OSTR.EQ.'          ' .AND. O3STR.EQ.'          ')THEN
              WRITE(KFUN(IFUN),'(3F10.5)')ZCVSA,ZTVSA,ZINVSA
          ELSE
              WRITE(KFUN(IFUN),'(3F10.5,2F10.6,A)')ZCVSA,ZTVSA,ZINVSA,  &
     &          H2OCOL/10,O3COL/10,' ! H2O & O3 COLUMNS [GM/CM2]'
          ENDIF
          WRITE(KFUN(IFUN),'(I5,A)')ML,HMODEL(MODEL)
          IF(MODEL.EQ.0)THEN
              HMDLZ(8)=SNGL(HRANGE(1))
              WRITE(KFUN(IFUN),'(3F10.5,1P,5E10.3)')(HMDLZ(I),I=1,8)
          ELSE
              WRITE(KFUN(IFUN),'((5F10.5,F12.5,I3,5X,2F10.5))')         &
     &          (H1ALT(ILOS),H2ALT(ILOS),OBSZEN(ILOS),                  &
     &          HRANGE(ILOS),BETA(ILOS),REARTH,LENN(ILOS),              &
     &          BCKZEN(ILOS),CKRANG(ILOS),ILOS=1,NLOS)
          ENDIF
          WRITE(KFUN(IFUN),'(4I5)')IPARM,IPH,IDAY,ISOURC
          WRITE(KFUN(IFUN),'(8F10.5)')                                  &
     &      PARM1,PARM2,SRCLAT,SRCLON,GMTIME,TRUEAZ,ANGLEM,G
          WRITE(KFUN(IFUN),'(4F10.1,2A1,A8,A6)')                        &
     &      IV1,IV2,IDV,IFWHM,YFLAG,XFLAG,DLIMIT,FLAGS
          WRITE(KFUN(IFUN),'(I5,A1,3(I3,F9.3))')ABS(IRPT),              &
     &      AMOD3D,NFACMN,SCALMN,NFACMX,SCALMX,IFAC,FACMC

!     END LOOP OVER OUTPUT FILES:
      ENDDO

!     RESTORE ORIGINAL USER SPECIFIED PROFILES HMODEL VALUES:
      IF(MODEL.EQ.7)THEN
          HMODEL(7)='USER PROFILES       '
      ELSEIF(MODEL.EQ.8)THEN
          HMODEL(8)='HYDROSTATIC EQUATION'
      ENDIF

!     RETURN TO DRIVER:
      RETURN
      END
