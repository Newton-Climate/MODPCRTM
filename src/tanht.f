      DOUBLE PRECISION FUNCTION TANHT(CPATH,REARTH)

!     THIS ROUTINE FINDS THE TANGENT HEIGHT GIVEN
!     CPATH (THE PATH CONSTANT). SEE ATMOSPHERIC
!     TRANSMITTANCE/RADIANCE:  LOWTRAN 6, AFGL-TR-83-018
      IMPLICIT NONE

!     PARAMETERS
      INCLUDE 'PARAMS.h'

!     ARGUMENTS:
!       REARTH   RADIUS OF THE EARTH [KM].
      DOUBLE PRECISION CPATH,REARTH

!     COMMONS:

!     /MPROF/
!       ZM       PROFILE LEVEL ALTITUDES [KM].
!       PM       PROFILE LEVEL PRESSURES [MBAR].
!       TM       PROFILE LEVEL TEMPERATURES [K].
!       RFNDX    PROFILE LEVEL REFRACTIVITIES.
!       LRHSET   FLAG, .TRUE. IF RELATIVE HUMIDITY IS NOT TO BE SCALED.
      DOUBLE PRECISION ZM
      REAL PM,TM,RFNDX
      LOGICAL LRHSET
      COMMON/MPROF/ZM(LAYDIM),PM(LAYDIM),TM(LAYDIM),                    &
     &  RFNDX(LAYDIM),LRHSET(LAYDIM)

!     /CNTRL/
!       NSEG     NUMBER OF PATH SEGMENTS ALONG LINE-OF-SIGHT.
!       ML       NUMBER OF ATMOSPHERIC PROFILE LEVELS.
!       MLFLX    NUMBER OF LEVELS FOR WHICH FLUX VALUES ARE WRITTEN.
!       IMULT    MULTIPLE SCATTERING FLAG
!                  (0=NONE, 1=AT SENSOR, -1=AT FINAL OR TANGENT POINT).
!       THERML   FLAG TO CALCULATE THERMAL SCATTER.
      INTEGER NSEG,ML,MLFLX,IMULT
      LOGICAL THERML
      COMMON/CNTRL/NSEG(0:MLOSP1),ML,MLFLX,IMULT,THERML

!     /GRND/
!       GNDALT   GROUND ALTITUDE, ABOVE SEA LEVEL [KM].
      DOUBLE PRECISION GNDALT
      COMMON/GRND/GNDALT

!     LOCAL VARIABLES:
      INTEGER J
      DOUBLE PRECISION RX,RXRAT,DZMBOT,DZMTOP,CP

!     FUNCTIONS:
      DOUBLE PRECISION RTBIS

!     CALCULATE TOP OF ATMOSPHERE PATH CONSTANT
      DZMTOP=ZM(ML)
      CALL IRFXN(DZMTOP,RX,RXRAT)
      CP=RX*(REARTH+DZMTOP)
      IF(CPATH.GT.CP)THEN

!         PATH ABOVE TOP OF ATMOSPHERE.
          TANHT=CPATH/RX-REARTH
      ELSE

!         PATH BELOW TOP OF ATMOSPHERE.
          DO 10 J=ML-1,1,-1
              DZMBOT=ZM(J)
              IF(DZMBOT.LT.GNDALT)THEN

!                 LAST LAYER
                  CALL IRFXN(GNDALT,RX,RXRAT)
                  CP=RX*(REARTH+GNDALT)
                  IF(CPATH.GE.CP)THEN
                      TANHT=RTBIS(REARTH,GNDALT,DZMTOP,CPATH)
                  ELSE

!                     TANGENT HEIGHT BELOW GROUND ALTITUDE.
                      TANHT=GNDALT
                  ENDIF
                  RETURN
              ENDIF
              CALL IRFXN(DZMBOT,RX,RXRAT)
              CP=RX*(REARTH+DZMBOT)
              IF(CPATH.GT.CP)THEN
                 TANHT=RTBIS(REARTH,DZMBOT,DZMTOP,CPATH)
                 RETURN
              ELSE
                 TANHT=DZMBOT
              ENDIF
              DZMTOP=DZMBOT
   10     CONTINUE

!         TANGENT HEIGHT BELOW GROUND ALTITUDE.
          TANHT=GNDALT
      ENDIF
      RETURN
      END
