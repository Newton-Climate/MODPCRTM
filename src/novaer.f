      SUBROUTINE NOVAER(IHAZE,VIS,ALTNOV,RHNOV,PNOV,TNOV,DENNOV)

!     THIS ROUTINE READS THE OUTPUT OF NOVAM AND
!     PUTS THE OUTPUT IN APPROPRIATE ARRAYS.

!     THERE IS A CHANGE FROM USUAL MODTRAN NOMENCLATURE AS FOLLOWS:
!     NOVAM HAS 4 AEROSOLS.  NORMALLY, THERE WOULD BE 4 PROFILES, EACH
!     WITH UP TO 'LAYDIM' (DEFINED IN PARAMS.h) LEVELS.  NOVAM HAS NO
!     MORE THAN N=11 LEVELS (10 LAYERS).  FOR A LOWER ESTIMATE OF THE
!     NUMBER OF VARIABLES, LET N = 11.  WE WOULD HAVE 4 ARRAYS OF
!     DIMENSION N OF AEROSOL CONCENTRATIONS.  EACH AEROSOL WILL HAVE
!     EXTINCTION COEFFICIENTS AT THE "NWVLEN" WAVELENGTHS, THE NUMBER
!     OF WAVELENGTH VALUES .LE. MXWVLN (SEE PARAMS.h).  FOR THE BUILTIN
!     AEROSOLS, THIS VALUE USED TO BE 47, OLD NWAVLN IN PARAMS.h.  FOR
!     NOVAM, WE MAY MAKE NWLNOV SAME AS MXWVLN.  AT THE MOMENT, IT IS
!     STILL 47.

!     THE UNITLESS OD (OPTICAL DEPTH) = CONC X EXT X PATH LENGTH.
!     UNITLESS BECAUSE (#/L**3)(L**2)(L) IS UNITLESS.
!     THE NUMBER OF VARIABLES FOR PROFILE IS 4*N.  THE NUMBER FOR
!     EXTINCTION USED IS 4*47.  THE TOTAL IS 4*(11+47)=232 IF N=11 AND
!     NWVLEN=47.  NOW THAT NWVLEN IS 788, SO THIS NUMBER CAN BE 3196.

!     NOVAM COMPUTES CONCENTRATION*EXTINCTION PRODUCTS IN 1/KM, ADDED UP
!     FOR ALL FOUR AEROSOLS.  THERE ARE NWVLEN VALUES OF THIS AT EACH
!     WAVELENGTH FOR EACH LEVEL.  TOTAL NUMBER IS NWVLEN*N=517 IF N=11,
!     NWVLEN=47.  TOTAL NUMBER IS NWVLEN*N=8668 IF N=11, NWVLEN=788.

!     NOTE THAT THE NUMBER OF AEROSOL LEVELS IS REALLY LOT LESS THAN
!     LAYDIM.  NOVAM DOES NOT GO ABOVE 2 KM (BOUNDARY LAYER).  WE WILL
!     ASSUME THAT NOVAM HAS AT MOST 11 LEVELS OR 10 LAYERS.

!     WE WANT NOVAM TO PARALLEL OTHER AEROSOLS IN MODTRAN.  MODTRAN
!     METHOD:  PRODUCT OF CONCENTRATION AND EXTINCTION COEFFICIENT IS
!     IN 1/KM.  SO WE WILL DO THIS:  THE I-TH AEROSOL OCCUPIES ONLY
!     THE I-TH LAYER WHERE CONCENTRATION IS 1.  CONCENTRATION IS ZERO
!     ELSEWHERE.  THE I-TH AEROSOL'S EXTINCTION COEFFICIENT IS EXT
!     (1/KM) OF THE I-TH LAYER.  NOTE THAT PRODUCT OF CONCENTRATION
!     AND EXTINCTION COEFFICIENT IS IMPORTANT AND MUST BE 1/KM.
!     THE I-TH LAYER'S COEFFICIENT EQUALS THE AVERAGE OF TOP AND BOTTOM
!     NOVAM VALUE.  STILL THERE IS A SLIGHT PROBLEM IN CALCULATING
!     ABSORBER AMOUNTS.  MODTRAN WON'T SET TO 0 THE I-TH SPECIES
!     IMMEDIATELY BELOW ITH LAYER.  SO EACH LEVEL WILL REALLY BE A
!     PAIR OF CLOSELY SPACED (5 M) LEVELS.  A LAYER WILL BE MODELED
!     BY FOUR LAYER BOUNDARIES AS SHOWN:

!     ----- LEVEL (DENSITY IS 0, TOP LAYER PAIR)
!     ----- LEVEL (DENSITY IS NONZERO, ACTUALLY 1)

!     ----- LEVEL (DENSITY IS NONZERO, ACTUALLY 1, BOTTOM LAYER PAIR)
!     ----- LEVEL (DENSITY IS 0)

!     SO FOR 10 AEROSOLS, WE NEED (10+1)*2=22 LAYER BOUNDARIES

!     IHAZE  = THE TYPE OF BOUNDARY LAYER AEROSOL (EXCLUDING NOVAM)

!     OUTPUT FROM NOVAER:

!     ALTNOV = HEIGHT IN KM
!     RHNOV  = RELATIVE HUMIDITY IN PERCENT
!     TNOV   = TEMPERATURE IN K
!     PNOV   = PRESSURE IN MB
!     ABSNOV = ABSORPTION (NWVLEN VALUES AT EACH LAYER, NOT LEVEL)
!     EXTNOV = EXTINCTION (NWVLEN ...)
!     ASMNOV = ASYMMETRY  (NWVLWN ...)
      IMPLICIT NONE

!     PARAMETERS:
!       HALF     HALF THE SPACING BETWEEN NEARLY COINCIDENT LEVELS [KM].
      INCLUDE 'PARAMS.h'
      DOUBLE PRECISION HALF
      PARAMETER(HALF=.0025D0)

!     ARGUMENTS:
      INTEGER I,IWVNOV,LEV,IHAZE,NNOV,NWLNOV
      DOUBLE PRECISION ALTNOV(MLNOV)
      REAL VIS,RHNOV(MLNOV),TNOV(MLNOV),PNOV(MLNOV)

      DOUBLE PRECISION TMPALT(MNOV)

!     COMMONS:
      INCLUDE 'IFIL.h'

      LOGICAL LNOVAM
      REAL EXTNOV(MNOV,MXWVLN),ABSNOV(MNOV,MXWVLN),                     &
     &     ASMNOV(MNOV,MXWVLN),WLNOV(MXWVLN),DENNOV(MNOV,MLNOV),        &
     &     TMPRH(MNOV),TMPT(MNOV),TMPP(MNOV)
      COMMON/COMNOV/LNOVAM,EXTNOV,ABSNOV,ASMNOV,WLNOV,NNOV,NWLNOV

!     FUNCTIONS:
!       NUNIT    RETURNS AN UNUSED FILE UNIT NUMBER.
      INTEGER NUNIT

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      EXTERNAL DEVCBD

!     LOCAL VARIABLES:
!       NOVAMU   UNIT NUMBER FOR 'NOVAM.OUT' FILE.
      REAL FAC,EXPINT
      INTEGER IAER, JAER, LEVBOT, NOVAMU

!*****
!     WRITE CODE TO RUN THE NOVAM DRIVER
!*****

!     THE FOLLOWING CODE IS TO READ NOVAM OUTPUT (NOVAM.OUT)

      NOVAMU=NUNIT()
      CALL OPNFL(NOVAMU,0,'NOVAM.OUT','OLD','FORMATTED','NOVAER')
      IF(.NOT.LJMASS)WRITE(IPR,'(/A)')' *** NOVAM.OUT FILE IS OPENED'   &
     &  //' *** EXTRA NOVAM ALTITUDES WILL BE INSERTED ***'

!     THE FIRST SET IN NOVAM.OUT IS THE NWVLEN WAVELENGTHS IN MICRONS
      READ(NOVAMU,*)NWLNOV
      READ(NOVAMU,*)(WLNOV(I),I=1,NWLNOV)

!     NOW READ THE ALTITUDES IN M AND CONVERT TO KM; ALSO READ RH.
      READ(NOVAMU,*)NNOV
      IF (NNOV*2.GT.MLNOV) THEN
          IF(LJMASS)CALL WRTBUF(FATAL)
          STOP 'NOVAER:  NOVAM OUTPUT HAS TOO MANY LAYERS'
      ENDIF
      READ(NOVAMU,*)(TMPALT(I),I=1,NNOV)
      DO I =1, NNOV

!        CONVERT FROM METERS TO KILOMETERS:
         TMPALT(I)=TMPALT(I)/1000
      ENDDO
      READ(NOVAMU,*)(TMPT(I),I=1,NNOV)
      READ(NOVAMU,*)(TMPP(I),I=1,NNOV)
      READ(NOVAMU,*)(TMPRH(I),I=1,NNOV)

!     READ EXTINCTION, ABSORPTION AND ASYMMETRY PARAMETERS:
      DO IWVNOV=1, NWLNOV
          READ(NOVAMU,*)(EXTNOV(I,IWVNOV),I=1,NNOV)
          DO I=1, NNOV
              IF(EXTNOV(I,IWVNOV).LT.0.)GOTO 10
          ENDDO
          READ(NOVAMU,*)(ABSNOV(I,IWVNOV),I=1,NNOV)
          READ(NOVAMU,*)(ASMNOV(I,IWVNOV),I=1,NNOV)
      ENDDO
   10 CONTINUE
      NWLNOV=IWVNOV-1
      CLOSE(NOVAMU)

!     LOOP OVER NOVAM AEROSOLS (ONE LAYER PER AEROSOL):
      JAER=2
      DO IAER=1, MNOV

!        DEFINE DENSITIES:
         LEVBOT=2*IAER
         DO LEV=1, MLNOV
            DENNOV(IAER,LEV)=0.
         ENDDO
         DENNOV(IAER,LEVBOT)=1.
         DENNOV(IAER,LEVBOT+1)=1.

!        DEFINE ALTITUDE, TEMPERATURE, PRESSURE AND RH PROFILES:
         IF (IAER .LE. NNOV) THEN
            LEV=LEVBOT-1
            ALTNOV(LEV)=TMPALT(IAER)-HALF

!           INTERPOLATE FOR T, P AND RH
            FAC=SNGL((ALTNOV(LEV)-TMPALT(IAER))                         &
     &             /(TMPALT(JAER)-TMPALT(IAER)))
            TNOV(LEV)=(TMPT(JAER)-TMPT(IAER))*FAC+TMPT(IAER)
            RHNOV(LEV)=(TMPRH(JAER)-TMPRH(IAER))*FAC+TMPRH(IAER)
            PNOV(LEV)=EXPINT(TMPP(IAER),TMPP(JAER),FAC)

            LEV=LEVBOT
            ALTNOV(LEV)=TMPALT(IAER)+HALF

!           INTERPOLATE FOR T, P AND RH
            IF(IAER.LT.NNOV)JAER=IAER+1
            FAC=SNGL((ALTNOV(LEV)-TMPALT(IAER))                         &
     &             /(TMPALT(JAER)-TMPALT(IAER)))
            TNOV(LEV)=(TMPT(JAER)-TMPT(IAER))*FAC+TMPT(IAER)
            RHNOV(LEV)=(TMPRH(JAER)-TMPRH(IAER))*FAC+TMPRH(IAER)
            PNOV(LEV)=EXPINT(TMPP(IAER),TMPP(JAER),FAC)
            JAER=IAER
         ENDIF
      ENDDO

!     THE FOLLOWING IS FROM VSA.F - DON'T THINK IT'S NEEDED
      IF(VIS.LE.0.)THEN

!        DEFAULT FOR VISIBILITY DEPENDS ON THE VALUE OF IHAZE.
         IF(IHAZE.EQ.8)VIS = 0.2
         IF(IHAZE.EQ.9)VIS = 0.5
         IF(IHAZE.EQ.2 .OR. IHAZE.EQ.5)VIS = 5.0
         IF(IHAZE.EQ.1 .OR. IHAZE.EQ.4 .OR. IHAZE.EQ.7)VIS = 23.0
         IF(IHAZE.EQ.6)VIS = 50.0
!        IF(IHAZE.EQ.3)VIS= OR IHAZE = 10 VIS IS DETERMINED ELSEWHERE
      ENDIF

!     CONVERT TO LAYER VALUES
      JAER=1
      DO IAER=2, NNOV
         DO IWVNOV=1,NWLNOV
            EXTNOV(JAER,IWVNOV)                                         &
     &        =(EXTNOV(JAER,IWVNOV)+EXTNOV(IAER,IWVNOV))/2
            ABSNOV(JAER,IWVNOV)                                         &
     &        =(ABSNOV(JAER,IWVNOV)+ABSNOV(IAER,IWVNOV))/2
            ASMNOV(JAER,IWVNOV)                                         &
     &        =(ASMNOV(JAER,IWVNOV)+ASMNOV(IAER,IWVNOV))/2
         ENDDO
         JAER=IAER
      ENDDO

!     CONVERT NNOV TO NUMBER OF NOVAM AEROSOLS
      NNOV=NNOV-1
      RETURN
      END
