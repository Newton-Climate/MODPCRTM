      REAL FUNCTION HENGNS(G,COSANG)

!     CALCULATES THE HENYEY-GREENSTEIN SCATTERING PHASE FUNCTION

!     INPUTS
!       G        HENYEY-GREENSTEIN ASYMMETRY FACTOR
!       COSANG   COSINE OF THE SCATTERING ANGLE
      REAL G,COSANG

!     OUTPUTS:
!       HENGNS   HENYEY-GREENSTEIN SCATTERING PHASE FUNCTION [STER-1]

!     COMMONS:

!     /RCNSTN/
!       PI       THE CONSTANT PI
!       DEG      NUMBER OF DEGREES IN ONE RADIAN.
!       BIGNUM   MAXIMUM SINGLE PRECISION NUMBER.
!       BIGEXP   MAXIMUM EXPONENTIAL ARGUMENT WITHOUT OVERFLOW.
!       RRIGHT   SMALLEST SINGLE PRECISION REAL ADDED TO 1 EXCEEDS 1.
      REAL PI,DEG,BIGNUM,BIGEXP,RRIGHT
      COMMON/RCNSTN/PI,DEG,BIGNUM,BIGEXP,RRIGHT
      INCLUDE 'IFIL.h'

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      EXTERNAL DEVCBD

!     LOCAL VARIABLES
      REAL ONEMG,DENOM
      ONEMG=1-G
      DENOM=ONEMG**2+2*G*(1-COSANG)
      DENOM=4*PI*DENOM*SQRT(DENOM)
      IF(DENOM.GE.0.)THEN
          HENGNS=ONEMG*(1+G)/DENOM
          RETURN
      ENDIF
      WRITE(IPR,'(2A,/14X,2(A,F9.5))')' FATAL ERROR: ',                 &
     &  ' HENYEY-GREENSTEIN PHASE FUNCTION IS NOT WELL DEFINED FOR',    &
     &  ' ASYMMETRY FACTOR (G) =',G,' AND COS(ANGLE) =',COSANG
      STOP 'Error in HENGNS:  Improper Henyey-Greenstein parameters.'
      END
