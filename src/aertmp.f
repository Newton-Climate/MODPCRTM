      SUBROUTINE AERTMP

!     DETERMINES AEROSOL DENSITY WEIGHTED PATH AVERAGED TEMPERATURES
!     FOR FIRST LINE-OF-SIGHT PATH (ONLY USED FOR MICROWAVE FOG MODEL).
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     COMMONS:
      INCLUDE 'SEGDAT.h'

!     /CNTRL/
!       NSEG     NUMBER OF PATH SEGMENTS ALONG LINE-OF-SIGHT.
!       ML       NUMBER OF ATMOSPHERIC PROFILE LEVELS.
!       MLFLX    NUMBER OF LEVELS FOR WHICH FLUX VALUES ARE WRITTEN.
!       IMULT    MULTIPLE SCATTERING FLAG
!                  (0=NONE, 1=AT SENSOR, -1=AT FINAL OR TANGENT POINT).
!       THERML   FLAG TO CALCULATE THERMAL SCATTER.
      INTEGER NSEG,ML,MLFLX,IMULT
      LOGICAL THERML
      COMMON/CNTRL/NSEG(0:MLOSP1),ML,MLFLX,IMULT,THERML

!     /AERTM/
      REAL TAER
      COMMON/AERTM/TAER(MAER)

!     LOCAL VARIABLES:
!       IAER     AEROSOL INDEX.
!       MAPAER   AEROSOL COLUMN AMOUNT INDEX.
!       ISEG     SEGMENT INDEX.
      INTEGER IAER,MAPAER,ISEG

!     DATA:
!       MAP      MAP FROM AEROSOL INDEX TO COLUMN AMOUNT INDEX.
      INTEGER MAP(MAER)
      DATA MAP/7,12,13,14,16,66,67,68,69,70,71,72,73,74,75,76,77/
      DO IAER=1,MAER
          MAPAER=MAP(IAER)
          TAER(IAER)=TSEG(1,1)
          IF(WTOTAL(MAPAER).GT.0.)THEN
              TAER(IAER)=TSEG(1,1)*WPTH(MAPAER,1,1)
              DO ISEG=2,NSEG(1)
                  TAER(IAER)=TAER(IAER)+TSEG(ISEG,1)*WPTH(MAPAER,ISEG,1)
              ENDDO
              TAER(IAER)=TAER(IAER)/WTOTAL(MAPAER)
          ENDIF
      ENDDO
      RETURN
      END
