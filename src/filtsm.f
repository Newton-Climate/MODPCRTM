      SUBROUTINE FLTSMT(IBINPT,NMOLY,UNIF,TRACE,TX)

!     FLTSMT PERFORMS ABSORBANCE FILTER RESPONSE SUMS FOR EACH CHANNEL.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     ARGUMENTS:
!       IBINPT   BIN NUMBER OF CURRENT SPECTRAL POINT.
!                (CENTER FREQUENCY = IBINPT * BNDWID + OSHIFT).
!       NMOLY    NUMBER OF ACTIVE Y-SPECIES.
!       UNIF     SPECTRAL TRANSMITTANCE OF UNIFORMLY MIXED GASES.
!       TRACE    SPECTRAL TRANSMITTANCE OF TRACE GASES.
!       TX       SPECIES SPECTRAL TRANSMITTANCE ARRAY.
      INTEGER IBINPT,NMOLY
      REAL UNIF,TRACE,TX(*)

!     COMMONS:
      INCLUDE 'CHANLS.h'

!     LOCAL VARIABLES:
!       ICHAN    CHANNEL INDEX.
!       LCHN     CURRENT CHANNEL NUMBER.
!       IOUT     CHANNEL OUTPUT VARIABLE INDEX.
!       WT       CURRENT CHANNEL WEIGHT.
      INTEGER ICHAN,LCHN,IOUT
      REAL WT

!     RETURN IF SPECTRAL BIN IS OUT OF RANGE.
      IF(IBINPT.LT.MNBIN .OR.IBINPT.GT.MXBIN)RETURN

!     LOOP OVER CHANNELS COVERING CURRENT SPECTRAL BIN.
      DO ICHAN=1,NUMCHN(IBINPT)

!         INCREMENT SUMS.
          LCHN=LSTCHN(ICHAN,IBINPT)
          WT=WTCHN(ICHAN,IBINPT)
          VLCHAN( 1,1,LCHN)=VLCHAN( 1,1,LCHN)+DBLE(WT*(1-TX(9)))
          VLCHAN( 2,1,LCHN)=VLCHAN( 2,1,LCHN)+DBLE(WT*(1-TX(17)))
          VLCHAN( 3,1,LCHN)=VLCHAN( 3,1,LCHN)+DBLE(WT*(1-UNIF))
          VLCHAN( 4,1,LCHN)=VLCHAN( 4,1,LCHN)+DBLE(WT*(1-TX(31)))
          VLCHAN( 5,1,LCHN)=VLCHAN( 5,1,LCHN)+DBLE(WT*(1-TRACE))
          VLCHAN( 6,1,LCHN)=VLCHAN( 6,1,LCHN)+DBLE(WT*(1-TX(4)))
          VLCHAN( 7,1,LCHN)=VLCHAN( 7,1,LCHN)+DBLE(WT*(1-TX(5)))
          VLCHAN( 8,1,LCHN)=VLCHAN( 8,1,LCHN)+DBLE(WT*(1-TX(6)))
          VLCHAN( 9,1,LCHN)=VLCHAN( 9,1,LCHN)+DBLE(WT*(1-TX(7)))
          VLCHAN(10,1,LCHN)=VLCHAN(10,1,LCHN)+DBLE(WT*(1-TX(11)))
          VLCHAN(11,1,LCHN)=VLCHAN(11,1,LCHN)+DBLE(WT*(1-TX(10)))
          VLCHAN(12,1,LCHN)=VLCHAN(12,1,LCHN)+DBLE(WT*(1-TX(36)))
          VLCHAN(13,1,LCHN)=VLCHAN(13,1,LCHN)+DBLE(WT*(1-TX(44)))
          VLCHAN(14,1,LCHN)=VLCHAN(14,1,LCHN)+DBLE(WT*(1-TX(46)))
          VLCHAN(15,1,LCHN)=VLCHAN(15,1,LCHN)+DBLE(WT*(1-TX(47)))
          VLCHAN(16,1,LCHN)=VLCHAN(16,1,LCHN)+DBLE(WT*(1-TX(50)))
          VLCHAN(17,1,LCHN)=VLCHAN(17,1,LCHN)+DBLE(WT*(1-TX(52)))
          VLCHAN(18,1,LCHN)=VLCHAN(18,1,LCHN)+DBLE(WT*(1-TX(54)))
          VLCHAN(19,1,LCHN)=VLCHAN(19,1,LCHN)+DBLE(WT*(1-TX(64)))
          VLCHAN(20,1,LCHN)=VLCHAN(20,1,LCHN)+DBLE(WT*(1-TX(65)))
          VLCHAN(21,1,LCHN)=VLCHAN(21,1,LCHN)+DBLE(WT*(1-TX(16)))
          DO IOUT=1,NMOLX+NMOLY
              VLCHAN(21+IOUT,1,LCHN)                                    &
     &          =VLCHAN(21+IOUT,1,LCHN)+DBLE(WT*(1-TX(MEXT+IOUT)))
          ENDDO
      ENDDO
      RETURN
      END

      SUBROUTINE FLTSMS(IBINPT,TS0,S0,TX9)

!     FLTSMS PERFORMS SOLAR IRRADIANCE AND TOTAL ABSORBANCE FILTER
!     RESPONSE SUMS FOR EACH CHANNEL.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     ARGUMENTS:
!       IBINPT   BIN NUMBER OF CURRENT SPECTRAL POINT.
!                (CENTER FREQUENCY = IBINPT * BNDWID + OSHIFT).
!       TS0      TRANSMITTED SOLAR IRRADIANCE [W CM-2 / CM-1].
!       S0       TOP-OF-ATMOSPHERE SOLAR IRRADIANCE [W CM-2 / CM-1].
!       TX9      SUN TO SENSOR SPECTRAL TRANSMITTANCE.
      INTEGER IBINPT
      REAL TS0,S0,TX9

!     COMMONS:
      INCLUDE 'CHANLS.h'

!     LOCAL VARIABLES:
!       ICHAN    CHANNEL INDEX.
!       LCHN     CURRENT CHANNEL NUMBER.
!       WT       CURRENT CHANNEL WEIGHT.
      INTEGER ICHAN,LCHN
      REAL WT

!     RETURN IF SPECTRAL BIN IS OUT OF RANGE.
      IF(IBINPT.LT.MNBIN .OR.IBINPT.GT.MXBIN)RETURN

!     LOOP OVER CHANNELS COVERING CURRENT SPECTRAL BIN.
      DO ICHAN=1,NUMCHN(IBINPT)

!         INCREMENT SUMS.
          LCHN=LSTCHN(ICHAN,IBINPT)
          WT=WTCHN(ICHAN,IBINPT)
          VLCHAN(1,1,LCHN)=VLCHAN(1,1,LCHN)+DBLE(WT*TS0)
          VLCHAN(2,1,LCHN)=VLCHAN(2,1,LCHN)+DBLE(WT*S0)
          VLCHAN(3,1,LCHN)=VLCHAN(3,1,LCHN)+DBLE(WT*(1-TX9))
      ENDDO
      RETURN
      END

      SUBROUTINE FLTSME(IBINPT,ILOS,DIS,                                &
     &  SUMT,THMCUM,SUMTMS,BBG,RFSURF,TX9,DEMIS)

!     FLTSME PERFORMS THERMAL EMITTANCE CALCULATION FILTER
!     RESPONSE SUMS FOR EACH CHANNEL.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     ARGUMENTS:
!       IBINPT   BIN NUMBER OF CURRENT SPECTRAL POINT.
!                (CENTER FREQUENCY = IBINPT * BNDWID + OSHIFT).
!       ILOS     LINE-OF-SIGHT PATH INDEX.
!       DIS      LOGICAL FLAG, TRUE FOR DISORT MULTIPLE SCATTERING.
!       SUMT     TOTAL SPECTRAL RADIANCE AT SENSOR [W SR-1 CM-2 / CM-1].
!       THMCUM   PATH & SCATTERED THERMAL EMISSION [W SR-1 CM-2 / CM-1].
!       SUMTMS   ISAACS SCATTERED THERMAL EMISSION [W SR-1 CM-2 / CM-1].
!       BBG      TRANSMITTED GROUND EMISSION [W SR-1 CM-2 / CM-1].
!       RFSURF   TRANSMITTED SURFACE REFLECTANCE [W SR-1 CM-2 / CM-1].
!       TX9      SENSOR PATH SPECTRAL TRANSMITTANCE.
!       DEMIS    SURFACE DIRECTIONAL EMISSIVITY TOWARDS SENSOR.
      INTEGER IBINPT,ILOS
      LOGICAL DIS
      REAL SUMT,THMCUM,SUMTMS,BBG,RFSURF,TX9,DEMIS

!     COMMONS:
      INCLUDE 'CHANLS.h'

!     LOCAL VARIABLES:
!       ICHAN    CHANNEL INDEX.
!       LCHN     CURRENT CHANNEL NUMBER.
!       WT       CURRENT CHANNEL WEIGHT.
      INTEGER ICHAN,LCHN
      REAL WT

!     RETURN IF SPECTRAL BIN IS OUT OF RANGE.
      IF(IBINPT.LT.MNBIN .OR.IBINPT.GT.MXBIN)RETURN

!     SCATTERING MODEL BRANCHING:
      IF(DIS)THEN

!         DISORT SCATTERING:  NO PARTITIONING OF PATH THERMAL SCATTER.
          DO ICHAN=1,NUMCHN(IBINPT)

!             INCREMENT SUMS.
              LCHN=LSTCHN(ICHAN,IBINPT)
              WT=WTCHN(ICHAN,IBINPT)
              VLCHAN(1,ILOS,LCHN)=VLCHAN(1,ILOS,LCHN)+DBLE(WT*SUMT)
              VLCHAN(2,ILOS,LCHN)=VLCHAN(2,ILOS,LCHN)+DBLE(WT*THMCUM)
              VLCHAN(3,ILOS,LCHN)=VLCHAN(3,ILOS,LCHN)+DBLE(WT*BBG)
              VLCHAN(4,ILOS,LCHN)=VLCHAN(4,ILOS,LCHN)+DBLE(WT*RFSURF)
              VLCHAN(5,ILOS,LCHN)=VLCHAN(5,ILOS,LCHN)+DBLE(WT*(1-TX9))
              VLCHAN(6,ILOS,LCHN)=VLCHAN(6,ILOS,LCHN)+DBLE(WT*DEMIS)
          ENDDO
      ELSE

!         NO DISORT SCATTERING:  ISAACS PARTITIONS PATH THERMAL SCATTER.
          DO ICHAN=1,NUMCHN(IBINPT)

!             INCREMENT SUMS.
              LCHN=LSTCHN(ICHAN,IBINPT)
              WT=WTCHN(ICHAN,IBINPT)
              VLCHAN(1,ILOS,LCHN)=VLCHAN(1,ILOS,LCHN)+DBLE(WT*SUMT)
              VLCHAN(2,ILOS,LCHN)=VLCHAN(2,ILOS,LCHN)+DBLE(WT*THMCUM)
              VLCHAN(3,ILOS,LCHN)=VLCHAN(3,ILOS,LCHN)+DBLE(WT*SUMTMS)
              VLCHAN(4,ILOS,LCHN)=VLCHAN(4,ILOS,LCHN)+DBLE(WT*BBG)
              VLCHAN(5,ILOS,LCHN)=VLCHAN(5,ILOS,LCHN)+DBLE(WT*RFSURF)
              VLCHAN(6,ILOS,LCHN)=VLCHAN(6,ILOS,LCHN)+DBLE(WT*(1-TX9))
              VLCHAN(7,ILOS,LCHN)=VLCHAN(7,ILOS,LCHN)+DBLE(WT*DEMIS)
          ENDDO
      ENDIF
      RETURN
      END

      SUBROUTINE FLTSMR(IBINPT,ILOS,DIS,SUMT,THMCUM,SUMTMS,BBG,         &
     &  SUMSSR,SUMSSS,RFSURF,RFLSS,TSNREF,TSNOBS,TX9,DEMIS)

!     FLTSMR PERFORMS THERMAL EMITTANCE PLUS SOLAR SCATTER
!     CALCULATION FILTER RESPONSE SUMS FOR EACH CHANNEL.
      IMPLICIT NONE

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     ARGUMENTS:
!       IBINPT   BIN NUMBER OF CURRENT SPECTRAL POINT.
!                (CENTER FREQUENCY = IBINPT * BNDWID + OSHIFT).
!       ILOS     LINE-OF-SIGHT PATH INDEX.
!       DIS      LOGICAL FLAG, TRUE FOR DISORT MULTIPLE SCATTERING.
!       SUMT     TOTAL SPECTRAL RADIANCE AT SENSOR [W SR-1 CM-2 / CM-1].
!       THMCUM   PATH & SCATTERED THERMAL EMISSION [W SR-1 CM-2 / CM-1].
!       SUMTMS   ISAACS SCATTERED THERMAL EMISSION [W SR-1 CM-2 / CM-1].
!       BBG      TRANSMITTED GROUND EMISSION [W SR-1 CM-2 / CM-1].
!       SUMSSR   PATH SCATTERED SOLAR [W SR-1 CM-2 / CM-1].
!       SUMSSS   PATH SINGLE SCATTERED SOLAR [W SR-1 CM-2 / CM-1].
!       RFSURF   TRANSMITTED SURFACE REFLECTANCE [W SR-1 CM-2 / CM-1].
!       RFLSS    SUN-SURFACE-SENSOR DIRECT RADIANCE[W SR-1 CM-2 / CM-1].
!       TSNREF   SENSOR-FINAL_ALTITUDE-SUN TRANSMITTANCE CONVOLVED WITH
!                THE TOP-OF-ATMOSPHERE SOLAR IRRADIANCE [W CM-2 / CM-1].
!       TSNOBS   TRANSMITTED SOLAR IRRADIANCE AT SENSOR [W CM-2 / CM-1].
!       TX9      SENSOR PATH SPECTRAL TRANSMITTANCE.
!       DEMIS    SURFACE DIRECTIONAL EMISSIVITY TOWARDS SENSOR.
      INTEGER IBINPT,ILOS
      LOGICAL DIS
      REAL SUMT,THMCUM,SUMTMS,BBG,                                      &
     &  SUMSSR,SUMSSS,RFSURF,RFLSS,TX9,TSNREF,TSNOBS,DEMIS

!     COMMONS:
      INCLUDE 'CHANLS.h'

!     LOCAL VARIABLES:
!       ICHAN    CHANNEL INDEX.
!       LCHN     CURRENT CHANNEL NUMBER.
!       WT       CURRENT CHANNEL WEIGHT.
      INTEGER ICHAN,LCHN
      REAL WT

!     RETURN IF SPECTRAL BIN IS OUT OF RANGE.
      IF(IBINPT.LT.MNBIN .OR.IBINPT.GT.MXBIN)RETURN

!     SCATTERING MODEL BRANCHING:
      IF(DIS)THEN

!         DISORT SCATTERING:  NO PARTITIONING OF PATH THERMAL SCATTER.
          DO ICHAN=1,NUMCHN(IBINPT)

!             INCREMENT SUMS.
              LCHN=LSTCHN(ICHAN,IBINPT)
              WT=WTCHN(ICHAN,IBINPT)
              VLCHAN( 1,ILOS,LCHN)=VLCHAN( 1,ILOS,LCHN)+DBLE(WT*SUMT)
              VLCHAN( 2,ILOS,LCHN)=VLCHAN( 2,ILOS,LCHN)+DBLE(WT*THMCUM)
              VLCHAN( 3,ILOS,LCHN)=VLCHAN( 3,ILOS,LCHN)+DBLE(WT*BBG)
              VLCHAN( 4,ILOS,LCHN)=VLCHAN( 4,ILOS,LCHN)+DBLE(WT*SUMSSR)
              VLCHAN( 5,ILOS,LCHN)=VLCHAN( 5,ILOS,LCHN)+DBLE(WT*SUMSSS)
              VLCHAN( 6,ILOS,LCHN)=VLCHAN( 6,ILOS,LCHN)+DBLE(WT*RFSURF)
              VLCHAN( 7,ILOS,LCHN)=VLCHAN( 7,ILOS,LCHN)+DBLE(WT*RFLSS)
              VLCHAN( 8,ILOS,LCHN)=VLCHAN( 8,ILOS,LCHN)+DBLE(WT*TSNREF)
              VLCHAN( 9,ILOS,LCHN)=VLCHAN( 9,ILOS,LCHN)+DBLE(WT*TSNOBS)
              VLCHAN(10,ILOS,LCHN)=VLCHAN(10,ILOS,LCHN)+DBLE(WT*(1-TX9))
              VLCHAN(11,ILOS,LCHN)=VLCHAN(11,ILOS,LCHN)+DBLE(WT*DEMIS)
          ENDDO
      ELSE

!         NO DISORT SCATTERING:  ISAACS PARTITIONS PATH THERMAL SCATTER.
          DO ICHAN=1,NUMCHN(IBINPT)

!             INCREMENT SUMS.
              LCHN=LSTCHN(ICHAN,IBINPT)
              WT=WTCHN(ICHAN,IBINPT)
              VLCHAN( 1,ILOS,LCHN)=VLCHAN( 1,ILOS,LCHN)+DBLE(WT*SUMT)
              VLCHAN( 2,ILOS,LCHN)=VLCHAN( 2,ILOS,LCHN)+DBLE(WT*THMCUM)
              VLCHAN( 3,ILOS,LCHN)=VLCHAN( 3,ILOS,LCHN)+DBLE(WT*SUMTMS)
              VLCHAN( 4,ILOS,LCHN)=VLCHAN( 4,ILOS,LCHN)+DBLE(WT*BBG)
              VLCHAN( 5,ILOS,LCHN)=VLCHAN( 5,ILOS,LCHN)+DBLE(WT*SUMSSR)
              VLCHAN( 6,ILOS,LCHN)=VLCHAN( 6,ILOS,LCHN)+DBLE(WT*SUMSSS)
              VLCHAN( 7,ILOS,LCHN)=VLCHAN( 7,ILOS,LCHN)+DBLE(WT*RFSURF)
              VLCHAN( 8,ILOS,LCHN)=VLCHAN( 8,ILOS,LCHN)+DBLE(WT*RFLSS)
              VLCHAN( 9,ILOS,LCHN)=VLCHAN( 9,ILOS,LCHN)+DBLE(WT*TSNREF)
              VLCHAN(10,ILOS,LCHN)=VLCHAN(10,ILOS,LCHN)+DBLE(WT*TSNOBS)
              VLCHAN(11,ILOS,LCHN)=VLCHAN(11,ILOS,LCHN)+DBLE(WT*(1-TX9))
              VLCHAN(12,ILOS,LCHN)=VLCHAN(12,ILOS,LCHN)+DBLE(WT*DEMIS)
          ENDDO
      ENDIF
      RETURN
      END
