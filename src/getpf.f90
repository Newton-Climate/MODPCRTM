      SUBROUTINE GETPF(CSSCAT,PCLDB,PICEB)

!     THIS ROUTINE ANGULARLY INTERPOLATES CLOUD SCATTERING
!     PHASE FUNCTIONS FROM THE CLDMOD AND CIRMOD MODULES.

!     MODULES:
      USE CLDMOD
      USE CIRMOD

!     INPUT ARGUMENTS:
!       CSSCAT   COSINE OF THE SCATTERING ANGLE.

!     OUTPUT ARGUMENTS:
!       PCLDB    WATER CLOUD SCATTERING PHASE FUNCTION VALUE.
!       PICEB    CIRRUS CLOUD SCATTERING PHASE FUNCTION VALUE.
      REAL CSSCAT,PCLDB,PICEB

!     COMMONS:

!     LOCAL VARIABLES:
!       IANGLO   LOWER INDEX FOR CLOUD SPECTRAL GRID INTERPOLATION.
!       FACTOR   INTERPOLATION FRACTION.
      INTEGER IANGLO
      REAL FACTOR

!     DATA:
!       ICLDHI   SAVED UPPER INDEX FOR WATER CLOUD SPECTRAL GRID.
!       ICIRHI   SAVED UPPER INDEX FOR CIRRUS CLOUD SPECTRAL GRID.
      INTEGER ICLDHI,ICIRHI
      SAVE ICLDHI,ICIRHI
      DATA ICLDHI,ICIRHI/2,2/

!     SET UP ANGULAR INTERPOLATION FOR WATER CLOUD DATA
!     (COSINE GRID IS MONOTONICALLY DECREASING):
      IF(ICLDHI.GT.NCLDAN)ICLDHI=NCLDAN
      IF(CLDCOS(ICLDHI-1).LT.CSSCAT .OR. CSSCAT.LT.CLDCOS(ICLDHI))THEN

!         SAVED BOUNDS NO LONGER APPLICABLE - FIND NEW BOUNDS:
          DO ICLDHI=2,NCLDAN
              IF(CSSCAT.GE.CLDCOS(ICLDHI))GOTO 10
          ENDDO
   10     CONTINUE
      ENDIF
      IANGLO=ICLDHI-1
      FACTOR=(CSSCAT-CLDCOS(IANGLO))/(CLDCOS(ICLDHI)-CLDCOS(IANGLO))

!     EXPONENTIALLY INTERPOLATE WATER CLOUD PHASE FUNCTION:
      PCLDB=CLDPFV(IANGLO)*(CLDPFV(ICLDHI)/CLDPFV(IANGLO))**FACTOR

!     SET UP ANGULAR INTERPOLATION FOR CIRRUS CLOUD DATA
!     (COSINE GRID IS MONOTONICALLY DECREASING):
      IF(ICIRHI.GT.NCIRAN)ICIRHI=NCIRAN
      IF(CIRCOS(ICIRHI-1).LT.CSSCAT .OR. CSSCAT.LT.CIRCOS(ICIRHI))THEN

!         SAVED BOUNDS NO LONGER APPLICABLE - FIND NEW BOUNDS:
          DO ICIRHI=2,NCIRAN
              IF(CSSCAT.GE.CIRCOS(ICIRHI))GOTO 20
          ENDDO
   20     CONTINUE
      ENDIF
      IANGLO=ICIRHI-1
      FACTOR=(CSSCAT-CIRCOS(IANGLO))/(CIRCOS(ICIRHI)-CIRCOS(IANGLO))

!     EXPONENTIALLY INTERPOLATE CIRRUS CLOUD PHASE FUNCTION:
      PICEB=CIRPFV(IANGLO)*(CIRPFV(ICIRHI)/CIRPFV(IANGLO))**FACTOR
      RETURN
      END
